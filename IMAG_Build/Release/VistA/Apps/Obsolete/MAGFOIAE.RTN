07/12/2007 14:41
Saved from VISTA
MAGFOIAE
MAGFOIAE ;Environment Check routine -- Prevent installation on VA Installations; 07/12/2007 14:41
 ;;3.0;IMAGING;;
 ;; Per VHA Directive 2004-038, this routine should not be modified.
 ;; +---------------------------------------------------------------+
 ;; | Property of the US Government.                                |
 ;; | No permission to copy or redistribute this software is given. |
 ;; | Use of unreleased versions of this software requires the user |
 ;; | to execute a written test agreement with the VistA Imaging    |
 ;; | Development Office of the Department of Veterans Affairs,     |
 ;; | telephone (301) 734-0100.                                     |
 ;; |                                                               |
 ;; | The Food and Drug Administration classifies this software as  |
 ;; | a medical device.  As such, it may not be changed             |
 ;; | in any way.  Modifications to this software may result in an  |
 ;; | adulterated medical device under 21CFR820, the use of which   |
 ;; | is considered to be a violation of US Federal Statutes.       |
 ;; +---------------------------------------------------------------+
 ;;
 ; This is the FOIA Environment Check Routine
 ; This check must prevent accidental installation of this KIDS
 ; in a VA or IHS institution.
 ;
 N ERR,I,MAGVRSN,MAGMSG,MAGENV
 ; XPDENV - =0 (loading process)
 ;          =1 (install process)
 ;
 S I=0
 I XPDENV=0,$G(DUZ(0))'="@" S I=I+1,MAGMSG(I)="Remember to set your DUZ(0)=@ before installing."
 ;
 S MAGENV=$$KSP^XUPARAM("WHERE")
 S:MAGENV["VA.GOV" I=I+1,MAGMSG(I)="Cannot install on a VA account."
 S:MAGENV["IHS.GOV" I=I+1,MAGMSG(I)="Cannot install on an IHS account."
 D:XPDENV
 . S:MAGENV["VA.GOV" I=I+1,MAGMSG(I)="Installation will abort."
 . S:MAGENV["IHS.GOV" I=I+1,MAGMSG(I)="Installation will abort."
 . S:$G(DUZ(0))'="@" I=I+1,MAGMSG(I)="Please set your Fileman Access code (DUZ(0)) to ""@"" before installing."
 . Q
 S:I>0 I=I+1,MAGMSG(I)="Transport global is not deleted."
 D:I>0 MES^XPDUTL(.MAGMSG)
 I XPDENV,I>0 S XPDABORT=1 Q
 ;
 S I=1,MAGMSG(I)=" "
 I DUZ(2)=$$KSP^XUPARAM("INST") S I=I+1,MAGMSG(I)="Environment check: OK!"
 E  D
 . S I=I+1,MAGMSG(I)="You must be logged in to the same institution on which you"
 . S I=I+1,MAGMSG(I)="are installing this KIDS package"
 . S I=I+1,MAGMSG(I)="Your DUZ(2) is: "_$G(DUZ(2))_" and the host system is: "_$$KSP^XUPARAM("INST")
 . S XPDQUIT=2
 . Q
 D MES^XPDUTL(.MAGMSG)
 Q
 ;


