11/06/2003  08:38
Saved from DCM,DCE
MAGIPS10
MAGIPS10 ;Post init routine to queue site activity at install. ; 11/06/2003  08:38
 ;;3.0;IMAGING;**10**;Oct 31, 2003
 ;; +---------------------------------------------------------------+
 ;; | Property of the US Government.                                |
 ;; | No permission to copy or redistribute this software is given. |
 ;; | Use of unreleased versions of this software requires the user |
 ;; | to execute a written test agreement with the VistA Imaging    |
 ;; | Development Office of the Department of Veterans Affairs,     |
 ;; | telephone (301) 734-0100.                                     |
 ;; |                                                               |
 ;; | The Food and Drug Administration classifies this software as  |
 ;; | a medical device.  As such, it may not be changed in any way. |
 ;; | Modifications to this software may result in an adulterated   |
 ;; | medical device under 21CFR820, the use of which is considered |
 ;; | to be a violation of US Federal Statutes.                     |
 ;; +---------------------------------------------------------------+
 ;;
 Q
POST ;
 D REMTASK^MAGQE4
 D STTASK^MAGQE4
 D INS(XPDNM,DUZ,$$NOW^XLFDT,XPDA)
 D POSTI^MAGQBUT
 Q
PRE ;Problems with DDs for the worklist.
 N FILE,ZOUT,DATA
 F FILE=2006.55,2006.56 I $$VFILE^DILFD(FILE) D DELFILE(FILE)
 ;Remove the test file 2006.555 & 2006.5832 - Remove data
 ;File 2006.5832 is no longer used t9 
 ;File 2006.5831 has changed in t9
 F FILE=2006.555,2006.5832 I $$VFILE^DILFD(FILE) D DELFILE(FILE,1)
 S FILE=2006.5831,DATA=1
 I $$VFILE^DILFD(FILE) D 
 . D FIELD^DID(FILE,2,"","SPECIFIER","ZOUT")
 . I $G(ZOUT("SPECIFIER"))="RP2005.84'" S DATA=0
 . D DELFILE(FILE,DATA)
 . Q
 Q
DELFILE(NUMBER,DATA) ;
 N DIU,SNDX
 I '$D(DATA) S DATA=0
 Q:'NUMBER
 I (NUMBER<2005)!(NUMBER>2006.9999) Q
 ; Remove subfile before deleting the file.
 S SNDX="" F  S SNDX=$O(^DD(NUMBER,"SB",SNDX)) Q:SNDX'?1N.N.".".N  D
 . S DIU=SNDX,DIU(0)="S" D EN^DIU2
 . Q
 S DIU=NUMBER,DIU(0)="E"
 I DATA S DIU(0)="DE"
 W !,"FILE # ",NUMBER D EN^DIU2
 K DIU
 Q
 ;
INS(XP,DUZ,DATE,IDA) ;
 N CT,CNT,COM,D,D0,D1,D2,DDATE,DG,DIC,DICR,DIW,MAGMSG,ST,XMID,XMY
 D GETENV^%ZOSV
 S CNT=0
 S CNT=CNT+1,MAGMSG(CNT)="PACKAGE INSTALL"
 S CNT=CNT+1,MAGMSG(CNT)="SITE: "_$$KSP^XUPARAM("WHERE")
 S CNT=CNT+1,MAGMSG(CNT)="PACKAGE: "_XP
 S CNT=CNT+1,MAGMSG(CNT)="Version: "_$$VER^XPDUTL(XP)
 S ST=$$GET1^DIQ(9.7,IDA,11,"I")
 S CNT=CNT+1,MAGMSG(CNT)="Start time: "_$$FMTE^XLFDT(ST)
 S CT=$$GET1^DIQ(9.7,IDA,17,"I") S:+CT'=CT CT=$$NOW^XLFDT
 S CNT=CNT+1,MAGMSG(CNT)="Completion time: "_$$FMTE^XLFDT(CT)
 S CNT=CNT+1,MAGMSG(CNT)="Run time: "_$$FMDIFF^XLFDT(CT,ST,3)
 S CNT=CNT+1,MAGMSG(CNT)="Environment: "_Y
 S COM=$$GET1^DIQ(9.7,IDA,6,"I")
 S CNT=CNT+1,MAGMSG(CNT)="FILE COMMENT: "_COM
 S CNT=CNT+1,MAGMSG(CNT)="DATE: "_DATE
 S CNT=CNT+1,MAGMSG(CNT)="Installed by: "_$$GET1^DIQ(9.7,IDA,9,"E")
 S CNT=CNT+1,MAGMSG(CNT)="Install Name: "_$$GET1^DIQ(9.7,IDA,.01,"E")
 S DDATE=$$GET1^DIQ(9.7,IDA,51,"I")
 S CNT=CNT+1,MAGMSG(CNT)="Distribution Date: "_$$FMTE^XLFDT(DDATE)
 S XMSUB=XP_" INSTALLATION"
 S XMID=$G(DUZ) S:'XMID XMID=.5
 S XMY(XMID)=""
 S XMY("G.MAG SERVER")=""
 S:$G(MAGDUZ) XMY(MAGDUZ)=""
 S XMSUB=$E(XMSUB,1,63)
 D SENDMSG^XMXAPI(XMID,XMSUB,"MAGMSG",.XMY,,.XMZ,)
 I $G(XMERR) M XMERR=^TMP("XMERR",$J) S $EC=",U13-Cannot send MailMan message,"
 Q
 ;


