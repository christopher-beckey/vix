09:50 AM 08/26/2005 
DICOM Gateway MUMPS routines Build V3.0p51Build720_T22
MAGIPS31
MAGIPS31 ;WOIFO/MLH - Pre & Post-Init routines for Patch 31 ; 31 Mar 2005  11:17 AM
 ;;3.0;IMAGING;**47**;13-April-2005
 ;; +---------------------------------------------------------------+
 ;; | Property of the US Government.                                |
 ;; | No permission to copy or redistribute this software is given. |
 ;; | Use of unreleased versions of this software requires the user |
 ;; | to execute a written test agreement with the VistA Imaging    |
 ;; | Development Office of the Department of Veterans Affairs,     |
 ;; | telephone (301) 734-0100.                                     |
 ;; |                                                               |
 ;; | The Food and Drug Administration classifies this software as  |
 ;; | a medical device.  As such, it may not be changed in any way. |
 ;; | Modifications to this software may result in an adulterated   |
 ;; | medical device under 21CFR820, the use of which is considered |
 ;; | to be a violation of US Federal Statutes.                     |
 ;; +---------------------------------------------------------------+
 ;;
 Q
PRE ; Entry point - pre-initialization for Patch 31
 ; Set ^XTMP subtree to expire in one year.
 ; 
 K DQ
 N CURDAT ; ----- current date and time
 N EXPDAT ; ----- expiration date of our ^XTMP subtree
 N IX ; --------- global subscript index
 ;
 ; 1-year retention for ^XTMP
 S CURDAT=$$DT^XLFDT
 S EXPDAT=$$FMADD^XLFDT(CURDAT,366)
 S ^XTMP("MAG30P25",0)=EXPDAT_U_CURDAT_U_"IMAGE INDEX CONVERSION"
 W !!,"^XTMP nodes will be auto-purged on ",$$FMTE^XLFDT(EXPDAT)
 ;
 ; Clear old subtree of generated uncommitted index values.
 K ^XTMP("MAG30P25","OUTPUT")
 ;
 ; Clear Fields 40-45 (subscript 2) on the MAG DESCRIPTIVE CATEGORIES 
 ; File (#2005.81) so the data merge works properly
 W !!,"Resetting Fields 40-45 on MAG DESCRIPTIVE CATEGORIES (#2005.81)..."
 S IX=0
 F  S IX=$O(^MAG(2005.81,IX)) Q:'IX  K ^(IX,2)
 W "done."
 ;
 ; Remove File 2005.85 preparatory to reinstallation.
 K ^MAG(2005.85)
EXITPRE ;
 Q
 ;
POST ; Entry point - post-initialization for Patch 31
 ;
 N ENTNUM ; --- entry number to be updated
 N ASPEC ; ---- array containing specialty information
 N PTRVAL ; --- pointer value from proc/event index file
 ;
 ; Make sure specialties of PHARMACY (60), RESEARCH (66), SOCIAL WORK (67),
 ; and SPINAL CORD INJURY (68) all have a CLASS value of CLIN (1).
 F ENTNUM=60,66,67,68 S ASPEC(2005.84,ENTNUM_",",1)=1
 D FILE^DIE("K",$NA(ASPEC))
 ;
 ; Correct PROC/EVENT INDEX mapping for CT images from DICOM Gateway.
 S ENTNUM=$O(^MAG(2005.872,"B","CT",""))
 I ENTNUM D
 . S PTRVAL=$O(^MAG(2005.85,"B","COMPUTED TOMOGRAPHY",""))
 . S $P(^MAG(2005.872,ENTNUM,1,1,0),U,2)=PTRVAL
 . Q
 ;
 ; Send an installation message.
 D INS(XPDNM,DUZ,$$NOW^XLFDT,XPDA)
 Q
 ;
INS(XP,DUZ,DATE,IDA) ; Compose an installation message in ^TMP.
 N CT,CNT,COM,DDATE,ST
 D GETENV^%ZOSV
 K ^TMP($J,"MAGQ")
 S CNT=0
 S CNT=CNT+1,^TMP($J,"MAGQ",CNT)="PACKAGE INSTALL"
 S CNT=CNT+1,^TMP($J,"MAGQ",CNT)="SITE: "_$$KSP^XUPARAM("WHERE")
 S CNT=CNT+1,^TMP($J,"MAGQ",CNT)="PACKAGE: "_XP
 S CNT=CNT+1,^TMP($J,"MAGQ",CNT)="Version: "_$$VER^XPDUTL(XP)
 S ST=$$GET1^DIQ(9.7,IDA,11,"I")
 S CNT=CNT+1,^TMP($J,"MAGQ",CNT)="Start time: "_$$FMTE^XLFDT(ST)
 S CT=$$GET1^DIQ(9.7,IDA,17,"I") S:+CT'=CT CT=$$NOW^XLFDT
 S CNT=CNT+1,^TMP($J,"MAGQ",CNT)="Completion time: "_$$FMTE^XLFDT(CT)
 S CNT=CNT+1,^TMP($J,"MAGQ",CNT)="Run time: "_$$FMDIFF^XLFDT(CT,ST,3)
 S CNT=CNT+1,^TMP($J,"MAGQ",CNT)="Environment: "_Y
 S COM=$$GET1^DIQ(9.7,IDA,6,"I")
 S CNT=CNT+1,^TMP($J,"MAGQ",CNT)="FILE COMMENT: "_COM
 S CNT=CNT+1,^TMP($J,"MAGQ",CNT)="DATE: "_DATE
 S CNT=CNT+1,^TMP($J,"MAGQ",CNT)="Installed by: "_$$GET1^DIQ(9.7,IDA,9,"E")
 S CNT=CNT+1,^TMP($J,"MAGQ",CNT)="Install Name: "_$$GET1^DIQ(9.7,IDA,.01,"E")
 S DDATE=$$GET1^DIQ(9.7,IDA,51,"I")
 S CNT=CNT+1,^TMP($J,"MAGQ",CNT)="Distribution Date: "_$$FMTE^XLFDT(DDATE)
 S XMSUB=XP_" INSTALLATION"
 D MAILSHR
 K ^TMP($J,"MAGQ")
 Q
MAILSHR ; Shared Mail server code
 ; This is copied from Routine MAGQE2 in Patch 29, because Patch 29
 ; will not necessarily be released ahead of Patch 31.
 N D,D0,D1,D2,DG,DIC,DICR,DIW,XMID,XMY
 S XMID=$G(DUZ) S:'XMID XMID=.5
 S XMY(XMID)=""
 S XMY("G.MAG SERVER")=""
 S:$G(MAGDUZ) XMY(MAGDUZ)=""
 S XMSUB=$E(XMSUB,1,63)
 D SENDMSG^XMXAPI(XMID,XMSUB,"^TMP($J,""MAGQ"")",.XMY,,.XMZ,)
 I $G(XMERR) M XMERR=^TMP("XMERR",$J) S $EC=",U13-Cannot send MailMan message,"
 Q
 ;


