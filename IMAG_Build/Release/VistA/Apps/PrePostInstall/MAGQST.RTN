09:50 AM 08/26/2005 
DICOM Gateway MUMPS routines Build V3.0p51Build720_T22
MAGQST
MAGQST ;WOIFO/DCB,RMP-SERVER TASKS [ 06/20/2001 08:57 ] ; 05/06/2004  06:32
 ;;3.0;IMAGING;**7,29,30**;5-November-2004
 ;; +---------------------------------------------------------------+
 ;; | Property of the US Government.                                |
 ;; | No permission to copy or redistribute this software is given. |
 ;; | Use of unreleased versions of this software requires the user |
 ;; | to execute a written test agreement with the VistA Imaging    |
 ;; | Development Office of the Department of Veterans Affairs,     |
 ;; | telephone (301) 734-0100.                                     |
 ;; |                                                               |
 ;; | The Food and Drug Administration classifies this software as  |
 ;; | a medical device.  As such, it may not be changed in any way. |
 ;; | Modifications to this software may result in an adulterated   |
 ;; | medical device under 21CFR820, the use of which is considered |
 ;; | to be a violation of US Federal Statutes.                     |
 ;; +---------------------------------------------------------------+
 ;;
 Q
 ;
RESTASK D RESTASK^MAGQE4 Q
STTASK D STTASK^MAGQE4 Q
REMTASK D REMTASK^MAGQE4 Q
 ;
CHECK ;Check to see if the task is running
 N REC,TASK,ZTSK
 S REC=$S($T(PLACE^MAGBAPI)'="":$$PLACE^MAGBAPI(DUZ(2)),1:1)
 S TASK=$P($G(^MAG(2006.1,REC,1)),U,7) Q:TASK=""
 S ZTSK=TASK D STAT^%ZTLOAD
 D:ZTSK(0)=0 RESTASK^MAGQE4
 Q
 ;
POST ;
 D REMTASK^MAGQE4,STTASK^MAGQE4
 D INS^MAGQST(XPDNM,DUZ,$$NOW^XLFDT,XPDA)
 D POSTI^MAGQBUT ;
 Q
 ;
ENHS ;Enable Health Summary Component
PRE ; REMOVE SITE 2006.19 ENTRIES SO THAT THEY CAN BE REPLACED
 ; THE "ID" NODE ON FIELD .2 MAKES CHANGING THEM DIFFICULT
 ;We require a DUZ(0)="@" for installer S DUZ(0)="@"
 N INDX
 S INDX=0 F  S INDX=$O(^MAG(2006.19,INDX)) Q:'INDX  D
 . N DA,DIE,DR
 . S DIE=2006.19,DA=INDX,DR=".01///@"
 . D ^DIE
 . Q
 Q
 ;
NUMBER(VAL,SP) ;
 Q $J($FN(VAL,",",0),SP)
 ;
INS(XP,DUZ,DATE,IDA) ;
 N ST,CT,DDATE
 D GETENV^%ZOSV
 K ^TMP($J,"MAGQ")
 S ^TMP($J,"MAGQ",1)="PACKAGE INSTALL"
 S ^TMP($J,"MAGQ",2)="SITE: "_$$KSP^XUPARAM("WHERE")
 S ^TMP($J,"MAGQ",3)="PACKAGE: "_XP
 S ^TMP($J,"MAGQ",4)="Version: "_$$VER^XPDUTL(XP)
 S ST=$$GET1^DIQ(9.7,IDA,11,"I")
 S ^TMP($J,"MAGQ",5)="Start time: "_$$FMTE^XLFDT(ST)
 S CT=$$GET1^DIQ(9.7,IDA,17,"I")
 S ^TMP($J,"MAGQ",6)="Completion time: "_$$FMTE^XLFDT(CT)
 S ^TMP($J,"MAGQ",7)="Run time: "_$$FMDIFF^XLFDT(CT,ST,3)
 S ^TMP($J,"MAGQ",8)="Environment: "_Y
 S ^TMP($J,"MAGQ",9)="DATE: "_DATE
 S ^TMP($J,"MAGQ",10)="Installed by: "_$$GET1^DIQ(9.7,IDA,9,"E")
 S ^TMP($J,"MAGQ",11)="Install Name: "_$$GET1^DIQ(9.7,IDA,.01,"E")
 S DDATE=$$GET1^DIQ(9.7,IDA,51,"I")
 S ^TMP($J,"MAGQ",11)="Distribution Date: "_$$FMTE^XLFDT(DDATE)
 S XMSUB="MAG "_XP_" INSTALLATION"
 D MAILSHR^MAGUSIT
 K ^TMP($J,"MAGQ")
 Q
 ;
TQ ; Test Queue functionality
 N CNT,RESULT
 S CNT=1,RESULT="" K ^TMP("MAGQ",$J)
 F  D GETQUE^MAGQBTM(.RESULT,"JBTOHD") S CNT=CNT+1 Q:CNT>200  D
 . S ^TMP("MAGQ",$J,CNT)=RESULT
 . Q
 Q
 ;
UPPER(X) ;
 Q $TR(X,"abcdefghijklmnopqrstuvwxyz","ABCDEFGHIJKLMNOPQRSTUVWXYZ")
 ;
TRIM(X) ; remove both leading and trailing blanks
 N I,J
 F I=1:1:$L(X) Q:$E(X,I)'=" "
 F J=$L(X):-1:I Q:$E(X,J)'=" "
 Q $S($E(X,I,J)=" ":"",1:$E(X,I,J))
 ;
CHKTSK(RTN) ;
 N ARR,I,BOOL
 S ARR="MAGCT",BOOL=0,I=""
 K @ARR
 D RTN^%ZTLOAD(RTN,ARR)
 I $D(@ARR) D
 . F  S I=$O(@ARR@(I)) Q:I'?1N.N  D  Q:BOOL
 . . S ZTSK=I D ISQED^%ZTLOAD S BOOL=ZTSK(0)
 Q BOOL
 ;


