09:50 AM 08/26/2005 
DICOM Gateway MUMPS routines Build V3.0p51Build720_T22
MAGIPO48
MAGIPO48 ;Pre and Post init routine for Imaging Version 3.0.48
 ;;3.0;IMAGING;**48**;4-Mar-2005
 ;; +---------------------------------------------------------------+
 ;; | Property of the US Government.                                |
 ;; | No permission to copy or redistribute this software is given. |
 ;; | Use of unreleased versions of this software requires the user |
 ;; | to execute a written test agreement with the VistA Imaging    |
 ;; | Development Office of the Department of Veterans Affairs,     |
 ;; | telephone (301) 734-0100.                                     |
 ;; |                                                               |
 ;; | The Food and Drug Administration classifies this software as  |
 ;; | a medical device.  As such, it may not be changed in any way. |
 ;; | Modifications to this software may result in an adulterated   |
 ;; | medical device under 21CFR820, the use of which is considered |
 ;; | to be a violation of US Federal Statutes.                     |
 ;; +---------------------------------------------------------------+
 ;;
 Q
PRE ; Remove partial DD definitions so that new definition can be laid down cleanly
 ;
 ; Image File         Origin Field
 S DIK="^DD(2005,",DA=45,DA(1)=2005 D ^DIK
 ; Image List Filters    Package Field
 S DIK="^DD(2005.87,",DA=1,DA(1)=2005.87 D ^DIK
 ; Imaging Site Parameters File    Purge-Retention Days Full File
 S DIK="^DD(2006.1,",DA=8,DA(1)=2006.1 D ^DIK
 ; Imaging Site Parameters File    Scheduled Purge Time
 S DIK="^DD(2006.1,",DA=64,DA(1)=2006.1 D ^DIK
 ; Imaging Site Parameters File    GCC EXPORT LOC
 S DIK="^DD(2006.1,",DA=.07,DA(1)=2006.1 D ^DIK
 ; Erase Imaging Site Parameters Field 126 to erase the Mixup
 ; KIDS Adds back : 126 LAST ROUTING ACTIVITY
 ;                              130 VERSION CHECKING.
 ;                              200 FUTURE USE
 S DIK="^DD(2006.1,",DA=126,DA(1)=2006.1 D ^DIK
 Q
POST ;
 D INACTIV
 D INDEX
 D UPREF
 D POST2
 Q
INACTIV ;Change STATUS for the OUTSIDE MEDICAL RECORDS Entries 
 ; in IMAGE INDEX FOR TYPES file to  Status = Inactive
 N J
 S J=$O(^MAG(2005.83,"B","OUTSIDE MEDICAL RECORD (DOD)",""))
 I J S $P(^MAG(2005.83,J,0),"^",3)="I"
 S J=$O(^MAG(2005.83,"B","OUTSIDE MEDICAL RECORD (FEE)",""))
 I J S $P(^MAG(2005.83,J,0),"^",3)="I"
 Q
INDEX ; Re-index the Cross references that were killed in Patch 8 Post init
 K ^MAG(2005.2,"E") S DIK="^MAG(2005.2,",DIK(1)="6^E" D ENALL^DIK
 K ^MAG(2006.81,"C") S DIK="^MAG(2006.81,",DIK(1)=".04^C" D ENALL^DIK
 K DIK
 Q
UPREF ;Set default for field ('View Remote Abs') for existing users
 ; the field isn't new, but may have left over value from old DD definition.
 N I S I=0 F  S I=$O(^MAG(2006.18,I)) Q:'I  S $P(^(I,0),"^",8,99)="0^"
 Q
POST2 ;
 N INSTALL,IDATE,ACTIONS,NET
 D FIND^DIC(9.7,"","","MX","MAG*3.0*8","","","","","RESULTS","ERR")
 I '$G(ERR) S INSTALL=$G(RESULTS("DILIST",2,1))
 I $D(INSTALL),INSTALL D
 . S IDATE=$$GET1^DIQ(9.7,INSTALL_",",17,"I")
 . Q:'IDATE
 . Q:'$$GIA(.ACTIONS)
 . Q:'$$NET(.NET)
 . D REQ(IDATE,ACTIONS,NET)
 . Q
 D REMTASK^MAGQE4
 D STTASK^MAGQE4
 D POSTI^MAGQBUT
 D INS(XPDNM,DUZ,$$NOW^XLFDT,XPDA)
 Q
REQ(DATE,ACT,NET) ; Requeue GCCs that where created between now and the Patch 8 install
 ; that where sent to a local rather than a GCC Network Location
 N X1,X2,Q,ISLOC,ISHEC
 S X1=" "
 F  S X1=$O(^MAG(2005,X1),-1) Q:'X1  Q:$P($G(^MAG(2005,X1,2)),U)<DATE  D
 . Q:(ACT'[(U_$P($G(^MAG(2005,X1,40)),U,3)_U))
 . S X2=0,Q=0,ISLOC=0,ISHEC=0
 . F  S X2=$O(^MAG(2005,X1,5,X2)) Q:'X2  D  Q:Q
 . . I NET'[(U_$P($G(^MAG(2005,X1,5,X2,0)),U)_U) S ISLOC=1
 . . E  S ISHEC=1
 . . Q
 . ; Only ReQueue if there ARE Local Entries and ARE NOT any HEC entries.
 . ;  in the EXPORT LOCATION multiple (5 node)
 . I ISLOC,'ISHEC S Q=$$GCC^MAGBAPI(X1,$$PLACE^MAGBAPI(+$G(DUZ(2))))
 . Q
 Q
NET(NET) ; List of GCC Network Locations
 N X1
 S X1=0,NET=U
 F  S X1=$O(^MAG(2005.2,X1)) Q:'X1  D
 . Q:$P($G(^MAG(2005.2,X1,0)),U,7)'="GCC"
 . S NET=NET_X1_U
 . Q
 Q $S($L(NET)>1:1,1:0)
GIA(OUT) ;LIST OF ACTIONS that require exporting
 N X1,X2
 S OUT=U
 S X1=0 F  S X1=$O(^MAG(2005.86,X1)) Q:'X1  D
 . ;Q:'$P($G(^MAG(2005.86,X1,0)),U,2)
 . S X2=0 F  S X2=$O(^MAG(2005.86,X1,2,X2)) Q:'X2  D
 . . S OUT=OUT_$G(^MAG(2005.86,X1,2,X2,0))_U
 . . Q
 . Q
 Q $S($L(OUT)>1:1,1:0)
 ;
INS(XP,DUZ,DATE,IDA) ;
 N CT,CNT,COM,D,D0,D1,D2,DDATE,DG,DIC,DICR,DIW,MAGMSG,ST,XMID,XMY,XMSUB,XMERR
 D GETENV^%ZOSV
 S CNT=0
 S CNT=CNT+1,MAGMSG(CNT)="PACKAGE INSTALL"
 S CNT=CNT+1,MAGMSG(CNT)="SITE: "_$$KSP^XUPARAM("WHERE")
 S CNT=CNT+1,MAGMSG(CNT)="PACKAGE: "_XP
 S CNT=CNT+1,MAGMSG(CNT)="Version: "_$$VER^XPDUTL(XP)
 S ST=$$GET1^DIQ(9.7,IDA,11,"I")
 S CNT=CNT+1,MAGMSG(CNT)="Start time: "_$$FMTE^XLFDT(ST)
 S CT=$$GET1^DIQ(9.7,IDA,17,"I") S:+CT'=CT CT=$$NOW^XLFDT
 S CNT=CNT+1,MAGMSG(CNT)="Completion time: "_$$FMTE^XLFDT(CT)
 S CNT=CNT+1,MAGMSG(CNT)="Run time: "_$$FMDIFF^XLFDT(CT,ST,3)
 S CNT=CNT+1,MAGMSG(CNT)="Environment: "_Y
 S COM=$$GET1^DIQ(9.7,IDA,6,"I")
 S CNT=CNT+1,MAGMSG(CNT)="FILE COMMENT: "_COM
 S CNT=CNT+1,MAGMSG(CNT)="DATE: "_DATE
 S CNT=CNT+1,MAGMSG(CNT)="Installed by: "_$$GET1^DIQ(9.7,IDA,9,"E")
 S CNT=CNT+1,MAGMSG(CNT)="Install Name: "_$$GET1^DIQ(9.7,IDA,.01,"E")
 S DDATE=$$GET1^DIQ(9.7,IDA,51,"I")
 S CNT=CNT+1,MAGMSG(CNT)="Distribution Date: "_$$FMTE^XLFDT(DDATE)
 S XMSUB=XP_" INSTALLATION"
 S XMID=$G(DUZ) S:'XMID XMID=.5
 S XMY(XMID)=""
 S XMY("G.MAG SERVER")=""
 S:$G(MAGDUZ) XMY(MAGDUZ)=""
 S XMSUB=$E(XMSUB,1,63)
 D SENDMSG^XMXAPI(XMID,XMSUB,"MAGMSG",.XMY,,.XMZ,)
 I $G(XMERR) M XMERR=^TMP("XMERR",$J) S $EC=",U13-Cannot send MailMan message,"
 Q


