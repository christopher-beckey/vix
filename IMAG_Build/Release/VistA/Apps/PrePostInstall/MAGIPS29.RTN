12/16/2003  11:11
Saved from DCM,DCE
MAGIPS29
MAGIPS29 ;Post init routine to queue site activity at install. ; 12/16/2003  11:11
 ;;3.0;IMAGING;**29**;Aug 22, 2003
 ;; +---------------------------------------------------------------+
 ;; | Property of the US Government.                                |
 ;; | No permission to copy or redistribute this software is given. |
 ;; | Use of unreleased versions of this software requires the user |
 ;; | to execute a written test agreement with the VistA Imaging    |
 ;; | Development Office of the Department of Veterans Affairs,     |
 ;; | telephone (301) 734-0100.                                     |
 ;; |                                                               |
 ;; | The Food and Drug Administration classifies this software as  |
 ;; | a medical device.  As such, it may not be changed in any way. |
 ;; | Modifications to this software may result in an adulterated   |
 ;; | medical device under 21CFR820, the use of which is considered |
 ;; | to be a violation of US Federal Statutes.                     |
 ;; +---------------------------------------------------------------+
 ;;
 Q
 ; This routine will be removed automatically after the installation
 ; of this patch is complete.
 ;
POST ;
 N FIRST,LAST,TASK
 F TASK="ISU^MAGQE2","ISU^MAGUSIT" D  ; Clean Up Existing TaskMan Entries
 . N DA,DIE,DR,I,MAGTSK,X,ZTDESC,ZTDTH,ZTIO,ZTRTN,ZTSAVE,ZTSK
 . D RTN^%ZTLOAD(TASK,"MAGTSK")
 . ; EdM: Note: The above procedure only returns tasks submitted
 . ;      with the DUZ of the current requestor, unless...:
 . ;      When $D(^XUSEC("ZTMQ",DUZ))>0,
 . ;      tasks with other DUZs are reported as well.
 . S I=0 F  S I=$O(MAGTSK(I)) Q:'I  K ZTSK S ZTSK=I D KILL^%ZTLOAD
 . Q
 ;
 D STTASK^MAGQE4
 ;
 ; For non consolidated sites only:
 ; Index the BP Workstation file,
 ; and the Windows Workstation files
 ;
 D:'$$CONSOLID^MAGQE5()
 . N D0,J,PLACE
 . S PLACE=$O(^MAG(2006.1," "),-1) Q:'PLACE
 . S D0=0 F  S D0=$O(^MAG(2006.8,D0)) Q:'D0  D
 . . S J=$P($G(^MAG(2006.8,D0,0)),"^",2)
 . . S:J'?1.N $P(^MAG(2006.8,D0,0),"^",2)=PLACE,J=PLACE
 . . S ^MAG(2006.8,"D",J,D0)=""
 . . Q
 . S D0=0 F  S D0=$O(^MAG(2006.81,D0)) Q:'D0  D
 . . S J=$P($G(^MAG(2006.81,D0,1)),"^",2)
 . . S:J'?1.N $P(^MAG(2006.81,D0,1),"^",2)=PLACE,J=PLACE
 . . S ^MAG(2006.81,"C",J,D0)=""
 . . Q
 . Q
 ;
 D INS(XPDNM,DUZ,$$NOW^XLFDT,XPDA)
 Q
 ;
UNDEF(NAME) ; The domain is undefined in the Image site file (2006.19)
 N ASK,Y,FN
 S DIR("A")="Enter the type of account"
 S DIR("?")="^D FNA^MAGQE2",DIR("B")="T"
 S DIR(0)="S0^T:Test;P:PRODUCTION"
 D ^DIR S ASK=Y
 I ASK="T" S FN="ZZT"
 I ASK="P" D
 . S FN=$$ONE^MAGQAI(NAME)
 . D  ; Mail message to LAVC-server/image developers
 . . N LOC,XMSUB,Y
 . . D NOW^%DTC S Y=% D DD^%DT
 . . S LOC=$$KSP^XUPARAM("WHERE")
 . . S ^TMP($J,"MAGQ",1)="            SITE: "_LOC
 . . S ^TMP($J,"MAGQ",2)="            DATE: "_Y_" "_$G(^XMB("TIMEZONE"))
 . . S ^TMP($J,"MAGQ",3)="          DOMAIN: "_NAME
 . . S ^TMP($J,"MAGQ",4)="        INITIALS: "_INIT
 . . ; Send the report
 . . S XMSUB="Update Site file from "_LOC
 . . D MAILSHR^MAGQE2
 . . K ^TMP($J,"MAGQ")
 . . Q
 . Q
 Q FN
 ;
FNA ; For setting domain initials in site parameter file
 W !!?10,"For the purpose of establishing a unique file"
 W !?10,"namespace or prefix. This will make it possible"
 W !?10,"to determine the origin of files which are copied"
 W !?10,"from one site to another."
 Q
 ;
INS(XP,DUZ,DATE,IDA) ;
 N CT,CNT,COM,D,D0,D1,D2,DDATE,DG,DIC,DICR,DIW,MAGMSG,ST,XMID,XMY
 D GETENV^%ZOSV
 S CNT=0
 S CNT=CNT+1,MAGMSG(CNT)="PACKAGE INSTALL"
 S CNT=CNT+1,MAGMSG(CNT)="SITE: "_$$KSP^XUPARAM("WHERE")
 S CNT=CNT+1,MAGMSG(CNT)="PACKAGE: "_XP
 S CNT=CNT+1,MAGMSG(CNT)="Version: "_$$VER^XPDUTL(XP)
 S ST=$$GET1^DIQ(9.7,IDA,11,"I")
 S CNT=CNT+1,MAGMSG(CNT)="Start time: "_$$FMTE^XLFDT(ST)
 S CT=$$GET1^DIQ(9.7,IDA,17,"I") S:+CT'=CT CT=$$NOW^XLFDT
 S CNT=CNT+1,MAGMSG(CNT)="Completion time: "_$$FMTE^XLFDT(CT)
 S CNT=CNT+1,MAGMSG(CNT)="Run time: "_$$FMDIFF^XLFDT(CT,ST,3)
 S CNT=CNT+1,MAGMSG(CNT)="Environment: "_Y
 S COM=$$GET1^DIQ(9.7,IDA,6,"I")
 S CNT=CNT+1,MAGMSG(CNT)="FILE COMMENT: "_COM
 S CNT=CNT+1,MAGMSG(CNT)="DATE: "_DATE
 S CNT=CNT+1,MAGMSG(CNT)="Installed by: "_$$GET1^DIQ(9.7,IDA,9,"E")
 S CNT=CNT+1,MAGMSG(CNT)="Install Name: "_$$GET1^DIQ(9.7,IDA,.01,"E")
 S DDATE=$$GET1^DIQ(9.7,IDA,51,"I")
 S CNT=CNT+1,MAGMSG(CNT)="Distribution Date: "_$$FMTE^XLFDT(DDATE)
 S XMSUB=XP_" INSTALLATION"
 S XMID=$G(DUZ) S:'XMID XMID=.5
 S XMY(XMID)=""
 S XMY("G.MAG SERVER")=""
 S:$G(MAGDUZ) XMY(MAGDUZ)=""
 S XMSUB=$E(XMSUB,1,63)
 D SENDMSG^XMXAPI(XMID,XMSUB,"MAGMSG",.XMY,,.XMZ,)
 I $G(XMERR) M XMERR=^TMP("XMERR",$J) S $EC=",U13-Cannot send MailMan message,"
 Q
 ;


