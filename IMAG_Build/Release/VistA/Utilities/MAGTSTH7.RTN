10/12/2004  10:39
Saved from DCM,DCE
MAGTSTH7
MAGTSTH7 ; ... ; 10/12/2004  10:39
 Q
 ;
BEFORE N D0
 K ^TMP("TEST","HL7")
 S D0=$O(^MAGDHL7(2006.5," "),-1)
 S:D0 ^TMP("TEST","HL7")=D0
 Q
 ;
AFTER N D0,D1,EDIT,ERR,F1,F2,I,LOC,OK,P,S1,S2,S3,S4,S5,X
 S D0=$G(^TMP("TEST","HL7")) I D0<1 D  Q
 . W !!,"First >D BEFORE^"_$T(+0)
 . W !,"Then order a CHEST 2 VIEWS PA&LAT for patient CROSS,KRISS."
 . W !,"Then call this subroutine.",!
 . Q
 S OK=0
 F  S D0=$O(^MAGDHL7(2006.5,D0)) Q:'D0  D
 . K LOC
 . S (ERR,F1,F2)=0,D1=0 F  S D1=$O(^MAGDHL7(2006.5,D0,1,D1)) Q:'D1  D
 . . S X=$G(^MAGDHL7(2006.5,D0,1,D1,0))
 . . D:$E(X,1,3)="MSH"  Q:ERR
 . . . S S1=$E(X,4),S2=$E(X,5),S3=$E(X,6),S4=$E(X,7),S5=$E(X,8)
 . . . S X=$E(X,1,3)_$E(X,9,$L(X))
 . . . I $E(X,4)'=S1,$E(X,1,3)'?3U W !,"hdr: ",X S ERR=ERR+1 Q
 . . . I S5'="&" W !,"sub: ",X S ERR=ERR+1 Q
 . . . Q
 . . S EDIT=0 F I=2:1:$L(X,S1) D
 . . . S P=$P(X,S1,I)
 . . . S:P="CROSS~KRISS" P="BLACK&WHITE~KRISS",(F1,EDIT)=1
 . . . S:P["CHEST 2 VIEWS PA&LAT" F2=1
 . . . F  Q:P'[S5  D
 . . . . S P=$P(P,S5,1)_S4_"T"_S4_$P(P,S5,2,$L(P)+2),EDIT=1
 . . . . Q
 . . . S $P(X,S1,I)=P
 . . . Q
 . . S:EDIT LOC(D1)=X
 . . Q
 . I F1,F2 D
 . . S OK=OK+1,EDIT(D0)=""
 . . S D1="" F  S D1=$O(LOC(D1)) Q:D1=""  S ^MAGDHL7(2006.5,D0,1,D1,0)=LOC(D1)
 . . Q
 . Q
 I 'OK D  Q
 . W !!,"Couldn't find CHEST 2 VIEWS PA&LAT order for patient CROSS,KRISS.",!
 . Q
 K ^TMP("TEST","HL7")
 W !,"Modified ",OK," HL7 message" W:OK>1 "s" W "."
 S D0="" F  S D0=$O(EDIT(D0)) Q:D0=""  W !?5,"Message # ",D0
 W !
 Q
 ;


