09/22/2000  13:54
Saved from DCM,DCE
MAGDFEDT
MAGDFEDT ;WOIFO/PMK - Text file editor ; [ 09/22/2000  13:54 ]
 ;;2.5T;DICOM42;;1-Nov-2000
 ;; +---------------------------------------------------------------+
 ;; | Property of the US Government.                                |
 ;; | No permission to copy or redistribute this software is given. |
 ;; | Use of unreleased versions of this software requires the user |
 ;; | to execute a written test agreement with the VistA Imaging    |
 ;; | Development Office of the Department of Veterans Affairs,     |
 ;; | telephone (301) 734-0100.                                     |
 ;; |                                                               |
 ;; | The Food and Drug Administration classifies this software as  |
 ;; | a Class II medical device.  As such, it may not be changed    |
 ;; | in any way.  Modifications to this software may result in an  |
 ;; | adulterated medical device under 21CFR820, the use of which   |
 ;; | is considered to be a violation of US Federal Statutes.       |
 ;; +---------------------------------------------------------------+
 ;;
 ; This routine uses the MUMPS routine editor to edit a small text file.
 ; Step 1: the text file is copied to a temporary MUMPS routine
 ; Step 2: the ^%E editor is invoked to edit the text file routine
 ; Step 3: the temporary MUMPS routine is copied back the the text file
 ; Step 4: the temporary MUMPS routine is erased
 ;
ENTRY ; entry point from programmer mode to edit a text file
 R !!,"Enter the name (full path) of the file to edit: ",FILENAME:$G(DTIME,300)
 I FILENAME="" Q
 S X=$$GETDIR^MAGOSDIR(FILENAME)
 I 'X W "  (not on file)" G ENTRY  ; not on file
 I X<0 W " *** FILE ERROR: ",X," ***" Q  ; error
 ;
 ; copy the text file to the temporary PGMNAME MUMPS routine
 S PGMNAME="zzzzzzzz" ; scratch routine for containing text file
 S STATUS=$$OPEN^MAGOSFIL(FILENAME,"R")
 I STATUS<0 D
 . N M
 . S M="Could not open file """_FILENAME_""" for Read."
 . W:'$$USEDEV^MAGOSFIL($P) !!!,M,!,STATUS
 . S $EC=",U"_M_","
 . Q  ; Error trap does not return
 S STATUS=$$USE^MAGOSFIL(FILENAME)
 X $$ZSAVE
 S STATUS=$$CLOSE^MAGOSFIL(FILENAME)
 S STATUS=$$USEDEV^MAGOSFIL($P)
 R !!,"Press <Enter> to edit the text file...",X:$G(DTIME,300)
 ;
 ; invoke the ^%E editor to modify the temporary PGMNAME MUMPS routine
 X "ZL "_PGMNAME_" X ^%E"
 ;
 ; copy the contexts of the PGMNAME routine back to the text file
 R !!,"Store the updated text file?  n//",X:$G(DTIME,300)
 I X="" S X="NO" W X
 I "Yy"'[$E(X) W "  -- file not updated",! Q
 S STATUS=$$OPEN^MAGOSFIL(FILENAME,"W")
 I STATUS<0 D
 . N M
 . S M="Could not open file """_FILENAME_""" for Write."
 . W:'$$USEDEV^MAGOSFIL($P) !!!,M,!,STATUS
 . S $EC=",U"_M_","
 . Q  ; Error trap does not return
 S STATUS=$$USE^MAGOSFIL(FILENAME)
 X $$ZLOAD
 S STATUS=$$CLOSE^MAGOSFIL(FILENAME)
 S STATUS=$$USEDEV^MAGOSFIL($P)
 ;
 ; remove the temporary PGMNAME MUMPS routine
 X "ZR  ZS "_PGMNAME
 W " -- file updated",!
 Q
ZLOAD() ; code for MSM ZLOAD
 Q "N I,T ZR  ZL @PGMNAME F I=2:1 S T=$T(+I) Q:T=""""  W $E(T,4,999),!"
 ;
ZSAVE() ; code for MSM ZSAVE
 Q "N T ZR  ZI PGMNAME_$C(9,59)  F  R T S T=$C(9,59,59)_T ZI:'$ZC T I $ZC ZS @PGMNAME QUIT"
 ;


