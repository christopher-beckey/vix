MAGGTU6 ;WOIFO/GEK - Silent Utilities ; [ 06/20/2001 08:57 ]
 ;;3.0;IMAGING;**24**;September 5, 2003
 ;; +---------------------------------------------------------------+
 ;; | Property of the US Government.                                |
 ;; | No permission to copy or redistribute this software is given. |
 ;; | Use of unreleased versions of this software requires the user |
 ;; | to execute a written test agreement with the VistA Imaging    |
 ;; | Development Office of the Department of Veterans Affairs,     |
 ;; | telephone (301) 734-0100.                                     |
 ;; |                                                               |
 ;; | The Food and Drug Administration classifies this software as  |
 ;; | a medical device.  As such, it may not be changed in any way. |
 ;; | Modifications to this software may result in an adulterated   |
 ;; | medical device under 21CFR820, the use of which is considered |
 ;; | to be a violation of US Federal Statutes.                     |
 ;; +---------------------------------------------------------------+
 ;;
 Q
 ;
LOGACT(MAGRY,ZY) ;RPC Call to LogAction from Delphi Window 
 ; From delphi this is called when user copies image from Display
 ; application and enters a reason ,and electronic signature.
 ;
 ; ZY is input variable it is '^' delimited string
 ; 'A|B|C|D|E' ^^ MAGIEN ^ 'Copy/Download' ^ DFN ^ '1';
 ;  DUZ is inserted as 2nd piece below.
 N Y
 S MAGRY="0^Logging access"
 ;
 IF $$NEWERR^%ZTER N $ETRAP,$ESTACK S $ETRAP="D ERR^MAGGTERR"
 E  S X="ERR^MAGGTERR",@^%ZOSF("TRAP")
 D ENTRY^MAGLOG($P(ZY,U),+$G(DUZ),$P(ZY,U,3),$P(ZY,U,4),$P(ZY,U,5),$P(ZY,U,6))
 S MAGRY="1^Action was Logged."
 Q
TIMEOUT(MAGRY,APP) ;RPC Call  Returns the timeout for the APP
 ;  from IMAGING SITE PARAMETERS File
 ;  APP is either 'DISPLAY'  'CAPTURE' or   'VISTARAD'
 N I,MAGTIMES
 S MAGRY=""
 S I=$O(^MAG(2006.1,0)) I 'I Q
 S MAGTIMES=$G(^MAG(2006.1,I,"KEYS"))
 I APP="DISPLAY" S MAGRY=$P(MAGTIMES,U,2)
 I APP="CAPTURE" S MAGRY=$P(MAGTIMES,U,3)
 I APP="VISTARAD" S MAGRY=$P(MAGTIMES,U,4)
 Q
EXIST() ;Does an ekg server exist in 2005.2
 Q $O(^MAG(2005.2,"E","EKG",""))
 ;
ONLINE(MAGR) ;ekg network location status    
 ;returns the status of the first EKG network location type
 ;O if offline or a network location doesn't exist
 ;1 if online
 ;
 N EKG1
 I $$EXIST D
 . S EKG1=$O(^MAG(2005.2,"E","EKG",""))
 . S MAGR=$P(^MAG(2005.2,EKG1,0),U,6)
 E  S MAGR=0
 Q
SHARE(MAGRY,TYPE) ;Get list of image shares
 ;TYPE = MAG, EKG, WORM, or ALL
 N TMP,INDX,DATA0,DATA2,DATA6,CNT,SHARE,VALUE
 I $$NEWERR^%ZTER N $ETRAP,$ESTACK S $ETRAP="D ERRA^MAGGTERR"
 E  S X="ERRA^MAGGTERR",@^%ZOSF("TRAP")
 S CNT=1,INDX=0
 I TYPE="" S TYPE="ALL"
 S MAGRY(0)="1^SUCCESS"
 F  S INDX=$O(^MAG(2005.2,INDX)) Q:'INDX  D
 . S DATA0=$G(^MAG(2005.2,INDX,0))
 . S DATA2=$G(^MAG(2005.2,INDX,2))
 . S DATA3=$G(^MAG(2005.2,INDX,3))
 . S DATA6=$G(^MAG(2005.2,INDX,6))
 . S DATA7=$G(^MAG(2005.2,INDX,7))
 . I TYPE'="ALL" Q:$P(DATA0,"^",7)'[TYPE
 . Q:$P(DATA0,"^",6)=0     ;SHARE IS OFFLINE (don't return offline shares)
 . Q:$P(DATA0,"^",2)[":"   ;Don't return local drives (i.e. c:\)
 . Q:$P(DATA0,"^",2)["."
 . S SHARE=$P(DATA0,"^",2)
 . Q:$E(SHARE,1,2)'="\\"
 . I $E(SHARE,$L(SHARE))="\" S SHARE=$E(SHARE,1,$L(SHARE)-1)
 . S $P(SHARE,"^",2)=$P($G(DATA0),"^",7) ;Physical reference (path)
 . S $P(SHARE,"^",3)=$P($G(DATA0),"^",6)  ;Operational Status 0=OFFLINE 1=ONLINE
 . S $P(SHARE,"^",4)=$P($G(DATA2),"^",1) ;Username
 . S $P(SHARE,"^",5)=$P($G(DATA2),"^",2) ;Password
 . S $P(SHARE,"^",6)=$P($G(DATA6),"^",1)  ;MUSE Site #
 . I $P($G(DATA6),"^",2)'="" S $P(SHARE,"^",7)=^MAG(2006.17,$P(DATA6,"^",2),0)  ;MUSE version #
 . S $P(SHARE,"^",8)=$P($G(DATA3),"^",5)  ;Network location SITE
 . S $P(SHARE,"^",9)=$P($G(DATA7),"^",1)  ;MUSE URL
 . Q:$D(TMP(SHARE))
 . S TMP(SHARE)=INDX
 S INDX=""
 F  S INDX=$O(TMP(INDX)) Q:INDX=""  D
 . S MAGRY(CNT)=TMP(INDX)_"^"_INDX,CNT=CNT+1
 K TMP
 Q
