08/24/2001 18:33
Saved by magvss from DVA
MAGDICHK
MAGDICHK ;WOIFO/EdM - Integrity Checker ; [ 08/24/2001 18:00 ]
 ;;2.5T;IMAGING*42*;**3**;1-Jun-2001
 ;; +---------------------------------------------------------------+
 ;; | Property of the US Government.                                |
 ;; | No permission to copy or redistribute this software is given. |
 ;; | Use of unreleased versions of this software requires the user |
 ;; | to execute a written test agreement with the VistA Imaging    |
 ;; | Development Office of the Department of Veterans Affairs,     |
 ;; | telephone (301) 734-0100.                                     |
 ;; |                                                               |
 ;; | The Food and Drug Administration classifies this software as  |
 ;; | a medical device.  As such, it may not be changed in any way. |
 ;; | Modifications to this software may result in an adulterated   |
 ;; | medical device under 21CFR820, the use of which is considered |
 ;; | to be a violation of US Federal Statutes.                     |
 ;; +---------------------------------------------------------------+
 ;;
 N D0,G0,IDFN,PF,P0,PD0,PD1,PDFN,PT,T,X0,X2,Z
 K ^TMP("MAG",$J,"IC")
 S PT(3.9)="^XMB(3.9,PD0|Mail message||2|^XMB(3.9,PD0,2005,"
 S PT(63)="^LR(PD0,GF,PD1|Autopsy (microscopic)|AY|1|^LR(PD0,GF,PD1,2005,"
 S PT(63.02)="^LR(PD0,GF,PD1|Electron microscopy|EM|1|^LR(PD0,GF,PD1,2005,"
 S PT(63.08)="^LR(PD0,GF,PD1|Surgical pathology|SP|1|^LR(PD0,GF,PD1,2005,"
 S PT(63.09)="^LR(PD0,GF,PD1|Cytology|CY|1|^LR(PD0,GF,PD1,2005,"
 S PT(63.2)="^LR(PD0,GF,PD1|Autopsy (gross)|AU|1|^LR(PD0,GF,PD1,2005,"
 S PT(74)="^RARPT(PD0|Radiology||2|^RARPT(PD0,2005,"
 S PT(130)="^SRF(PD0|Surgery||1|^SRF(PD0,2005,"
 S PT(691)="^MCAR(691,PD0|Echocardiogram||2|^MCAR(691,PD0,2005,"
 S PT(691.1)="^MCAR(691.1,PD0|Cardiac catheterization||2|^MCAR(691.1,PD0,2005,"
 S PT(691.5)="^MCAR(691.5,PD0|Electrocardiography||2|^MCAR(691.5,PD0,2005,"
 S PT(694)="^MCAR(694,PD0|Hematology||2|^MCAR(694,PD0,2005,"
 S PT(699)="^MCAR(699,PD0|Endoscopy||2|^MCAR(699,PD0,2005,"
 S PT(699.5)="^MCAR(699.5,PD0|Medicine||2|^MCAR(699.5,PD0,2005,"
 S PT(8925)="^TIU(8925,PD0|TIU||2|^TIU(8925.91,""ADI"",PD0,"
 ;
 ; Image File
 S D0=" " F  S D0=$O(^MAG(2005,D0),-1) Q:'D0  D
 . S Z="Image "_D0_", "
 . I $D(^MAG(2005.1,D0)) D LOG(Z_"Deleted copy exists as well as active one")
 . S X0=$G(^MAG(2005,D0,0))
 . S X2=$G(^MAG(2005,D0,2))
 . S IDFN=$P(X0,"^",7)
 . I 'IDFN D LOG(Z_"No patient") Q
 . S PF=$P(X2,"^",6),PD0=$P(X2,"^",7),PD1=$P(X2,"^",10)
 . I 'PF D LOG(Z_"No parent file") Q
 . I 'PD0 D LOG(Z_"No entry in file "_PF) Q
 . I PF\1=63,'PD1 D LOG(Z_"no sub-entry for ^LR("_PD0) Q
 . D  ; Check parent data
 . . I '$D(PT(PF)) D LOG(Z_"Unknown parent file: "_PF) Q
 . . N GF,GP,GR,GR0,GRD,GRG,GT
 . . S GR=$P(PT(PF),"|",1),GR0=GR_",0)"
 . . S T=$P(PT(PF),"|",5),GRD=T_"G0,0)",GRG=T_"G0)"
 . . S GT=$P(PT(PF),"|",2),GF=$P(PT(PF),"|",3),GP=$P(PT(PF),"|",4)
 . . S P0=$G(@GR0,"^not defined"),PDFN=$P(P0,"^",GP)
 . . I PF\1=63 S PDFN=PD0
 . . I 'PDFN,$P($G(^VA(200,IDFN,0)),"^",1)=PDFN S PDFN=IDFN
 . . I IDFN'=PDFN D LOG(Z_"Patient in image: "_IDFN_", in "_GT_": "_PDFN) Q
 . . S G0=0 F  S G0=$O(@GRG) Q:'G0  D
 . . . D SAME(+$P($G(@GRD),"^",1),Z_GT_" group member ")
 . . . Q
 . . Q
 . S P0=$P(X0,"^",10)
 . I P0=D0 D
 . . D LOG(Z_"has itself for image group parent")
 . . S P0=""
 . . Q
 . F PT=D0,P0 D:PT
 . . S G0=0 F  S G0=$O(^MAG(2005,PT,1,G0)) Q:'G0  D
 . . . I PT=P0 D
 . . . . D LOG(Z_"has an image group, as well as a group parent pointer: "_P0)
 . . . . S P0=""
 . . . . Q
 . . . D SAME(+$P($G(^MAG(2005,PT,1,G0,0)),"^",1),Z_"Image group member ")
 . . . Q
 . . Q
 . Q
 ; Still to do:
 ;  * is there a way to check that all images in a group
 ;    indeed belong to the same study?
 ;  * Locate "text" file -- report if doesn't exist
 ;    Check that patient in database matches patient in text file
 ;    Check that study in database matches study in text file
 ;
 ; 2. Mail message (FileMan table #3.9)
 ;    Radiology Report (FileMan table #74)
 ;    Surgery (FileMan table #130)
 ;    Echocardiogram (FileMan table #691)
 ;    Cardiac catheterization (FileMan table #691.1)
 ;    Electrocardiography (FileMan table #691.5)
 ;    Hematology (FileMan table #694)
 ;    Endoscopy (FileMan table #699)
 ;    Generic Medicine (FileMan table #699.5)
 ;    Text Integration Utilities (TIU) (FileMan table #8925)
 S PF="" F  S PF=$O(PT(PF)) Q:PT=""  D:PF\1'=63
 . N GF,GL,GP,GR,GR0,GRD,GRG,GT
 . S GR=$P(PT(PF),"|",1),GL=GR_")",GR0=GR_",0)"
 . S T=$P(PT(PF),"|",5),GRD=T_"G0,0)",GRG=T_"G0)"
 . S GT=$P(PT(PF),"|",2),GF=$P(PT(PF),"|",3),GP=$P(PT(PF),"|",4)
 . S PD0=0 F  S PD0=$O(@GL) Q:'PD0  D
 . . S P0=$G(@GR0,"^not defined"),PDFN=$P(P0,"^",GP)
 . . I 'PDFN,$P($G(^VA(200,IDFN,0)),"^",1)=PDFN S PDFN=IDFN
 . . S Z=GT_" "_PD0_", "
 . . S G0=0 F  S G0=$O(@GTG) Q:'G0  D
 . . . S D0=+$P($G(@GTD),"^",1)
 . . . I 'D0 D LOG(Z_"invalid image pointer "_G0_" = "_D0) Q
 . . . I $D(^MAG(2005.1,D0)) D LOG(Z_"pointer to deleted image "_D0) Q
 . . . I '$D(^MAG(2005,D0)) D LOG(Z_"pointer to non-existent image "_D0) Q
 . . . S IDFN=$P($G(^MAG(2005,D0,0)),"^",7)
 . . . I PDFN'=IDFN D LOG(Z_"patient different in image: "_PDFN_" vs "_IDFN)
 . . . S X2=$G(^MAG(2005,D0,2))
 . . . S T=$P(X2,"^",6) I T'=PF D LOG(Z_"image has parent file type "_T)
 . . . S T=$P(X2,"^",7) I T'=PD0 D LOG(Z_"image has parent file D0 "_T)
 . . . Q
 . . Q
 . Q
 ;
 ; 3. Lab Result (FileMan table #63)
 S PD0=0 F  S PD0=$O(^LR(PD0)) Q:'PD0  D
 . K IDFN,PDFN
 . S PDFN=$P($G(^LR(PD0,0)),"^",1)
 . F PF=63,63.02,63.08,63.09,63.2 D
 . . N GT
 . . S GT=$P(PT(PF),"|",2)
 . . S PD1=0 F  S PD1=$O(^LR(PD0,GT,PD1)) Q:'PD1  D
 . . . S Z="Lab Result "_PD0_","_GT_","_PD1_", "
 . . . S G0=0 F  S G0=$O(^LR(PD0,GT,PD1,2005,G0)) Q:'G0  D
 . . . . S D0=+$P($G(^LR(PD0,2005,G0,0)),"^",1)
 . . . . I 'D0 D LOG(Z_"invalid image pointer "_G0_" = "_D0) Q
 . . . . I $D(^MAG(2005.1,D0)) D LOG(Z_"pointer to deleted image "_D0) Q
 . . . . I '$D(^MAG(2005,D0)) D LOG(Z_"pointer to non-existent image "_D0) Q
 . . . . S IDFN=$P($G(^MAG(2005,D0,0)),"^",7)
 . . . . I PDFN'=IDFN D LOG(Z_"patient different in image "_D0_": "_PDFN_" vs "_IDFN)
 . . . . S X2=$G(^MAG(2005,D0,2))
 . . . . S T=$P(X2,"^",6) I T'=PF D LOG(Z_"image "_D0_" has parent file type "_T)
 . . . . S T=$P(X2,"^",7) I T'=PD0 D LOG(Z_"image "_D0_" has parent file D0 "_T)
 . . . . S T=$P(X2,"^",10) I T'=PD1 D LOG(Z_"image "_D0_" has parent sub file D1 "_T)
 . . . . Q
 . . . Q
 . . Q
 . Q
 Q
 ;
LOG(X) S ^TMP("MAG",$J,"IC",$O(^TMP("MAG",$J,"IC"," "),-1)+1)=X
 Q
 ;
SAME(G0,Z) N T,X0,X1
 S X0=$G(^MAG(2005,G0,0))
 S X2=$G(^MAG(2005,G0,2))
 S T=$P(X0,"^",7) I T'=IDFN D
 . D LOG(Z_G0_": patient "_IDFN_" vs "_T)
 . Q
 S T=$P(X2,"^",6) I T'=PF D
 . D LOG(Z_G0_": parent file "_PF_" vs "_T)
 . Q
 S T=$P(X2,"^",7) I T'=PD0 D
 . D LOG(Z_G0_": parent file entry "_PD0_" vs "_T)
 . Q
 S T=$P(X2,"^",10) I T'=PD1 D
 . D LOG(Z_G0_": parent file sub-entry "_PD1_" vs "_T)
 . Q
 Q
 ;
