MAGDMFB1 ;WOIFO/PMK/JSL/SAF/DAC - Set some ^MAGDICOM(*) static fields ; 12 Jun 2012 4:12 PM
 ;;3.0;IMAGING;**9,21,11,30,51,69,54,53,123,118**;Mar 19, 2002;Build 4525;May 01, 2013
 ;; Per VHA Directive 2004-038, this routine should not be modified.
 ;; +---------------------------------------------------------------+
 ;; | Property of the US Government.                                |
 ;; | No permission to copy or redistribute this software is given. |
 ;; | Use of unreleased versions of this software requires the user |
 ;; | to execute a written test agreement with the VistA Imaging    |
 ;; | Development Office of the Department of Veterans Affairs,     |
 ;; | telephone (301) 734-0100.                                     |
 ;; | The Food and Drug Administration classifies this software as  |
 ;; | a medical device.  As such, it may not be changed in any way. |
 ;; | Modifications to this software may result in an adulterated   |
 ;; | medical device under 21CFR820, the use of which is considered |
 ;; | to be a violation of US Federal Statutes.                     |
 ;; +---------------------------------------------------------------+
 ;;
 ; initialize the ^MAGDICOM(*) fields
 N BGACC,BGCHK,BGVER,CHANNEL,CMOVEAET,COMMPACS,CONSOLID,DELAYCMV,DICTDIR,DOMAIN
 N EVALUATE,EXAMCOMP,FREESPAC,HISIPADR,HISPORT,I,IMAGEDIR,IMAGGATE,IMPORTER
 N LOCATION,M2MOK,MACHID,MAILGRP,MODCPT,MODWLST,OK,PORTCSTO,POSTPORT,PORTWLST
 N POSTOFF,QUIT,ROUTER,SENDPACS,SHOWPID,SSNDASH,STEP,SYSTITLE,TEXTDIR
 N TEXTGATE,UIDROOT,VALLOC,WORKLIST,X
 ;
 S M2MOK=1,(DOMAIN,UIDROOT)=""
 D
 . I '$$ISCONT^MAGM2VCU() D  Q:'M2MOK
 . . S X=$$RECONCT^MAGDMENA() Q:X
 . . W !!,"Cannot connect to VistA at this time - update capabilities limited.",!
 . . W !,$P(X,",",2,$L(X)+2)
 . . S M2MOK=0
 . . Q
 . I $$CALLRPC^MAGM2VCU("MAG DICOM VALID LOCATIONS","M",.VALLOC)<0 S M2MOK=0 Q
 . I $$CALLRPC^MAGM2VCU("MAG DICOM GET DOMAIN","M",.DOMAIN)<0 S M2MOK=0 Q
 . I $$CALLRPC^MAGM2VCU("MAG DICOM GET UID ROOT","M",.UIDROOT)<0 S M2MOK=0 Q
 . Q
 ;
 D BEFORE^MAGDMFBB
 ;
 S STEP=1 K QUIT F  D  Q:STEP<1  Q:STEP>33
 . D @("Q"_STEP) I $G(QUIT) S STEP=0 Q
 . I $D(QUIT) S STEP=STEP-1 Q
 . S STEP=STEP+1
 . Q
 I 'STEP W !!,"No parameters changed..." Q
 ;
 D AFTER^MAGDMFBB
 ;
 ; Send DGW Config Info to VistA
 ;
 D SETGWCFG^MAGVDGW1
 ;
 Q
 ;
YN(X) S X=$E(X_"?",1) Q $S("Yy"[X:"YES","Nn"[X:"NO",1:"")
 ;
Q1 K QUIT S OK=0 F  D  Q:OK  Q:$D(QUIT)
 . W !!,"Please enter the system title: ",SYSTITLE W:SYSTITLE'="" !?30
 . W " " R X:DTIME E  S X="^"
 . I X="^" S QUIT=0 Q
 . I X="^^" S QUIT=1 Q
 . I X="" S X=SYSTITLE W SYSTITLE
 . I "?"'[X,X'["^",X'["|" S SYSTITLE=X,OK=1 Q
 . W !!,"The system title is a short character string that appears"
 . W !,"on the top of the main DICOM application menu."
 . W !,"The system title may not contain caret (^) or vertical bar (|)."
 . W !,"Examples:"
 . W !?10,"""DICOM Image Server System #3"""
 . W !?10,"""DICOM Text Gateway and Background Processor"""
 . Q
 Q
 ;
Q2 ; The location of the DICOM gateway must be in the site parameter file
 N N,I,LNAM,LOCNAME,LSTA,X
 I 'M2MOK D  Q
 . W !!,"Not currently connected to VistA,"
 . W !,"Cannot set up Location of Gateway."
 . Q
 S N=0,(LNAM,LSTA,I)="" F  S I=$O(VALLOC(I)) Q:I=""  D
 . S X=VALLOC(I) Q:X'["^"  Q:'X
 . S N=N+1,N(N)=X
 . S:+X=LOCATION LNAM=$P(X,"^",2),LSTA=$P(X,"^",3)
 . Q
 I N<1 D  Q
 . W !!,"No valid locations defined for this DICOM Gateway."
 . W !,"Please first set up the Imaging Site Parameters on VistA."
 . Q
 ; the value of ^MAG(2006.563,"LOCATION") is a pointer to ^DIC(4)
 I N=1 D  Q
 . S LOCATION=+N(1),LOCNAME=$P(N(1),"^",2)
 . W !!,"This Gateway is located at ",LOCNAME," (#",LOCATION,")"
 . Q
 K QUIT
 I LOCATION'="" S OK=0 F  D  Q:OK  Q:$D(QUIT)
 . W !!,"This computer is currently located at "
 . W LNAM," (",LSTA,")"
 . W !,"Are you sure you wish to change it?  NO// " R X:DTIME E  S X="^"
 . I X="^" S QUIT=0 Q
 . I X="^^" S QUIT=1 Q
 . I X="" S X="No" W X
 . S X=$$YN(X) I X'="" S OK=1 Q
 . W !!,"Please enter ""Yes"" to change the location,"
 . W !,"or ""No"" to leave it as it is."
 . Q
 I $D(QUIT)!(X="NO") Q
 N H,PROMPT,X
 S H(1)="VistA DICOM Gateways may be located in different institutions"
 S H(2)="that belong to one consolidated healthcare network."
 S H(3)="Enter the name of the institution where this computer is located."
 S PROMPT="At which institution is this computer located?"
 S N=$$LOCATION^MAGDMFB(PROMPT,.H,LOCATION)
 I N=-1 S QUIT=1 Q
 I N=0 S QUIT=0 Q
 I LOCATION=N W " (no change)" Q
 S I="" F  S I=$O(VALLOC(I)) Q:I=""  D
 . S X=VALLOC(I) Q:X'["^"  Q:'X  Q:+X'=N
 . W " -- changed to ",$P(X,"^",2)," (",$P(X,"^",3),")"
 . S LOCATION=N
 . Q
 Q
 ;
Q3 K QUIT S OK=0 F  D  Q:OK  Q:$D(QUIT)
 . W !!,"Please identify the disk-share for",!,"the DICOM text directory: "
 . W TEXTDIR,"// " R X:DTIME E  S X="^"
 . I X="^" S QUIT=0 Q
 . I X="^^" S QUIT=1 Q
 . S:X="" X=TEXTDIR
 . S:$TR($E(X,1),"AaBb","????")["?" X="?"
 . S DISP=$$LET2UNC^MAGOSMSC(X) W:DISP'=X "   ",DISP
 . I X'["?",DISP'="" S TEXTDIR=DISP,OK=1 Q
 . W !!,"The DICOM text directory is usually on the local system,"
 . W !,"and is used to hold the DICOM text files."
 . W !,"C:\DICOM (or D:\DICOM) is typically the DICOM text directory."
 . W !,"Any other device letter may be entered, as well as a"
 . W !,"fully specified UNC name."
 . Q
 Q
 ;
Q4 K QUIT S OK=0 F  D  Q:OK  Q:$D(QUIT)
 . N DISP
 . W !!,"Please identify the disk-share for",!,"the DICOM image directories: "
 . W IMAGEDIR,"// " R X:DTIME E  S X="^"
 . I X="^" S QUIT=0 Q
 . I X="^^" S QUIT=1 Q
 . S:X="" X=IMAGEDIR
 . S:$TR($E(X,1),"AaBb","????")["?" X="?"
 . S DISP=$$LET2UNC^MAGOSMSC(X) W:DISP'=X "   ",DISP
 . I X'["?",DISP'="" S IMAGEDIR=DISP,OK=1 Q
 . W !!,"The DICOM image directories are usually on the local system,"
 . W !,"and is used to hold both the input and output image files."
 . W !,"C:\DICOM (or D:\DICOM) is typically the DICOM image directory."
 . W !,"Any other device letter may be entered, as well as a"
 . W !,"fully specified UNC name."
 . Q
 Q
 ;
Q5 K QUIT S OK=0 F  D  Q:OK  Q:$D(QUIT)
 . W !!,"Please enter the percentage of free disk space"
 . W !,"required to allow storage of image files: "
 . W FREESPAC,"%// " R X:DTIME E  S X="^"
 . I X="^" S QUIT=0 Q
 . I X="^^" S QUIT=1 Q
 . S X=$TR(X," %") I X="" S X=FREESPAC W X
 . I +X=X,X>1,X<99 S FREESPAC=X\1,OK=1 Q
 . W:X'="?" "  ???" W !
 . W !,"Storage of image files should not be allowed when there"
 . W !,"is not enough free disk space left to allow for proper"
 . W !,"processing of these files."
 . W !,"A typical percentage of free space to require is 15%."
 . Q
 Q
 ;
Q6 K QUIT S OK=0 F  D  Q:OK  Q:$D(QUIT)
 . W !!,"identify the disk-share for",!,"the DICOM dictionary directory: "
 . W DICTDIR,"// " R X:DTIME E  S X="^"
 . I X="^" S QUIT=0 Q
 . I X="^^" S QUIT=1 Q
 . S:X="" X=DICTDIR
 . S:$TR($E(X,1),"AaBb","????")["?" X="?"
 . S DISP=$$LET2UNC^MAGOSMSC(X) W:DISP'=X "   ",DISP
 . I X'["?",DISP'="" S DICTDIR=DISP,OK=1 Q
 . W !!,"The DICOM dictionary directory is usually on a networked system,"
 . W !,"and is used to hold both the DICOM text and image files."
 . W !,"F:\DICOM is typically the DICOM dictionary data directory."
 . W !,"Any other device letter may be entered, as well as a"
 . W !,"fully specified UNC name."
 . Q
 Q
 ;
Q7 K QUIT S OK=0 F  D  Q:OK  Q:$D(QUIT)
 . W !!,"Please enter the number of communication channels  ",CHANNEL,"// "
 . R X:DTIME E  S X="^"
 . I X="^" S QUIT=0 Q
 . I X="^^" S QUIT=1 Q
 . I X="" S X=CHANNEL W X
 . I X?1N S CHANNEL=X,OK=1 Q
 . W:X'="?" "  ???" W !
 . W !,"Communication channels are used to broadcast VistA event data."
 . W !,"A separate channel is needed for each different destination."
 . W !,"For instance, event data may be sent to both a commercial PACS"
 . W !,"and to one or more Modality Worklist service class providers."
 . W !,"Each destination must have its own event channel <N> and a"
 . W !,"dedicated ",TEXTDIR,":\dicom\data<N> subdirectory."
 . W !!,"The number of communication channels must be between 1 and 9."
 . Q
 Q
 ;
Q8 D
 . N CHK,ERR,HOST,X
 . S HOST=$$HOSTNAME^MAGOSMSC()
 . S ERR="Cannot check VistA to validate Machine ID at this time."
 . I 'M2MOK W !!,ERR,! Q
 . S X=$$CALLRPC^MAGM2VCU("MAG DICOM GET MACHINE ID","M",.CHK,HOST)
 . I X<0 W !!,ERR,! Q
 . I CHK>0 S MACHID=CHK Q
 . W !!,"Warning reported by VistA:",!,CHK,!
 . Q
 Q
 ;
Q9 D Q9^MAGDMFBC Q
Q10 D Q10^MAGDMFBC Q
Q11 D Q11^MAGDMFBC Q
Q12 D Q12^MAGDMFBC Q
Q13 D Q13^MAGDMFBC Q
Q14 D Q14^MAGDMFBC Q
Q15 D Q15^MAGDMFBC Q
Q16 D Q16^MAGDMFBD Q
Q17 D Q17^MAGDMFBD Q
Q18 D Q18^MAGDMFBD Q
Q19 D Q19^MAGDMFBD Q
Q20 D Q20^MAGDMFBD Q
Q21 Q  ;  question 21 was removed
Q22 D Q22^MAGDMFBE Q
Q23 D Q23^MAGDMFBE Q
Q24 D Q24^MAGDMFBE Q
Q25 D Q25^MAGDMFBE Q
Q26 D Q26^MAGDMFBE Q
Q27 D Q27^MAGDMFBE Q
Q28 D Q28^MAGDMFBE Q
Q29 D Q29^MAGDMFCC Q
Q30 D Q30^MAGDMFBE Q
Q31 D Q31^MAGDMFBE Q
Q32 Q  ;  question 32 was removed
Q33 D Q33^MAGDMFBE Q
 ;
