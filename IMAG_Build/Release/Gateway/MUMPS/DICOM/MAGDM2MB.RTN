MAGDM2MB ;WOIFO/PMK - Read a DICOM image file ; 12/20/2006 09:49
 ;;3.0;IMAGING;**11,51,54**;MAR 19, 2002;;OCT 28, 2009
 ;; Per VHA Directive 2004-038, this routine should not be modified.
 ;; +---------------------------------------------------------------+
 ;; | Property of the US Government.                                |
 ;; | No permission to copy or redistribute this software is given. |
 ;; | Use of unreleased versions of this software requires the user |
 ;; | to execute a written test agreement with the VistA Imaging    |
 ;; | Development Office of the Department of Veterans Affairs,     |
 ;; | telephone (301) 734-0100.                                     |
 ;; | The Food and Drug Administration classifies this software as  |
 ;; | a medical device.  As such, it may not be changed in any way. |
 ;; | Modifications to this software may result in an adulterated   |
 ;; | medical device under 21CFR820, the use of which is considered |
 ;; | to be a violation of US Federal Statutes.                     |
 ;; +---------------------------------------------------------------+
 ;;
 Q
 ; Call MUMPS-to-MUMPS Broker Client
 ;
 ; This routine invokes the RPC passing the REQUEST array.  The output
 ; of the RPC is placed in the RESULT array.
 ;
IMAGPROC(RESULT,REQUEST) ; image processing entry point
 D CALL("MAG DICOM IMAGE PROCESSING",.RESULT,.REQUEST) ; ENTRY^MAGDIR8
 Q
 ;
TEXTPROC(RESULT,REQUEST) ; text processing entry point
 D CALL("MAG DICOM TEXT PROCESSING",.RESULT,.REQUEST) ; ENTRY^MAGDHRS1
 Q
 ;
CALL(RPCNAME,RESULT,REQUEST) ; issue the M-to-M Broker RPC invocation
 N I,LOSTCON,OK,TIME
 ;
 ; output debug REQUEST messages, if necessary
 I $G(^MAGDICOM(2006.563,1,"M-to-M BROKER MSGS"))="YES" D
 . W ! F I=1:1:$G(REQUEST(1)) W "REQUEST(",I,")=",REQUEST(I),!
 . Q
 ;
 K ^TMP("MAG",$J,"M2MB") ; REQUEST and RESULT passed through global
 S ^TMP("MAG",$J,"M2MB","REQUEST","TYPE")="ARRAY"
 S RESULT=$NAME(^TMP("MAG",$J,"M2MB","RESULT"))
 F I=1:1:$G(REQUEST(1)) D
 . S ^TMP("MAG",$J,"M2MB","REQUEST","VALUE",I)=REQUEST(I)
 . Q
 S X=$$PARAM^MAGM2VC(1,$NAME(^TMP("MAG",$J,"M2MB","REQUEST")))
 S (OK,LOSTCON)=0 F  D  Q:OK
 . N MSG,R
 . S X=$$CALLRPC^MAGM2VC(RPCNAME,RESULT,1)
 . I X=1 S OK=1 Q
 . S R=0 S:X["no connection" R=1 S:X["Connect error" R=1
 . I 'R S OK=1 Q
 . Q:$$RECONCT^MAGDMENA(1)  ; This QUIT means that the CALLRPC is re-tried
 . S MSG(1)="Lost connection with VistA System."
 . S MSG(2)="Waiting 5 minutes before next attempt to re-connect."
 . D MESSAGE^MAGDMLOG(2,0,.MSG,"tB")
 . H 300
 . S LOSTCON=1
 . Q
 D:LOSTCON
 . N MSG
 . S MSG(1)="Connection with VistA system re-established"
 . D MESSAGE^MAGDMLOG(2,0,.MSG,"tB")
 . W !
 . Q
 I X'=1 D
 . N MSG
 . S MSG(1)="M-to-M RPC BROKER CALLRPC ERROR"
 . S MSG(2)="CALLRPC Return Code: """_X_""""
 . F I=1:1 Q:'$D(@RESULT@("ERROR",I))  S MSG(I+2)=@RESULT@("ERROR",I)
 . D TRAPERR^MAGDSTRT(1,$T(+0),"DICOM M-to-M BROKER ERROR",.MSG)
 . Q  ; Error trap does not return ; output debug messages, if necessary
 ;
 ; output debug RESULT messages, if necessary
 I $G(^MAGDICOM(2006.563,1,"M-to-M BROKER MSGS"))="YES",$D(@RESULT) D
 . W ! S I="" F  S I=$O(@RESULT@(I)) Q:'I  D
 . . W "RESULT(",I,")=",@RESULT@(I),!
 . . Q
 . W !
 . Q
 ;
 Q
 ;
DEBUG ; turn on/off the m2mb message switch
 N X
 I $G(^MAGDICOM(2006.563,1,"M-to-M BROKER MSGS"))="YES" D
 . W !,"M-to-M Broker Messages are being displayed"
 . R " - turn them off?  Y// ",X:$G(DTIME,300) I X="" S X="Y" W X
 . I "Yy"[$E(X) D
 . . S ^MAGDICOM(2006.563,1,"M-to-M BROKER MSGS")="NO"
 . . W " - off"
 . . Q
 . E  W " - still on"
 . Q
 E  D
 . W !,"M-to-M Broker Messages are not being displayed"
 . R " - turn them on?  Y// ",X:$G(DTIME,300) I X="" S X="Y" W X
 . I "Yy"[$E(X) D
 . . S ^MAGDICOM(2006.563,1,"M-to-M BROKER MSGS")="YES"
 . . W " - on"
 . . Q
 . E  W " - still off"
 . Q
 Q
 ;
