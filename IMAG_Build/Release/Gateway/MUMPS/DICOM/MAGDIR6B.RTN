MAGDIR6B ;WOIFO/PMK/JSL/SAF - Read a DICOM image file ; 11 Dec 2008 6:38 AM
 ;;3.0;IMAGING;**11,30,51,54,53,99,123**;Mar 19, 2002;Build 60;Jul 16, 2012
 ;; Per VHA Directive 2004-038, this routine should not be modified.
 ;; +---------------------------------------------------------------+
 ;; | Property of the US Government.                                |
 ;; | No permission to copy or redistribute this software is given. |
 ;; | Use of unreleased versions of this software requires the user |
 ;; | to execute a written test agreement with the VistA Imaging    |
 ;; | Development Office of the Department of Veterans Affairs,     |
 ;; | telephone (301) 734-0100.                                     |
 ;; | The Food and Drug Administration classifies this software as  |
 ;; | a medical device.  As such, it may not be changed in any way. |
 ;; | Modifications to this software may result in an adulterated   |
 ;; | medical device under 21CFR820, the use of which is considered |
 ;; | to be a violation of US Federal Statutes.                     |
 ;; +---------------------------------------------------------------+
 ;;
READFILE() ; read a DICOM header
 N FILE ;----- name of image file with out the path
 N QUEUEIN ;-- input file array
 N ODEVNAME,ODEVTYPE,X
 ;
 S FILE=$RE($P($RE(FROMPATH),"\"))
 W !!,FILE
 S X=$$GETDIR^MAGOSDIR(FROMPATH)
 I 'X W "  (not on file)" Q 0 ; not on file
 I X<0 W " *** FILE ERROR: ",X," ***" Q -1 ; error
 S FILESIZE=$P($G(^TMP("MAG",$J,"DIR LIST",FILE)),";")
 I 'FILESIZE W " *** ERROR: ZERO-LENGTH FILE ***" Q -2 ; error
 S QUEUEIN("FILE")=FROMPATH,(ODEVTYPE,ODEVNAME)="",INODE="IN"
 D ENTRY^MAGDDR2 ; read the header
 S SOPCLASS=$$GETDATA^MAGDFCNS(INODE,"0008,0016")
 I "<unknown>"[SOPCLASS W " *** DICOM Error: No SOP Class UID ***" Q -3
 S IMAGEUID=$$GETDATA^MAGDFCNS(INODE,"0008,0018")
 I "<unknown>"[IMAGEUID W " *** DICOM ERROR: No Image UID ***" Q -4
 S STUDYUID=$$GETDATA^MAGDFCNS(INODE,"0020,000D")
 I "<unknown>"[STUDYUID W " *** DICOM ERROR: No Study UID ***" Q -5
 S SERIEUID=$$GETDATA^MAGDFCNS(INODE,"0020,000E")
 I "<unknown>"[SERIEUID W " *** DICOM ERROR: No Series UID ***" Q -6
 S STUDYDAT=$$GETDATA^MAGDFCNS(INODE,"0008,0020")
 S STUDYTIM=$$GETDATA^MAGDFCNS(INODE,"0008,0030")
 S ACNUMB=$$GETDATA^MAGDFCNS(INODE,"0008,0050")
 S PNAMEDCM=$$GETDATA^MAGDFCNS(INODE,"0010,0010")
 S PID=$$GETDATA^MAGDFCNS(INODE,"0010,0020")
 I $$ISIHS^MAGSPID() S PID=+PID ; P123 strip off leading zero(s)
 S MODALITY=$$GETDATA^MAGDFCNS(INODE,"0008,0060")
 S MFGR=$$GETDATA^MAGDFCNS(INODE,"0008,0070")
 S MODEL=$$GETDATA^MAGDFCNS(INODE,"0008,1090")
 F X="STUDYDAT","STUDYTIM","ACNUMB","PNAMEDCM","PID","MODALITY","MFGR","MODEL" D
 . S @X=$TR(@X,"|","~") ; remove "|" so it doesn't blow the RPC request
 . Q
 ;
 ; if secondary capture, get MFGR & MODEL attributes if not defined
 I "<unknown>"[MFGR S MFGR=$$GETDATA^MAGDFCNS(INODE,"0018,1016")
 I "<unknown>"[MODEL S MODEL=$$GETDATA^MAGDFCNS(INODE,"0018,1018")
 ;
 ; set missing values to defaults
 I "<unknown>"[MFGR S MFGR="*"
 I "<unknown>"[MODALITY S MODALITY="*"
 I "<unknown>"[MODEL S MODEL="*"
 ;
 S ROWS=$$GETDATA^MAGDFCNS(INODE,"0028,0010")
 S COLUMNS=$$GETDATA^MAGDFCNS(INODE,"0028,0011")
 S OFFSET=$P($$GETDATA^MAGDFCNS(INODE,"7FE0,0010",1,3),"offset=",2)
 Q 1
 ;
MODALITY() ; obtain modality-specific parameters for processing
 N CASECODE ;- M routine that gets the case number from the image header
 N UMFGR,UMODALTY,UMODEL
 S UMFGR=$$UP^MAGDFCNS(MFGR)
 S UMODEL=$$UP^MAGDFCNS(MODEL)
 S UMODALTY=$$UP^MAGDFCNS(MODALITY)
 ;
 ; secondary capture sop class - override the MODALITY with "<SC>"
 D:$$CODE^MAGDUID4(SOPCLASS,"Process Image")=2
 . S:$O(^MAGDICOM(2006.582,"B",UMFGR,UMODEL,"<SC>","")) UMODALTY="<SC>"
 . Q
 ;
 S MODIEN=$O(^MAGDICOM(2006.582,"B",UMFGR,UMODEL,UMODALTY,""))
 S X=$G(^MAGDICOM(2006.582,+MODIEN,0))
 I $P(X,"^",9)'="",'$P(X,"^",9) S MODIEN=0
 ;
 ; use the DEFAULT specification, if the modality is not defined
 I 'MODIEN D
 . S MODIEN=$O(^MAGDICOM(2006.582,"B","DEFAULT","DEFAULT",UMODALTY,""))
 . Q:MODIEN  ; use the modality specific default, not the generic one
 . S MODIEN=$O(^MAGDICOM(2006.582,"B","DEFAULT","DEFAULT","DEFAULT",""))
 . Q
 ;
 I 'MODIEN D MODUNDEF^MAGDIR6D Q 0 ; log undefined modality and go on
 S MODPARMS=$P(^MAGDICOM(2006.582,MODIEN,0),"^",4) ; DCMTOTGA parameters
 S CASECODE=$P(^MAGDICOM(2006.582,MODIEN,0),"^",5) ; code to find case #
 S CASECODE=$TR(CASECODE,".","^") ; translate FileMan entry point to M
 D @CASECODE ; get the case number from the image
 Q 1
 ;
