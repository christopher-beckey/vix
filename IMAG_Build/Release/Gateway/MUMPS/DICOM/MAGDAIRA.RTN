MAGDAIRA ;WOIFO/PMK - Automatic Import Reconciliation Workflow ; 08 Dec 2009 8:56 AM
 ;;3.0;IMAGING;**53**;Mar 19, 2002;Build 1715;Apr 22, 2010
 ;; Per VHA Directive 2004-038, this routine should not be modified.
 ;; +---------------------------------------------------------------+
 ;; | Property of the US Government.                                |
 ;; | No permission to copy or redistribute this software is given. |
 ;; | Use of unreleased versions of this software requires the user |
 ;; | to execute a written test agreement with the VistA Imaging    |
 ;; | Development Office of the Department of Veterans Affairs,     |
 ;; | telephone (301) 734-0100.                                     |
 ;; | The Food and Drug Administration classifies this software as  |
 ;; | a medical device.  As such, it may not be changed in any way. |
 ;; | Modifications to this software may result in an adulterated   |
 ;; | medical device under 21CFR820, the use of which is considered |
 ;; | to be a violation of US Federal Statutes.                     |
 ;; +---------------------------------------------------------------+
 ;;
ORDERS(DFN,ACNUMB,STUDYDAT,RADPROC,PROCNAME,MODIFIER,RADTI,RACNI,RAOIEN,IMAGLOCN) ;
 ; look up orders that are have already been placed
 N COUNT,DASH,DONE,I,N,ORDERS,ORDERDT,REASON,RETURN,STATUS,X,Z
 S RPCERR=$$CALLRPC^MAGM2VCU("MAG DICOM GET RAD ORDERS","MT",.ORDERS,DFN)
 I ORDERS(1)<0 D  Q 0
 . W !
 . D ERRORMSG^MAGDAIR0(0,"Error in rpc MAG DICOM GET RAD ORDERS",.ORDERS)
 . Q
 I ORDERS(1)=0 D  Q 0
 . W $$HIGHLITE^MAGDAIR0("YELLOW")
 . W !!,"There are no pending or active radiology orders for this patient."
 . W $$HIGHLITE^MAGDAIR0("")
 . Q
 S $P(DASH,"-",41)="" ; generate a sting of 41 dashes
 S DONE=0 F  D  Q:DONE
 . S N=$$DISPLAY()
 . I N="" D  Q:DONE=-1
 . . W !!,"Please enter the corresponding study: "
 . . R N:DTIME E  S DONE=-1 Q
 . . Q
 . I N="" Q
 . I N?1"^".E S DONE=-2 Q
 . I (N'?1N.N)!(N'>0)!(N>ORDERS(1)) D  Q
 . . W !,"Please enter a number between 1 and ",ORDERS(1),"."
 . . D CONTINUE^MAGDAIR0
 . . Q
 . D GETDATA(N,.RAOIEN,.RADPROC,.PROCNAME,.MODIFIER,.STATUS,.ORDERDT,.STUDYDAT,.REASON,.RADTI,.RACNI,.ACNUMB,.IMAGLOCN)
 . W !!,"#",N,"  Status: ",STATUS,?25,"Ordered: "
 . W $$DATE^MAGDAIR0($P(ORDERDT,"."),"S")," "
 . W $$TIME^MAGDAIR0($E($P(ORDERDT,".",2)_"000000",1,6))
 . W ?60,"Accession Number: ",ACNUMB
 . W ?100,"Study Date: " I STUDYDAT'="" W $$DATE^MAGDAIR0(STUDYDAT,"S")
 . W !,"Procedure Name: ",PROCNAME,?100,"Procedure Number: ",RADPROC
 . W !,"Modifier(s): " F I=1:1:$L(MODIFIER,"~") W:I>1 ", " W $P($P(MODIFIER,"~",I),"|",1)
 . W !,"Reason for Study: ",REASON
 . D YESNO^MAGDAIR0("Is this the correct study?","y",.X)
 . I X'="YES" Q
 . I $$VERIFY^MAGDAIRW(DFN,STUDYN,PNAMEDCM,PID,DOB,SEX,ACNUMB,STUDYDAT,PROCNAME,MODIFIER) S DONE=1
 . Q
 Q DONE>0
 ;
GETDATA(I,RAOIEN,RADPROC,PROCNAME,MODIFIER,STATUS,ORDERDT,STUDYDAT,REASON,RADTI,RACNI,ACNUMB,IMAGLOCN) ;
 N X,Z
 S X=ORDERS(I+1)
 S RAOIEN=$P(X,"^",1),RADPROC=$P(X,"^",2)
 S Z=$O(^MAGDICOM(2006.5758,"C",RADPROC,""))
 S PROCNAME=$P($G(^MAGDICOM(2006.5758,Z,0)),"^",1)
 S MODIFIER=$P(X,"^",3),STATUS=$P(X,"^",4)
 S ORDERDT=$P(X,"^",5),$E(ORDERDT)=$E(ORDERDT)+17
 S REASON=$P(X,"^",6),RADTI=$P(X,"^",7),RACNI=$P(X,"^",8),ACNUMB=$P(X,"^",9)
 S STUDYDAT=$P(X,"^",10) I STUDYDAT'="" S $E(STUDYDAT)=$E(STUDYDAT)+17
 S IMAGLOCN=$P(X,"^",11)
 Q
 ;
DISPLAY() ;
 N RAOIEN,RADPROC,PROCNAME,MODIFIER,STATUS,ORDERDT,STUDYDAT,REASON,RADTI,RACNI,ACNUMB,IMAGLOCN,RETURN,I,X
 S RETURN=""
 D HEADING
 F I=1:1:ORDERS(1) D  Q:RETURN'=""
 . I $Y>43 D  Q:RETURN'=""
 . . W !!,"Please enter the corresponding study number, or press <Enter> for more studies: "
 . . R X:$G(DTIME,1E5)
 . . I X'="" S RETURN=X Q
 . . D HEADING
 . . Q
 . D GETDATA(I,.RAOIEN,.RADPROC,.PROCNAME,.MODIFIER,.STATUS,.ORDERDT,.STUDYDAT,.REASON,.RADTI,.RACNI,.ACNUMB,.IMAGLOCN)
 . W !,$J(I,3),") ",?11-($L(STATUS)/2),STATUS
 . W ?17,$$DATE^MAGDAIR0($P(ORDERDT,"."),"S")
 . W ?27,$E(PROCNAME,1,40)
 . W " " F  Q:$X>66  W "-"
 . W ?72,$E(REASON,1,40)
 . W:$L(REASON) " " F  Q:$X>111  W "-"
 . W "  ",?115,$J(ACNUMB,16)
 . Q
 Q RETURN
 ;
HEADING ; output the heading
 N X
 W @IOF,?20,"VistA Radiology Orders      "
 W $$NAME^MAGDAIR0(PNAMEDCM),"      ",PID S X=$X
 W !?20 F  W "-" Q:$X'<X
 W !!," #  ",?7," Status ",?17,"Ordered",?27,"Procedure",?72,"Reason for Study",?120,"Accession #"
 W !,"----",?7,"--------",?17,"--------",?27,DASH,?72,DASH,?120,"-----------"
 Q
