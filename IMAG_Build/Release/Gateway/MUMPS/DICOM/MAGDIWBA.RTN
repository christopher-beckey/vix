MAGDIWBA ;WOIFO/EdM/PMK - Export of radiology images from VistA ; 10/18/2007 10:35
 ;;3.0;IMAGING;**30,50,54**;MAR 19, 2002;;OCT 28, 2009
 ;; Per VHA Directive 2004-038, this routine should not be modified.
 ;; +---------------------------------------------------------------+
 ;; | Property of the US Government.                                |
 ;; | No permission to copy or redistribute this software is given. |
 ;; | Use of unreleased versions of this software requires the user |
 ;; | to execute a written test agreement with the VistA Imaging    |
 ;; | Development Office of the Department of Veterans Affairs,     |
 ;; | telephone (301) 734-0100.                                     |
 ;; | The Food and Drug Administration classifies this software as  |
 ;; | a medical device.  As such, it may not be changed in any way. |
 ;; | Modifications to this software may result in an adulterated   |
 ;; | medical device under 21CFR820, the use of which is considered |
 ;; | to be a violation of US Federal Statutes.                     |
 ;; +---------------------------------------------------------------+
 ;;
 Q
 ;
ENTRY ; Entry point from main menu
 N ACCNUM,BATCHSIZ,BEGDATE,D0,D1,DATE,DFN,DEFAULT,DOB,DONE
 N HOURS,FIRSTIME,FMDATETM,I,IMAGE,INFO,LOCATION,MAGIEN,MSG
 N NAME,NIMAGES,OK,OUT,QUESTION,QUIT
 N RARPT0,RARPT1,REASON,REGDATE,REVERSE,RPCERR,RUNNUMB
 N SCANMODE,SEED,SKIP,SORTORDR,SSN,STOPDATE,STORESCP,TICKER,TYPE,X,Y,Z
 ;
 S MSG(1)=""
 S MSG(2)="V i s t A   D I C O M   I m a g e   E x p o r t e r"
 S MSG(3)="---------------------------------------------------"
 S MSG(4)=""
 S MSG(5)="This program may be used to copy radiology images from VistA to a"
 S MSG(6)="another DICOM image archive."
 S MSG(7)=""
 S MSG(8)="The regular ""Transmit DICOM Images to a Storage SCP"" menu option"
 S MSG(9)="is used to copy the images to the other DICOM image archive."
 S MSG(10)="You may use Ctrl-C (^C) to stop this process, if you need to abort it."
 S MSG(11)=""
 W BOLD W @IOF D WARNING^MAGDFCNS(.MSG) K MSG
 ;
 S DEFAULT=$G(^MAGDICOM(2006.563,1,"EXPORT SCAN MODE"),"DATE")
 W !! S OK=0 F  D  Q:OK
 . W !!,"Scan by Date, Report Number, or Patient (D, N, or P): "
 . I $L(DEFAULT) W DEFAULT," // "
 . R X:DTIME E  S OK=-1 Q
 . I X="",$L(DEFAULT) S X=DEFAULT W X
 . I X="" S OK=-1 Q
 . I X["^" S OK=-1 Q
 . I "Dd"[$E(X) S SCANMODE="DATE",OK=1 Q
 . I "Nn"[$E(X) S SCANMODE="NUMBER",OK=1 Q
 . I "Pp"[$E(X) S SCANMODE="PATIENT",OK=1 Q
 . W "   ???",!,"Please enter ""D"", ""N"", or ""P""."
 . Q
 Q:OK'=1  ; didn't enter a choice
 S ^MAGDICOM(2006.563,1,"EXPORT SCAN MODE")=SCANMODE
 ;
 S DONE=0 D REASON^MAGDIWB1 Q:DONE=-1  D ESIGN^MAGDIWB1 Q:DONE=-1
 ;
 I SCANMODE="NUMBER" S QUESTION="STORESCP^MAGDIWBB SORTORDR^MAGDIWBB RARPT1^MAGDIWBB BATCHSIZ^MAGDIWBB HOURS^MAGDIWBB VERIFY^MAGDIWBD"
 E  I SCANMODE="DATE" S QUESTION="STORESCP^MAGDIWBB SORTORDR^MAGDIWBB BEGDATE^MAGDIWBB STOPDATE^MAGDIWBB HOURS^MAGDIWBB VERIFY^MAGDIWBD"
 E  I SCANMODE="PATIENT" S QUESTION="STORESCP^MAGDIWBB PATIENT^MAGDIWBB SORTORDR^MAGDIWBB BEGDATE^MAGDIWBB STOPDATE^MAGDIWBB HOURS^MAGDIWBB VERIFY^MAGDIWBD"
 ;
 F I=1:1:$L(QUESTION," ") D  I I<0 S QUIT=-1 Q
 . S QUIT=0 D @$P(QUESTION," ",I)
 . I QUIT>0 S I=I-2
 . E  I QUIT<0 S I=-1
 . Q
 I QUIT<0 Q
 ;
 S LOCATION=^MAGDICOM(2006.563,1,"LOCATION")
 S ^MAGDICOM(2006.563,1,"EXPORT BATCH COUNT")=0
 S ^MAGDICOM(2006.563,1,"EXPORT BATCH #IMAGES")=0
 S ^MAGDICOM(2006.563,1,"EXPORT ACTIVE")="YES"
 ;
 ; Gather pre-run statistics
 S BEGDATE=$G(^MAGDICOM(2006.563,1,"EXPORT BEGIN DATE"))
 S STOPDATE=$G(^MAGDICOM(2006.563,1,"EXPORT STOP DATE"))
 S DFN=$G(^MAGDICOM(2006.563,1,"EXPORT PATIENT"))
 S (REGDATE,SEED)=$S(SORTORDR="ASCENDING":BEGDATE,1:STOPDATE)
 I SCANMODE="PATIENT" D
 . I SORTORDR="ASCENDING" D
 . . S RARPT1=^MAGDICOM(2006.563,1,"EXPORT RADIOLOGY REPORT # START")
 . . Q
 . E  D
 . . S RARPT1=^MAGDICOM(2006.563,1,"EXPORT RADIOLOGY REPORT # STOP")
 . . Q
 . Q
 S RARPT1=$G(RARPT1) ; need a value, even if not used
 ;
 L +^MAGDICOM(2006.565,0):30 E  D  Q
 . W !!,"Cannot obtain LOCK on Batch Export Run Table.",!!
 . Q
 S X=$G(^MAGDICOM(2006.565,0))
 S $P(X,"^",1,2)="EXPORT DICOM RUN^2006.565D"
 S (RUNNUMB,$P(X,"^",3))=$O(^MAGDICOM(2006.565," "),-1)+1 ; get next IEN
 S $P(X,"^",4)=$P(X,"^",4)+1 ; increment total count
 S ^MAGDICOM(2006.565,0)=X
 ;
 S X=$$NOW^XLFDT()_"^"_STORESCP_"^"_SORTORDR_"^"_(+RARPT1)_"^"_BATCHSIZ_"^"_HOURS
 S X=X_"^^^^^"_SCANMODE_"^"_BEGDATE_"^"_STOPDATE_"^"_REGDATE_"^"_DFN
 S ^MAGDICOM(2006.565,RUNNUMB,0)=X
 L -^MAGDICOM(2006.565,0)
 S ^MAGDICOM(2006.563,1,"EXPORT DICOM RUN NUMBER")=RUNNUMB
 ;
 ; The main loop for the program
 W !!,"Report #",?11,"Accession",?26,"Group #",?40,"#Images",?69,"Date"
 W !,"--------",?11,"---------",?26,"-------",?40,"-------",?69,"----"
 S RARPT1=RARPT1+$S(SORTORDR="ASCENDING":-1,1:1) ; Massage value to prime $o
 S (DONE,SKIP)=0,FIRSTIME=1 F  D RADLKUP Q:DONE
 ;
 ; Update post-run statistics
 S $P(^MAGDICOM(2006.565,RUNNUMB,0),"^",7)=$$NOW^XLFDT()
 S RARPT1=RARPT1+$S(SORTORDR="ASCENDING":1,1:-1) ; Massage value the other way
 S ^MAGDICOM(2006.563,1,"EXPORT RADIOLOGY REPORT #")=RARPT1
 S ^MAGDICOM(2006.563,1,"EXPORT ACTIVE")="NO"
 Q
 ;
RADLKUP ; Find the next study to export
 N D,RARPT3
 I "23"[($H#7) S HOURS=$TR($J("",24)," ","Y") ; Saturday and Sunday are 24 hrs OK?
 S X=$E(HOURS,$P($H,",",2)\3600+1)
 I X'="Y" D  Q
 . I FIRSTIME W !!,"Waiting for time to start " S FIRSTIME=0
 . D IDLE^MAGDBB(.TICKER,1,1)
 . Q
 S FIRSTIME=1
 ;
 S D=$S(SORTORDR="ASCENDING":1,SORTORDR="DESCENDING":-1)
 I SCANMODE="NUMBER" D  ; numeric report order
 . S X=$$CALLRPC^MAGM2VCU("MAG DICOM GET RAD RPT INFO","MT",.RARPT1,"O1",RARPT1,D)
 . Q
 E  I SCANMODE="PATIENT" D  ; patient report order
 . S X=$$CALLRPC^MAGM2VCU("MAG RAD GET NEXT RPT BY PT","MT",.RARPT1,DFN,RARPT1,D)
 . I RARPT1 D
 . . S X=$$CALLRPC^MAGM2VCU("MAG DICOM GET RAD RPT INFO","MT",.RARPT0,"G1",RARPT1)
 . . S REGDATE=$P(RARPT0,"^",3)
 . . S SKIP=$S(REGDATE>STOPDATE:1,REGDATE<BEGDATE:1,1:0)
 . . Q
 . Q
 E  D  ; date scan order
 . S Z=$$CALLRPC^MAGM2VCU("MAG RAD GET NEXT RPT BY DATE","MT",.OUT,SEED,D)
 . S SEED=$P(OUT,"^",2,9999),RARPT1=$P(OUT,"^",1),REGDATE=$P(OUT,"^",2)
 . I SORTORDR="ASCENDING",REGDATE<BEGDATE S SKIP=1 Q
 . I SORTORDR="DESCENDING",REGDATE>STOPDATE S SKIP=1 Q
 . I SORTORDR="ASCENDING",REGDATE>STOPDATE S DONE=1 Q
 . I SORTORDR="DESCENDING",REGDATE<BEGDATE S DONE=1
 . Q
 Q:DONE  I RARPT1<1 S DONE=1 Q  ; End of data
 I SKIP Q  ; must come after checking DONE and RARPT1
 ;
 S X=$$CALLRPC^MAGM2VCU("MAG DICOM GET RAD RPT INFO","MT",.RARPT0,"G1",RARPT1)
 S ACCNUM=$P(RARPT0,"^",1),FMDATETM=$P(RARPT0,"^",3)
 S DATE=$P($$FMTE^XLFDT(FMDATETM,1),"@",1)
 W !,$J(RARPT1,8)," : ",ACCNUM
 S RARPT3=0 F  D  Q:'RARPT3
 . S Z=$$CALLRPC^MAGM2VCU("MAG DICOM GET RAD RPT INFO","MT",.RARPT3,"O2",RARPT1,2005,RARPT3)
 . I RARPT3 D
 . . S Z=$$CALLRPC^MAGM2VCU("MAG DICOM GET RAD RPT INFO","MT",.MAGIEN,"G2",RARPT1,2005,RARPT3)
 . . D GROUP
 . . Q
 . Q
 ;
 ; Bump RARPT1 counter and check for completion of the batch
 ; Record time for this study
 I SCANMODE="NUMBER" D  ; numeric report order
 . S ^MAGDICOM(2006.563,1,"EXPORT RADIOLOGY REPORT #")=RARPT1
 . S $P(^MAGDICOM(2006.565,RUNNUMB,0),"^",8)=RARPT1
 . I ^MAGDICOM(2006.563,1,"EXPORT BATCH COUNT")'<^("EXPORT BATCH SIZE") S DONE=1
 . Q
 E  D  ; patient or date scan order
 . I SORTORDR="ASCENDING" S ^MAGDICOM(2006.563,1,"EXPORT BEGIN DATE")=REGDATE\1
 . E  S ^MAGDICOM(2006.563,1,"EXPORT STOP DATE")=REGDATE\1+.2359
 . S $P(^MAGDICOM(2006.565,RUNNUMB,0),"^",14)=REGDATE
 . Q
 Q
 ;
GROUP ; Look up group in ^MAG(2005)
 N A6,DONE,GROUP,GRPDATA,I,MAGGROUP,PRIORITY,R,SENT,X
 W:RARPT3>1 ! W ?25,$J(MAGIEN,8)
 D GETDATA(MAGIEN,.GRPDATA)
 S TYPE=+$G(GRPDATA("OBJECT TYPE"))
 I TYPE'=3,TYPE'=11,TYPE'=100 W ?36," *** NOT IMAGES ***" Q
 I TYPE=11 D
 . S X=$$CALLRPC^MAGM2VCU("MAG DICOM GET IMAGE GROUP","M",.GROUP,MAGIEN)
 . S NIMAGES=GROUP(1)
 . Q
 E  S NIMAGES=1 ; Type 3 and 100 consist of a singe image
 I NIMAGES?1"0^".E D  Q
 . N MSG
 . S MSG(1)="Problem with Image Group #"_MAGIEN_"  Accession Number: "_ACCNUM
 . S MSG(2)=$P(NIMAGES,"^",2,999)
 . D TRAPERR^MAGDSTRT(-1,$T(+0),"DICOM Image Export Patient Safety Error",.MSG,1)
 . Q
 W ?40,$J(NIMAGES,4)," image" W:NIMAGES>1 "s" W ?66,DATE
 S (X,^("EXPORT BATCH #IMAGES"))=^MAGDICOM(2006.563,1,"EXPORT BATCH #IMAGES")+NIMAGES
 S $P(^MAGDICOM(2006.565,RUNNUMB,0),"^",10)=X
 ; Issue the RPC
 S LOCATION=^MAGDICOM(2006.563,1,"LOCATION")
 S X=$G(^MAGDICOM(2006.585,+$O(^MAGDICOM(2006.585,"B",STORESCP,"")),0))
 S PRIORITY=$P(X,"^",7) S:'PRIORITY PRIORITY=500
 S A6=$G(^MAGDICOM(2006.563,1,"MAILGROUP"))
 S R="MAG DICOM QUEUE IMAGE"
 S X=$$CALLRPC^MAGM2VCU(R,"M",.IMAGE,MAGIEN,STORESCP,LOCATION,$G(ACCNUM),REASON,A6,PRIORITY-100,0)
 I X W !!,X
 ; Wait for images to complete transmitting
 S SENT=""
 W !,"Waiting for transmission to complete..."
 S DONE=0 F  D  Q:DONE
 . S RPCERR=$$CALLRPC^MAGM2VCU("MAG DICOM GET NEXT QUEUE ENTRY","MT",.INFO,LOCATION,.SENT,1)
 . I RPCERR<0 W !!,RPCERR S DONE=1 ; ignore RPC error
 . E  I +INFO(1)=-2 S DONE=1 ; no studies to be transmitted
 . E  D IDLE^MAGDBB(.TICKER,1,1)
 . Q
 S (X,^("EXPORT BATCH COUNT"))=^MAGDICOM(2006.563,1,"EXPORT BATCH COUNT")+1 ; Bump count
 S $P(^MAGDICOM(2006.565,RUNNUMB,0),"^",9)=X
 Q
 ;
GETDATA(IEN,DATA) ; Get basic data and return it in associative array format
 N F,J,K,SCRATCH,X
 K DATA
 S X=$$CALLRPC^MAGM2VCU("MAG DICOM GET BASIC IMAGE","M",.SCRATCH,IEN)
 S K=$O(SCRATCH("")) I K="" D  Q
 . W !!,"Cannot retrieve information for image # ",IEN,!
 . Q
 S J=K F  S J=$O(SCRATCH(J)) Q:J=""  D
 . S X=SCRATCH(J),F=$P(X,"^",1) S:F'="" DATA(F)=$P(X,"^",2,3)
 . Q
 Q
