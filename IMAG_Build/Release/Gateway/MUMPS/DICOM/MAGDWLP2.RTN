MAGDWLP2 ;WOIFO/PMK -- Display DICOM worklist information ; 18 Dec 2009 12:10 PM
 ;;3.0;IMAGING;**11,30,50,54,53**;Mar 19, 2002;Build 1715;Apr 22, 2010
 ;; Per VHA Directive 2004-038, this routine should not be modified.
 ;; +---------------------------------------------------------------+
 ;; | Property of the US Government.                                |
 ;; | No permission to copy or redistribute this software is given. |
 ;; | Use of unreleased versions of this software requires the user |
 ;; | to execute a written test agreement with the VistA Imaging    |
 ;; | Development Office of the Department of Veterans Affairs,     |
 ;; | telephone (301) 734-0100.                                     |
 ;; | The Food and Drug Administration classifies this software as  |
 ;; | a medical device.  As such, it may not be changed in any way. |
 ;; | Modifications to this software may result in an adulterated   |
 ;; | medical device under 21CFR820, the use of which is considered |
 ;; | to be a violation of US Federal Statutes.                     |
 ;; +---------------------------------------------------------------+
 ;;
 Q
 ;
STATS ; entry point from ^MAGDMENU to list statistics, by modality
 N ACNUMB,ANY,DATE,DFN,DIVISION,DIVNAME,DO,HALT,I,INFO,ITYPCODE,LINES
 N NOW,PAGE,STATS,STUDYID,STUDYIEN,TODAY,USER,X
 W @IOF
 S DIVISION=0,USER=1
 F  S DIVISION=$O(^MAGDWLST(2006.55,DIVISION)) Q:'DIVISION  D
 . Q:$$CALLRPC^MAGM2VCU("MAG DICOM FILEMAN GET","M",.INFO,4,DIVISION,.01)<0
 . S DIVNAME=INFO
 . I DIVNAME="" S DIVNAME="*** Division #"_DIVISION_" ***"
 . ; output one division's stats
 . K STATS
 . D SCANWLST("STAT1^MAGDWLP2",9999999)
 . S PAGE=23,LINES=PAGE+1,ANY=0
 . S DATE="" F  S DATE=$O(STATS("DATE",DATE),-1) Q:DATE=""  D
 . . S LINES=LINES+1 D:LINES>PAGE
 . . . D:ANY RETURN^MAGDMENU
 . . . W @IOF,"Compiling modality worklist statistics for ",DIVNAME
 . . . S ITYPCODE=""
 . . . W !!," Exam",?25,"Imaging Type"
 . . . W !," Date",?25,"-------------",!
 . . . F I=15:7 S ITYPCODE=$O(STATS("ITYPCODE",ITYPCODE)) Q:ITYPCODE=""  D
 . . . . W ?I,$J(ITYPCODE,7)
 . . . . Q
 . . . S LINES=7
 . . . Q
 . . W !,$J(DATE\1#100,2)," ",$P("JAN FEB MAR APR MAY JUN JUL AUG SEP OCT NOV DEC"," ",DATE\100#100)," ",DATE\10000+1700
 . . S ANY=1
 . . F I=15:7 S ITYPCODE=$O(STATS("ITYPCODE",ITYPCODE)) Q:ITYPCODE=""  D
 . . . W ?I,$J($G(STATS("COUNTS",ITYPCODE,DATE)),7)
 . . . Q
 . . Q
 . I $O(^MAGDWLST(2006.55,DIVISION)) D RETURN^MAGDMENU W @IOF
 . Q
 D RETURN^MAGDMENU
 S HALT=0 F  D  Q:HALT
 . N D0,DESC,MODLIST,STATION
 . S NOW=$$NOW^XLFDT(),TODAY=NOW\1
 . ;
 . ; add all the defined modalities to the list
 . S STATION=""
 . F  S STATION=$O(^MAGDICOM(2006.583,"B",STATION)) Q:STATION=""  D
 . . S MODLIST(STATION)=""
 . . S D0=+$O(^MAGDICOM(2006.583,"B",STATION,""))
 . . S DESC(STATION)=$P($G(^MAGDICOM(2006.583,D0,0)),"^",6)
 . . Q
 . ;
 . ; get statistics on each modality's activity
 . M MODLIST=^MAGDWLST("STAT",TODAY)
 . ;
 . S PAGE=23,LINES=PAGE+1 D NEWLINE(0)
 . S STATION=""
 . F  S STATION=$O(MODLIST(STATION)) Q:STATION=""  D
 . . N COUNT,MATCH,TIME
 . . S COUNT=MODLIST(STATION)
 . . S TIME=$P(COUNT,"^",2),MATCH=$P(COUNT,"^",3)
 . . S:TIME'="" TIME=$E(TIME,9,10)_":"_$E(TIME,11,12)_":"_$E(TIME,13,14)
 . . S:MATCH="" MATCH="Failed" S:COUNT="" COUNT="<none>",MATCH=""
 . . I 'USER,COUNT="<none>" Q
 . . D NEWLINE(1) Q:HALT
 . . W $J(STATION,17),?21,$J($P(COUNT,"^",1),6)
 . . W ?30,TIME,?42,$J(MATCH,7),?51,$G(DESC(STATION))
 . . Q
 . D BGNDSTAT^MAGDMENU
 . R !,"Exit?  no // ",X:30 S USER=$T S:X="" X="no"
 . I "^Yy"[$E(X) S HALT=1
 . Q
 Q
 ;
NEWLINE(WAIT) N I,X
 S LINES=LINES+1,X="" I LINES<PAGE W ! Q
 D:WAIT  Q:HALT
 . W !,"Press <Enter> to continue: " R X:$G(DTIME,300) E  S X="^"
 . I X="^" S HALT=1
 . Q
 S LINES=5
 W @IOF
 W ?20,"Modality Worklist Activity",!
 W ?20,"--------------------------",!!
 W "  Instrument Name   Queries     Time      Matches  Description",!
 W "  ---------------   -------     ----      -------  -----------",!
 Q
 ;
STAT1 ; count one entry
 S DATE=$P(SCHSDYDT,".")
 S STATS("DATE",DATE)="",STATS("ITYPCODE",ITYPCODE)=""
 S STATS("COUNTS",ITYPCODE,DATE)=$G(STATS("COUNTS",ITYPCODE,DATE))+1
 Q
 ;
SCANWLST(ACTION,STOPDATE) ; scan the worklist
 N SERVICE,ITYPCODE,SCHSDYDT
 S SERVICE=""
 F  S SERVICE=$O(^MAGDWLST(2006.56,DIVISION,1,"E",SERVICE)) Q:SERVICE=""  D
 . S ITYPCODE=""
 . F  S ITYPCODE=$O(^MAGDWLST(2006.56,DIVISION,1,"E",SERVICE,ITYPCODE)) Q:ITYPCODE=""  D
 . . S SCHSDYDT=""
 . . F  S SCHSDYDT=$O(^MAGDWLST(2006.56,DIVISION,1,"E",SERVICE,ITYPCODE,SCHSDYDT)) Q:SCHSDYDT=""  Q:SCHSDYDT'<STOPDATE  D
 . . . S STUDYIEN=""
 . . . F  S STUDYIEN=$O(^MAGDWLST(2006.56,DIVISION,1,"E",SERVICE,ITYPCODE,SCHSDYDT,STUDYIEN)) Q:STUDYIEN=""  D
 . . . . S X=$G(^MAGDWLST(2006.56,DIVISION,1,STUDYIEN,0))
 . . . . I X="" D  Q
 . . . . . ; kill dangling cross-reference
 . . . . . K ^MAGDWLST(2006.56,DIVISION,1,"E",SERVICE,ITYPCODE,SCHSDYDT,STUDYIEN)
 . . . . . Q
 . . . . S DFN=$P(X,"^",2)
 . . . . S ACNUMB=$P(X,"^",3),STUDYID=$P(X,"^",4)
 . . . . D @ACTION ; different actions are performed for each hit
 . . . . Q
 . . . Q
 . . Q
 . Q
 Q
 ;
