MAGDAIRR ;WOIFO/MS - Automatic Import Reconciliation Workflow ; 17 Nov 2009 7:36 AM
 ;;3.0;IMAGING;**53**;Mar 19, 2002;Build 1715;Apr 22, 2010
 ;; Per VHA Directive 2004-038, this routine should not be modified.
 ;; +---------------------------------------------------------------+
 ;; | Property of the US Government.                                |
 ;; | No permission to copy or redistribute this software is given. |
 ;; | Use of unreleased versions of this software requires the user |
 ;; | to execute a written test agreement with the VistA Imaging    |
 ;; | Development Office of the Department of Veterans Affairs,     |
 ;; | telephone (301) 734-0100.                                     |
 ;; | The Food and Drug Administration classifies this software as  |
 ;; | a medical device.  As such, it may not be changed in any way. |
 ;; | Modifications to this software may result in an adulterated   |
 ;; | medical device under 21CFR820, the use of which is considered |
 ;; | to be a violation of US Federal Statutes.                     |
 ;; +---------------------------------------------------------------+
 ;;
 ;
 ;
BUILD() ; Build Procedure file
 ;
 N PROCS,I,DIV
 ;
 W !!,"Building Radiology Procedure file... "
 S DIV=+$G(^MAGDICOM(2006.563,1,"LOCATION"))
 I $$CALLRPC^MAGM2VCU("MAG DICOM RADIOLOGY PROCEDURES","M",.PROCS,DIV)<0 Q:$QUIT -1 Q
 ; Returns PROC(I)=RADPROCNAME^RADPROCIEN^RADPROCTP^RADPROCCPT^RADPROCMAGTP^RADPROCINACT
 ; $P(1)= RADPROCNAME = RADIOLOGY PROCEDURE NAME
 ; $P(2)= RADPROCIEN = RADIOLOGY PROCEDURE IEN (VISTA)
 ; $P(3)= RADPROCTP = PROCEDURE TYPE (B)ROAD, (D)ETAILED, (S)ERIES, (P)ARENT
 ; $P(4)= RADPROCCPT = CPT CODE
 ; $P(5)= RADPROCMAGTP = IMAGE TYPE (79.2)
 ; $P(6)= RADPROCINACT = INACTIVE DATE
 ; $P(7)= MAGOIMGLOC = OUTSIDE IMAGE LOCATION (#79.1 -- Imaging Location)
 ; $P(8)= MAGHOSPLOC = HOSPITAL LOCATION (#44) - IEN
 ; $P(9)= MAGHOSPLOC = HOSPITAL LOCATION (#44) - NAME
 ;
 I PROCS(1)<0 D  Q:$QUIT -2 Q
 . N MSG
 . S MSG(1)="The following error was encountered in the downloading of the Radiology"
 . S MSG(2)="procedures from the VistA system:"
 . S MSG(3)=""
 . S MSG(4)="Error Number: "_$P(PROCS(1),",",1)
 . S MSG(5)=$P(PROCS(1),",",2,999)
 . F I=2:1 Q:'$D(PROCS(I))  S MSG(4+I)=PROCS(I)
 . D WARNING^MAGDFCNS(.MSG)
 . Q 
 K ^MAGDICOM(2006.5758)
 F I=1:1:PROCS(1) D
 . S ^MAGDICOM(2006.5758,"B",$P(PROCS(I+1),"^",1),I)=""
 . S ^MAGDICOM(2006.5758,"C",$P(PROCS(I+1),"^",2),I)=""
 . S ^MAGDICOM(2006.5758,I,0)=PROCS(I+1)
 . Q
 ;
 S ^MAGDICOM(2006.5758,0)="DICOM RADIOLOGY PROCEDURES"_"^2006.5758"_"^"_I_"^"_I
 W "Complete!"
 Q:$QUIT 0 Q
 ;
 ;
BUILD1 ; Build modifiers file
 ;
 ;RPC = MAG DICOM RADIOLOGY MODIFIERS
 ;Pulls from PROCEDURE MODIFIERS FILE (#71.2)
 ;
 ;$P(1)=RADPROCMODNM -- RAD PROC MOD NAME (71.2,.01)
 ;$P(2)=RADPROCMODIEN
 ;$P(3)= IMAGTYP -- TYPE OF IMAGING (71.23,.01) points to (79.2)
 ;
 N MODS,I,DIV
 W !!,"Building Radiology Modifiers file... "
 S DIV=+$G(^MAGDICOM(2006.563,1,"LOCATION"))
 Q:$$CALLRPC^MAGM2VCU("MAG DICOM RADIOLOGY MODIFIERS","M",.MODS)<0
 ;
 K ^MAGDICOM(2006.5757)
 F I=1:1:MODS(1) D 
 . S ^MAGDICOM(2006.5757,"B",$P(MODS(I+1),"^",1),I)=""
 . S ^MAGDICOM(2006.5757,"C",$P(MODS(I+1),"^",2),I)=""
 . S ^MAGDICOM(2006.5757,"D",$P(MODS(I+1),"^",3),I)=""
 . S ^MAGDICOM(2006.5757,I,0)=MODS(I+1)
 . Q
 ;
 S ^MAGDICOM(2006.5757,0)="DICOM RADIOLOGY PROCEDURES MODIFIERS"_"^2006.5757"_"^"_I_"^"_I
 W "Complete!"
 Q
 ;
SELECT() ; Select Procedure file
 N ANSWER,EPROC,EXIT,IEN
 S EXIT=0
 F  D  Q:EXIT=1
 . W !!,"Enter Procedure: "
 . R EPROC:DTIME E  S EPROC="^"
 . I EPROC["^" S ANSWER=0,EXIT=1 Q
 . I $L(EPROC)=0 Q
 . S EPROC=$$UPPER^MAGOSMSC(EPROC)
 . I EPROC["?" D  Q
 . . W !!,"Select the Radiology Procedure.  Enter the first few characters of the"
 . . W !,"procedure name.  Any word in the procedure name may also be used to find it."
 . . Q
 . ;
 . ; check for partial and keyword in context matches
 . S EXIT=$$SELECT1(EPROC,.ANSWER) Q:EXIT=1
 . W !,"NO MATCHES FOUND"
 . Q
 Q ANSWER
 ;
SELECT1(EPROC,ANSWER) ; find the answer
 N CPTCODE,HIT,I,J,JMAX,LIST,LOOP,MAXNUMB,N,PATTERN,RADPROC,RETURN,PROC,X
 S (ANSWER,RETURN)=0,MAXNUMB=20
 ;
 ; first do a partial name match
 ; then do a keyword in context search
 ;
 ; partial name match
 S PATTERN="1"""_EPROC_""".E"
 S PROC=EPROC
 F I=1:1 S PROC=$O(^MAGDICOM(2006.5758,"B",PROC)) Q:PROC'?@PATTERN  D
 . S IEN=$O(^MAGDICOM(2006.5758,"B",PROC,""))
 . S RADPROC=$P(^MAGDICOM(2006.5758,IEN,0),"^",2),CPTCODE=$P(^(0),"^",4)
 . S LIST(I)=PROC_"^"_RADPROC_"^"_CPTCODE,LIST("B",RADPROC,I)=""
 . Q
 S N=I-1 ; number of matches
 ;
 ; keyword search - check all names in alphabetic order
 S PATTERN=".E1P1"""_EPROC_"""1P.E"
 S PROC=""
 F  S PROC=$O(^MAGDICOM(2006.5758,"B",PROC)) Q:PROC=""  D
 . I (" "_PROC_" ")?@PATTERN D
 . . S IEN=$O(^MAGDICOM(2006.5758,"B",PROC,""))
 . . S RADPROC=$P(^MAGDICOM(2006.5758,IEN,0),"^",2),CPTCODE=$P(^(0),"^",4)
 . . I $D(LIST("B",RADPROC)) Q  ; already in list
 . . S N=N+1
 . . S LIST(N)=PROC_"^"_RADPROC_"^"_CPTCODE
 . . Q
 . Q
 ;
 ; no matches
 I 'N Q 0
 ;
 ; one match
 I N=1 D  Q RETURN
 . W !,$P(LIST(1),"^",1)," -"
 . F  Q:$X>65  W "-"
 . W "- ",$P(LIST(1),"^",3)
 . I $$YESNO^MAGDAIR0("Is this the right procedure?","Yes",.X)<0 Q
 . I X="YES" S ANSWER=$P(LIST(1),"^",2),RETURN=1
 . Q
 ;
 ; multiple matches
 I N>MAXNUMB W !!,"Note: There are ",N," matches.  They will be listed ",MAXNUMB," at a time."
 W !!,"Please select one of the following:"
 S HIT=0 F J=1:MAXNUMB:N D  Q:HIT
 . S JMAX=$S((J+MAXNUMB)>N:N,1:J+MAXNUMB-1)
 . F  D  Q:'LOOP
 . . S LOOP=0 F I=J:1:JMAX D
 . . . W !,$J(I,3),") ",$P(LIST(I),"^",1)," -"
 . . . F  Q:$X>70  W "-"
 . . . W "- ",$P(LIST(I),"^",3)
 . . . Q
 . . W !,"Enter the number of the procedure (1-",JMAX,")"
 . . W:N>(J+MAXNUMB) " or push <Enter> for more" W ":  "
 . . R X:DTIME
 . . I X="" W ! Q
 . . I X?1"^".E S HIT=-1 W ! Q
 . . I X?1N.N,X>0,X'>JMAX S HIT=X
 . . E  D
 . . . W "  ???",!,"Please enter a number between 1 and ",JMAX,".",!
 . . . S LOOP=1
 . . . Q
 . . Q
 . Q
 I HIT>0 D
 . W !!,"You have chosen: ",$P(LIST(HIT),"^",1)
 . I $$YESNO^MAGDAIR0("Is this the correct procedure?","Yes",.X)<0 Q
 . I X="YES" S ANSWER=$P(LIST(HIT),"^",2),RETURN=1 W !
 . Q
 Q RETURN
 ;
SELMODS(RAIMGTYP) ;Select modifiers
 ;RAIMGTYP = IMAGE TYPE
 N CNT,EXIT,HOLDMOD,X,Y,Z
 S EXIT=0,X="",Y="",Z=0,CNT=0
 ;
 F  S X=$O(^MAGDICOM(2006.5757,"D",RAIMGTYP,X)) Q:X=""  D
 . S Y=$P(^MAGDICOM(2006.5757,X,0),"^",1)
 . S CNT=CNT+1
 . S HOLDMOD(CNT)=Y_"^"_X
 . Q
 ;
 Q $$SELMOD2()
 ;
SELMOD2() ;
 N DONE,I,LIST,MOD,N,VERIFY,X,Y,Z
 F I=1:1:CNT D
 . S Z=HOLDMOD(I),Y=$P(Z,"^",1),X=$P(Z,"^",2)
 . S LIST(1,Y)=X
 . Q
 S DONE=0 F  D  Q:DONE
 . W !,"Select Procedure Modifier(s): "
 . R X:DTIME E  S X="^"
 . I X="" S DONE=1 Q
 . I X["^" S DONE=-1 Q
 . I X["?" D  Q
 . . W !?3,"Choose from:"
 . . S MOD="" F  S MOD=$O(LIST(1,MOD)) Q:MOD=""  W !,?3,MOD
 . . W !
 . . Q
 . S X=$$UPPER^MAGOSMSC(X)
 . I $D(LIST(1,X)) D ADDMOD(X) Q
 . K LIST(3)
 . S VERIFY="1"""_X_""".E"
 . S Y=X F  S Y=$O(LIST(1,Y)) Q:Y'?@VERIFY  S LIST(3,Y)=""
 . S Y=$O(LIST(3,""))
 . I Y="" W " ???" Q
 . I $O(LIST(3,Y))="" D  Q
 . . W $P(Y,X,2,999)
 . . D ADDMOD(Y) Q
 . . Q
 . S Y="" F I=1:1 S Y=$O(LIST(3,Y)) Q:Y=""  W !?5,I,"   ",Y
 . S N=I-1
 . W !,"Choose 1-",N,": "
 . R X:DTIME E  S X="^"
 . I X="" Q
 . I X["^" S DONE=-1 Q
 . I X'?1N W " ???" Q
 . I (X=0)!(X>N) W " ???" Q
 . S Y="" F I=1:1:X S Y=$O(LIST(3,Y))
 . D ADDMOD(Y) Q
 . Q
 S (Z)="",Y=0 F I=1:1 S Y=$O(LIST(2,Y)) Q:Y=""  D
 . S X=^MAGDICOM(2006.5757,LIST(2,Y),0)
 . S:Z'="" Z=Z_"~" S Z=Z_$P(X,"^",1)_"|"_$P(X,"^",2)
 . Q
 Q Z
 ;
ADDMOD(Z) ;
 S LIST(2,0)=$G(LIST(2,0))+1
 S LIST(2,LIST(2,0))=LIST(1,Z)
 K LIST(1,Z)
 Q
 ;
GETPROV(NAME) ; get ordering provider
 N DONE,I,N,X,Y,Z,RESULT
 S Z=0
 S DONE=0
 F  D  Q:DONE
 . W !,"Ordering Provider: "
 . R X:DTIME E  S X="^"
 . S X=$$UPPER^MAGOSMSC(X)
 . I X="" Q
 . I X["^" S DONE=-1 Q
 . I X["?" W !,"Answer with PROVIDER NAME" Q
 . S Y=$$CALLRPC^MAGM2VCU("PSB GETPROVIDER","M",.RESULT,X)
 . I Y<0 S DONE=-1 Q
 . I RESULT(1)=1 D  Q
 . . I +RESULT(2)=-1 W !,$P(RESULT(2),"^",2) Q
 . . W !,$P(RESULT(2),"^",2) S Z=+RESULT(2),DONE=1,NAME=$P(RESULT(2),"^",2) Q
 . . Q
 . S Y=1 F I=1:1 S Y=$O(RESULT(Y)) Q:Y=""  W !?5,I,"   ",$P(RESULT(Y),"^",2)
 . S N=I-1
 . W !,"Choose 1-",N,": "
 . R X:DTIME E  S X="^"
 . I X="" Q
 . I X["^" S DONE=-1 Q
 . I X'?1N.N W " ???" Q
 . I (X=0)!(X>N) W " ???" Q
 . S Y=1 F I=1:1:X S Y=$O(RESULT(Y))
 . S Z=+RESULT(Y),DONE=1,NAME=$P(RESULT(Y),"^",2) W "  ",NAME,!
 . Q
 I DONE<0 S Z=DONE
 Q Z
 ;
GETLOC(NAME) ; get ordering location
 N DONE,I,N,X,Y,Z,ZZ,RESULT,PATTERN
 S Z=0
 S DONE=0
 F  D  Q:DONE
 . W !,"Ordering Location: "
 . R X:DTIME E  S X="^"
 . S X=$$UPPER^MAGOSMSC(X)
 . I X="" Q
 . I X["^" S DONE=-1 Q
 . I X["?" W !,"Answer with HOSPITAL LOCATION NAME" Q
 . S ZZ=X I $L(X)>1 S ZZ=$E(X,1,($L(X)-1))
 . S Y=$$CALLRPC^MAGM2VCU("MAG DICOM GET HOSP LOCATION","M",.RESULT,ZZ)
 . I Y<0 S DONE=-1 Q
 . I '$D(RESULT) W !,"No Matches found." Q
 . I +RESULT(1)=-1 W !,$P(RESULT(1),"^",2) Q
 . I $P(RESULT(1),"^",2)=X D  Q
 . . W !,$P(RESULT(1),"^",2) S Z=+RESULT(1),NAME=$P(RESULT(1),"^",2),DONE=1
 . . Q
 . S PATTERN="1"""_X_""".E"
 . S Y="" F I=1:1 S Y=$O(RESULT(Y)) Q:Y=""  D
 . . I $P(RESULT(Y),"^",2)'?@PATTERN  K RESULT(Y)
 . . Q
 . I '$D(RESULT) W !,"No Matches found." Q
 . S I=$O(RESULT(""))
 . I '$O(RESULT(I)) D  Q
 . . W " ",$P(RESULT(I),"^",2)
 . . W !,"CORRECT? Y//:"
 . . R X:DTIME E  S X="^"
 . . I "Yy"[$E(X) S X=""
 . . I X="" S Z=+RESULT(I),DONE=1,NAME=$P(RESULT(I),"^",2) Q
 . . I X["^" S DONE=-1 Q
 . . Q
 . S Y=""
 . F I=1:1 S Y=$O(RESULT(Y)) Q:Y=""  Q:$P(RESULT(Y),"^",2)'?@PATTERN  D
 . . W !?5,I,"   ",$P(RESULT(Y),"^",2)
 . . Q
 . S N=I-1
 . W !,"Choose 1-",N,": "
 . R X:DTIME E  S X="^"
 . I X="" Q
 . I X["^" S DONE=-1 Q
 . I X'?1N.N W " ???" Q
 . I (X=0)!(X>N) W " ???" Q
 . S Y="" F I=1:1:X S Y=$O(RESULT(Y))
 . S Z=+RESULT(Y),DONE=1,NAME=$P(RESULT(Y),"^",2) W "  ",NAME,!
 . Q
 I DONE<0 S Z=DONE
 Q Z
