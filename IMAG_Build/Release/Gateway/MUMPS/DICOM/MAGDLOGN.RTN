MAGDLOGN ;WOIFO/PMK/JSL/SAF - Login Program ; 31 Oct 2008 1:54 PM
 ;;3.0;IMAGING;**11,30,51,54,123**;Mar 19, 2002;Build 60;Jul 16, 2012
 ;; Per VHA Directive 2004-038, this routine should not be modified.
 ;; +---------------------------------------------------------------+
 ;; | Property of the US Government.                                |
 ;; | No permission to copy or redistribute this software is given. |
 ;; | Use of unreleased versions of this software requires the user |
 ;; | to execute a written test agreement with the VistA Imaging    |
 ;; | Development Office of the Department of Veterans Affairs,     |
 ;; | telephone (301) 734-0100.                                     |
 ;; | The Food and Drug Administration classifies this software as  |
 ;; | a medical device.  As such, it may not be changed in any way. |
 ;; | Modifications to this software may result in an adulterated   |
 ;; | medical device under 21CFR820, the use of which is considered |
 ;; | to be a violation of US Federal Statutes.                     |
 ;; +---------------------------------------------------------------+
 ;;
ENTRY D ^MAGDLOGI ; Get Server IP address & Port, if needed
 S MENU=$$CHECKID() D:MENU ^MAGDMENU
 Q
 ;
CHECKID() ; get access code and verify code
 N ACCESS,ACCLEV,COUNT,VERIFY,AV
 K ^TMP("MAG",$J,"DICOM RPC USER")
 S ACCLEV=0 F COUNT=1:1:3 D  Q:ACCLEV
 . N AV,D1,IP,MAGDUZ,PORT,SHOW
 . W $C(27)_"[1;37;44m",#,$C(27,91,72,27,91,74) ; bold blue
 . K ^TMP("MAG",$J),^TMP("MAGM2V",$J)
 . D INIT^MAGOSFIL
 . ;
 . ; Display welcome message
 . ;
 . D WELCOME^MAGDLOGI
 . ;
 . W ! S SHOW=0
 . W "Login using M-to-M RPC Broker Server "
 . W """",$G(^MAGDICOM(2006.563,1,"M-to-M BROKER ADDR")),""""
 . W " on Port ",$G(^MAGDICOM(2006.563,1,"M-to-M BROKER PORT"))
 . W:$G(^MAGDICOM(2006.563,1,"M2M DEBUG")) !,"Warning: DEBUG is turned on."
 . ;
 . ; check access and verify code
 . W !! S ACCESS=$$NOECHO("ACCESS CODE",.VERIFY)
 . I VERIFY="" W ! S VERIFY=$$NOECHO("VERIFY CODE")
 . D  Q:ACCLEV
 . . S ACCLEV=1
 . . S:$$ENCRYPT(ACCESS)'=$G(^MAGDICOM(2006.563,1,"LOGIN ACCESS")) ACCLEV=0
 . . S:$$ENCRYPT(VERIFY)'=$G(^MAGDICOM(2006.563,1,"LOGIN VERIFY")) ACCLEV=0
 . . Q
 . I $$ENCRYPT(VERIFY)=$G(^MAGDICOM(2006.563,1,"LOGIN PROGRAMMER ACCESS")) S ACCLEV=1 Q
 . I $$ENCRYPT(VERIFY)=$G(^MAGDICOM(2006.563,1,"LOGIN SUPPORT")) S ACCLEV=1 Q
 . I $$ENCRYPT(VERIFY)=$G(^MAGDICOM(2006.563,1,"LOGIN PRINT/VIEW ONLY")) S ACCLEV=2 Q
 . S IP=$G(^MAGDICOM(2006.563,1,"M-to-M BROKER ADDR"))
 . S PORT=$G(^MAGDICOM(2006.563,1,"M-to-M BROKER PORT"))
 . I IP'="",PORT'="" D  Q:ACCLEV
 . . N $ET,$ES,PF,SVC
 . . W !
 . . S AV=ACCESS_";"_VERIFY,ACCLEV=0
 . . S $ET="Q:($ES>1)&$Q """" Q:($ES>1)  G VISTERR^"_$T(+0)
 . . S MAGDUZ=$$CONNECT^MAGM2VC(PORT,IP,AV)
 . . Q:ACCLEV  ; Level gets set when a cached profile is used
 . . I 'MAGDUZ D  Q
 . . . N X
 . . . S X=$P(MAGDUZ," - ",3,$L(MAGDUZ)+2) Q:X=""
 . . . W !,"VistA reports: " F  Q:X=""  W $P(X," - ",1) S X=$P(X," - ",2,$L(X)+2) W:X'="" !?5
 . . . W !!,"Press <Enter> to continue: " R X:$G(DTIME,300)
 . . . S SHOW=1
 . . . Q
 . . S X=$$SETCONTX^MAGM2VC("MAG DICOM GATEWAY FULL")
 . . I X S ACCLEV=1
 . . E  S:$$SETCONTX^MAGM2VC("MAG DICOM GATEWAY VIEW") ACCLEV=2
 . . K MAGRPCP("PARAMS")
 . . K SVC S X=$$CALLRPC^MAGM2VC("XUS INTRO MSG","SVC",1),D1=0
 . . W ! S X="" F  S X=$O(SVC(X)) Q:X=""  W !,SVC(X) S D1=1
 . . I D1 W !!,"Press <Enter> to continue: " R X:$G(DTIME,300)
 . . K SVC S X=$$CALLRPC^MAGM2VC("MAG DICOM GET SERVICE INFO","SVC",1)
 . . S X=$$CLOSE^MAGM2VC(),SVC=$G(SVC)
 . . Q:'ACCLEV
 . . S PF=MAGDUZ_"^"_ACCLEV_"^"_$$ENCRYPT^MAGM2VCU(AV)_"^"_SVC
 . . S ^TMP("MAG",$J,"DICOM RPC USER")=PF
 . . S X=$$DECRYP^MAGM2VCU(SVC) S:$P(X,";",1)="" SVC="" S:$P(X,";",2)="" SVC=""
 . . I SVC'="" S ^TMP("MAG",$J,"DICOM RPC SERVICE")=SVC
 . . E  D
 . . . W !!,"No credentials available for access to service account"
 . . . W !,"on VistA System."
 . . . W !!,"This means that no automatic failover can be executed"
 . . . W !,"when regular credentials are changed on the VistA System."
 . . . W !!,"Press <Enter> to continue: " R X:$G(DTIME,300)
 . . . Q
 . . S D1=$$PROFILE(AV),X=0
 . . D  ; P123 - Get Agency code - V=VA, I=IHS
 . . . Q:$$CALLRPC^MAGM2VCU("MAG DICOM GET AGENCY","M",.AGENCY)<0
 . . . S DUZ("AG")=$G(AGENCY)
 . . . Q
 . . D:'D1
 . . . L +^MAGDICOM(2006.563,1,"PROFILE"):1E9
 . . . S D1=$O(^MAGDICOM(2006.563,1,"PROFILE"," "),-1)+1,X=1
 . . . Q
 . . S ^MAGDICOM(2006.563,1,"PROFILE",D1,0)=PF
 . . L:X -^MAGDICOM(2006.563,1,"PROFILE")
 . . D
 . . . N HOST,MACHID
 . . . S HOST=$$HOSTNAME^MAGOSMSC()
 . . . Q:$$CALLRPC^MAGM2VCU("MAG DICOM GET MACHINE ID","M",.MACHID,HOST)<0
 . . . S:$G(MACHID)>0 ^MAGDICOM(2006.563,1,"MACHINE ID")=MACHID
 . . . Q
 . . Q
 . I $$USEPRIN^MAGOSFIL(1)
 . Q:ACCLEV  Q:SHOW
 . W !!,"Not an authorized user for the DICOM Gateway."
 . W !!,"Press <Enter> to continue: " R X:$G(DTIME,300)
 . Q
 Q ACCLEV
 ;
PGMRMODE ; get programmer access code -- entry point from ^MAGDMENU
 N PGMRMODE
 W !! S PGMRMODE=$$NOECHO("PROGRAMMER ACCESS CODE")
 I $$ENCRYPT(PGMRMODE)=$G(^MAGDICOM(2006.563,1,"LOGIN SUPPORT"))
 E  I $$ENCRYPT(PGMRMODE)'=$G(^MAGDICOM(2006.563,1,"LOGIN PROGRAMMER ACCESS")) D  Q
 . W !!,"Not a valid PROGRAMMER ACCESS CODE."
 . Q
 D ^%PMODE
 W "  (Programmer Mode)"
 K ^TMP("MAG",$J,"DICOM MENU OPTION")
 Q
 ;
INIT ; initialize (or change) the security variables
 N X,Z
 ; If first time, force user to enter all the security codes
 I $E($O(^MAGDICOM(2006.563,1,"LOGIN ")),1,6)'="LOGIN " G ALL
 ;
 W !!,"Change Login Security Codes"
 W !,"----------------------------",!
 W !,"  1 - Change ACCESS Code"
 W !,"  2 - Change VERIFY Code"
 W !,"  3 - Change PROGRAMMER ACCESS Code"
 W !,"  4 - Change PRINT/VIEW ONLY Code"
 W !,"  5 - Change SUPPORT Code",!
 W !,"  A - Change ALL THE CODES"
 W !!,"Enter 1-5 or ""A"" to change security codes, <Enter> to exit: "
 R X:$G(DTIME,300) Q:X=""
 G ALL:"Aa"[$E(X),ACCESS:X=1,VERIFY:X=2,PROG:X=3,PRINT:X=4,SUPPORT:X=5
 W " ??? - bad input"
 G INIT
 ;
ALL ; change all the security codes
 N S,Z
 ;
 S Z="LOGIN " F  S Z=$O(^MAGDICOM(2006.563,1,Z)) Q:$E(Z,1,6)'="LOGIN "  D
 . K ^MAGDICOM(2006.563,1,Z)
 . Q
 ;
 F S="ACCESS","VERIFY","PROGRAMMER ACCESS","PRINT/VIEW ONLY","SUPPORT" D
 . F  S Z=$$NEWPW(S) Q:Z>0
 . Q
 Q
 ;
ACCESS ; Update the Access code
 N S S S="ACCESS" D INITPW(S)
 Q
 ;
VERIFY ; Update the Verify code
 N S S S="VERIFY" D INITPW(S)
 Q
 ;
PROG ; Update the Programmer code
 N S S S="PROGRAMMER ACCESS" D INITPW(S)
 Q
 ;
PRINT ; Update the Print/View Only code
 N S S S="PRINT/VIEW ONLY" D INITPW(S)
 Q
 ;
SUPPORT ; Update the Support password
 N S S S="SUPPORT" D INITPW(S)
 Q
 ;
INITPW(S) ; enter a new password
 K ^MAGDICOM(2006.563,1,"LOGIN "_S)
 F  S Z=$$NEWPW(S) Q:Z>0
 Q
 ;
NEWPW(CLASS) ; Update password
 N OLD,SEEN,X,Y,Z
 S Z="LOGIN " F  S Z=$O(^MAGDICOM(2006.563,1,Z)) Q:$E(Z,1,6)'="LOGIN "  D
 . S X=$G(^MAGDICOM(2006.563,1,Z)) Q:X=""
 . Q:$E(Z,7,$L(Z))=CLASS
 . S SEEN(X)=$E(Z,7,$L(Z))
 . Q
 ;
 W !! S X=$$NOECHO("Enter new "_CLASS_" code")
 S Z=$$ENCRYPT(X) I 'Z W "  ??? ",Z,$C(7) S Z=0 Q 0
 W ! S Y=$$NOECHO(" Re-enter "_CLASS_" code (to make sure I got it right)")
 I X'=Y W "   ??? not the same",$C(7) S Z=0 Q 0
 ;
 ; Store the encrypted value of the security variable
 S ^MAGDICOM(2006.563,1,"LOGIN "_CLASS)=Z
 Q 1
 ;
ENCRYPT(S) ; return an encrypted value for a string
 N I,X
 S S=$$UPPER^MAGOSMSC(S)
 I $L(S)<6 Q "need to have six or more characters"
 I S?.E1P.E Q "may not contain punctuation characters"
 I '((S?.E1A.E)&(S?.E1N.E)) Q "must be a mix of alphabetic and numeric characters"
 ; compute a weighted sum of characters, return last 10 digits of sqrt
 S X=0 F I=1:1:$L(S) S X=$A(S,I)+(128*X)
 S X=X**.5,X=$E(X,$L(X)-9,999)
 I $D(SEEN(X)) Q "already used for "_SEEN(X)_" code"
 Q X
 ;
NOECHO(PROMPT,MORE) N X
 F  D  Q:$L(X)<31
 . W PROMPT,": " X ^%ZOSF("EOFF") R X:$G(DTIME,300) E  S X="^"
 . X ^%ZOSF("EON")
 . S MORE=$P(X,";",2),X=$P(X,";",1)
 . Q:$L(X)<31  Q:$L(MORE)<31
 . W !!,"Remember: it is almost certain that you will have to"
 . W !,"          enter this password one day."
 . W !,"Most people are not very good at remembering passwords this long.",!!
 . Q
 Q X
 ;
VISTERR N D1,X
 S:$$USEPRIN^MAGOSFIL(1) ACCLEV=0
 W !!,"Error while connecting to port ",PORT," on ",IP
 I $EC'["DSCON"  W !,"Error message is ",$EC
 E  D
 . W !,"VistA is temporarily not available..." S $EC=""
 . S D1=$$PROFILE($G(AV)) D:D1
 . . N PF,SVC,X
 . . S PF=$G(^MAGDICOM(2006.563,1,"PROFILE",D1,0)),X=$P(PF,"^",2)
 . . Q:'X
 . . S ACCLEV=X,SVC=$P(PF,"^",4)
 . . S ^TMP("MAG",$J,"DICOM RPC USER")=PF
 . . W !!,"Cached Profile Gives ",$S(ACCLEV=1:"Full",1:"View Only")," Access."
 . . S:SVC'="" ^TMP("MAG",$J,"DICOM RPC SERVICE")=SVC
 . . Q
 . Q
 W !!,"Press <Enter> to continue: " R X:$G(DTIME,300)
 Q:$Q ACCLEV Q
 ;
PROFILE(AV) N D1,R,X
 S (D1,R)=0 F  S D1=$O(^MAGDICOM(2006.563,1,"PROFILE",D1)) Q:'D1  D  Q:R
 . S:$$DECRYP^MAGM2VCU($P($G(^MAGDICOM(2006.563,1,"PROFILE",D1,0)),"^",3))=AV R=D1
 . Q
 Q R
 ;
