MAGBRTA6 ;WOIFO/EdM - Action Queue Processor ; 16 Jun 2008 7:33 AM
 ;;3.0;IMAGING;**11,51,54**;MAR 19, 2002;;OCT 28, 2009
 ;; Per VHA Directive 2004-038, this routine should not be modified.
 ;; +---------------------------------------------------------------+
 ;; | Property of the US Government.                                |
 ;; | No permission to copy or redistribute this software is given. |
 ;; | Use of unreleased versions of this software requires the user |
 ;; | to execute a written test agreement with the VistA Imaging    |
 ;; | Development Office of the Department of Veterans Affairs,     |
 ;; | telephone (301) 734-0100.                                     |
 ;; | The Food and Drug Administration classifies this software as  |
 ;; | a medical device.  As such, it may not be changed in any way. |
 ;; | Modifications to this software may result in an adulterated   |
 ;; | medical device under 21CFR820, the use of which is considered |
 ;; | to be a violation of US Federal Statutes.                     |
 ;; +---------------------------------------------------------------+
 ;;
 Q
 ;
DICOM ; DICOM type file copy for routing
 N CALLEDAE ;- AE Title of remote location (Called Application Entity)
 N XMIT ;----- Timestamp (start of transmission)
 ; DEST ;----- Pointer to destination in 2006.587
 N LDEST ;---- Pointer to destination in 2006.585
 N NAME ;----- Name of destination
 ;
 S NAME=$P(DEST,"^",2),LDEST=0
 S:NAME'="" LDEST=$O(^MAGDICOM(2006.585,"B",NAME,""))
 I 'LDEST D STATUS^MAGBRTA5(D0,"SENT",LOCATION) Q
 ;
 S X=$G(^MAGDICOM(2006.585,LDEST,0))
 S NET=$P(X,"^",4)_" "_$P(X,"^",5) Q:$TR(NET," ")=""
 S CALLEDAE=$P(X,"^",2)
 S:FILSRC_FILDST["~NO NETWORK LOCATION DEFINED" (FILSRC,FILDST)="-1~No routable files found for image "_IMAGE
 I FILSRC<0 D STATUS^MAGBRTA5(D0,"SENT",LOCATION) Q
 I FILDST<0 D STATUS^MAGBRTA5(D0,"SENT",LOCATION) Q
 I FILSRC="" D STATUS^MAGBRTA5(D0,"SENT",LOCATION) Q
 S (OK,RETRY)=0 F  D  Q:OK  Q:RETRY>$P(NODE3,"^",3)  Q:STOP
 . N CMD ;--- MS-DOS command to be executed
 . N E ;--- Array containing error output
 . N O ;--- Array containing standard output
 . N S ;--- Processing Step
 . D CHECK^MAGBRTA4 Q:STOP
 . S CMD="Send_Image -c "_CALLEDAE_" -m 100000 -q -Z "_NET_" "_FILDST
 . D MESSAGE^MAGDMLOG(1,0,"Transmit "_FILSRC_" to "_CALLEDAE,"t")
 . S XMIT=$$NOW^XLFDT()
 . S X=$$HOSTCMD^MAGOSMSC(CMD,.O,.E)
 . S S=0,I=0 F  S I=$O(O(I)) Q:I=""  D  Q:OK
 . . I O(I)[" bytes transmitted of " S S=1
 . . I S=1,O(I)["Store Response" S S=2
 . . I S=2,O(I)["Successful operation" S OK=1
 . . I O(I)[" failed to open file " S OK=666
 . . I O(I)[" Peer aborted Association " S OK=13
 . . Q
 . I OK'=1 D  S RETRY=RETRY+1
 . . D MESSAGE^MAGDMLOG(1,0,CMD,"t")
 . . S I="" F  S I=$O(O(I)) Q:I=""  D MESSAGE^MAGDMLOG(1,0,O(I),"t")
 . . S I="" F  S I=$O(E(I)) Q:I=""  D MESSAGE^MAGDMLOG(1,0,E(I),"t")
 . . Q
 . Q
 D STATUS^MAGBRTA5(D0,$S(OK=666:"NOT FOUND",OK:"SENT",1:"FAILED"),LOCATION)
 Q:'OK
 S:$$CALLRPC^MAGM2VCU("MAG DICOM ROUTE LOG XMIT","MT",.OUT,D0,DEST,LOCATION,$S(OK=666:"NOT FOUND",OK:"SENT",1:"FAILED"),FILDST,XMIT,2)<0 FATAL=1
 Q
 ;
