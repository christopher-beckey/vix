MAGDBB ;WOIFO/EdM/RRB - ET Phone Home ; 19 Dec 2008 3:01 PM
 ;;3.0;IMAGING;**9,10,11,30,51,69,54**;MAR 19, 2002;;OCT 28, 2009
 ;; Per VHA Directive 2004-038, this routine should not be modified.
 ;; +---------------------------------------------------------------+
 ;; | Property of the US Government.                                |
 ;; | No permission to copy or redistribute this software is given. |
 ;; | Use of unreleased versions of this software requires the user |
 ;; | to execute a written test agreement with the VistA Imaging    |
 ;; | Development Office of the Department of Veterans Affairs,     |
 ;; | telephone (301) 734-0100.                                     |
 ;; | The Food and Drug Administration classifies this software as  |
 ;; | a medical device.  As such, it may not be changed in any way. |
 ;; | Modifications to this software may result in an adulterated   |
 ;; | medical device under 21CFR820, the use of which is considered |
 ;; | to be a violation of US Federal Statutes.                     |
 ;; +---------------------------------------------------------------+
 ;;
 Q
 ;
IDLE(I,W,BB) ; Show "idle" marker and produce report if first call of the day
 N X
 I $G(W),$$USEPRIN^MAGOSFIL(1) S I=$G(I)+1 S:I<1 I=1 S:I>4 I=1 W $E("-\|/",I),$C(8)
 H 1
 L +^MAGDICOM(2006.598,"RETRY"):0 I  D RETRY^MAGDMMSG L -^MAGDICOM(2006.598,"RETRY")
 S X=$G(^TMP("MAG",$J,"SERVICE REMINDER TIME")) D:X
 . N H
 . S H=$H,H=H*86400+$P(H,",",2)
 . D:H>X REMIND^MAGDMENA
 . Q
 Q:$G(BB)
 Q:$G(^MAGDICOM(2006.563,1,"DAILY REPORT"))'<$H
 S (^TMP("MAG",$J,"DAILY REPORT"),X)=$G(^TMP("MAG",$J,"DAILY REPORT"))+1
 I X>10 D  Q
 . S ^MAGDICOM(2006.563,1,"DAILY REPORT")=(+$H)_",tried 10 times"
 . K ^TMP("MAG",$J,"DAILY REPORT")
 . Q
 L +^MAGDICOM("MAGDBB"):2 E  Q
 W:$G(W) !,"Collecting Daily Statistics",!,"Just a minute...",!
 ; If a different process just compiled the report, don't do it again...
 D:$G(^MAGDICOM(2006.563,1,"DAILY REPORT"))<$H
 . N TASK,X
 . S X="MAG VISTA CHECKSUMS"
 . I $G(^MAGDICOM(2006.563,1,X)),$$CALLRPC^MAGM2VCU(X,"",.TASK,$$MAGDBB^MAGDMFBE())
 . S ^MAGDICOM(2006.563,1,"DAILY REPORT")=(+$H)_","_$$MSG()
 . K ^TMP("MAG",$J,"DAILY REPORT")
 . Q
 L -^MAGDICOM("MAGDBB")
 Q
 ;
 ; Transmit on a daily basis:
 ;  -- station info
 ;  -- condensed error trap info
 ;  -- routine check sums
 ;  -- routing tallies
 ;  -- master file contents
 ;
MSG() N H,INFO,L,LOCATION,NL,OFF,PHY,POSTPORT,R,SID,STN,STS,T,V,VER,X,Z
 N $ET,TRAP
 I $G(^MAGDICOM(2006.563,1,"MAGDBB"))="NO" Q "Turned Off"
 ;
 S LOCATION=$G(^MAGDICOM(2006.563,1,"LOCATION")) Q:'LOCATION "Location not defined"
 Q:$$CALLRPC^MAGM2VCU("MAG DICOM ET PHONE HOME","M",.INFO,LOCATION)<0 "Cannot call RPC"
 Q:($G(INFO(1))="")!($G(INFO(2))="") "RPC returned no information"
 S SID=INFO(2),INFO=2
 ;
 S $ET="D TRAP^"_$T(+0),TRAP=$ST+2
 S NL=$C(13,10),V="",H=$H,PHY=""
 S OFF=$G(^MAGDICOM(2006.563,1,"POST OFFICE")) S:OFF="" OFF="10.2.29.131"
 S POSTPORT=$G(^MAGDICOM(2006.563,1,"POST PORT")) S:'POSTPORT POSTPORT=25
 S Z=$$TIMEZONE^MAGDMSGT() ; Get TIMEZONE offset
 I '$G(Z) S Z="-0500" ; Default TIMEZONE offset
 D MSGEX
 Q V
 ;
MSGEX S STS=$$TCPCLOSE^MAGOSTCP(OFF,POSTPORT)
 S STS=$$TCPOPEN^MAGOSTCP(OFF,POSTPORT,1)
 I STS<0 S V=STS Q
 S STS=$$TCPUSE^MAGOSTCP(OFF,POSTPORT)
 I STS<0 S V=STS Q
 ; Expected result: 220 <mailserver> <version> ready
 R R:1000 E  S V=V_"; Timed Out" Q
 S:+R'=220 V=V_";"_R
 W "HELO med.va.gov",NL S STS=$$TCPFLUSH^MAGOSTCP(OFF,POSTPORT)
 ; Expected result: 250 OK
 R R:1000 E  S V=V_"; Timed Out" Q
 S:+R'=250 V=V_";"_R
 W "MAIL FROM:<ET.Phone.Home@"_SID_">"_NL S STS=$$TCPFLUSH^MAGOSTCP(OFF,POSTPORT)
 ; Expected result: 250 OK
 R R:1000 E  S V=V_"; Timed Out" Q
 S:+R'=250 V=V_";from "_R
 F T=$$MAGDBB^MAGDMFBE() D
 . W "RCPT TO:<"_T_">"_NL S STS=$$TCPFLUSH^MAGOSTCP(OFF,POSTPORT)
 . ; Expected result: 250 OK
 . R R:1000 E  S V=V_"; Timed Out" Q
 . S:+R'=250 V=V_";to "_R
 . Q
 Q:V["; Timed Out"
 W "DATA",NL S STS=$$TCPFLUSH^MAGOSTCP(OFF,POSTPORT)
 ; Expected result: 354 Send data.  End with CRLF.CRLF
 R R:1000 E  S V=V_"; Timed Out" Q
 S:+R'=354 V=V_";"_R
 W "Subject: Daily Report",NL
 D
 . N D,M,W,Y
 . W "Date: " D H2YMD(+H,.Y,.M,.D,.W)
 . W W,", ",D," ",$P("Jan Feb Mar Apr May Jun Jul Aug Sep Oct Nov Dec"," ",M)
 . W " ",Y S Y=$P(H,",",2)
 . W " ",$TR($J(Y\3600,2)," ",0),":",$TR($J(Y\60#60,2)," ",0)," ",Z
 . Q
 W $C(13,10,13,10)
 ;
 W "SID="_SID_NL
 W "DT="_$$FM($H)_NL
 D GETIP^MAGDBB2 ; Get IP-address(es)
 S X=$T(+2^MAGDVRSN),VER=$P(X,";;",2,$L(X)+2)
 W "BLD="_VER_NL
 S X=$G(^MAGDICOM(2006.563,1,"SYSTEM TITLE")) W:X'="" "TTL="_X_NL
 W:PHY'="" "PHY="_PHY_NL
 W "LOC="_LOCATION_NL
 W "EML="_$G(^MAGDICOM(2006.563,1,"MAILGROUP"))_NL
 D  ; License Info
 . N I,LIC
 . Q:$$LICBB^MAGOSMSC(.LIC)<0
 . S I="" F  S I=$O(LIC(I)) Q:I=""  W "Lic"_I_"="_LIC(I)_NL
 . Q
 D  ; Get Error Counts
 . N C,D,E,I
 . S E="%ER"
 . S D="" F  S D=$O(^UTILITY(E,D)) Q:D=""  D
 . . S I="" F  S I=$O(^UTILITY(E,D,I)) Q:I=""  D
 . . . S X=$G(^UTILITY(E,D,I,0)) Q:X=""
 . . . S C(D,X)=$G(C(D,X))+1
 . . . Q
 . . Q
 . S D="" F  S D=$O(^ERRORS(D)) Q:D=""  D
 . . S I="" F  S I=$O(^ERRORS(D,I)) Q:I=""  D
 . . . S X=$G(^ERRORS(D,I,"STACK",0,"V","$ZE")) Q:X=""
 . . . S C(D,X)=$G(C(D,X))+1
 . . . Q
 . . Q
 . S D="" F  S D=$O(C(D)) Q:D=""  D
 . . W "ERD="_$$FM(D)_NL
 . . S X="" F  S X=$O(C(D,X)) Q:X=""  D
 . . . W "ERR="_X_NL
 . . . W "ERC="_C(D,X)_NL
 . . . Q
 . . Q
 . Q
 D  ; Get routine check sums
 . N R
 . S R="" F  S R=$O(^$R(R)) Q:R=""  D
 . . W "RTN="_R_NL
 . . W "CHK="_$$CHK^MAGDVRSN(R)_"^"_$$CHK2^MAGDVRSN(R)_NL
 . . Q
 . Q
 D ROUTE^MAGDBB2 ; Routing Statistics
 D MASTER^MAGDBB2
 D MENU^MAGDBB2 ; Menu Options
 D TEXT^MAGDBB2 ; Text Gateway Statistics
 D MODALITY^MAGDBB2 ; Modality Worklist Statistics
 D IMGPROC^MAGDBB2 ; Image Processing Statistics
 ;
 W NL,".",NL S STS=$$TCPFLUSH^MAGOSTCP(OFF,POSTPORT)
 ; Expected result: 250 OK
 R R:1000 E  S V=V_"; Timed Out" Q
 S:+R'=250 V=V_";"_R
 W "QUIT",NL S STS=$$TCPFLUSH^MAGOSTCP(OFF,POSTPORT)
 ; Expected result: 221 OK
 R R:1000 E  S V=V_"; Timed Out" Q
 S:+R'=221 V=V_";"_R
 S STS=$$TCPCLOSE^MAGOSTCP(OFF,POSTPORT)
 ;
 S STN=$$UPPER^MAGOSMSC(STN)
 Q:$$CALLRPC^MAGM2VCU("MAG DICOM WORKSTATION VERSION","T",.INFO,STN,VER)<0
 Q
 ;
FM(H) N D,M,Y
 D H2YMD(H,.Y,.M,.D)
 Q Y-1700*100+M*100+D
 ;
H2YMD(H,Y,M,D,W) ; Convert $H value to year, month and day numbers
 N I,L,R,Z
 K Y,M,D
 S W=$P("Thu Fri Sat Sun Mon Tue Wed"," ",H#7+1),H=H>21914+H
 S L=H\1461,R=H#1461,Y=L*4+1841+(R\365),D=R#365,M=1
 S Z=$S(Y#4:0,Y#100:1,Y#400:0,1:1)
 I R=1460,L'=14 S D=365,Y=Y-1
 F I=31,Z+28,31,30,31,30,31,31,30,31,30 Q:I'<D  S M=M+1,D=D-I
 S:D=0 Y=Y-1,M=12,D=31
 Q
 ;
TRAP ;
 I $G(OFF)'="" S X=$$TCPCLOSE^MAGOSTCP(OFF,POSTPORT) S OFF=""
 S V=" "_$EC
 S:$G(TRAP)=$ST $EC=""
 Q:$Q "" Q
 ;
ERRLOG(MAGM2VE) N D,E,I,L,N
 S E="%ER",D=+$H,L=100
 L +^UTILITY(E):1E9 ; Background process MUST wait
 S (^UTILITY(E,D,0),I)=$G(^UTILITY(E,D,0))+1
 L -^UTILITY(E)
 S ^UTILITY(E,D,I,0)="$ZE="_$G(MAGM2VE(1)) K MAGM2VE(1)
 S ^UTILITY(E,D,I,0.5)="$ECODE=Error in remote procedure"
 S ^UTILITY(E,D,I,1)="$H="_$H
 S ^UTILITY(E,D,I,2)="$J=Remote"
 S ^UTILITY(E,D,I,3)="$I=VistA"
 S N="" F  S N=$O(MAGRPCP("REQUEST","XML",N)) Q:N=""  D
 . S L=L+1,^UTILITY(E,D,I,L)="XML"_N_"="_MAGRPCP("REQUEST","XML",N)
 . Q
 S N="" F  S N=$O(MAGM2VE(N)) Q:N=""  D
 . S L=L+1,^UTILITY(E,D,I,L)="MSG"_N_"="_MAGM2VE(N)
 . Q
 S ^UTILITY(E,D,I,100)=L-100
 Q
