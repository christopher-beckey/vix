MAGDFND4 ;WOIFO/PMK/JSL/SAF - DICOM FIND Handler ; 27 Jul 2010 7:28 AM
 ;;3.0;IMAGING;**10,11,30,50,54,49,123**;Mar 19, 2002;Build 60;Jul 16, 2012
 ;; Per VHA Directive 2004-038, this routine should not be modified.
 ;; +---------------------------------------------------------------+
 ;; | Property of the US Government.                                |
 ;; | No permission to copy or redistribute this software is given. |
 ;; | Use of unreleased versions of this software requires the user |
 ;; | to execute a written test agreement with the VistA Imaging    |
 ;; | Development Office of the Department of Veterans Affairs,     |
 ;; | telephone (301) 734-0100.                                     |
 ;; | The Food and Drug Administration classifies this software as  |
 ;; | a medical device.  As such, it may not be changed in any way. |
 ;; | Modifications to this software may result in an adulterated   |
 ;; | medical device under 21CFR820, the use of which is considered |
 ;; | to be a violation of US Federal Statutes.                     |
 ;; +---------------------------------------------------------------+
 ;;
 Q
 ;
ENTRY() ; called by ^MAGDFND3 to lookup patient/study info on main VistA system
 N INFO,IMGSVC
 ;
 K DFN
 ; lookup what kind of query to perform
 S X=^MAGDICOM(2006.583,WLSTIEN,0)
 S STATNUMB=$P(X,"^",2),IMGSVC=$P(X,"^",3)
 I IMGSVC="RAD" D  ; radiology service
 . D RAD
 . Q
 E  I IMGSVC="CON" D  ; not supported for Healthcare Providers
 . S STATUS="" ; for Healthcare Providers
 . Q
 S (ADDRESS,REQPRPRI,SPSLOC)=""
 Q $$PATIENT()
 ;
RAD ; radiology lookup - called from ^MAGDIW3B
 N I,X
 ; check that the service account access/verify code are good
 S X=$$RECONCT^MAGDMENA()
 I 'X D  Q
 . N MSG
 . S (MSG(1),MSG(5))=""
 . S MSG(2)="Fatal Modality Worklist Error"
 . S MSG(3)="Access Code / Verify Code for VistA Login (RPC) are incorrect."
 . S MSG(4)="MWL Error: "_X
 . D MESSAGE^MAGDMLOG(2,0,.MSG,"BV")
 . Q
 Q:$$CALLRPC^MAGM2VCU("MAG DICOM LOOKUP RAD STUDY","T",.INFO,$G(CASENUMB))<0
 Q:$G(INFO(1))'>0
 Q:'$G(INFO(2))  ; RADPT1
 Q:'$G(INFO(3))  ; RADPT2
 S DFN=$G(INFO(10)) Q:'DFN
 S MODALITY=$G(INFO(13)) D:MODALITY[","
 . N I,KM,OK,P
 . S KM=$G(KEY("MODALITY"))
 . S:KM="<unknown>" KM="" S:KM="*" KM=""
 . I KM="" S MODALITY=$P(MODALITY,",",1) Q
 . S OK=0 F I=1:1:$L(KM,",") D  Q:OK
 . . S P=$P(MODALITY,",",I) Q:P=""
 . . I $$WLDMATCH(P,KM) S MODALITY=P,OK=1 Q
 . . Q
 . S:'OK MODALITY=$P(MODALITY,",",1)
 . Q
 S STATNUMB=$G(INFO(14))
 ;
 ; Study variables
 S Y=INFO(3)_"."_INFO(4)_"."_$TR(CASENUMB,"-",".")
 S STUDYUID=$$STUDY^MAGDUID1(STATNUMB,Y)
 S SCHSDYDT=9999999.9999-INFO(3) ; 9's complement conversion
 S SCHSDYDT=$E(SCHSDYDT_"000000",1,14)
 S CPTNAME=$G(INFO(7))
 S CPTCODE=$G(INFO(6))
 S CPTSCHM="C4" ; assume version 4 of CPT
 S PROCIEN=$G(INFO(5))
 S STATUS=$G(INFO(9))
 I STATUS="CANCELLED" S DFN=0 Q  ; don't return cancelled exams
 S PROCDESC=$G(INFO(12))
 S PREGSTAT=$G(INFO(15)) S:PREGSTAT="" PREGSTAT="u"
 S ACNUMB=$G(INFO(16)) S:ACNUMB="" ACNUMB=CASENUMB
 S STUDYID=$P(ACNUMB,"-",$L(ACNUMB,"-")) ; short case #
 Q
 ;
PATIENT() ; patient lookup on VistA HIS
 N DIQUIET,INFO,VADM,VAIN,VAPA,VASD
 Q:'$G(DFN) 0 ; No valid Patient Identifier
 ;
 Q:$$CALLRPC^MAGM2VCU("MAG DICOM GET PATIENT","T",.INFO,DFN)<0 0
 D VADPT^MAGDRPC0(.INFO)
 ;
 S PNAME=$TR(VADM(1),", .","^^")
 I $$ISIHS^MAGSPID() S (PID,DCMPID)=VA("PID") ; P123 IHS
 E  S PID=$P(VADM(2),"^",1),DCMPID=$P(VADM(2),"^",2) ; p123 VA
 S DOB=$P(VADM(3),"^",1),$E(DOB)=17+$E(DOB)
 S:$E(DOB,5,6)="00" $E(DOB,5,6)="01" ; default unknown month to Jan
 S:$E(DOB,7,8)="00" $E(DOB,7,8)="01" ; default unknown date to first
 S SEX=$P(VADM(5),"^",1) S:SEX="M" PREGSTAT="n"
 S RACE=$P($G(VADM(12,1)),"^",2) D ETHNIC ; set race to a 16-character value
 S ICN=$G(VAICN) ; Other PID values (SITEDFN is set in VADPT^MAGDRPC0)
 S PREGSTAT=$S(PREGSTAT="y":3,PREGSTAT="n":1,1:4) ; translate to DICOM
 S WARD=$P($G(VAIN(4)),"^",2)
 Q 1
 ;
ETHNIC ; DICOM allows only a short 16-character string for ethnic group names
 ; the text values for race are standard, but IENs vary between sites
 ; convert long name to a 16-character ethnic group abbreviation
 ; (this entry point is also called by ^MAGDHRC3, ^MAGDIW2A, and ^MAGDIW3B)
 I RACE?4N1"-"1N D  ; convert from HL7 representation to text
 . I RACE="0000-0" S RACE="DECLINED TO ANSWER" Q
 . I RACE="1002-5" S RACE="AMERICAN INDIAN OR ALASKA NATIVE" Q
 . I RACE="2028-9" S RACE="ASIAN" Q
 . I RACE="2054-5" S RACE="BLACK OR AFRICAN AMERICAN" Q
 . I RACE="2076-8" S RACE="NATIVE HAWAIIAN OR OTHER PACIFIC ISLANDER" Q
 . I RACE="2106-3" S RACE="WHITE" Q
 . I RACE="2135-2" S RACE="HISPANIC OR LATINO" Q
 . I RACE="2186-5" S RACE="NOT HISPANIC OR LATINO" Q
 . I RACE="9999-4" S RACE="UNKNOWN BY PATIENT" Q
 . Q
 I RACE="AMERICAN INDIAN OR ALASKA NATIVE" S RACE="NATIVE AMERICAN"
 E  I RACE="ASIAN OR PACIFIC ISLANDER" S RACE="ASIAN/PACIFIC IS"
 E  I RACE="BLACK, NOT OF HISPANIC ORIGIN" S RACE="BLACK, NOT HISP."
 E  I RACE="BLACK,NOT OF HISPANIC ORIGIN" S RACE="BLACK, NOT HISP."
 E  I RACE="WHITE, NOT OF HISPANIC ORIGIN" S RACE="WHITE, NOT HISP."
 E  I RACE="WHITE,NOT OF HISPANIC ORIGIN" S RACE="WHITE, NOT HISP."
 S RACE=$E(RACE,1,16) ; truncate race
 F  Q:$E(RACE,$L(RACE))'=" "  S $E(RACE,$L(RACE))=""
 Q
 ;
WLDMATCH(VAL,WILD) ;
 ;
 ; Returns true if VAL=WILD (Val=Actual value, Wild=Wildcard)
 ;
 ; Wild characters are:
 ;   ?   matches any single character
 ;   *   matches any string of characters
 ;
 N I,M
 F  Q:VAL=""  Q:WILD=""  D
 . I $E(VAL,1)=$E(WILD,1) S VAL=$E(VAL,2,$L(VAL)),WILD=$E(WILD,2,$L(WILD)) Q
 . I $E(WILD,1)="?" S VAL=$E(VAL,2,$L(VAL)),WILD=$E(WILD,2,$L(WILD)) Q
 . I $E(WILD,1)="*" D  Q:M
 . . I WILD="*" S (VAL,WILD)="",M=1 Q
 . . S WILD=$E(WILD,2,$L(WILD)),M=0
 . . F I=1:1:$L(VAL) I $$WLDMATCH($E(VAL,I,$L(VAL)),WILD) S M=1,VAL=$E(VAL,I,$L(VAL)) Q
 . . Q
 . S VAL="!",WILD=""
 . Q
 Q:VAL'="" 0 Q:WILD'="" 0 Q 1
 ;
