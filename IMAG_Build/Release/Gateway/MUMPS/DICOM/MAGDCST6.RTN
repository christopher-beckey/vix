MAGDCST6 ;WOIFO/PMK - Statistics Report ; 05/06/2004  08:32
 ;;3.0;IMAGING;**10,11,30**;MAR 19, 2002;;NOV 5, 2004
 ;; Per VHA Directive 2004-038, this routine should not be modified.
 ;; +---------------------------------------------------------------+
 ;; | Property of the US Government.                                |
 ;; | No permission to copy or redistribute this software is given. |
 ;; | Use of unreleased versions of this software requires the user |
 ;; | to execute a written test agreement with the VistA Imaging    |
 ;; | Development Office of the Department of Veterans Affairs,     |
 ;; | telephone (301) 734-0100.                                     |
 ;; | The Food and Drug Administration classifies this software as  |
 ;; | a medical device.  As such, it may not be changed in any way. |
 ;; | Modifications to this software may result in an adulterated   |
 ;; | medical device under 21CFR820, the use of which is considered |
 ;; | to be a violation of US Federal Statutes.                     |
 ;; +---------------------------------------------------------------+
 ;;
 ; Statistics for each of the instruments
 ;
 N HALT,INFO,INSTR,LOCATION,LOCNAM,MAGDATE,OK,START,STOP,T,TODAY,X
 S LOCATION=$G(^MAGDICOM(2006.563,1,"LOCATION"))
 I 'LOCATION D  Q
 . W !,"Location of DICOM Gateway not known."
 . Q
 ;
 Q:$$CALLRPC^MAGM2VCU("MAG DICOM FILEMAN GET","M",.LOCNAM,4,LOCATION,.01)<0
 ;
 D SETUP^MAGDMENU,DT^DICRW
 W @IOF,"VistA DICOM Image Storage Server Statistics",!
 ;
 S OK=0 F  D  Q:OK
 . W !!,"Please enter beginning date: T // " R X:$G(DTIME,300) E  S X="^"
 . I X["^" S OK=-1 Q
 . I X="" S X="T" W X
 . D ^%DT I Y>0 S (MAGDATE,START)=Y,OK=1 Q
 . W !!,"Enter a start date for the server statistics report."
 . W !,"When today's date is selected, the report will be shown"
 . W !,"repeatedly, until terminated by the user."
 . Q
 Q:OK<0
 S TODAY=(DT=START),STOP=START
 ;
 I 'TODAY S OK=0 F  D  Q:OK
 . W !!,"Please enter ending date: T // " R X:$G(DTIME,300) E  S X="^"
 . I X["^" S OK=-1 Q
 . I X="" S X="T" W X
 . D ^%DT I Y>0,Y'<START S (MAGDATE,STOP)=Y,OK=1 Q
 . W !!,"Enter a end date for the server statistics report."
 . W !,"Note that the end date must not be earlier than the start date."
 . Q
 Q:OK<0
 ;
 S HALT=0 F  D  Q:HALT
 . N DATE,STAT1,STAT2
 . ;
 . I $$CALLRPC^MAGM2VCU("MAG DICOM IMAGE AUDIT GET","M",.INFO,LOCATION,START,STOP)<0 S HALT=1 Q
 . ;
 . S I=$O(INFO("")) I I="" D  S HALT=1 Q
 . . W !!,"No data collected ",$S(START'=STOP:"between ",1:"on ")
 . . W $$DATE(START) W:START'=STOP " and ",$$DATE(STOP) W "..."
 . . Q
 . ;
 . F  S I=$O(INFO(I)) Q:I=""  Q:INFO(I)
 . I I="" D  S HALT=1 Q
 . . W !!,"No data collected ",$S(START'=STOP:"between ",1:"on ")
 . . W $$DATE(START) W:START'=STOP " and ",$$DATE(STOP) W "..."
 . . Q
 . ;
 . S I=I-1,DATE="" F  S I=$O(INFO(I)) Q:I=""  D  Q:HALT
 . . S X=INFO(I)
 . . D:$P(X,"^",1)'=DATE  Q:HALT
 . . . I DATE'="" D  Q:HALT
 . . . . N X
 . . . . W !!,"Display more days?  Yes// " R X:$G(DTIME,30) E  S X="Y"
 . . . . I X="" S X="Y" W "Yes"
 . . . . I X["^" S HALT=1 Q
 . . . . I "Nn"[$E(X,1) S HALT=1 Q
 . . . . Q
 . . . S DATE=$P(X,"^",1)
 . . . W @IOF,!?80-$L(LOCNAM)/2,LOCNAM,!
 . . . W ?10,"VistA DICOM Image Storage Server Statistics for ",$$DATE(DATE),!
 . . . W !,"Instrument",?20,"Images Acquired",?50,"Images Processed"
 . . . W !,"----------",?20,"---------------",?50,"----------------"
 . . . Q
 . . S INSTR=$P(X,"^",3)
 . . W !?(4-($L(INSTR)/2)),INSTR
 . . S (STAT1,STAT2)="*** N O N E ***"
 . . S:$P(X,"^",2) STAT1=$J($P(X,"^",2),5)_"  ("_$$TIME($P(X,"^",4))_")"
 . . S:$P(X,"^",5) STAT2=$J($P(X,"^",5),5)_"  ("_$$TIME($P(X,"^",6))_")"
 . . S:STAT1=STAT2 STAT2="--- same ---"
 . . W ?20,STAT1 W:$P(X,"^",5) ?52,STAT2
 . . Q
 . I 'TODAY S HALT=1 Q
 . W !!,"Continue  Yes// " R X:$G(DTIME,30) E  S X="Y"
 . I X="" S X="Y" W "Yes"
 . I X["^" S HALT=1 Q
 . I "Nn"[$E(X,1) S HALT=1 Q
 . I "Yy"[$E(X,1) Q
 . W !!,"Please answer ""Yes"" to update the display,"
 . W !,"or ""No"" to stop this program."
 . W !!,"Press <Enter> to continue: " R X:$G(DTIME,30)
 . Q
 Q
 ;
DATE(X) Q $J(+(X#100),2)_"-"_$P("Jan Feb Mar Apr May Jun Jul Aug Sep Oct Nov Dec"," ",X\100#100)_"-"_(X\10000+1700)
 ;
TIME(X) N T
 S T=$E($P(X,".",2)_"0000",1,4)
 Q $E(T,1,2)_":"_$E(T,3,4)
 ;
