MAGDDEL2 ;WOIFO/PMK - Program to remove old DICOM files ; 05/04/2007 08:20
 ;;3.0;IMAGING;**10,11,51,69,54**;MAR 19, 2002;;OCT 28, 2009
 ;; Per VHA Directive 2004-038, this routine should not be modified.
 ;; +---------------------------------------------------------------+
 ;; | Property of the US Government.                                |
 ;; | No permission to copy or redistribute this software is given. |
 ;; | Use of unreleased versions of this software requires the user |
 ;; | to execute a written test agreement with the VistA Imaging    |
 ;; | Development Office of the Department of Veterans Affairs,     |
 ;; | telephone (301) 734-0100.                                     |
 ;; | The Food and Drug Administration classifies this software as  |
 ;; | a medical device.  As such, it may not be changed in any way. |
 ;; | Modifications to this software may result in an adulterated   |
 ;; | medical device under 21CFR820, the use of which is considered |
 ;; | to be a violation of US Federal Statutes.                     |
 ;; +---------------------------------------------------------------+
 ;;
 Q
 ;
ENTRY ; entry point from ^MAGDHRC
 N DRIVE,FREE,MINSPACE,PCT,TOTAL
 S DRIVE=$$DRIVE^MAGDSTAT($G(^MAGDICOM(2006.563,1,"DATA PATH",1,0)))
 D DISK1^MAGDSTAT(DRIVE) ; get disk space
 I 'TOTAL D  Q  ; the drive may not be mapped
 . W !!,"The disk-share """_DRIVE_""" no longer appears"
 . W !,"to be defined on this system."
 . W !!,"Press <Enter> to continue..." R X:DTIME
 . Q
 D MINSPACE ; check the minimum free space on the disk
 ; delete old DICOM message files to make more room
 I PCT<MINSPACE D  ; automatically do the delete
 . N DTIME
 . S DTIME=10 ; non-interactive, so set the timer to a short value
 . D DICOM
 . Q
 Q
 ;
DICOM ; Entry point from ^MAGDMENU to delete old DICOM messages
 N CHANNELS,CURRENT,CUTOFF,DELDATE,DIRNUMB,LINES,OK,PAGE,PATH,QLETTER,X
 W !!,"Ready to remove old DICOM files from servers?  y// " R X:DTIME E  S X="^"
 I X="" S X="yes" W X
 I "Yy"'[$E(X) Q
 D CUTOFF^MAGDDEL Q:CUTOFF<1
 ;
 ; get delete date range
 S OK=0 F  D  Q:OK
 . W !!,"Delete DICOM files that are older than how many days?"
 . W "  ",CUTOFF,"// " R X:DTIME E  S X="^"
 . I X="" S X=CUTOFF W X S OK=1 Q
 . I X="^" S OK=1 Q
 . I X?.N,X>0 S OK=1 Q
 . W !!,"Enter a positive integer number, indicating the number of days"
 . W !,"that files are to be retained."
 . Q
 Q:X["^"
 ;
 S DELDATE=$$HTFM^XLFDT($H-X,0)+17000000
 ;
 S PAGE=23,LINES=3
 S CHANNELS=$O(^MAGDICOM(2006.563,1,"DATA PATH"," "),-1)
 I 'CHANNELS S CHANNELS=2  ; norm is 2 channels (DATA1 & DATA2)
 F DIRNUMB=1:1:CHANNELS D
 . ; find the old directories and delete them
 . S PATH=^MAGDICOM(2006.563,1,"DATA PATH",DIRNUMB,0)
 . S QLETTER=""
 . F  S QLETTER=$O(^MAGDICOM(2006.577,DIRNUMB,1,"B",QLETTER)) Q:QLETTER=""  D  ;
 . . N D1
 . . S D1=$O(^MAGDICOM(2006.577,DIRNUMB,1,"B",QLETTER,""))
 . . S CURRENT=+$PIECE(^MAGDICOM(2006.577,DIRNUMB,1,D1,0),"^",2)
 . . D FINDOLD
 . . Q
 . Q
 Q
 ;
FINDOLD ; find the old directories
 N DIRDATE,DIRNAME,DIRPATH,I,SAVEDIR,TEXTDATE
 S DIRPATH=PATH_"\"_QLETTER_"*."
 D NEWLINE(1) W "Scanning the ",QLETTER," queue: ",DIRPATH
 S X=$$GETDIR^MAGOSDIR(DIRPATH,"D")
 I X<0 D NEWLINE(2) W "*** Host File System Error: ",X," ***" Q
 I 'X W "  *** Not on file ***" Q
 ;
 ; save ^TMP(...,"DIR LIST",...) under ^TMP(...,"RMDIR LIST",...)
 K ^TMP("MAG",$J,"RMDIR LIST")
 M ^TMP("MAG",$J,"RMDIR LIST")=^TMP("MAG",$J,"DIR LIST")
 ;
 ; don't delete the most current three directories, no matter how old
 S SAVEDIR=CURRENT\100
 F I=1:1:3 D  ;
 . S DIRNAME=QLETTER_$E(100000+SAVEDIR,2,6)
 . D NEWLINE(1) W "Save Directory: ",DIRNAME
 . K ^TMP("MAG",$J,"RMDIR LIST",DIRNAME)
 . S SAVEDIR=SAVEDIR-1 I SAVEDIR<0 S SAVEDIR=99999
 . Q
 ;
 ; delete the old directories
 S DIRNAME=""
 F  S DIRNAME=$O(^TMP("MAG",$J,"RMDIR LIST",DIRNAME)) Q:DIRNAME=""  D
 . S TEXTDATE=$P(^TMP("MAG",$J,"RMDIR LIST",DIRNAME),";",2)
 . S DIRPATH=PATH_"\"_DIRNAME
 . S DIRDATE=$P(TEXTDATE,"-",2) ; text month
 . S DIRDATE=$F("JANFEBMARAPRMAYJUNJULAUGSEPOCTNOVDEC",DIRDATE)/3
 . S DIRDATE=$E(100+DIRDATE,2,3) ; month
 . S DIRDATE=$P(TEXTDATE,"-",3)_DIRDATE ; year
 . S DIRDATE=DIRDATE_$E(100+$P(TEXTDATE,"-",1),2,3) ; day
 . D NEWLINE(1) W DIRNAME,?10,TEXTDATE,?25,DIRDATE
 . I DIRDATE<DELDATE D
 . . D NEWLINE(1) W "Delete ",DIRPATH,"  "
 . . W $E(DIRDATE,5,6),"/",$E(DIRDATE,7,8),"/",$E(DIRDATE,1,4)
 . . D DELETE
 . . Q
 . E  D
 . . D NEWLINE(1) W "Skip   ",DIRPATH,"  "
 . . W $E(DIRDATE,5,6),"/",$E(DIRDATE,7,8),"/",$E(DIRDATE,1,4)
 . Q
 Q
 ;
DELETE ; delete a DICOM directory
 N CMD,ERROR,VERBOSE
 S VERBOSE=1
 S CMD="RMDIR /S /Q "_DIRPATH_" > nul"
 D DOS^MAGDOS(CMD,.ERROR)
 I ERROR(0) D  ; fatal error
 . N I,MSG
 . S MSG(1)="ERROR DELETING DIRECTORY """_DIRPATH_""""
 . S MSG(2)="Unexpected error in """_CMD_""""
 . F I=1:1:ERROR(0) S MSG(I+2)=$J(I,3)_") "_ERROR(I)
 . D TRAPERR^MAGDSTRT(1,$T(+0),"DICOM TEXT PROCESSING ERROR",.MSG)
 . Q  ; Error trap does not return
 Q
 ;
MINSPACE ; Get Minimum Percentage of Free Disk Space
 N D0,RPCERR
 S D0=$S($G(^MAGDICOM(2006.563,1,"CONSOLIDATED"))="YES":$$GETPLACE^MAGDMFB(),1:1)
 I D0<0 D ERRMSG
 E  D
 . S RPCERR=$$CALLRPC^MAGM2VCU("MAG DICOM PACS MINIMUM SPACE","M",.MINSPACE,D0)
 . I RPCERR<0 D ERRMSG
 . Q
 Q
 ;
ERRMSG ;
 S MINSPACE=25 ; default require a minimum of 25% free space
 W !,"Using default value of minimum disk space of ",MINSPACE,"%.",!
 Q
 ;
NEWLINE(N) N I,X
 S LINES=LINES+N I LINES'>PAGE W @$E("!!!!!",N) Q
 W !!,"Press <Enter> to Continue: " R X:$G(DTIME,300) E  S X="^"
 W !! S LINES=3
 Q
 ;
