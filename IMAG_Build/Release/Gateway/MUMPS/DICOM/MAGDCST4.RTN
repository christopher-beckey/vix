MAGDCST4 ;WOIFO/PMK,DAC - Cumulative Statistics ; 22 Jan 2009 9:28 AM
 ;;3.0;IMAGING;**10,11,30,51,69,54,53,118**;Mar 19, 2002;Build 4525;May 01, 2013
 ;; Per VHA Directive 2004-038, this routine should not be modified.
 ;; +---------------------------------------------------------------+
 ;; | Property of the US Government.                                |
 ;; | No permission to copy or redistribute this software is given. |
 ;; | Use of unreleased versions of this software requires the user |
 ;; | to execute a written test agreement with the VistA Imaging    |
 ;; | Development Office of the Department of Veterans Affairs,     |
 ;; | telephone (301) 734-0100.                                     |
 ;; | The Food and Drug Administration classifies this software as  |
 ;; | a medical device.  As such, it may not be changed in any way. |
 ;; | Modifications to this software may result in an adulterated   |
 ;; | medical device under 21CFR820, the use of which is considered |
 ;; | to be a violation of US Federal Statutes.                     |
 ;; +---------------------------------------------------------------+
 ;;
 ;
ENTRY ; output statistics for each of the instruments -- invoked by ^MAGMENU
 ; stub to start ^MAGDCST4 to output statistics
 S ENTRY="REENTRY^"_$T(+0)
 S PROMPT=""
 G SETUP^MAGDSTRT
 ;
REENTRY ; Re-entry point from ^MAGDSTRT
 N INSTIEN,INSTRMNT,TODAY
 D SETUP^MAGDMENU W @IOF
 W ?20,"VistA DICOM Image Storage Server",!
 W !,"Instrument",?15,"Description"
 W ?60,"Port",?70,"Service"
 W !,"----------",?15,"-----------"
 W ?60,"----",?70,"-------"
 S INSTIEN=0
 F N=1:1 S INSTIEN=$O(^MAGDICOM(2006.581,INSTIEN)) Q:'INSTIEN  D
 . S X=^MAGDICOM(2006.581,INSTIEN,0),INSTRMNT=$P(X,"^")
 . W !?(4-($L(INSTRMNT)/2)),INSTRMNT
 . W ?15,$P(X,"^",2) ; description
 . W ?59,$J($P(X,"^",4),5) ; port (socket)
 . W ?70,$P(X,"^",3) ; preferred server
 . Q
 R !!,"Press <Enter> for current status",X:30 ; Buffer flush intentional
 S ENTRY="RESTART^"_$T(+0)
 ;
 S HALT=0 F  D LOOP Q:HALT
 Q
 ;
RESTART ; Restart here after an error occurred
 N HALT,INSTIEN,TODAY
 D SETUP^MAGDMENU
 S HALT=0 F  D LOOP Q:HALT
 Q
 ;
LOOP ; output the screen
 N STUDYCNT
 W @IOF
 S LOCATION=^MAGDICOM(2006.563,1,"LOCATION")
 S TODAY=$$NOW^XLFDT()\1
 W ?20,"VistA DICOM Image Storage Server Status",!
 W !,"Instrument"
 W ?20,"Associations",?42,"Images  (Time)"
 W !,"----------"
 W ?20,"------------",?42,"---------------"
 S INSTIEN=0
 F N=1:1 S INSTIEN=$O(^MAGDICOM(2006.581,INSTIEN)) Q:'INSTIEN  D
 . N ASS,D0,LOCK,NASS,NIMG
 . S X=^MAGDICOM(2006.581,INSTIEN,0),INSTRMNT=$P(X,"^")
 . W !?(4-($L(INSTRMNT)/2)),INSTRMNT
 . ; display storage server interface statistics
 . L +^MAGDINPT("INSTRUMENT",INSTRMNT):0 S LOCK=$T
 . L:LOCK -^MAGDINPT("INSTRUMENT",INSTRMNT)
 . S D0=$O(^MAGDICOM(2006.5764,"B",INSTRMNT,"")) Q:'D0
 . S Y=^MAGDICOM(2006.5764,D0,0),DATETIME=$P(Y,"^",3),ASS=$P(Y,"^",4)
 . D DATETIME(DATETIME)
 . ; display association statistics
 . S Y=$G(^MAGDICOM(2006.5764,D0,1,TODAY,0))
 . S NASS=$P(Y,"^",3),NIMG=$P(Y,"^",3),DATETIME=$P(Y,"^",4)
 . W:NASS ?20,$J(NASS,3)
 . ;
 . W:ASS ?24,"(active)"
 . ; display image acquisitions statistics
 . I NIMG'="" D
 . . W ?40,$J(NIMG,6)
 . . D DATETIME(DATETIME)
 . . W "  (",TIME,")"
 . . Q
 . Q
 ; query for end of session
 D
 . N DRIVE,FREE,PCT,TOTAL
 . S DRIVE=$$DRIVE^MAGDSTAT($G(^MAGDICOM(2006.563,1,"IMAGE INPUT PATH")))
 . D DISK1^MAGDSTAT(DRIVE)
 . W !!,FREE," (",$J(PCT,3,1),"%) of free space on drive ",DRIVE
 . W ?55,"(Total=",TOTAL,")"
 . Q
 ;
 I $G(^MAGDICOM(2006.563,1,"PACS EXAM COMPLETE"))="YES" D
 . ; output count of commercial PACS imaging transactions
 . N AUD,I,LOCATION,LOCNAM
 . S TODAY=$$NOW^XLFDT()\1
 . W !
 . S LOCATION=^MAGDICOM(2006.563,1,"LOCATION")
 . ;
 . S RPCERR=$$CALLRPC^MAGM2VCU("MAG DICOM FILEMAN GET","MT",.LOCNAM,4,LOCATION,.01)
 . I RPCERR<0 S HALT=1 Q
 . ;
 . W !!,LOCNAM
 . ;
 . S RPCERR=$$CALLRPC^MAGM2VCU("MAG DICOM TEXT AUDIT GET","MT",.AUD,LOCATION,1)
 . I RPCERR<0 S HALT=1 Q
 . ;
 . S I="" F  S I=$O(AUD(I)) Q:I=""  Q:AUD(I)
 . Q:I=""
 . K X F  S X($P(AUD(I),"^",3)_" ")=AUD(I),I=$O(AUD(I)) Q:I=""
 . S X="" F  S X=$O(X(X)) Q:X=""  D
 . . N COUNT,LAST
 . . S COUNT=$P(X(X),"^",2),LAST=$P(X,"^",4) S:'COUNT COUNT="<none>"
 . . W !,$J(X,30),": ",$J(COUNT,6)
 . . W:LAST "  (",$E(LAST,9,10),":",$E(LAST,11,12),")"
 . . Q
 . W !
 . Q
 Q:HALT
 ;
 ; check for images waiting to be processed
 S N=$P($G(^MAGDINPT(2006.571,0)),"^",3)-$G(^MAGDINPT(2006.571,"ACOUNT"))
 I N>1 W !,"There are ",N," images to be processed."
 I N=1 W !,"There is one image yet to be processed."
 ;
 ; check for images that were only partially transmitted
 S N=+$P($G(^MAGDINPT(2006.5714,0)),"^",4)
 I N>1 W !,"There are ",N," incomplete images."
 I N=1 W !,"There is one incomplete image."
 ;
 D ENTRIES^MAGDIR6D ; check for undefined modalities
 ;
 I $G(^MAGDICOM(2006.563,1,"IMPORTER"))="YES" D
 . N DATAFILE ;- global reference for import data
 . N NOIMPORT
 . ; get list of studies to import
 . S DATAFILE="^MAGDICOM(2006.5751)"
 . S STUDYCNT=$$GETSTUDY^MAGDAIR3(.NOIMPORT)
 . K ^TMP("MAG",$J,"IMPORT","LIST")
 . Q
 E  D
 . N HOSTNAME
 . S HOSTNAME=$$HOSTNAME^MAGOSMSC()
 . S:$$CALLRPC^MAGM2VCU("MAG DICOM INCORRECT IMAGE CT","",.STUDYCNT,LOCATION,HOSTNAME)<0 STUDYCNT=0
 . Q
 ;
 I STUDYCNT D
 . W !,"There ",$S(STUDYCNT=1:"is one",1:"are "_STUDYCNT)
 . W " stud",$S(STUDYCNT=1:"y",1:"ies")
 . W " to be corrected.",!
 . Q
 ;
 D BGNDSTAT^MAGDMENU
 W !!,"Exit?  no // " R X:30 S:X="" X="no"
 I "^Yy"[$E(X) S HALT=1
 Q
 ;
DATETIME(X) ; format date/time
 N THISYEAR,YEAR
 S THISYEAR=$$NOW^XLFDT()\10000,YEAR=$E(X,1,3)
 S YEAR=$S(YEAR=THISYEAR:"",1:"-"_(YEAR+1700)) ; omit year if same as current
 S DATE=$E(X,6,7)_"-"_$P("Jan Feb Mar Apr May Jun Jul Aug Sep Oct Nov Dec"," ",$E(X,4,5))_YEAR
 S TIME=$E($P(X,".",2)_"000000",1,6)
 S TIME=$E(TIME,1,2)_":"_$E(TIME,3,4) ; omit seconds
 Q
 ;
