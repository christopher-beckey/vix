MAGDIR4A ;WOIFO/JHC - GET ADDITIONAL VARIABLES ; 05/10/2004  10:55
 ;;3.0;IMAGING;**30**;MAR 19, 2002;;NOV 5, 2004
 ;; Per VHA Directive 2004-038, this routine should not be modified.
 ;; +---------------------------------------------------------------+
 ;; | Property of the US Government.                                |
 ;; | No permission to copy or redistribute this software is given. |
 ;; | Use of unreleased versions of this software requires the user |
 ;; | to execute a written test agreement with the VistA Imaging    |
 ;; | Development Office of the Department of Veterans Affairs,     |
 ;; | telephone (301) 734-0100.                                     |
 ;; | The Food and Drug Administration classifies this software as  |
 ;; | a medical device.  As such, it may not be changed in any way. |
 ;; | Modifications to this software may result in an adulterated   |
 ;; | medical device under 21CFR820, the use of which is considered |
 ;; | to be a violation of US Federal Statutes.                     |
 ;; +---------------------------------------------------------------+
 ;;
GECT ; entry point from ^MAGDIR4 for GE CTs
 N STATUS
 S STATUS=$$USE^MAGOSFIL(FILETXT)
 W "HAUNSFIELD=-1024",! ; GECT CT ONLY
 G CTCOMMON
 ;
PICKERUZ ; Entry point from ^MAGDIR4 for Picker UltraZ CTs
 N A,STATUS
 S STATUS=$$USE^MAGOSFIL(FILETXT)
 S A=$TR(MODPARMS,"a","A") S:A["/" A=$P(A,"/",2)
 S A=$P(A,"A",2) S:A'="" A=-A
 W:A "HAUNSFIELD=",A,! ; Picker UltraZ CT
 G CTCOMMON
 ;
GECT1000 ; entry point from ^MAGDIR4 for GE ProSpeed CT
PICKERCT ; entry point from ^MAGDIR4 for Picker CTs
 ; these units send raw Hounsfield units
 N STATUS
 S STATUS=$$USE^MAGOSFIL(FILETXT)
 W "HAUNSFIELD=-1000",! ; Picker CT and GE ProSpeed CT
 G CTCOMMON
 ;
PHILIPCT ; entry point from ^MAGDIR4 for Philips CTs
 N STATUS
 S STATUS=$$USE^MAGOSFIL(FILETXT)
 W "HAUNSFIELD=-1000",! ; Philips CT ONLY (*** GUESS ***)
 G CTCOMMON
 ;
CTCOMMON ; common CT code
 N STATUS,X
 ;
 S X=$$GETDATA^MAGDFCNS(INODE,"0018,0010")
 W "CONTRAST_INDICATOR="
 W $S(X="":"",$E("NONE",1,$L(X))=$TR(X,"one","ONE"):"",1:"C+"),!
 ;
 S X=$$GETDATA^MAGDFCNS(INODE,"0018,0050")
 I X]"" S X=$$ENUM^MAGDFCNS(X)
 W "SLICE_THICKNESS_2=",$S(X]"":$J(X,0,1),1:X),!
 ;
 S X=$$GETDATA^MAGDFCNS(INODE,"0018,1120")
 I X]"" S X=$$ENUM^MAGDFCNS(X)
 W "GANTRY_DETECTOR_TILT_2=",$S(X]"":$J(X,0,1),1:X),!
 ;
 S X=$$GETDATA^MAGDFCNS(INODE,"0018,1150")
 I X]"" S X=$$ENUM^MAGDFCNS(X)
 W "EXPOSURE_TIME_2=",$S(X]"":$J(X/1000,0,1),1:X),!
 ;
 S X=$$GETDATA^MAGDFCNS(INODE,"0020,0032",3,1)
 I X]"" S X=$$ENUM^MAGDFCNS(X)
 W "IMAGE_POSITION_PATIENT_2(3)=",$S(X]"":$J(X,0,1),1:X),!
 ;
 S X=$$GETDATA^MAGDFCNS(INODE,"0018,1100")
 I X]"" S X=X/10
 W "RECONSTRUCTION_DIAMETER_2=",$S(X]"":$J(X,0,1),1:X),!
 ;
 I $$USEPRIN^MAGOSFIL(1)
 Q
 ;
GELCA ; entry point from ^MAGDIR4 for GE LCA DLX
 N STATUS
 S STATUS=$$USE^MAGOSFIL(FILETXT)
 W "FRAME_NUMBER=",IFRAME,"/",NFRAME,!
 W "FRAME_TIME=",+$G(FRAMETIM),!
 I $$USEPRIN^MAGOSFIL(1)
 Q
