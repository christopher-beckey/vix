MAGBRTP1 ;WOIFO/EdM - Routing Purge Functions ; 11/05/2004  13:53
 ;;3.0;IMAGING;**11,30,51**;MAR 19, 2002;;JUN 16, 2006
 ;; Per VHA Directive 2004-038, this routine should not be modified.
 ;; +---------------------------------------------------------------+
 ;; | Property of the US Government.                                |
 ;; | No permission to copy or redistribute this software is given. |
 ;; | Use of unreleased versions of this software requires the user |
 ;; | to execute a written test agreement with the VistA Imaging    |
 ;; | Development Office of the Department of Veterans Affairs,     |
 ;; | telephone (301) 734-0100.                                     |
 ;; | The Food and Drug Administration classifies this software as  |
 ;; | a medical device.  As such, it may not be changed in any way. |
 ;; | Modifications to this software may result in an adulterated   |
 ;; | medical device under 21CFR820, the use of which is considered |
 ;; | to be a violation of US Federal Statutes.                     |
 ;; +---------------------------------------------------------------+
 ;;
 Q
 ;
SETUP ;
 S FATAL=0
 S LOCATION=$G(^MAGDICOM(2006.563,1,"LOCATION"))
 I 'LOCATION D  Q
 . W !,"Location for current DICOM Gateway not defined.",!
 . S FATAL=1
 . Q
 Q
 ;
STARTE N D0,D1,D2,FATAL,I,LOCATION,P,Q1,RULES
 D SETUP Q:FATAL
 ; Set up "RULES" array
 ;
 ; RULES(i)=rule # ^ q1 ^ value
 ; RULES(i)=rule # ^ q1 ^ d1 # ^ value
 ; RULES(i)=rule # ^ q1 ^ d1 # ^ q2 ^ d2 ^ value
 ;
 ; Q1 = "ACTION" | "CONDITION" | "PRIORITY" | "PRIORSTUDY"
 ;
 S I=0 F X="ACTION","CONDITION","PRIORITY","PRIORSTUDY" S Q1(X)=""
 S D0=0 F  S D0=$O(^MAGDICOM(2006.59,D0)) Q:'D0  D
 . S Q1="" F  S Q1=$O(^MAGDICOM(2006.59,D0,Q1)) Q:Q1=""  D:$D(Q1(Q1))
 . . I Q1="PRIORSTUDY" S I=I+1,RULES(I)=D0_"^"_Q1_"^"_^MAGDICOM(2006.59,D0,Q1) Q
 . . I Q1="PRIORITY" S I=I+1,RULES(I)=D0_"^"_Q1_"^"_^MAGDICOM(2006.59,D0,Q1) Q
 . . I Q1="ACTION" D  Q
 . . . S I=I+1,RULES(I)=D0_"^"_Q1_"^"_^MAGDICOM(2006.59,D0,Q1,1,0)
 . . . S D2=0 F  S D2=$O(^MAGDICOM(2006.59,D0,Q1,1,1,D2)) Q:'D2  D
 . . . . S I=I+1,RULES(I)=D0_"^"_Q1_"^"_D2_"^"_^MAGDICOM(2006.59,D0,Q1,1,1,D2,0)
 . . . . Q
 . . . Q
 . . I Q1="CONDITION" D  Q
 . . . S D1=0 F  S D1=$O(^MAGDICOM(2006.59,D0,Q1,D1)) Q:'D1  D
 . . . . S I=I+1,RULES(I)=D0_"^"_Q1_"^"_D1_"^KW^"_$G(^MAGDICOM(2006.59,D0,Q1,D1,0))
 . . . . S I=I+1,RULES(I)=D0_"^"_Q1_"^"_D1_"^DT^"_$G(^MAGDICOM(2006.59,D0,Q1,D1,"DATATYPE"))
 . . . . S I=I+1,RULES(I)=D0_"^"_Q1_"^"_D1_"^OP^"_$G(^MAGDICOM(2006.59,D0,Q1,D1,"OPERATOR"))
 . . . . S I=I+1,RULES(I)=D0_"^"_Q1_"^"_D1_"^VA^"_$G(^MAGDICOM(2006.59,D0,Q1,D1,"VALUE"))
 . . . . S D2=0 F  S D2=$O(^MAGDICOM(2006.59,D0,Q1,D1,1,D2)) Q:'D2  D
 . . . . . S I=I+1,RULES(I)=D0_"^"_Q1_"^"_D1_"^VA^M^"_D2_"^"_$G(^MAGDICOM(2006.59,D0,Q1,D1,1,D2,"MASK"))
 . . . . . S I=I+1,RULES(I)=D0_"^"_Q1_"^"_D1_"^VA^B^"_D2_"^"_$G(^MAGDICOM(2006.59,D0,Q1,D1,1,D2,"BEGIN"))
 . . . . . S I=I+1,RULES(I)=D0_"^"_Q1_"^"_D1_"^VA^E^"_D2_"^"_$G(^MAGDICOM(2006.59,D0,Q1,D1,1,D2,"END"))
 . . . . . Q
 . . . . Q
 . . . Q
 . . Q
 . Q
 ;
 Q:$$CALLRPC^MAGM2VCU("MAG DICOM ROUTE EVAL START","M",.OUT,LOCATION,.RULES)<0
 W:OUT "No task started on VistA system.",!,"Reason: ",OUT,!
 D:'OUT
 . N I,MSG,TASK,TICK
 . S TASK=$P(OUT,"#=",2)
 . W !,"Task on Vista System has TaskMan ID # ",TASK,!
 . S (TICK,MSG)=0 F  D  Q:FATAL  D IDLE^MAGDBB(.TICK,1,0)
 . . K OUT
 . . Q:$$CALLRPC^MAGM2VCU("MAG DICOM ROUTE EVAL LOG","M",.OUT,TASK,MSG,100,LOCATION)<0
 . . S I=$O(OUT("")) Q:I=""
 . . I +OUT(I)=-1 W !,"Waiting for task #",TASK," to start." Q
 . . I +OUT(I)=-3 W !,"Remote process has ended: ",OUT(I) S FATAL=1 Q
 . . I OUT(I)<0 W !,"Error: ",OUT(I) Q
 . . S MSG=$P(OUT(I)," ",2)
 . . F  S I=$O(OUT(I)) Q:I=""  W !,OUT(I)
 . . Q
 . Q
 S P=$$CLOSE^MAGM2VC()
 I $$USEPRIN^MAGOSFIL(1)
 Q
 ;
STOPE N FATAL,LOCATION
 D SETUP Q:FATAL
 Q:$$CALLRPC^MAGM2VCU("MAG DICOM ROUTE EVAL STOP","MC",.OUT)<0
 W:'OUT "Task not stopped on VistA System.",!,"Reason: ",OUT,!
 W:OUT !,"Task on Vista System will stop.",!
 Q
 ;
PURGEALL N FATAL,LOCATION
 D SETUP Q:FATAL
 Q:$$CALLRPC^MAGM2VCU("MAG DICOM ROUTE PURGE DONE","MC",.OUT,0,LOCATION)<0
 W !,$S(OUT:OUT,1:"No")," entr",$S(OUT=1:"y",1:"ies")," purged.",!
 Q
 ;
PURGEEXP N DAYS,FATAL,LOCATION,OK,P,X
 D SETUP Q:'LOCATION
 S OK=0 F  D  Q:OK
 . W !!,"Retain how many days? 4//" R X:$G(DTIME,300) E  S X="^"
 . I X="" S X=4 W X
 . I X["^" S OK=13 Q
 . I +X\1=X S DAYS=X,OK=1 Q
 . W !!,"Enter an integer number."
 . W !,"Data older than this number of days will be purged.",!
 . Q
 Q:OK=13
 Q:$$CALLRPC^MAGM2VCU("MAG DICOM ROUTE PURGE DONE","MC",.OUT,DAYS,LOCATION)<0
 W !,$S(OUT:OUT,1:"No")," entr",$S(OUT=1:"y",1:"ies")," purged.",!
 Q
 ;
FAILREQ N FATAL,LOCATION,P
 D SETUP Q:'LOCATION
 Q:$$CALLRPC^MAGM2VCU("MAG DICOM ROUTE REQUEUE","MC",.OUT,LOCATION)<0
 W !,$S(OUT:OUT,1:"No")," entr",$S(OUT=1:"y",1:"ies")," re-queued.",!
 Q
 ;
PURGEOBS N FATAL,LOCATION,OK,P,UPTO,X,Y
 D SETUP Q:'LOCATION
 S OK=0 F  D  Q:OK
 . W !!,"Purge up to which date? T-4//" R X:$G(DTIME,300) E  S X="^"
 . I X="" S X="T-4" W X
 . I X["^" S OK=13 Q
 . D ^%DT I Y S UPTO=Y,OK=1 Q
 . W !!,"Enter a (FileMan compatible) date."
 . W !,"Unsent and failed queue entries created before this date"
 . W !,"will be purged.",!
 . Q
 Q:OK=13
 Q:$$CALLRPC^MAGM2VCU("MAG DICOM ROUTE REMOVE OBSO","MC",.OUT,UPTO,LOCATION)<0
 W !,$S(OUT:OUT,1:"No")," entr",$S(OUT=1:"y",1:"ies")," purged.",!
 Q
 ;
