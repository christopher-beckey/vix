MAGDAIRC ;WOIFO/PMK - Automatic Import Reconciliation Workflow ; 16 Mar 2010 12:33 PM
 ;;3.0;IMAGING;**53**;Mar 19, 2002;Build 1715;Apr 22, 2010
 ;; Per VHA Directive 2004-038, this routine should not be modified.
 ;; +---------------------------------------------------------------+
 ;; | Property of the US Government.                                |
 ;; | No permission to copy or redistribute this software is given. |
 ;; | Use of unreleased versions of this software requires the user |
 ;; | to execute a written test agreement with the VistA Imaging    |
 ;; | Development Office of the Department of Veterans Affairs,     |
 ;; | telephone (301) 734-0100.                                     |
 ;; | The Food and Drug Administration classifies this software as  |
 ;; | a medical device.  As such, it may not be changed in any way. |
 ;; | Modifications to this software may result in an adulterated   |
 ;; | medical device under 21CFR820, the use of which is considered |
 ;; | to be a violation of US Federal Statutes.                     |
 ;; +---------------------------------------------------------------+
 ;;
 ;
 ; Modify Examined/Exam Complete configuration parameters
 ; 
DISPLAY ; show the variables
 N COMPLETE,EXAMINED,I,INDEX,INFO,J,KEY,RAIMGTYP,RPCERR,VALUE,X
 ; get the imaging types from the procedures
 S I=0 F  S I=$O(^MAGDICOM(2006.5758,I)) Q:'I  D
 . S X=^MAGDICOM(2006.5758,I,0)
 . S RAIMGTYP($P(X,"^",5))=""
 . Q
 S I=0 F  S I=$O(RAIMGTYP(I)) Q:I=""  D
 . S RPCERR=$$CALLRPC^MAGM2VCU("RAMAG EXAM STATUS REQUIREMENTS","M",.INFO,"E",I)
 . I INFO(1)'<0 F J=1:1:16 I $P(INFO(1),"^",J) S EXAMINED(J)=1
 . S RPCERR=$$CALLRPC^MAGM2VCU("RAMAG EXAM STATUS REQUIREMENTS","M",.INFO,"C",I)
 . I INFO(1)'<0 F J=1:1:16 I $P(INFO(1),"^",J) S COMPLETE(J)=1
 . Q
 F INDEX=1,4,5,6,14 D
 . S X=$$REQUIRED(INDEX,.KEY)
 . S VALUE=$G(^MAGDICOM(2006.563,1,KEY_" NAME"))
 . I $D(EXAMINED(INDEX\1))!$D(COMPLETE(INDEX\1)) S X=X_"(*)"
 . I '$G(^MAGDICOM(2006.563,1,KEY)) S VALUE=""
 . W !,$J(X,30),": "
 . I VALUE="" W "<not assigned yet>"
 . E  W VALUE
 . Q
 W !!,"Fields with an asterisk (*) require values."
 Q
 ;
TECH ; modify the technologist
 S X=$$SELECT(1,"MAG DICOM GET RAD PERSON","T")
 Q
 ;
FILM ; modify the film
 S X=$$SELECT(4,"MAG DICOM GET RAD FILM")
 Q
 ;
DXCODE ; modify the diagnostic code
 S X=$$SELECT(5,"MAG DICOM GET RAD DX CODE")
 Q
 ;
CAMERA ; modify the camera
 S X=$$SELECT(6,"MAG DICOM GET RAD CAMERA")
 Q
 ; 
CPTMOD ; modify the cpt modifier
 S X=$$SELECT(14,"MAG DICOM GET RAD CPT MOD")
 Q
 ; 
ALL ; enter them all
 N I,OPTIONS
 S OPTIONS="TECH^FILM^DXCODE^CAMERA^CPTMOD"
 F I=1:1:$L(OPTIONS,"^") D @$P(OPTIONS,"^",I) I X<0 Q:X=-3  S I=I-2 Q:I=-1
 Q
 ;
SELECT(INDEX,RPC,PARAM) ; select default
 N CHOICE,DEFAULT,DONE,I,INFO,J,K,KEY,MSG,N,OK,PROMPT,RESULT,RPCERR,X
 S PROMPT=$$REQUIRED(INDEX,.KEY)
 ;
 ; do you really want to change the value?
 S DEFAULT=$G(^MAGDICOM(2006.563,1,KEY_" NAME"))
 I '$G(^MAGDICOM(2006.563,1,KEY)) S DEFAULT=""
 I DEFAULT'="" D  Q:CHOICE="NO" 1 Q:OK<0 -1
 . S MSG=PROMPT_": """_DEFAULT_"""   Change it?"
 . S OK=$$YESNO^MAGDAIR0(MSG,"n",.CHOICE)
 . Q
 ;
 S OK=0 F  D  Q:OK
 . W !!,"Enter the ",PROMPT,": "
 . R X:DTIME E  S OK="-1,Timeout" Q
 . I X?1"^^".E S OK=-3 Q
 . I X?1"^".E S OK=-2 Q
 . I X="" W " ???" Q
 . I $G(PARAM)'="" D
 . . S RPCERR=$$CALLRPC^MAGM2VCU(RPC,"M",.INFO,PARAM,X)
 . . Q
 . E  D
 . . S RPCERR=$$CALLRPC^MAGM2VCU(RPC,"M",.INFO,X)
 . . Q
 . I RPCERR<0 S OK="-2,RPC Error: "_RPCERR Q
 . I $P(INFO(1),"^",1)=-1 W " ???  ",$P(INFO(1),"^",2,999) Q
 . I INFO(1)<0 S OK="-3,RPC Error: "_INFO(1) Q
 . I INFO(1)=1 D  Q
 . . S MSG=""""_$P(INFO(2),"^",2)_"""  Ok?"
 . . S X=$$YESNO^MAGDAIR0(MSG,"n",.CHOICE)
 . . I CHOICE="YES" S RESULT=INFO(2),OK=1
 . . Q
 . W !!,"Select the ",PROMPT," from this list:"
 . S DONE=0 F  D  Q:DONE
 . . F J=1:1:INFO(1) D
 . . . W !,$J(J,3)," -- ",$P(INFO(J+1),"^",2)
 . . . I $P(INFO(J+1),"^",3)'="" W " (",$P(INFO(J+1),"^",3),")"
 . . . S K=J
 . . . Q
 . . W !!,"Enter the number [1-",K,"]: "
 . . R N:DTIME E  S OK="-1,Timeout",DONE=1 Q
 . . I N?1"^".E S OK="-2",DONE=1 Q
 . . I N?1N.N,N>0,N'>K S RESULT=INFO(N+1),(DONE,OK)=1 Q
 . . W " ???"
 . . Q
 . Q
 I OK<0 W !,$P(OK,",",2,999) Q OK
 I OK=1 D
 . D SAVE(INDEX,$P(RESULT,"^",1,2))
 . W !!,PROMPT," changed to """,$P(RESULT,"^",2),""""
 . Q
 Q OK
 ;
SAVE(INDEX,RESULT) ; save default
 N KEY,X
 S X=$$REQUIRED(INDEX,.KEY)
 S ^MAGDICOM(2006.563,1,KEY)=$P(RESULT,"^",1)
 S ^MAGDICOM(2006.563,1,KEY_" NAME")=$P(RESULT,"^",2)
 Q
 ;
REQUIRED(INDEX,KEY) ; return required field
 I INDEX=1 S KEY="IMPORTER RAD TECH" Q "Radiology Technologist"
 I INDEX=4 S KEY="IMPORTER RAD FILM SIZE" Q "Film Size"
 I INDEX=5 S KEY="IMPORTER RAD DX CODE" Q "Radiology diagnostic code"
 I INDEX=6 S KEY="IMPORTER RAD CAMERA" Q "Camera/Equipment/Room"
 I INDEX=14 S KEY="IMPORTER CPT MOD" Q "CPT Modifier"
 K KEY Q ""
 ;
VERIFY(LIST) ; check that all fields are defined
 N INDEX,KEY,VALUE,X
 K LIST
 F INDEX=1,4,5,6,14 D
 . S X=$$REQUIRED(INDEX,.KEY)
 . S VALUE=$G(^MAGDICOM(2006.563,1,KEY))
 . I VALUE="" D VERIFY1(.LIST,KEY_" (internal entry number)")  ; missing IEN
 . S VALUE=$G(^MAGDICOM(2006.563,1,KEY_" NAME"))
 . I VALUE="" D VERIFY1(.LIST,KEY_" NAME") ; missing NAME
 . Q
 Q $G(LIST(0))
 ;
VERIFY1(LIST,ITEM) ; push ITEM onto the LIST
 S LIST(0)=$G(LIST(0))+1
 S LIST(LIST(0))=ITEM
 Q
 ;
EXAMINED(RAEXAM,RAIMGTYP,IMAGLOCN) ; generate required fields for EXAMINED
 N INFO,RAMSC,RPCERR,TEXT
 D MAKELIST("E",RAIMGTYP,.RAMSC)
 D OUTPUT("FLAGS^^S",.RAMSC) ; do not send HL7 message to speech recognition systems
 S RPCERR=$$CALLRPC^MAGM2VCU("RAMAG EXAMINED","M",.INFO,RAEXAM,.RAMSC)
 I INFO(1)="-31^1" Q 0 ; already complete warning message 
 I INFO(1)="-11^2" D  ; add the OUTSIDE STUDY and try the RPC again 
 . D ADDROOM(RAEXAM)
 . S RPCERR=$$CALLRPC^MAGM2VCU("RAMAG EXAMINED","M",.INFO,RAEXAM,.RAMSC)
 . Q
 I INFO(1)<0 D
 . I INFO(1)="-11^2" S TEXT="WARNING: Exam status not advanced to EXAMINED"
 . E  S TEXT="Error in rpc RAMAG EXAMINED"
 . D RPCERROR(TEXT,RAEXAM,RAIMGTYP,IMAGLOCN,.RAMSC,.INFO)
 . Q
 Q 0
 ;
COMPLETE(RAEXAM,RAIMGTYP,IMAGLOCN) ; generate required fields for COMPLETE
 N INFO,RAMSC,RPCERR,TEXT
 D MAKELIST("C",RAIMGTYP,.RAMSC)
 D OUTPUT("FLAGS^^S",.RAMSC) ; do not send HL7 message to speech recognition systems
 S RPCERR=$$CALLRPC^MAGM2VCU("RAMAG EXAM COMPLETE","M",.INFO,RAEXAM,.RAMSC)
 I INFO(1)="-11^2" D  ; add the OUTSIDE STUDY and try the RPC again 
 . D ADDROOM(RAEXAM)
 . S RPCERR=$$CALLRPC^MAGM2VCU("RAMAG EXAM COMPLETE","M",.INFO,RAEXAM,.RAMSC)
 . Q
 I INFO(1)<0 D
 . I INFO(1)="-11^2" S TEXT="WARNING: Exam/Report status not advanced to COMPLETE/ELECTRONICALLY FILED"
 . E  S TEXT="Error in rpc RAMAG EXAM COMPLETE"
 . D RPCERROR(TEXT,RAEXAM,RAIMGTYP,IMAGLOCN,.RAMSC,.INFO)
 . Q
 Q 0
 ;
ADDROOM(RAEXAM) ; add the OUTSIDE STUDY camera equipment room to the IMAGING LOCATION
 S RPCERR=$$CALLRPC^MAGM2VCU("MAG DICOM ADD CAMERA EQUIP RM","M",.INFO,RAEXAM)
 I INFO(1)<0 D
 . D ERRORMSG^MAGDAIR0(1,"Error in rpc MAG DICOM ADD CAMERA EQUIP RM",.INFO)
 . Q
 Q
 ;
RPCERROR(TEXT,RAEXAM,RAIMGTYP,IMAGLOCN,RAMSC,INFO) ; generate message for rpc error
 N I,J,MSG,X
 S I=0
 I $G(INFO(1))="-11^2" D  ; bad camera/equipment/room
 . S X=$G(^MAGDICOM(2006.563,1,"IMPORTER RAD CAMERA NAME"))
 . S I=I+1,MSG(I)="The Camera/Equipment/Room """_X_""" may not be defined for the"
 . S I=I+1,MSG(I)="Radiology Imaging Location """_IMAGLOCN_""" that is used for this study."
 . S I=I+1,MSG(I)=""
 . S I=I+1,MSG(I)="The data required to advance the status of this case performed at an"
 . S I=I+1,MSG(I)="outside location may not have been entered. "
 . S I=I+1,MSG(I)=""
 . Q
 S I=I+1,MSG(I)="The status of the study had not been automatically advanced."
 S I=I+1,MSG(I)="Please manually advance its status by entering the necessary"
 S I=I+1,MSG(I)="data using the Radiology package status tracking option."
 S I=I+1,MSG(I)=""
 S I=I+1,MSG(I)="RAEXAM="_RAEXAM
 S I=I+1,MSG(I)="RAIMGTYPE="_RAIMGTYP
 F J=1:1 Q:'$D(RAMSC(J))  S I=I+1,MSG(I)="RAMSC("_J_")="_RAMSC(J)
 S I=I+1,MSG(I)=""
 F J=1:1 Q:'$D(INFO(J))  S I=I+1,MSG(I)="INFO("_J_")="_INFO(J)
 D ERRORMSG^MAGDAIR0(1,TEXT,.MSG)
 Q
 ;
MAKELIST(RACTION,RAIMGTYP,RAMSC) ; output required fields
 N INDEX,INFO,LIST,RPCERR,VALUE
 S RPCERR=$$CALLRPC^MAGM2VCU("RAMAG EXAM STATUS REQUIREMENTS","M",.INFO,RACTION,RAIMGTYP)
 I INFO(1)<0 Q -1
 F INDEX=1:1:16 I $P(INFO(1),"^",INDEX) D
 . S VALUE=$$GETVALUE(INDEX)
 . D @("REQ"_$E(1000+INDEX,2,4))
 . Q
 Q 0
 ;
REQ001 ; technologist
 D OUTPUT("TECH^1^"_VALUE,.RAMSC)
 Q
 ;
REQ002 Q  ; resident or staff
 ;
REQ003 Q  ; detailed procedure - taken care of already
 ;
REQ004 ; film entry
 D OUTPUT("FILMSIZE^1^"_VALUE_"^0",.RAMSC)
 Q
 ;
REQ005 ; diagnosic code
 D OUTPUT("PRIMDXCODE^^"_VALUE,.RAMSC)
 Q
 ;
REQ006 ; camera / equipment / room
 D OUTPUT("PRIMCAM^^"_VALUE,.RAMSC)
 Q
 ;
REQ007 Q  ; reserved
REQ008 Q  ; reserved
REQ009 Q  ; reserved
REQ010 Q  ; reserved
 ;
REQ011 ; report entered
 D OUTPUT("REPORT^1^Electronically generated report for outside study.",.RAMSC)
 D OUTPUT("RPTDTE^^"_TODAYHL7,.RAMSC)
 Q
 ;
REQ012 ; verified report
 D OUTPUT("VERDTE^^"_TODAYHL7,.RAMSC)
 D OUTPUT("RPTSTATUS^^EF",.RAMSC)
 Q
 ;
REQ013 Q  ; procedure modifiers required - previously done
 ;
REQ014 ; cpt modifiers
 D OUTPUT("CPTMODS^^"_VALUE,.RAMSC)
 Q
 ;
REQ015 Q  ; reserved
 ;
REQ016 ; impression
 D OUTPUT("IMPRESSION^1^Electronically generated report for outside study.",.RAMSC)
 Q
 ;
GETVALUE(INDEX) ;
 N KEY,X
 S X=$$REQUIRED(INDEX,.KEY)
 I $G(KEY)="" Q "" ; no key implies no result
 Q $G(^MAGDICOM(2006.563,1,KEY))
 ;
OUTPUT(TEXT,ARRAY) ;
 N I
 S I=$O(ARRAY(""),-1)+1
 S ARRAY(I)=TEXT
 Q
