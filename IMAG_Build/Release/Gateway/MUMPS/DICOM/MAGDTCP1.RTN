MAGDTCP1 ;WOIFO/PMK - DICOM Networking / Upper Layer Service ; 31 Oct 2008 9:58 AM
 ;;3.0;IMAGING;**30,54**;MAR 19, 2002;;OCT 28, 2009
 ;; Per VHA Directive 2004-038, this routine should not be modified.
 ;; +---------------------------------------------------------------+
 ;; | Property of the US Government.                                |
 ;; | No permission to copy or redistribute this software is given. |
 ;; | Use of unreleased versions of this software requires the user |
 ;; | to execute a written test agreement with the VistA Imaging    |
 ;; | Development Office of the Department of Veterans Affairs,     |
 ;; | telephone (301) 734-0100.                                     |
 ;; | The Food and Drug Administration classifies this software as  |
 ;; | a medical device.  As such, it may not be changed in any way. |
 ;; | Modifications to this software may result in an adulterated   |
 ;; | medical device under 21CFR820, the use of which is considered |
 ;; | to be a violation of US Federal Statutes.                     |
 ;; +---------------------------------------------------------------+
 ;;
 Q
 ; Initialization and shutdown of the TCP circuits
 ;
LISTEN(PORT) ; SOP Class Provider (listens for SOP Class Users connections)
 N X
 S X=$$CLOSE^MAGM2VC() ; close any open RPC connection
 D CONNECT("",PORT) ; force opening of socket in listen mode
 Q
 ;
CONNECT(DESTINAT,PORT) ; Open the connection to the destination
 N MESSAGE,STATUS
 ; initialization of standard networking variables
 ; open the socket and bind it to TCP
 S STATUS=$$REINIT^MAGOSTCP() ; Just in case
 ;
 I DESTINAT="" S MESSAGE="Socket Available on Port "_PORT
 E  S MESSAGE="Connecting to "_DESTINAT_" on Port "_PORT
 D MESSAGE^MAGDMLOG(2,0,MESSAGE,"V")
 ;
 S IPADDR=$$TCPOPEN^MAGOSTCP(DESTINAT,PORT)
 I $$USEPRIN^MAGOSFIL(1)
 I IPADDR<0 D  ; serious TCP error
 . I IPADDR?1"-4,Could not connect to".E S $EC=",U13-TCP Connection Failed,"
 . N MSG
 . S MSG(1)="Connecting to IP Address """_DESTINAT_""", port """_PORT_"""."
 . S MSG(2)="Cannot open Socket"
 . S MSG(3)="Error Message: "_IPADDR
 . D TRAPERR^MAGDSTRT(1,$T(+0),"TCP not setup correctly",.MSG)
 . Q  ; Error trap does not return
 Q
 ;
