MAGDIWBC ;WOIFO/EdM/PMK - Export of radiology images from VistA ; 10/18/2007 09:38
 ;;3.0;IMAGING;**50,54**;MAR 19, 2002;;OCT 28, 2009
 ;; Per VHA Directive 2004-038, this routine should not be modified.
 ;; +---------------------------------------------------------------+
 ;; | Property of the US Government.                                |
 ;; | No permission to copy or redistribute this software is given. |
 ;; | Use of unreleased versions of this software requires the user |
 ;; | to execute a written test agreement with the VistA Imaging    |
 ;; | Development Office of the Department of Veterans Affairs,     |
 ;; | telephone (301) 734-0100.                                     |
 ;; | The Food and Drug Administration classifies this software as  |
 ;; | a medical device.  As such, it may not be changed in any way. |
 ;; | Modifications to this software may result in an adulterated   |
 ;; | medical device under 21CFR820, the use of which is considered |
 ;; | to be a violation of US Federal Statutes.                     |
 ;; +---------------------------------------------------------------+
 ;;
 Q
 ; Entry point from ^MAGDIWBB
PATIENT ; get the patient for the export
 N DFN,RPCERR,VADM,VAICN,VAIN,VAINFO,VAPA,X
 S DFN=$G(^MAGDICOM(2006.563,1,"EXPORT PATIENT"))
 I $L(DFN) D
 . W !!,"The patient is currently defined as follows: "
 . W !!,?4,"Social Sec#",?17,"Patient's Name",?50,"Birth Date"
 . W !?4,"-----------",?17,"--------------",?50,"----------"
 . S RPCERR=$$CALLRPC^MAGM2VCU("MAG DICOM GET PATIENT","M",.VAINFO,DFN)
 . I RPCERR<0 S QUIT=-1 Q
 . D VADPT^MAGDRPC0(.VAINFO)
 . S NAME=VADM(1),DOB=$P(VADM(3),"^",2),SSN=$P(VADM(2),"^",2)
 . W !?4,SSN,?17,$E(NAME,1,30),?50,DOB
 . I $$YESNO^MAGDIWBB("Do you wish to change it?","n",.X)<0 S QUIT=1 Q
 . I X="YES" D PATIENT1
 . Q
 E  D PATIENT1
 Q
 ;
PATIENT1 N I,IN,INFO,OK,X
 S OK=0 F  D  Q:OK
 . W !!,"Enter Patient: "
 . R X:DTIME E  S OK=-1 Q
 . I X["^" S OK=-1 Q
 . I "?"[X D  Q
 . . W !!,"Enter either the Patient Name (last,first), the Social Security Number,"
 . . W !,"or the Quick PID (initial + last four).  (You may also use `IEN.)"
 . . Q
 . S IN("VALUE")=X
 . S RPCERR=$$CALLRPC^MAGM2VCU("SC PATIENT LOOKUP","MT",.INFO,.IN)
 . I '$D(INFO) W " -- NO MATCH" Q
 . S COUNT=$O(INFO(""),-1)
 . W " -- ",COUNT," MATCHES"
 . I COUNT=1 D
 . . S I=1 D SINGLE
 . . Q
 . E  D
 . . D MULTIPLE
 . . Q
 . Q
 I OK<0 S QUIT=1
 Q
 ;
SINGLE ; single match
 N RARPTIEN
 W !!,?4,"Social Sec#",?17,"Patient's Name",?50,"Birth Date"
 W !?4,"-----------",?17,"--------------",?50,"----------"
 S X=INFO(I),DFN=$P(X,"^",1),NAME=$P(X,"^",2),DOB=$P(X,"^",3),SSN=$P(X,"^",4)
 W !,$J(I,2),")",?4,SSN,?17,$E(NAME,1,30),?50,DOB
 S RPCERR=$$CALLRPC^MAGM2VCU("MAG RAD GET NEXT RPT BY PT","MT",.RARPTIEN,DFN,"")
 I RARPTIEN<0 D
 . I (+RARPTIEN)=-2 W !!,"*** Patient does not have any radiology reports on file. ***"
 . E  W !!,"*** ",RARPTIEN," ***"
 . R !!,"Press <Enter> to continue...",X:$G(DTIME,1E5)
 . Q
 E  D
 . N DATETIME,FIRSTDAY,FIRSTIEN,LASTDAY,LASTIEN,RARPT0
 . S FIRSTDAY=999999999999
 . S LASTDAY=0
 . F N=1:1 D  Q:RARPTIEN<0  Q:RARPTIEN=""
 . . S RPCERR=$$CALLRPC^MAGM2VCU("MAG DICOM GET RAD RPT INFO","MT",.RARPT0,"G1",RARPTIEN)
 . . S DATETIME=$P(RARPT0,"^",3)
 . . I DATETIME<FIRSTDAY S FIRSTDAY=DATETIME,FIRSTIEN=RARPTIEN ; get earliest date
 . . I DATETIME>LASTDAY S LASTDAY=DATETIME,LASTIEN=RARPTIEN ; get lastest date
 . . S RPCERR=$$CALLRPC^MAGM2VCU("MAG RAD GET NEXT RPT BY PT","MT",.RARPTIEN,DFN,RARPTIEN)
 . . Q
 . S FIRSTDAY=FIRSTDAY\1,LASTDAY=LASTDAY\1 ; want dates only, not times
 . I N>1 D
 . . W !!,"Patient has ",N," radiology reports on file, from "
 . . W $$FMTE^XLFDT(FIRSTDAY,1)," to ",$$FMTE^XLFDT(LASTDAY,1)
 . . Q
 . E  D
 . . W !!,"Patient has just one radiology report in file for "
 . . W $$FMTE^XLFDT(FIRSTDAY,1)
 . . Q
 . S X=$$YESNO^MAGDIWBB("Is this the correct patient?","No",.Y)
 . I Y="YES" D
 . . S ^MAGDICOM(2006.563,1,"EXPORT PATIENT")=$P(INFO(I),"^",1)
 . . S ^MAGDICOM(2006.563,1,"EXPORT BEGIN DATE")=FIRSTDAY
 . . S ^MAGDICOM(2006.563,1,"EXPORT STOP DATE")=LASTDAY+.2359 ; end of day
 . . S ^MAGDICOM(2006.563,1,"EXPORT RADIOLOGY REPORT # START")=FIRSTIEN
 . . S ^MAGDICOM(2006.563,1,"EXPORT RADIOLOGY REPORT # STOP")=LASTIEN
 . . S (DONE,FINIS,OK)=1
 . . Q
 . Q
 Q
 ;
MULTIPLE ; display information for multiple patients
 N DONE
 S DONE=0
 W !!,"There are ",COUNT," matches...  Press <Enter> for list"
 R X:$G(DTIME,300)
 I COUNT>20 D  ; more than twenty patients
 . F K=1:20:COUNT D MULTI Q:DONE
 . Q
 E  D  ; twenty or less patients
 . S K=1 D MULTI Q:DONE
 Q
 ;
MULTI ; display one set of patients
 N K20,FINIS
 S K20=K+19 I K20>COUNT S K20=COUNT
 S FINIS=0 F  D  Q:FINIS
 . W @IOF,?4,"Social Sec#",?17,"Patient's Name",?50,"Birth Date"
 . W !?4,"-----------",?17,"--------------",?50,"----------"
 . F I=K:1:K20 D 
 . . S X=INFO(I),NAME=$P(X,"^",2),DOB=$P(X,"^",3),SSN=$P(X,"^",4)
 . . W !,$J(I,2),")",?4,SSN,?17,$E(NAME,1,30),?50,DOB
 . . Q
 . W !!,"Enter ",K,"-",K20," to select the patient"
 . I K20<COUNT W ", or <Enter> to see more patients"
 . W ": " R I:$G(DTIME,300) I I="" S FINIS=1 Q
 . I I["^" S DONE=1,I="",FINIS=-1 Q
 . I I?1N.N,I'<K,I'>K20 D SINGLE Q
 . W " ???" R X:$G(DTIME,300)
 . Q
 Q
 ;
 ;
 ;
STATS ; Entry point to output statistics
 N BATCH,BEGDATE,DFN,SORTORDR,ENDDATE,HOURS,I,LASTDATE
 N NIMAGES,OLD,RUNDATE,RUNNING
 N SCANMODE,STARTPTR,STOP,STOPDATE,STOPPTR,STORESCP,STUDIES,X
 ;
 S (RUNNING,STOP)=0
 L +^MAGDICOM("Menu",28,5):0 E  S RUNNING=1
 L -^MAGDICOM("Menu",28,5):0
 ;
 S $Y=0
 W !!,"VistA DICOM Export Utility Statistics"
 W !,"-------------------------------------"
 W !!,"Current Status: Export Utility "
 I $G(^MAGDICOM(2006.563,1,"EXPORT ACTIVE"))="YES" D
 . W $S(RUNNING:"is currently running",1:"has ABORTED")
 . Q
 E  W "is not running"
 ;
 S I=0 F  S I=$O(^MAGDICOM(2006.565,I)) Q:'I  D  Q:STOP
 . S X=$G(^MAGDICOM(2006.565,I,0))
 . S RUNDATE=$P(X,"^",1),STORESCP=$P(X,"^",2),SORTORDR=$P(X,"^",3)
 . S STARTPTR=$P(X,"^",4),BATCH=$P(X,"^",5),HOURS=$P(X,"^",6)
 . S ENDDATE=$P(X,"^",7),STOPPTR=$P(X,"^",8),STUDIES=$P(X,"^",9)
 . S NIMAGES=$P(X,"^",10),SCANMODE=$P(X,"^",11)
 . S BEGDATE=$P(X,"^",12),STOPDATE=$P(X,"^",13),LASTDATE=$P(X,"^",14)
 . S DFN=$P(X,"^",15)
 . ;
 . I HOURS'=$G(OLD("HOURS")) D  Q:STOP
 . . D NEWLINE,NEWLINE Q:STOP
 . . W "Active hours of operation: ","M12345678901N12345678901 (M=midnight, N=noon)"
 . . D NEWLINE Q:STOP
 . . W "                           ",HOURS
 . . S OLD("HOURS")=HOURS
 . . Q
 . ; 
 . I STORESCP'=$G(OLD("STORESCP")) D  Q:STOP
 . . D NEWLINE,NEWLINE Q:STOP
 . . W "DICOM storage provider: ",STORESCP
 . . S OLD("STORESCP")=STORESCP
 . . K OLD("SCANMODE")
 . . Q
 . I SCANMODE="DATE" D  ; date scan order
 . . D DATESCAN
 . . Q
 . E  I SCANMODE="PATIENT" D  ; patient scan order
 . . D PATSCAN
 . . Q
 . E  D  ; numeric report order
 . . D NUMERIC
 . . Q
 . S OLD("SCANMODE")=SCANMODE
 . Q
 Q
 ;
DATESCAN ; date scan order
 K OLD("DFN")
 I SCANMODE'=$G(OLD("SCANMODE")) D  Q:STOP
 . D NEWLINE,NEWLINE Q:STOP
 . S X="*** DATE/TIME SCAN ORDER ***" W X D NEWLINE
 . W "Run    Started   A/D  Begin Date   Stop Date  Last Date     Finished    #Images"
 . D NEWLINE Q:STOP
 . W "---    -------   ---  ----------   ---------  ---------     --------    -------"
 . S OLD("STORESCP")=STORESCP
 . Q
 ;
 D NEWLINE Q:STOP
 W $J(I,3),?5,$$STATDATE(RUNDATE,"T"),?18,$E(SORTORDR),?22,$$STATDATE(BEGDATE,"Y")
 W ?35,$$STATDATE(STOPDATE,"Y"),?46,$$STATDATE(LASTDATE,"Y")
 ;
 I ENDDATE="" W ?57,"(incomplete)"
 E  W ?58,$$STATDATE(ENDDATE,"T")
 W ?70,$J(NIMAGES,8)
 Q
 ;
PATSCAN ; patient report order
 I (SCANMODE'=$G(OLD("SCANMODE")))!(DFN'=$G(OLD("DFN"))) D  Q:STOP
 . N RPCERR,VADM,VAICN,VAIN,VAINFO,VAPA,X
 . D NEWLINE,NEWLINE Q:STOP
 . S RPCERR=$$CALLRPC^MAGM2VCU("MAG DICOM GET PATIENT","M",.VAINFO,DFN)
 . I RPCERR<0 S QUIT=-1 Q
 . D VADPT^MAGDRPC0(.VAINFO)
 . S NAME=VADM(1),DOB=$P(VADM(3),"^",2),SSN=$P(VADM(2),"^",2)
 . S X="*** PATIENT: "_NAME_"    "_SSN_"    "_DOB_" ***"
 . W X D NEWLINE
 . W "Run    Started   A/D  Begin Date   Stop Date    Finished    #Images"
 . D NEWLINE Q:STOP
 . W "---    -------   ---  ----------   ---------    --------    -------"
 . S OLD("STORESCP")=STORESCP
 . S OLD("DFN")=DFN
 . Q
 ;
 D NEWLINE Q:STOP
 W $J(I,3),?5,$$STATDATE(RUNDATE,"T"),?18,$E(SORTORDR),?23,$$STATDATE(BEGDATE,"Y")
 W ?35,$$STATDATE(STOPDATE,"Y")
 ;
 I ENDDATE="" W ?45,"(incomplete)"
 E  W ?46,$$STATDATE(ENDDATE,"T")
 W ?59,$J(NIMAGES,8)
 Q
 ;
NUMERIC ; numeric report order
 K OLD("DFN")
 I SCANMODE'=$G(OLD("SCANMODE")) D  Q:STOP
 . D NEWLINE,NEWLINE Q:STOP
 . S X="*** REPORT NUMBER SCAN ORDER ***" W X D NEWLINE
 . W "Run    Started   A/D   First   Batch/Studies  Finished      Last        #Images"
 . D NEWLINE Q:STOP
 . W "---    -------   ---   ------  ------------   --------     ------       -------"
 . S OLD("STORESCP")=STORESCP
 . Q
 ;
 D NEWLINE Q:STOP
 W $J(I,3),?5,$$STATDATE(RUNDATE,"T"),?18,$E(SORTORDR),$J(STARTPTR,10)
 ;
 W $J(BATCH,6),"/",STUDIES
 ;
 I ENDDATE="" W ?44,"(incomplete)"
 E  W ?45,$$STATDATE(ENDDATE,"T")
 W ?55,$J(STOPPTR,10),?70,$J(NIMAGES,8)
 Q
 ;
STATDATE(FMDATETM,FORMAT) ; Output abbreviated date/time without years or seconds
 N X
 S X=$E(FMDATETM,4,5)
 S X=X_"/"_$E(FMDATETM,6,7)
 I $G(FORMAT)["Y" D
 . S X=X_"/"_($E(FMDATETM,1,3)+1700)
 . Q
 I $G(FORMAT)["T" D
 . S X=X_" "
 . S X=X_$E(FMDATETM,9,10)
 . S X=X_":"_$E(FMDATETM,11,12)
 . Q
 Q X
 ;
NEWLINE ; Output a <carriage return> <line feed> with scrolling control
 W ! Q:$Y<23
 N I,X
 W "more..." R X:DTIME F I=1:1:$X W $C(8,32,8)
 S $Y=0 Q:X=""
 S:$TR(X,"quitexnQUITEXN","^^^^^^^^^^^^^^")["^" STOP=1
 Q
 ;
