MAGVDGW1 ;WOIFO/NST/RRB/DAC - Send information from DICOM Gateway to VistA ; 31 Aug 2011 1:29 PM
 ;;3.0;IMAGING;**118**;Mar 19, 2002;Build 4525;May 01, 2013
 ;; Per VHA Directive 2004-038, this routine should not be modified.
 ;; +---------------------------------------------------------------+
 ;; | Property of the US Government.                                |
 ;; | No permission to copy or redistribute this software is given. |
 ;; | Use of unreleased versions of this software requires the user |
 ;; | to execute a written test agreement with the VistA Imaging    |
 ;; | Development Office of the Department of Veterans Affairs,     |
 ;; | telephone (301) 734-0100.                                     |
 ;; | The Food and Drug Administration classifies this software as  |
 ;; | a medical device.  As such, it may not be changed in any way. |
 ;; | Modifications to this software may result in an adulterated   |
 ;; | medical device under 21CFR820, the use of which is considered |
 ;; | to be a violation of US Federal Statutes.                     |
 ;; +---------------------------------------------------------------+
 ;;
 Q
 ;
SENDINST ; Send INSTRUMENT DICTIONARY file (#2006.581) to VistA
 N HOSTNAME,I,LOCATION,OUT,VISTA,RPCERR
 S LOCATION=+$G(^MAGDICOM(2006.563,1,"LOCATION")) Q:'LOCATION
 S HOSTNAME=$$HOSTNAME^MAGOSMSC() ; Obtain name of computer
 S I=0
 F  S I=$O(^MAGDICOM(2006.581,I)) Q:'+I  D
 . S VISTA(I)=^MAGDICOM(2006.581,I,0)
 S RPCERR=$$CALLRPC^MAGM2VCU("MAGV DICOM SET INSTRUMENT LIST","MT",.OUT,HOSTNAME,LOCATION,.VISTA)
 I OUT'=0 D
 . D MESSAGE^MAGDMLOG(1,0,"Could not update the main VistA hospital system","t")
 . D MESSAGE^MAGDMLOG(1,0,"file (#2006.911).","t")
 . D MESSAGE^MAGDMLOG(1,0,"please try again later.","t")
 . D MESSAGE^MAGDMLOG(1,0,"Reason: "_$P(RPCERR,", ",2,999),"t")
 . Q
 Q
 ;
SENDMOD ; Send MODALITY TYPE DICTIONARY file (#2006.582) to VistA
 N HOSTNAME,I,LOCATION,OUT,VISTA,RPCERR
 S LOCATION=+$G(^MAGDICOM(2006.563,1,"LOCATION")) Q:'LOCATION
 S HOSTNAME=$$HOSTNAME^MAGOSMSC() ; Obtain name of computer
 S I=0
 F  S I=$O(^MAGDICOM(2006.582,I)) Q:'+I  D
 . S VISTA(I)=^MAGDICOM(2006.582,I,0)
 S RPCERR=$$CALLRPC^MAGM2VCU("MAGV DICOM SET MODALITY LIST","MT",.OUT,HOSTNAME,LOCATION,.VISTA)
 I OUT'=0 D
 . D MESSAGE^MAGDMLOG(1,0,"Could not update the main VistA hospital system","t")
 . D MESSAGE^MAGDMLOG(1,0,"file (#2006.912).","t")
 . D MESSAGE^MAGDMLOG(1,0,"please try again later.","t")
 . D MESSAGE^MAGDMLOG(1,0,"Reason: "_$P(RPCERR,", ",2,999),"t")
 . Q
 Q
 ;
SETGWCFG ; Send DGW Config Info to VistA
 ;
 N HOSTNAME,MAILGROUP,NODE,POSTOFFICE,POSTPORT,IMPORTER,LOCATION
 S HOSTNAME=$$HOSTNAME^MAGOSMSC() ; Obtain name of computer
 Q:HOSTNAME=""
 S MAILGROUP=$G(^MAGDICOM(2006.563,1,"MAILGROUP"))
 Q:$TR(MAILGROUP," ","")=""
 S POSTOFFICE=$G(^MAGDICOM(2006.563,1,"POST OFFICE"))
 Q:$TR(POSTOFFICE," ","")=""
 S POSTPORT=$G(^MAGDICOM(2006.563,1,"POST PORT"))
 Q:$TR(POSTPORT," ","")=""
 S IMPORTER=$G(^MAGDICOM(2006.563,1,"IMPORTER"))
 I IMPORTER="" S IMPORTER="NO"
 S LOCATION=$G(^MAGDICOM(2006.563,1,"LOCATION"))
 Q:LOCATION=""
 S RPCERR=$$CALLRPC^MAGM2VCU("MAGV SET DGW CONFIG","MT",.OUT,HOSTNAME,MAILGROUP,POSTOFFICE,POSTPORT,IMPORTER,LOCATION)
 I +OUT=-1 D
 . D MESSAGE^MAGDMLOG(1,0,"Could not update the main VistA hospital system","t")
 . D MESSAGE^MAGDMLOG(1,0,"file (#2006.9191).","t")
 . D MESSAGE^MAGDMLOG(1,0,"please try again later.","t")
 . D MESSAGE^MAGDMLOG(1,0,"Reason: "_$P(RPCERR,", ",2,999),"t")
 . Q
 Q
