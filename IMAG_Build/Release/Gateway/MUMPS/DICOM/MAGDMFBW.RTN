MAGDMFBW ;WOIFO/PMK - Program to build DICOM Master File ; 03/29/2006 07:45
 ;;3.0;IMAGING;**9,10,11,30,69**;MAR 19, 2002;;NOV 7, 2007
 ;; Per VHA Directive 2004-038, this routine should not be modified.
 ;; +---------------------------------------------------------------+
 ;; | Property of the US Government.                                |
 ;; | No permission to copy or redistribute this software is given. |
 ;; | Use of unreleased versions of this software requires the user |
 ;; | to execute a written test agreement with the VistA Imaging    |
 ;; | Development Office of the Department of Veterans Affairs,     |
 ;; | telephone (301) 734-0100.                                     |
 ;; | The Food and Drug Administration classifies this software as  |
 ;; | a medical device.  As such, it may not be changed in any way. |
 ;; | Modifications to this software may result in an adulterated   |
 ;; | medical device under 21CFR820, the use of which is considered |
 ;; | to be a violation of US Federal Statutes.                     |
 ;; +---------------------------------------------------------------+
 ;;
WORKLIST ; Modality Worklist SCUs
 N ACNFMT ;--- Specified format for accession number (Long or Short)
 N AETITLE ;-- Specified AE Title for Worklist SCU
 N D0 ;------- Counter for Worklist records
 N DESC ;----- Specified description
 N ERR ;------ Error counter
 N FILE ;----- Full name of master file
 N IMAGTYPE ;- Specified Imaging Type
 N IMGSVC ;--- Specified Imaging Service
 N INFO ;----- Scratch array
 N ISITE ;---- Index for site in list
 N OK ;------- Current value matches validity check
 N PATH ;----- Directory for master files
 N RECORD ;--- Data record as read from master file
 N SITE ;----- Specified name or number for site
 N SITELIST ;- List of sites
 N SITEIEN ;-- IEN of SITE in ^DIC(4,...
 N SSNFMT ;--- Specified format for SSN (w/wo dashes - Dash or NoDash)
 N STATUS ;--- Result of last I/O operation
 N VALTYPE ;-- Array with acceptable values for Imaging Type
 N X ;-------- Scratch variable
 ;
 S FILE=$$INIT^MAGDMFB(2006.583,"Modality WorkList","WorkList")
 Q:FILE=""
 ;
 ; Lists of valid imaging types
 K INFO
 Q:$$CALLRPC^MAGM2VCU("MAG DICOM GET IMAGING TYPES","M",.INFO)<0
 S I="" F  S I=$O(INFO(I)) Q:I=""  D
 . S X=INFO(I) Q:X'["^"
 . S VALTYPE($P(X,"^",1),$P(X,"^",2))=""
 . Q
 K INFO
 ;
 K ^MAGDICOM(2006.583)
 S ^MAGDICOM(2006.583,0)="MODALITY WORKLIST DICTIONARY^2006.583^^"
 S D0=0
 ;
 S STATUS=$$OPEN^MAGOSFIL(FILE,"R")
 I STATUS<0 D  Q
 . D MESSAGE^MAGDMLOG(1,0,"Open Error: "_STATUS,"t")
 . S FATAL=FATAL+1
 . Q
 F  D  Q:STATUS'=""
 . S STATUS=$$READ^MAGOSFIL(FILE,.RECORD)
 . I $$USEPRIN^MAGOSFIL(1)
 . S ERR=0
 . I STATUS<0 Q:+STATUS=-2  D  Q
 . . D MESSAGE^MAGDMLOG(1,0,"Read Error: "_STATUS,"t")
 . . S FATAL=FATAL+1
 . . Q
 . Q:$TR(RECORD,$C(12,32,9))=""
 . S RECORD=$TR(RECORD,$C(145,146,147,148),$C(39,39,34,34))
 . I RECORD?1"#".E D  Q
 . . D:'SILENT MESSAGE^MAGDMLOG(1,0,"Comment: <<"_$P(RECORD,"#",2,999)_">>","t")
 . . Q
 . D:$L(RECORD,"|")'=6
 . . D MESSAGE^MAGDMLOG(1,0,"*** Record should contain 6 fields (separated by |) ***","t")
 . . D MESSAGE^MAGDMLOG(1,0,RECORD,"t")
 . . S FATAL=FATAL+1,ERR=1
 . . Q
 . S SITELIST=$$PIECE^MAGDMFBI(RECORD,2) ; Site is CASE SENSITIVE
 . S DESC=$$PIECE^MAGDMFBI(RECORD,6)
 . S RECORD=$$UPPER^MAGOSMSC(RECORD)
 . S AETITLE=$$PIECE^MAGDMFBI(RECORD,1)
 . S IMGSVC=$$PIECE^MAGDMFBI(RECORD,3)
 . S IMAGTYPE=$$PIECE^MAGDMFBI(RECORD,4)
 . S ACNFMT=$$PIECE^MAGDMFBI(RECORD,5)
 . F ISITE=1:1:$L(SITELIST,"/") D
 . . S SITE=$P(SITELIST,"/",ISITE)
 . . S SITEIEN(ISITE)=$$SITEIEN^MAGDMFB8(SITE,0)
 . . I SITEIEN(ISITE)<0 D
 . . . D MESSAGE^MAGDMLOG(1,0,"*** Error: Site """_SITE_""" not recognized: "_SITEIEN(ISITE)_" ***","t")
 . . . S SITEIEN(ISITE)="",FATAL=FATAL+1,ERR=1
 . . . Q
 . . Q
 . S OK=$L(IMGSVC)
 . I OK,'$D(VALTYPE(IMGSVC)) S OK=0
 . I 'OK D
 . . D MESSAGE^MAGDMLOG(1,0,"Unknown imaging service: "_IMGSVC,"t")
 . . S X="Possible values are",S=": "
 . . S T="" F  S T=$O(VALTYPE(T)) Q:T=""  D
 . . . S:$O(VALTYPE(T))="" S=" and "
 . . . S X=X_S
 . . . I $X+$L(T)>72 D MESSAGE^MAGDMLOG(1,0,X,"t") S X=""
 . . . S X=X_""""_T_"""",S=", "
 . . . Q
 . . D:X'="" MESSAGE^MAGDMLOG(1,0,X,"t")
 . . S FATAL=FATAL+1,ERR=1,IMGSVC="?"
 . . Q
 . S OK=$L(IMAGTYPE)
 . I OK,'$D(VALTYPE(IMGSVC,IMAGTYPE)) S OK=0
 . I 'OK D
 . . N R,S,T
 . . D MESSAGE^MAGDMLOG(1,0,"Erroneous value specified for Imaging Type: """_IMAGTYPE_""".","t")
 . . S X="Possible value",S="s are: "
 . . S T="" F  S T=$O(VALTYPE(IMGSVC,T)) Q:T=""  D
 . . . I $O(VALTYPE(IMGSVC,T))="" S S=$S(S=", ":" and ",1:" is: ")
 . . . S X=X_S
 . . . I $X+$L(T)>72 D MESSAGE^MAGDMLOG(1,0,X,"t") S X=""
 . . . S X=X_""""_T_"""",S=", "
 . . . Q
 . . S:S="s are: " X=X_"s cannot be shown at this time."
 . . D:X'="" MESSAGE^MAGDMLOG(1,0,X,"t")
 . . S FATAL=FATAL+1,ERR=1
 . . Q
 . ;
 . ; long/short accession number format
 . S X=$E(ACNFMT,1),SSNFMT=$P(ACNFMT,"/",2)
 . I X'="L",X'="S" D
 . . D MESSAGE^MAGDMLOG(1,0,"*** Invalid Accession Number Format: """_ACNFMT_""" ('Long' or 'Short') ***","t")
 . . S FATAL=FATAL+1,ERR=1
 . . Q
 . S ACNFMT=X
 . ;
 . ; social security number with or without dashes (with dashes is the default)
 . S X=$S(SSNFMT'="":$E(SSNFMT),1:"D")
 . I X'="D",X'="N" D
 . . D MESSAGE^MAGDMLOG(1,0,"*** Invalid Social Security Number Format: """_SSNFMT_""" ('Dash' or 'NoDash') ***","t")
 . . S FATAL=FATAL+1,ERR=1
 . . Q
 . S SSNFMT=X
 . ;
 . D:AETITLE=""
 . . D MESSAGE^MAGDMLOG(1,0,"*** AE-Title must be specified. ***","t")
 . . S FATAL=FATAL+1,ERR=1
 . . Q
 . ;
 . D:'SILENT!ERR
 . . S X=AETITLE_" -- "
 . . F ISITE=1:1:$L(SITELIST,"/")  S:ISITE>1 X=X_"," S X=X_SITEIEN(ISITE)
 . . S X=X_" -- "_IMGSVC_" -- "_IMAGTYPE_" -- "_ACNFMT_" -- "_SSNFMT
 . . D MESSAGE^MAGDMLOG(1,0,X,"t")
 . . Q
 . I ERR,FBSWITCH="FOREGROUND" W !,"Press <Enter> to continue: " R Z:DTIME Q
 . S X=AETITLE_"^^"_IMGSVC_"^"_IMAGTYPE_"^"_ACNFMT_"^"_DESC_"^"_SSNFMT
 . S D0=D0+1
 . S ^MAGDICOM(2006.583,D0,0)=X
 . S ^MAGDICOM(2006.583,"B",AETITLE,D0)=""
 . S $P(^MAGDICOM(2006.583,0),"^",3,4)=D0_"^"_D0
 . S ^MAGDICOM(2006.583,D0,1,0)="^2006.5832PA^0^0"
 . S D1=0 F ISITE=1:1:$L(SITELIST,"/") I SITEIEN(ISITE) D
 . . S D1=D1+1
 . . S ^MAGDICOM(2006.583,D0,1,D1,0)=SITEIEN(ISITE)
 . . S ^MAGDICOM(2006.583,D0,1,"B",SITEIEN(ISITE),D1)=""
 . . S $P(^MAGDICOM(2006.583,D0,1,0),"^",3,4)=D1_"^"_D1
 . . Q
 . Q
 S STATUS=$$CLOSE^MAGOSFIL(FILE)
 S X=D0_" entr"_$S(D0=1:"y",1:"ies")_" added to WorkList database."
 D MESSAGE^MAGDMLOG(1,0,X,"t")
 Q
 ;
