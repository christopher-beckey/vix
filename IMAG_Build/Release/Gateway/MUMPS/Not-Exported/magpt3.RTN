magpt3 ;WOIFO/MLH - individual PACS tests ; 16 Jun 2005  3:46 PM
 ; *** CAUTION:  not for distribution beyond Silver Spring OIFO!
 q
 ;
A206 ; exception flow - same case with different SSN
 ;
 n result,stat,logent,ids,ssn,irep
 k HLA
 ;
 d LOG^magpt($t(A206)_" ; begin")
 ;
 ; new order message
 d ORM^magpt("NW")
 s stat=$$SEND^magptm("ORM",.result)
 s logent="A.2.6.1 ; "_result w !,logent,! d LOG^magpt(logent,$na(HLA("HLS")))
 ;
 ; change the order code & status (OK) and diddle the SSN (not OK)
 s $p(HLA("HLS",3),HLFS,2)="XO",$p(HLA("HLS",3),HLFS,6)="CM"
 s ssn=$p(HLA("HLS",1),HLFS,20)
 s $p(ssn,HLCS,1)="998"_$e(1000000+$r(1000000),2,7)
 s $p(HLA("HLS",1),HLFS,20)=ssn
 s stat=$$SEND^magptm("ORM",.result)
 s logent="A.2.6.2 ; "_result w !,logent,! d LOG^magpt(logent,$na(HLA("HLS")))
 ;
 d LOG^magpt($t(A206)_" ; end")
 q
 ;
A207 ; exception flow - same case with different DOB
 ;
 n result,stat,logent,ids,dob
 k HLA
 ;
 d LOG^magpt($t(A207)_" ; begin")
 ;
 ; new order message
 d ORM^magpt("NW")
 s stat=$$SEND^magptm("ORM",.result)
 s logent="A.2.7.1 ; "_result w !,logent,! d LOG^magpt(logent,$na(HLA("HLS")))
 ;
 ; change the order code & status (OK) and diddle the DOB (not OK)
 s $p(HLA("HLS",3),HLFS,2)="XO",$p(HLA("HLS",3),HLFS,6)="CM"
 s dob=$p(HLA("HLS",1),HLFS,8)
 i $r(2) d
 . s $e(dob,1,4)=$e(dob,1,4)+$r(10)+1
 . q
 e  d
 . s $e(dob,5,6)=$e(100+(13-$e(dob,5,6)),2,3)
 . q
 s $p(HLA("HLS",1),HLFS,8)=dob
 s stat=$$SEND^magptm("ORM",.result)
 s logent="A.2.7.2 ; "_result w !,logent,! d LOG^magpt(logent,$na(HLA("HLS")))
 ;
 d LOG^magpt($t(A207)_" ; end")
 q
 ;
A208 ; exception flow - same case with different sex
 ;
 n result,stat,logent,ids
 k HLA
 ;
 d LOG^magpt($t(A208)_" ; begin")
 ;
 ; new 'order' message
 d ORM^magpt("NW")
 s stat=$$SEND^magptm("ORM",.result)
 s logent="A.2.8.1 ; "_result w !,logent,! d LOG^magpt(logent,$na(HLA("HLS")))
 ;
 ; change the order code & status (OK) and diddle the sex value (not OK)
 s $p(HLA("HLS",3),HLFS,2)="XO",$p(HLA("HLS",3),HLFS,6)="CM"
 s $p(HLA("HLS",1),HLFS,9)=$s($p(HLA("HLS",1),HLFS,9)="M":"F",1:"M")
 s stat=$$SEND^magptm("ORM",.result)
 s logent="A.2.8.2 ; "_result w !,logent,! d LOG^magpt(logent,$na(HLA("HLS")))
 ;
 d LOG^magpt($t(A208)_" ; end")
 q
 ;
A209 ; exception flow - same case with different name
 ;
 n result,stat,logent,ids,name
 k HLA
 ;
 d LOG^magpt($t(A209)_" ; begin")
 ;
 ; new 'order' message
 d ORM^magpt("NW")
 s stat=$$SEND^magptm("ORM",.result)
 s logent="A.2.9.1 ; "_result w !,logent,! d LOG^magpt(logent,$na(HLA("HLS")))
 ;
 ; change the order code & status (OK) and diddle the name (not OK)
 s $p(HLA("HLS",3),HLFS,2)="XO",$p(HLA("HLS",3),HLFS,6)="CM"
 s name=$p(HLA("HLS",2),HLFS,6)
 s $p(name,HLCS,1)="LAST"_($r(10)+1)
 s $p(name,HLCS,2)="FIRST"_($r(10)+1)
 s $p(name,HLCS,3)="MID"_($r(10)+1)
 s $p(HLA("HLS",1),HLFS,6)=name
 s stat=$$SEND^magptm("ORM",.result)
 s logent="A.2.9.2 ; "_result w !,logent,! d LOG^magpt(logent,$na(HLA("HLS")))
 ;
 d LOG^magpt($t(A209)_" ; end")
 q
 ;
A210 ; exception flow - orderable item code not found
 n ocd,ocd1,ocd4,i
 n junk
 s junk="!@#%*()_-+={}[]:;<>?"
 ;
 d LOG^magpt($t(A210)_" ; skipped") g A210X
 ;d LOG^magpt($t(A210)_" ; begin")
 ;
 ; new order message
 d ORM^magpt("NW")
 ;
 ; change the order codes to be nonsense values
 s ocd=$p(HLA("HLS",4),"^",5),(ocd1,ocd4)=""
 f i=1:1:5 s ocd1=ocd1_$e(junk,$r($l(junk)+1))
 f i=1:1:3 s ocd4=ocd4_$e(junk,$r($l(junk)+1))
 s $p(ocd,"~",1)=ocd1,$p(ocd,"~",4)=ocd4
 s $p(HLA("HLS",4),"^",5)=ocd
 s stat=$$SEND^magptm("ORM",.result)
 s logent="A.2.10.1 ; "_result w !,logent,! d LOG^magpt(logent,$na(HLA("HLS")))
 ;
 d LOG^magpt($t(A210)_" ; end")
A210X ;
 q
 ;
A211 ; exception flow - same case with different orderable item
 ;
 n result,orditm,flddta
 k HLA
 ;
 d LOG^magpt($t(A211)_" ; begin")
 ;
 ; new order message
 d ORM^magpt("NW")
 s stat=$$SEND^magptm("ORM",.result)
 s logent="A.2.11.1 ; "_result w !,logent,! d LOG^magpt(logent,$na(HLA("HLS")))
 ;
 ; change the orderable item
 s orditm=$p(HLA("HLS",4),"^",5)
 s chg=0
 f  s stat=$$rncmitm^magptu(.flddta) q:flddta'=orditm
 s $p(HLA("HLS",4),"^",5)=flddta
 s stat=$$SEND^magptm("ORM",.result)
 s logent="A.24.2 ; "_result w !,logent,! d LOG^magpt(logent,$na(HLA("HLS")))
 ;
 d LOG^magpt($t(A211)_" ; end")
 q
 ;
A212 ; exception flow - cancel order with different ICN
 ;
 n result,stat,logent,case,cas1,cas2,irep
 k HLA
 ;
 d LOG^magpt($t(A212)_" ; begin")
 ;
 ; new order message
 d ORM^magpt("NW")
 s stat=$$SEND^magptm("ORM",.result)
 s logent="A.2.12.1 ; "_result w !,logent,! d LOG^magpt(logent,$na(HLA("HLS")))
 ;
 ; cancel order with different ICN
 s $p(HLA("HLS",3),"^",2)="CA",$p(HLA("HLS",3),"^",6)="CA"
 s case=$p(HLA("HLS",3),"^",3),cas1=$p(case,"-",1),cas2=$p(case,"-",2)
 s ids=$p(HLA("HLS",1),"^",4)
 f irep=1:1:10 s icn=$p(ids,"|",irep) q:$p(icn,HLCS,4)="USVHA"
 q:irep>9
 s $p(icn,"~",1)="999V998"_$e(1000000+$r(1000000),2,7)
 s $p(ids,"|",irep)=icn,$p(HLA("HLS",1),"^",4)=ids
 s stat=$$SEND^magptm("ORM",.result)
 s logent="A.2.12.2 ; "_result w !,logent,! d LOG^magpt(logent,$na(HLA("HLS")))
 ;
 d LOG^magpt($t(A212)_" ; end")
 q
A213 ; exception flow - cancel order with different case
 ;
 n result,stat,logent,case,cas1,cas2,irep
 k HLA
 ;
 d LOG^magpt($t(A213)_" ; begin")
 ;
 ; new order message
 d ORM^magpt("NW")
 s stat=$$SEND^magptm("ORM",.result)
 s logent="A.2.13.1 ; "_result w !,logent,! d LOG^magpt(logent,$na(HLA("HLS")))
 ;
 ; cancel order with different case
 s $p(HLA("HLS",3),"^",2)="CA",$p(HLA("HLS",3),"^",6)="CA"
 s case=$p(HLA("HLS",3),"^",3),cas1=$p(case,"-",1),cas2=$p(case,"-",2)
 s case=$r(cas1)_"-"_$r(cas2)
 f i=3,4 f j=3,4 s $p(HLA("HLS",i),"^",j)=case
 s $p(HLA("HLS",4),"^",20)=case
 s stat=$$SEND^magptm("ORM",.result)
 s logent="A.2.13.2 ; "_result w !,logent,! d LOG^magpt(logent,$na(HLA("HLS")))
 ;
 d LOG^magpt($t(A213)_" ; end")
 q
A214 ; exception flow - cancel order with different study instance
 ; no longer performed - this is a valid case
 ;
 n result,stat,logent,siuid,i
 k HLA
 ;
 d LOG^magpt($t(A202)_" ; skipped") g A214X
 ;d LOG^magpt($t(A214)_" ; begin")
 ;
 ; new order message
 d ORM^magpt("NW")
 s stat=$$SEND^magptm("ORM",.result)
 s logent="A.2.14.1 ; "_result w !,logent,! d LOG^magpt(logent,$na(HLA("HLS")))
 ;
 ; cancel order with different study instance UID
 s $p(HLA("HLS",3),HLFS,2)="CA",$p(HLA("HLS",3),HLFS,6)="CA"
 s siuid=$p(HLA("HLS",5),HLFS,2)
 f i=8,9,10 s $p(siuid,".",i)=$r(9999)+1
 s $p(HLA("HLS",5),HLFS,2)=siuid
 s stat=$$SEND^magptm("ORM",.result)
 s logent="A.2.14.2 ; "_result w !,logent,! d LOG^magpt(logent,$na(HLA("HLS")))
 ;
 d LOG^magpt($t(A214)_" ; end")
A214X ;
 q
 ;
A215 ; non-exception flow - order for pregnant male patient
 ;
 n result,stat,logent
 k HLA
 ;
 d LOG^magpt($t(A215)_" ; begin")
 ;
 ; new order message
 d ORM^magpt("NW")
 s $p(HLA("HLS",1),HLFS,9)="M" ; make sure sex is male
 s $p(HLA("HLS",2),HLFS,16)="B6" ; pregnancy flag
 s stat=$$SEND^magptm("ORM",.result)
 s logent="A.2.15.1 ; "_result w !,logent,! d LOG^magpt(logent,$na(HLA("HLS")))
 ;
 d LOG^magpt($t(A215)_" ; end")
 q
 
