MAGDDEL1 ;WOIFO/PMK - Program to remove old worklist entries ; 06/06/2005  08:13
 ;;3.0;IMAGING;**10,11,51**;MAR 19, 2002;;JUN 16, 2006
 ;; Per VHA Directive 2004-038, this routine should not be modified.
 ;; +---------------------------------------------------------------+
 ;; | Property of the US Government.                                |
 ;; | No permission to copy or redistribute this software is given. |
 ;; | Use of unreleased versions of this software requires the user |
 ;; | to execute a written test agreement with the VistA Imaging    |
 ;; | Development Office of the Department of Veterans Affairs,     |
 ;; | telephone (301) 734-0100.                                     |
 ;; | The Food and Drug Administration classifies this software as  |
 ;; | a medical device.  As such, it may not be changed in any way. |
 ;; | Modifications to this software may result in an adulterated   |
 ;; | medical device under 21CFR820, the use of which is considered |
 ;; | to be a violation of US Federal Statutes.                     |
 ;; +---------------------------------------------------------------+
 ;;
ENTRY ; Entry point from ^MAGDMENU and ^MAGDDEL
 N RPCERR,STOPDATE
 W !!,"Ready to remove old DICOM Worklist entries?  YES// " R X:DTIME E  S X="^"
 I X="" S X="yes" W X
 I "Yy"'[$E(X) Q
 D CUTOFF^MAGDDEL Q:CUTOFF=""
 ;
 ; Get retention period
 F  D  Q:X'=""
 . W !!,"Delete DICOM Worklist entries that are older than how many days?"
 . W "  ",CUTOFF,"// " R X:DTIME E  S X="^"
 . Q:X["^"
 . I X="" S X=CUTOFF W X
 . I X'?1.N S X="^" Q  ; Stop if anything but a number is entered
 . I X<5 W " ???",!,"At least 5 days are necessary before file deletion" S X="" Q
 . ;
 . S X="T-"_X D ^%DT I Y<0 S X="" Q
 . S STOPDATE=Y,X="OK"
 . Q
 Q:X["^"
 ;
 S DIVISION=0 F  S DIVISION=$O(^MAGDWLST(2006.55,DIVISION)) Q:'DIVISION  D
 . ; invoke scan the worklist and delete all the entries
 . S RPCERR=$$CALLRPC^MAGM2VCU("MAG DICOM FILEMAN GET","M",.DIVNAME,4,DIVISION,.01)
 . I RPCERR<0 W !,"Deletion may not be able to be performed at this time." Q
 . I DIVNAME="" D
 . . S DIVNAME="*** Unknown Division ("_DIVISION_") ***"
 . . Q
 . W !!,"Deleting for ",DIVNAME
 . D SCANWLST^MAGDWLP2("VERIFIED^MAGDWLPB",Y)
 . Q
 W !!,"Done"
 Q
