MAGDMFBP ;WOIFO/PMK - Program to build DICOM Master File ; 01/04/2006  13:51
 ;;3.0;IMAGING;**9,10,30,69**;MAR 19, 2002;;NOV 7, 2007
 ;; Per VHA Directive 2004-038, this routine should not be modified.
 ;; +---------------------------------------------------------------+
 ;; | Property of the US Government.                                |
 ;; | No permission to copy or redistribute this software is given. |
 ;; | Use of unreleased versions of this software requires the user |
 ;; | to execute a written test agreement with the VistA Imaging    |
 ;; | Development Office of the Department of Veterans Affairs,     |
 ;; | telephone (301) 734-0100.                                     |
 ;; | The Food and Drug Administration classifies this software as  |
 ;; | a medical device.  As such, it may not be changed in any way. |
 ;; | Modifications to this software may result in an adulterated   |
 ;; | medical device under 21CFR820, the use of which is considered |
 ;; | to be a violation of US Federal Statutes.                     |
 ;; +---------------------------------------------------------------+
 ;;
PORTLIST ; Ports for PACS, DeJarnette MediShare, Mitra Broker, etc.
 N D0 ;------- Internal Entry Number for port description
 N ERR ;------ Error flag per record
 N FILE ;----- Input file name
 N MENUOPT ;-- Menu option for device
 N RECORD ;--- One line from master file
 N AETITLE ;-- Application Entity Title for device
 N PORT ;----- Port number to which device connects
 N FILEMODE ;- Either D (Direct) or F (FIFO Queue)
 N CHANNEL ;-- Data Channel number
 ;
 S FILE=$$INIT^MAGDMFB(2006.584,"Provider TCP/IP Port","PortList")
 Q:FILE=""
 ;
 K ^MAGDICOM(2006.584)
 S ^MAGDICOM(2006.584,0)="TCP/IP PROVIDER PORT LIST^2006.584^^"
 S D0=0
 ;
 S STATUS=$$OPEN^MAGOSFIL(FILE,"R")
 I STATUS<0 D  Q
 . D MESSAGE^MAGDMLOG(1,0,"Open Error: "_STATUS,"t")
 . S FATAL=FATAL+1
 . Q
 F  D  Q:STATUS'=""
 . S STATUS=$$READ^MAGOSFIL(FILE,.RECORD)
 . I $$USEPRIN^MAGOSFIL(1)
 . S ERR=0
 . I STATUS<0 Q:+STATUS=-2  D  Q
 . . D MESSAGE^MAGDMLOG(1,0,"Read Error: "_STATUS,"t")
 . . S FATAL=FATAL+1
 . . Q
 . Q:RECORD=""  Q:RECORD=$C(12)
 . S RECORD=$TR(RECORD,$C(145,146,147,148),$C(39,39,34,34))
 . I RECORD?1"#".E D  Q  ; comment
 . . D:'SILENT MESSAGE^MAGDMLOG(1,0,"Comment: <<"_$P(RECORD,"#",2,999)_">>","t")
 . . Q
 . ; data
 . S:$L(RECORD,"|")'=5 ERR=1
 . S MENUOPT=$$PIECE^MAGDMFBI(RECORD,1)
 . S RECORD=$$UPPER^MAGOSMSC(RECORD)
 . S AETITLE=$$PIECE^MAGDMFBI(RECORD,2)
 . S PORT=$$PIECE^MAGDMFBI(RECORD,3)
 . S FILEMODE=$$PIECE^MAGDMFBI(RECORD,4)
 . S CHANNEL=$$PIECE^MAGDMFBI(RECORD,5)
 . S X=$E(FILEMODE,1)
 . I X'="F",X'="D" D  Q
 . . D MESSAGE^MAGDMLOG(1,0,"*** Illegal File Mode: """_FILEMODE_""" ***","t")
 . . S FATAL=FATAL+1,ERR=1
 . . Q
 . S FILEMODE=X
 . I CHANNEL'="",CHANNEL'?1N D  Q
 . . D MESSAGE^MAGDMLOG(1,0,"*** Illegal Communications Channel: """_CHANNEL_""" ***","t")
 . . D MESSAGE^MAGDMLOG(1,0,"The number of channels must be between 1 and 5.","t")
 . . S FATAL=FATAL+1,ERR=1
 . . Q
 . S CHANNEL=$E(CHANNEL,1)
 . I (PORT'?1.5N)!(PORT>65535) D  Q
 . . D MESSAGE^MAGDMLOG(1,0,"*** Illegal TCP/IP Port Number: """_PORT_""" ***","t")
 . . D MESSAGE^MAGDMLOG(1,0,"The port number must be an integer between 1 and 65535.","t")
 . . S FATAL=FATAL+1,ERR=1
 . . Q
 . D:'ERR
 . . S X=MENUOPT_"^"_AETITLE_"^"_PORT_"^"_FILEMODE_"^"_CHANNEL
 . . S D0=D0+1
 . . S ^MAGDICOM(2006.584,D0,0)=X
 . . S ^MAGDICOM(2006.584,"B",MENUOPT,D0)=""
 . . S $P(^MAGDICOM(2006.584,0),"^",3,4)=D0_"^"_D0
 . . Q
 . I 'ERR,SILENT Q
 . D:ERR
 . . D MESSAGE^MAGDMLOG(1,0,"  *** FORMAT ERROR ***","t")
 . . D MESSAGE^MAGDMLOG(1,0,RECORD,"t")
 . . Q
 . S X=MENUOPT_" -- "_AETITLE_" -- "_PORT_" -- "_FILEMODE_" -- "_CHANNEL
 . D MESSAGE^MAGDMLOG(1,0,X,"t")
 . I ERR,FBSWITCH="FOREGROUND" R Z:DTIME
 . Q
 S STATUS=$$CLOSE^MAGOSFIL(FILE)
 S X=D0_" entr"_$S(D0=1:"y",1:"ies")_" added to database."
 D MESSAGE^MAGDMLOG(1,0,X,"t")
 Q
 ;
