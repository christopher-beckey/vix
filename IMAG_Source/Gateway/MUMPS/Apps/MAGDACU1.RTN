MAGDACU1 ;WOIFO/PMK - DICOM ACSE for SOP Class User ; 29 May 2009 8:30 AM
 ;;3.0;IMAGING;**30,51,54**;MAR 19, 2002;;OCT 28, 2009
 ;; Per VHA Directive 2004-038, this routine should not be modified.
 ;; +---------------------------------------------------------------+
 ;; | Property of the US Government.                                |
 ;; | No permission to copy or redistribute this software is given. |
 ;; | Use of unreleased versions of this software requires the user |
 ;; | to execute a written test agreement with the VistA Imaging    |
 ;; | Development Office of the Department of Veterans Affairs,     |
 ;; | telephone (301) 734-0100.                                     |
 ;; | The Food and Drug Administration classifies this software as  |
 ;; | a medical device.  As such, it may not be changed in any way. |
 ;; | Modifications to this software may result in an adulterated   |
 ;; | medical device under 21CFR820, the use of which is considered |
 ;; | to be a violation of US Federal Statutes.                     |
 ;; +---------------------------------------------------------------+
 ;;
 Q
 ; Association Control Service Element (ACSE) for a SOP Class User
 ;
 ; SOP Class User initiates the connection to the SOP Class Provider
 ;
ENTRY ; Initiate the connection with the SOP Class Provider
 ;
 ; The following variables must be set before the call
 ; FBSWITCH="FOREGROUND" or "BACKGROUND"
 ; FILEMODE="DIRECT" or "FIFO QUEUE"
 ; CHANNEL=1 or 2
 ;
 ; The following variables will be set by this routine
 ; DESTINAT=<SCP's ip address>
 ; PORT=<SCP's port number>
 ; CALLED=<Vendor SCP Application Entity Title>
 ; CALLING=<VistA SCU Application Entity Title>
 ;
 N CALLED ;--- the called application entity, i.e., the "listener"
 N CALLING ;-- the calling application entity, i.e., the "initiator"
 N DESTINAT ;- IP address of destination application entity
 N IPADDR ;--- SCU application's IP address and name
 N PDULEN ;--- the byte length of the Protocol Data Unit
 N PDUTYPE ;-- the PDU type 01H-07H -- see ^MAGDMFB6
 N PORT ;----- TCP/IP port number for the destination AE
 N TICKER ;--- ticker counter for idle loop
 ;
 N APPIEN,ERROR,I,STATUS,X
 ;
 S APPIEN=$O(^MAGDICOM(2006.585,"B",USERAPP,""))
 I 'APPIEN W !!,"*** Illegal User Application: """,USERAPP,""" ***" Q
 S X=^MAGDICOM(2006.585,APPIEN,0)
 S CALLED=$P(X,"^",2),CALLING=$P(X,"^",3)
 S DESTINAT=$P(X,"^",4),PORT=$P(X,"^",5)
 ;
 S ^TMP("MAG",$J,"DICOM","PORT")=PORT
 ;
 D INIT^MAGDTLOG ; initialize the message log
 I FILEMODE="FIFO QUEUE" D INIT^MAGDQUE1
 I FILEMODE="DIRECT" D  ;
 . D INIT^MAGDQUE3
 . S QUEUEIN("FILE")=INCOMING,QUEUEOUT("FILE")=OUTGOING
 . Q
 ;
 D NEWLOG^MAGDMLOG("User with "_USERAPP)
 D MESSAGE^MAGDMLOG(3,0,"User Process Started","BTV")
 ;
 ; initiate the connection with the destination socket
 D CONNECT^MAGDTCP1(DESTINAT,PORT) ; start the TCP session
 D MESSAGE^MAGDMLOG(1,0,"","V") ; output a line-feed to the log
 ;
 ; The User must now send an A-ASSOCIATE-RQ PDU to the Provider
 ;
 S PDUOUT=$$NEWFILE^MAGDTLOG("O")
 D REQUEST^MAGDACU2(PDUOUT) ; create the association request PDU
 ; read the message being sent for the log
 S STATUS=$$OPEN^MAGOSFIL(PDUOUT,"B")
 I STATUS<0 D
 . N M
 . S M="Could not open file """_PDUOUT_""" for Binary Read."
 . W:$$USEPRIN^MAGOSFIL(1) !!!,M,!,STATUS
 . S $EC=",U"_M_","
 . Q  ; Error trap does not return
 D OREQUEST^MAGDACR1(PDUOUT)
 ;
 W:$$USEPRIN^MAGOSFIL(1) !,"Sending the PDU to the SOP Class Provider via TCP",!
 D SEND^MAGDTCP2(PDUOUT,DESTINAT,PORT) ; send the PDU to SCP via TCP
 ;
 ; wait for the Provider to accept or reject the association request
 S PDUIN=$$NEWFILE^MAGDTLOG("I")
 F  D  Q:X  D IDLE^MAGDBB(.TICKER,1,1) ; loop until association
 . S X=$$RECEIVE^MAGDTCP3(PDUIN,.PDUTYPE,.PDULEN,DESTINAT,PORT)
 . I $$USEPRIN^MAGOSFIL(1)
 . Q
 ;
 ; A PDU has been received from the User.  Respond to it appropriately.
 ;
 I PDUTYPE="02H" D  ; an A-ASSOCIATE-AC was received
 . D RESPONSE^MAGDACU3(PDUIN) ; decode the reply PDU
 . ;
 . S PDUIN=$$NEWFILE^MAGDTLOG("I")
 . S PDUOUT=$$NEWFILE^MAGDTLOG("O")
 . I FILEMODE="FIFO QUEUE" D ENTRY^MAGDQUE1(DESTINAT,PORT)
 . I FILEMODE="DIRECT" D  ; send the message and wait
 . . S ERROR=$$SENDDATA^MAGDTCP2(OUTGOING,DESTINAT,PORT)
 . . I ERROR D  Q  ; error occurred
 . . . N MESSAGE
 . . . S MESSAGE(1)="Error Number "_$P(ERROR,",")
 . . . S MESSAGE(1)=MESSAGE(1)_" occurred trying to send a DICOM message"
 . . . S MESSAGE(2)=$P(ERROR,",",2)
 . . . D MESSAGE^MAGDMLOG(1,0,.MESSAGE,"BV")
 . . . S DONEFLAG=-1
 . . . Q
 . . D ENTRY^MAGDQUE3(DESTINAT,PORT)
 . . Q
 . ;
 . I '$G(DONEFLAG) D  Q  ; A non-P-DATA-TF PDU has been received.
 . . D MESSAGE^MAGDMLOG(2,0,"Illegal PDU type: "_PDUTYPE,"BVT")
 . . Q
 . Q
 E  I PDUTYPE="03H" D  ; association rejected
 . D REJECT^MAGDACU3(PDUIN) ; decode the reply PDU
 . Q
 E  D  Q  ; unknown PDU type
 . D MESSAGE^MAGDMLOG(2,0,"Unknown PDU type: "_PDUTYPE,"BT")
 . ; close the TCP/IP channel
 . S STATUS=$$TCPCLOSE^MAGOSTCP(DESTINAT,PORT)
 . I $$USEPRIN^MAGOSFIL(1)
 . Q
 ;
 ; create the A-RELEASE-RQ PDU to request termination of the association
 S PDUOUT=$$NEWFILE^MAGDTLOG("O")
 S STATUS=$$OPEN^MAGOSFIL(PDUOUT,"W")
 I STATUS<0 D
 . N M
 . S M="Could not open file """_PDUOUT_""" for Write."
 . W:$$USEPRIN^MAGOSFIL(1) !!!,M,!,STATUS
 . S $EC=",U"_M_","
 . Q  ; Error trap does not return
 I $G(^MAGDICOM(2006.563,1,"SIMULATED ABORT TEST"))="YES" D
 . ; special SCU abort test processing
 . D ABORT^MAGDACW2(PDUOUT) ; create the A-ABORT PDU (for testing)
 . D SEND^MAGDTCP2(PDUOUT,DESTINAT,PORT) ; send it to the Provider
 . Q
 E  D  ; normal processing
 . D RELEASRQ^MAGDACW2(PDUOUT) ; create the A-RELEASE-RQ PDU
 . D SEND^MAGDTCP2(PDUOUT,DESTINAT,PORT) ; send it to the Provider
 . ;
 . ; wait for the A-RELEASE-RP response for the termination
 . I FILEMODE="FIFO QUEUE" D ENTRY^MAGDQUE1(DESTINAT,PORT)
 . I FILEMODE="DIRECT" D ENTRY^MAGDQUE3(DESTINAT,PORT)
 . Q
 ;
 S STATUS=$$TCPCLOSE^MAGOSTCP(DESTINAT,PORT) ; close the TCP/IP channel
 I $$USEPRIN^MAGOSFIL(1)
 ;
 I $G(DONEFLAG) Q  ; finished processing
 ;
 ; A non-P-DATA-TF PDU has been received.  Is it an A-RELEASE-RP?
 I PDUTYPE'="06H" D  Q  ; nope
 . D MESSAGE^MAGDMLOG(2,0,"Illegal PDU type: "_PDUTYPE,"BVT")
 . Q
 S DONEFLAG=2  ; make sure that DONEFLAG is set
 Q
 ;
