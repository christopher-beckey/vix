MAGDIR7G ;WOIFO/PMK - Read a DICOM image file ; 03/08/2005  06:58
 ;;3.0;IMAGING;**11,51**;MAR 19, 2002;;JUN 16, 2006
 ;; Per VHA Directive 2004-038, this routine should not be modified.
 ;; +---------------------------------------------------------------+
 ;; | Property of the US Government.                                |
 ;; | No permission to copy or redistribute this software is given. |
 ;; | Use of unreleased versions of this software requires the user |
 ;; | to execute a written test agreement with the VistA Imaging    |
 ;; | Development Office of the Department of Veterans Affairs,     |
 ;; | telephone (301) 734-0100.                                     |
 ;; | The Food and Drug Administration classifies this software as  |
 ;; | a medical device.  As such, it may not be changed in any way. |
 ;; | Modifications to this software may result in an adulterated   |
 ;; | medical device under 21CFR820, the use of which is considered |
 ;; | to be a violation of US Federal Statutes.                     |
 ;; +---------------------------------------------------------------+
 ;;
 Q
 ;
DCMTOTGA(TOFILE,FILETYPE) ; convert DICOM to TGA
 ; this is a two-step process (1) create the temp tga file (2) copy it
 N CMD,ERROR,FILESIZE
 ;
 ; delete the previous temporary targa image first
 D DELETE^MAGDIR71(IMAGETGA,"TEMPORARY TARGA IMAGE",1)
 ;
 ; create the temporary IMAGETGA tga file
 S CMD="MAG_DCMTOTGA "_FROMPATH_" "_IMAGETGA
 S CMD=CMD_" X"_COLUMNS ; X-dimension
 S CMD=CMD_" Y"_ROWS ; Y-dimension
 S CMD=CMD_" O"_OFFSET ; image offset
 S CMD=CMD_" B"_BITSTORE ; pixel bits actually stored
 I BITSTORE<16 D  ; don't output F0 and Cnnnnn for a 16-bit image
 . S CMD=CMD_" F0" ; floor value
 . S CMD=CMD_" C"_((2**BITSTORE)-1) ; ceiling value
 . Q
 I PHOTOINT="MONOCHROME1" S CMD=CMD_" I" ; invert the pixels
 S CMD=CMD_" "_$P(MODPARMS,"/",FILETYPE)
 S VERBOSE=1 D DOS^MAGDOS(CMD,.ERROR)
 I ERROR(0) D  ; fatal error
 . N I,MSG
 . S MSG(1)="TARGA IMAGE FILE CREATION ERROR"
 . S MSG(2)="Unexpected error in """_CMD_""""
 . F I=1:1:ERROR(0) S MSG(I+2)=$J(I,3)_") "_ERROR(I)
 . D ROLLBACK^MAGDIR75(IMAGEPTR,.MSG) ; delete ^MAG(2005,IMAGEPTR) entry
 . D TRAPERR^MAGDSTRT(1,$T(+0),"DICOM IMAGE PROCESSING ERROR",.MSG)
 . Q  ; Error trap does not return
 ;
 ; get length of temporary targa file
 S FILESIZE=$$FILESIZE^MAGDIR75(IMAGETGA,"TARGA")
 ;
 ; copy the temporary IMAGETGA targa file to TOFILE on the server
 D FILECOPY^MAGDIR75(IMAGETGA,TOFILE,"TARGA")
 ;
 ; Check that the full size image was written properly
 D FILECHK^MAGDIR75(TOFILE,FILESIZE)
 Q
 ;
VALIDATE N N
 W !!,"Starting Validation of data in DICOM Failed Images Table.",!
 Q:$$CALLRPC^MAGM2VCU("MAG DICOM CORRECT VALIDATE","MT",.N)<0
 W !!,N," entr",$S(N=1:"y",1:"ies")," currently in database.",!
 Q
 ;
