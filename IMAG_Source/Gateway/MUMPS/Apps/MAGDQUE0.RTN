MAGDQUE0 ;WOIFO/PMK - FIFO QUEUE file management ; 19 Dec 2008 3:12 PM
 ;;3.0;IMAGING;**30,54**;MAR 19, 2002;;OCT 28, 2009
 ;; Per VHA Directive 2004-038, this routine should not be modified.
 ;; +---------------------------------------------------------------+
 ;; | Property of the US Government.                                |
 ;; | No permission to copy or redistribute this software is given. |
 ;; | Use of unreleased versions of this software requires the user |
 ;; | to execute a written test agreement with the VistA Imaging    |
 ;; | Development Office of the Department of Veterans Affairs,     |
 ;; | telephone (301) 734-0100.                                     |
 ;; | The Food and Drug Administration classifies this software as  |
 ;; | a medical device.  As such, it may not be changed in any way. |
 ;; | Modifications to this software may result in an adulterated   |
 ;; | medical device under 21CFR820, the use of which is considered |
 ;; | to be a violation of US Federal Statutes.                     |
 ;; +---------------------------------------------------------------+
 ;;
 Q
 ; subroutines for queue management
 ;
GETWPTR ; read the queue write pointer and increment it
 N SUBDIR,X
 S QUEUEOUT("FILEPTR")=QUEUEOUT("FPTRPATH")_"\"_QUEUEOUT("QLETTER")_"_WRITE.PTR"
 L +^MAGDICOM("LOCK",QUEUEOUT("FILEPTR")):1E9 ; serialize output for competing processes
 D NEXTFILE(.QUEUEOUT) ; use call by reference to pass the entire array
 I DEBUG D  ; output file info
 . S X=QUEUEOUT("FILE")_" -- "_MESSAGE_" -- "_MSGHANDL
 . D MESSAGE^MAGDMLOG($S(DEBUG=2:2,1:1),0,X,"tV")
 . Q
 S QUEUEOUT("FILE")=QUEUEOUT("FILE")_".TMP"
 ;
 ; does the appropriate subdirectory exist?
 S X=$$GETDIR^MAGOSDIR(SUBDIR,"D") ; if SUBDIR exists, X will be >0
 I X<0 D  ; subdirectory lookup error
 . N MSG
 . S MSG(1)="Cannot locate directory "_SUBDIR
 . S MSG(2)="Host File System Error "_X
 . D TRAPERR^MAGDSTRT(1,$T(+0),"DICOM TEXT GATEWAY ERROR",.MSG)
 . Q  ; Error trap does not return
 E  I 'X D  ; subdirectory does not exist
 . S X=$$MKDIR^MAGOSDIR(SUBDIR) ; create the subdirectory it
 . I X<0 D
 . . N MSG
 . . S MSG(1)="Cannot create directory "_SUBDIR
 . . S MSG(2)="Host File System Error: "_X
 . . D TRAPERR^MAGDSTRT(1,$T(+0),"DICOM TEXT GATEWAY ERROR",.MSG)
 . . Q  ; Error trap does not return
 . Q
 Q
 ;
GETRPTR() ; read the queue read pointer and increment it for the next file
 N SUBDIR,X
 S QUEUEIN("FILEPTR")=QUEUEIN("FPTRPATH")_"\"_QUEUEIN("QLETTER")_"_READ.PTR"
 D NEXTFILE(.QUEUEIN) ; use call by reference to pass the entire array
 S QUEUEIN("FILE")=QUEUEIN("FILE")_".DCM"
 S X=$$GETDIR^MAGOSDIR(QUEUEIN("FILE")) ; does the DICOM message file exist?
 Q X_" ("_QUEUEIN("FILE")_")"
 ;
NEXTFILE(QUEUE) ; get the next file number for input or output
 N STATUS,FILENUMB
 S STATUS=$$OPEN^MAGOSFIL(QUEUE("FILEPTR"),"R")
 I STATUS<0 D
 . N M
 . S M="Could not open file """_QUEUE("FILEPTR")_""" for Read."
 . W:$$USEPRIN^MAGOSFIL(1) !!!,M,!,STATUS
 . H 30
 . S $EC=",U"_M_","
 . Q  ; Error trap does not return
 S STATUS=$$READ^MAGOSFIL(QUEUE("FILEPTR"),.FILENUMB)
 S STATUS=$$CLOSE^MAGOSFIL(QUEUE("FILEPTR"))
 I $$USEPRIN^MAGOSFIL(1)
 S QUEUE("FILENUMB")=FILENUMB
 I QUEUE("FILENUMB")'?7N D  Q
 . N MSG
 . S MSG(1)="Bad File Pointer:"
 . S MSG(2)=""""_QUEUE("FILEPTR")_""" = """_QUEUE("FILENUMB")_"""."
 . S MSG(3)="Value of pointer should be 7 digits."
 . D TRAPERR^MAGDSTRT(1,$T(+0),"DICOM TEXT GATEWAY ERROR -- WRITING DICOM FILE",.MSG)
 . Q  ; Error trap does not return
 S QUEUE("FILENUMB")=QUEUE("FILENUMB")+1
 I QUEUE("FILENUMB")>9999999 S QUEUE("FILENUMB")=0 ; handle numeric wrap
 ;
 ; generate the actual file name and path
 S QUEUE("FILENUMB")=$TR($J(QUEUE("FILENUMB"),7)," ",0)
 S QUEUE("FILE")=QUEUE("QLETTER")_QUEUE("FILENUMB")
 S SUBDIR=QUEUE("DATAPATH")_"\"_$E(QUEUE("FILE"),1,6)
 S QUEUE("FILE")=SUBDIR_"\"_QUEUE("FILE")
 Q
 ;
FINISHW ; entry point from ^MAGDDW1 and ^MAGDQUE2 to finish the processing
 ; rename the temporary file to the *.DCM
 N TMPFILE,X
 S TMPFILE=QUEUEOUT("FILE")
 S QUEUEOUT("FILE")=$P(QUEUEOUT("FILE"),".")_".DCM"
 S X=$$RENAME^MAGOSDIR(TMPFILE,QUEUEOUT("FILE"))
 I X<0 D  ; rename failed the first time, try to correct it
 . D MESSAGE^MAGDMLOG(1,0,"WARNING -- Having to RENAME file "_QUEUEOUT("FILE"),"BV")
 . S X=$$DEL^MAGOSDIR(QUEUEOUT("FILE")) ; delete the old *.DCM file
 . S X=$$RENAME^MAGOSDIR(TMPFILE,QUEUEOUT("FILE")) ; retry the rename
 . I X<0 D  Q  ; rename failed the second time -- stop the system
 . . N MSG
 . . S MSG(1)="Renaming "_TMPFILE_" to "_QUEUEOUT("FILE")_" failed"
 . . S MSG(2)="Delete the file """_QUEUEOUT("FILE")_""" and restart."
 . . D TRAPERR^MAGDSTRT(1,$T(+0),"DICOM TEXT GATEWAY ERROR -- WRITING DICOM FILE",.MSG)
 . . Q  ; Error trap does not return
 . Q
 ;
 D FINISH(.QUEUEOUT) ; use call by reference to pass the entire array
 L -^MAGDICOM("LOCK",QUEUEOUT("FILEPTR")) ; clear the serialize i/o lock
 Q
 ;
FINISHR ; entry point from xxx to update the queue read pointer file
 D FINISH(.QUEUEIN) ; use call by reference to pass the entire array
 Q
 ;
FINISH(QUEUE) ; update the queue i/o pointer file with the incremented value
 N STATUS
 S STATUS=$$OPEN^MAGOSFIL(QUEUE("FILEPTR"),"W")
 I STATUS<0 D
 . N M
 . S M="Could not open file """_QUEUE("FILEPTR")_""" for Write."
 . W:$$USEPRIN^MAGOSFIL(1) !!!,M,!,STATUS
 . H 30
 . S $EC=",U"_M_","
 . Q  ; Error trap does not return
 S STATUS=$$USE^MAGOSFIL(QUEUE("FILEPTR"))
 W QUEUE("FILENUMB"),!
 S STATUS=$$CLOSE^MAGOSFIL(QUEUE("FILEPTR"))
 I $$USEPRIN^MAGOSFIL(1)
 Q
 ;
