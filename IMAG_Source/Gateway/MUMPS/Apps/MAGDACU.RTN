Cache for Windows^INT^P156^~Format=Cache.S~
%RO on 26 Feb 2015  11:38 PM
MAGDACU^INT^1^63609,85084^0
MAGDACU ;WOIFO/PMK,DAC - DICOM ACSE for SOP Class User ; 2/26/2015  12:17
 ;;3.0;IMAGING;**21,30,138,156**;MAR 19, 2002;;NOV 5, 2004
 ;; Per VHA Directive 2004-038, this routine should not be modified.
 ;; +---------------------------------------------------------------+
 ;; | Property of the US Government.                                |
 ;; | No permission to copy or redistribute this software is given. |
 ;; | Use of unreleased versions of this software requires the user |
 ;; | to execute a written test agreement with the VistA Imaging    |
 ;; | Development Office of the Department of Veterans Affairs,     |
 ;; | telephone (301) 734-0100.                                     |
 ;; | The Food and Drug Administration classifies this software as  |
 ;; | a medical device.  As such, it may not be changed in any way. |
 ;; | Modifications to this software may result in an adulterated   |
 ;; | medical device under 21CFR820, the use of which is considered |
 ;; | to be a violation of US Federal Statutes.                     |
 ;; +---------------------------------------------------------------+
 ;;
 ; Association Control Service Element (ACSE) for a SOP Class User
 ;
 ; SOP Class User initiates the connection to the SOP Class Provider
 ;
ENTRY ; Initiate the connection with the SOP Class Provider
 S DTIME=300
 S USERAPP=$$PICKSCP^MAGDACU("") Q:USERAPP=""
 W !!,"Ready to try the """,USERAPP,""" SCP Application?  y// "
 R X:DTIME I X="" S X="yes" W X
 I "Yy"'[$E(X) Q
 R !!,"Use FIFO QUEUE or DIRECT filemode  F // ",X:$G(DTIME,300) E  S X="^"
 I X="" S X="f" W X
 I "FfDd"'[$E(X) W " (quit)" Q
 S FILEMODE=$S("Dd"[X:"DIRECT",1:"FIFO QUEUE")
 S FBSWITCH="FOREGROUND",CHANNEL=1
 D ENTRY^MAGDACU1
 Q
 ;
PICKSCP(DEFAULT,SCPTYPE) ; P156 DAC - pick the SOP Class Providers that VistA can access as a SCU - filter on SCPTYPE
 N HIT,I,N,X,USERAPP,TARGET,SYSTITLE,MAGIEN,MAGSCP
 S SYSTITLE=$G(^MAGDICOM(2006.563,1,"SYSTEM TITLE"),"<unknown>")
 K TARGET S USERAPP="",HIT=0,I=1
 F  S USERAPP=$O(^MAGDICOM(2006.585,"B",USERAPP)) Q:USERAPP=""  D  ; P156 DAC
 . S MAGIEN=$O(^MAGDICOM(2006.585,"B",USERAPP,""))
 . I $G(SCPTYPE)'="" S MAGSCP=$P($G(^MAGDICOM(2006.585,MAGIEN,0)),"^",9) ; P156 DAC
 . I $G(SCPTYPE)'=($G(MAGSCP)) Q  ; P156 DAC
 . S TARGET(I)=USERAPP
 . I USERAPP=$G(DEFAULT) S HIT=I
 . S I=I+1 ; P156 DAC
 . Q
 S N=I-1
 I N<1 W !!,"No Service Class Providers defined in SCU_LIST.DIC." Q ""
 F  D  Q:X'=""
 . W !,"DICOM Service Class Providers"
 . W !,"-----------------------------"
 . F I=1:1:N W !,$J(I,3)," -- ",$P(TARGET(I),"^")
 . I N=1 W " (selected)" S X=1 Q
 . W !!,"Select the provider application (1-",N,"): "
 . W:HIT HIT,"// " R X:DTIME E  S X="^"
 . I X["^" Q  ; a caret will terminate the program
 . I X="" S X=HIT W X I X="" S X="^" Q
 . I (X<1)!(X>N)!'$D(TARGET(X)) W " ??? -- try again",!! S X="" Q
 . Q
 I X["^" S X=""
 I X S X=TARGET(X)
 Q X



