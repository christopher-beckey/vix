Cache for Windows NT^INT^Saved from DICOM,ROU (ISW-KUZMAKP3-LT)^~Format=Cache.S~
%RO on 07/03/2013  11:26
MAGDIW2A^INT^1^62913,55330.59792^0
MAGDIW2A ;WOIFO/PMK - Send images to DICOM storage ; 01 Apr 2013 3:22 PM
 ;;3.0;IMAGING;**10,11,30,51,50,54,123,110**;Mar 19, 2002;Build 61;Oct 10, 2012
 ;; Per VHA Directive 2004-038, this routine should not be modified.
 ;; +---------------------------------------------------------------+
 ;; | Property of the US Government.                                |
 ;; | No permission to copy or redistribute this software is given. |
 ;; | Use of unreleased versions of this software requires the user |
 ;; | to execute a written test agreement with the VistA Imaging    |
 ;; | Development Office of the Department of Veterans Affairs,     |
 ;; | telephone (301) 734-0100.                                     |
 ;; | The Food and Drug Administration classifies this software as  |
 ;; | a medical device.  As such, it may not be changed in any way. |
 ;; | Modifications to this software may result in an adulterated   |
 ;; | medical device under 21CFR820, the use of which is considered |
 ;; | to be a violation of US Federal Statutes.                     |
 ;; +---------------------------------------------------------------+
 ;;
 Q
 ;
ENTRY ; called from ^MAGDIWB2 to handle PID correction of DICOM images
 N AGE,DATA,DCMOUT,DICOM,DICTVR,DFN,FIRSTNAM,I,LASTNAME,MIDNAME,MODALITY
 N NAME,RACE,STATUS,TAG,TEXT,TXTOUT,VADATA,VALUE,X,Z
 ;
 ; figure out the modality from the SOP Class, if needed
 S MODALITY=$$GETDATA^MAGDFCNS(INODE,"0008,0060")
 I MODALITY["," D
 . N SOPCLASS
 . S SOPCLASS=$$GETDATA^MAGDFCNS(INODE,"0008,0016")
 . D MODALITY^MAGDIW3A(.MODALITY,SOPCLASS)
 . Q
 ;
 ; get radiology procedure description
 S PROCDESC="" I '$$GMRCIEN^MAGDFCNS(ACCNUMB) D RADLKUP^MAGDIW3B
 I PROCDESC="" S PROCDESC=$$GETDATA^MAGDFCNS(INODE,"0032,1060")
 ;
 ; get AGE from DATA1 block - most accurate value on day of study
 S AGE=$E(1000+^TMP("MAG",$J,"DICOM","DATA1 BLOCK","PATIENTS_AGE"),2,4)_"Y"
 ;
 ; build VADATA array
 S VADATA("0008,0050")=ACCNUMB ; Accession Number
 S VADATA("0008,0060")=MODALITY ; updated (maybe) modality
 ; format patient name: last^first^middle
 S LASTNAME=$P(VADM(1),","),Z=$P(VADM(1),",",2,999)
 S FIRSTNAM=$P(Z," ",1),MIDNAME=$TR($P(Z," ",2,999),".")
 S VADATA("0010,0010")=LASTNAME_"^"_FIRSTNAM_"^"_MIDNAME ; Patient's name
 I $$ISIHS^MAGSPID() S VADATA("0010,0020")=VA("PID") ; P123 IHS Patient ID
 E  S VADATA("0010,0020")=$P(VADM(2),"^",2) ; p123 VA Patient ID
 S VADATA("0010,0030")=$P(VADM(3),"^",1)+17000000 ; Patient DOB
 S VADATA("0010,0040")=$P(VADM(5),"^",1) ; Patient's Sex
 S VADATA("0010,1010")=AGE ; patient's age from DATA1 block
 S VADATA("0010,1000")=VAICN_"\"_SITEDFN ; Other Patient IDs
 S RACE=$P($G(VADM(12,1)),"^",2) D ETHNIC^MAGDFND4
 S VADATA("0010,2000")="See patient's chart." ; medical alerts
 S VADATA("0010,2160")=RACE ; Ethnic Group
 S VADATA("0032,1060")=PROCDESC ; requested procedure description
 ;
 ; build the DATA array
 F I=1:1 S TEXT=$P($T(ELEMENTS+I),";;",2,999) Q:TEXT="end"  D
 . S TAG=$P(TEXT,";") Q:TAG=""
 . S X=$O(^MAGDICOM(2006.51,"B",TAG,"")),X=^MAGDICOM(2006.51,X,0)
 . S NAME=$P(X,"^",2),DICTVR=$P(X,"^",3)
 . I DICTVR?1"US/SS".E S DICTVR="US" ; may be US/SS/OW
 . I NAME'=$P(TEXT,";",2) W !,TEXT," Name should be <<",NAME,">>"
 . S VALUE=VADATA(TAG)
 . S ^TMP("MAG",$J,"DCF_STORE_SCU",DCMCOUNT,"REPLACE",I)=TAG_" = "_VALUE
 . Q
 ;
 S ^TMP("MAG",$J,"DCF_STORE_SCU",DCMCOUNT,"REPLACE",0)=I-1 ; count of modifications
 ;
 ; remove two tags
 S ^TMP("MAG",$J,"DCF_STORE_SCU",DCMCOUNT,"REMOVE",0)=2
 S ^TMP("MAG",$J,"DCF_STORE_SCU",DCMCOUNT,"REMOVE",1)="tag = 0008,1050" ; Performing Physician's Name
 S ^TMP("MAG",$J,"DCF_STORE_SCU",DCMCOUNT,"REMOVE",2)="tag = 0010,1040" ; Patient's Address
 Q
 ;
ELEMENTS ; list of elements to output
 ;;0008,0050;Accession Number
 ;;0008,0060;Modality
 ;;0010,0010;Patient's Name
 ;;0010,0020;Patient ID
 ;;0010,0030;Patient's Birth Date
 ;;0010,0040;Patient's Sex
 ;;0010,1000;Other Patient IDs
 ;;0010,1010;Patient's Age
 ;;0010,2000;Medical Alerts
 ;;0010,2160;Ethnic Group
 ;;0032,1060;Requested Procedure Description
 ;;end



