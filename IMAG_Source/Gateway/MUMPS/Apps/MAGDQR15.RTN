Cache for Windows^INT^P160^~Format=Cache.S~
%RO on 28 Jul 2015   7:27 AM
MAGDQR15^INT^1^63760,75606^0
MAGDQR15 ;WOIFO/EdM,DAC - Imaging RPCs for Query/Retrieve ; 27 Jul 2015 3:14 PM
 ;;3.0;IMAGING;**54,160**;MAR 19, 2002;Build 6002;Jul 27, 2015
 ;; Per VHA Directive 2004-038, this routine should not be modified.
 ;; +---------------------------------------------------------------+
 ;; | Property of the US Government.                                |
 ;; | No permission to copy or redistribute this software is given. |
 ;; | Use of unreleased versions of this software requires the user |
 ;; | to execute a written test agreement with the VistA Imaging    |
 ;; | Development Office of the Department of Veterans Affairs,     |
 ;; | telephone (301) 734-0100.                                     |
 ;; | The Food and Drug Administration classifies this software as  |
 ;; | a medical device.  As such, it may not be changed in any way. |
 ;; | Modifications to this software may result in an adulterated   |
 ;; | medical device under 21CFR820, the use of which is considered |
 ;; | to be a violation of US Federal Statutes.                     |
 ;; +---------------------------------------------------------------+
 ;;
 Q
 ;
SENDINFO N DIR,FILE,FILES,HOSTNAME,LOCATION,OUT,VER,X
 S LOCATION=+$G(^MAGDICOM(2006.563,1,"LOCATION")) Q:'LOCATION
 S DIR=$G(^MAGDICOM(2006.563,1,"DICT PATH")) Q:DIR=""
 S:$E(DIR,$L(DIR))'="\" DIR=DIR_"\"
 S X=$$GETDIR^MAGOSDIR(DIR_"*.DIC","*")
 K FILES
 S FILE="" F  S FILE=$O(^TMP("MAG",$J,"DIR LIST",FILE)) Q:FILE=""  D
 . D FILE(DIR,FILE,.FILES,".dic")
 . Q
 ; Obtain meta-information about .DIC files
 N D,I,O
 S X=$$HOSTCMD^MAGOSMSC("echo %ProgramFiles%~",.O)
 D PROGFILE(1,FILE,.VER) 
 N D,I,O
 S X=$$HOSTCMD^MAGOSMSC("echo %ProgramFiles(x86)%~",.O) ; P160 DAC - Fix for 64-bit operating systems
 D PROGFILE(1,FILE,.VER) 
 S HOSTNAME=$$HOSTNAME^MAGOSMSC() ; Obtain name of computer
 S VER("GW")=$T(+2^MAGDVRSN)
 S X=$$CALLRPC^MAGM2VCU("MAG DICOM STORE GATEWAY INFO","MT",.OUT,HOSTNAME,LOCATION,.FILES,.VER)
 Q
 ;
PROGFILE(I,FILE,VER) ; P160 DAC - Created new subroutine for multiple calls
 S DIR=""
 S I="" F  S I=$O(O(I)) Q:I=""  S:O(I)'="" DIR=O(I)
 S DIR=$P(DIR,"~",1,$L(DIR,"~")-1)
 Q:DIR=""
 S:$E(DIR,$L(DIR))'="\" DIR=DIR_"\"
 S D=DIR_"VistA\Imaging\DCMView\"
 S X=$$GETDIR^MAGOSDIR(D_"*.exe","*")
 S FILE="" F  S FILE=$O(^TMP("MAG",$J,"DIR LIST",FILE)) Q:FILE=""  D
 . D FILE(D,FILE,.VER,".exe")
 . Q
 S D=DIR_"VistA\Imaging\DICOM\"
 S X=$$GETDIR^MAGOSDIR(D_"*.exe","*")
 S FILE="" F  S FILE=$O(^TMP("MAG",$J,"DIR LIST",FILE)) Q:FILE=""  D
 . D FILE(D,FILE,.VER,".exe")
 . Q
 Q 
FILE(DIR,NAME,OUT,EXT) N D,DAT,F,H,M,S,TIM,Y
 ; Ferret out the meta-information about the files
 ; Data in ^TMP looks like:
 ;     ^TMP("MAG",$j,"DIR LIST","CT_PARAM.DIC")=50862;12-MAY-2006;6:24;A
 ;                              "DATAGECT.DIC")=1973;18-JUL-2002;7:38;A
 ;     etc.
 ; Extension is passed as a parameter to ensure consistency in
 ; usage of upper/lower case.
 ;
 S F=$P(FILE,".",1) Q:F=""
 S X=$G(^TMP("MAG",$J,"DIR LIST",FILE))
 ;
 ; Convert date/time to FileMan format
 S DAT=$P(X,";",2),TIM=$P(X,";",3) Q:DAT=""
 S D=$P(DAT,"-",1),M=$P(DAT,"-",2),Y=$P(DAT,"-",3)
 S M=$F("JANFEBMARAPRMAYJUNJULAUGSEPOCTNOVDEC",M)\3
 S X=Y-1700*100+M*100+D
 S H=$P(TIM,":",1),M=$P(TIM,":",2),S=$P(TIM,":",3)
 S X=X*100+H*100+M*100+S
 ;
 ; Return formatted record
 S OUT(F)=X/1000000_"^"_DIR_F_EXT
 Q
 ;
SHOWVER N HOSTNAME,KEY,OUT,X
 D SENDINFO
 S HOSTNAME=$$HOSTNAME^MAGOSMSC()
 S X=$$CALLRPC^MAGM2VCU("MAG DICOM GET GATEWAY INFO","MT",.OUT,HOSTNAME)
 S I=$O(OUT(""))
 I I="" D  Q
 . W !!,"Remote procedure returned no information..."
 . Q
 I OUT(I)<0 D  Q
 . W !!,"Error in remote procedure: ",OUT(I)
 . Q
 S OUT(I)="Hn="_HOSTNAME
 S KEY("Hn")="Host Name"
 S KEY("Loc")="Location"
 S KEY("In")="Instrument"
 S KEY("Mo")="Modality"
 S KEY("PL")="Port List"
 S KEY("SL")="SCU List"
 S KEY("WL")="WorkList"
 S KEY("CP")="CT Parameters"
 S KEY("Ver")="Version"
 S KEY("Vw")="DICOM Viewer"
 S KEY("Ab")="Abstract Maker"
 S KEY("CS")="C-Store"
 S KEY("Rc")="Reconstructor"
 S KEY("DT")="DICOM to Targa"
 S I="" F  S I=$O(OUT(I)) Q:I=""  D
 . S KEY=$P(OUT(I),"=",1),X=$P(OUT(I),"=",2,$L(OUT(I))+2)
 . S K=$P(KEY,"_",1) S:K'="" K=$G(KEY(K)) S:K="" K=KEY
 . S:KEY["_p" K=K_" (path)"
 . S:KEY["_s" K=K_" (stamp)"
 . W !,$J(K,22)," = ",X
 . Q
 Q
 ;



