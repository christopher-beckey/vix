MAGDDEL3 ;WOIFO/PMK - Program to remove old HL7 transactions from ^MAGDHL7 ; 05/04/2007 08:37
 ;;3.0;IMAGING;**10,11,54**;MAR 19, 2002;;OCT 28, 2009
 ;; Per VHA Directive 2004-038, this routine should not be modified.
 ;; +---------------------------------------------------------------+
 ;; | Property of the US Government.                                |
 ;; | No permission to copy or redistribute this software is given. |
 ;; | Use of unreleased versions of this software requires the user |
 ;; | to execute a written test agreement with the VistA Imaging    |
 ;; | Development Office of the Department of Veterans Affairs,     |
 ;; | telephone (301) 734-0100.                                     |
 ;; | The Food and Drug Administration classifies this software as  |
 ;; | a medical device.  As such, it may not be changed in any way. |
 ;; | Modifications to this software may result in an adulterated   |
 ;; | medical device under 21CFR820, the use of which is considered |
 ;; | to be a violation of US Federal Statutes.                     |
 ;; +---------------------------------------------------------------+
 ;;
 Q
 ;
HL7 ; Entry point from ^MAGDMENU to delete old HL7 transactions
 N CUTOFF,ENDDATE,INFO,RPCERR,X
 W !,"Query VistA for retention period...",!
 D CUTOFF^MAGDDEL
 I CUTOFF<1 D
 . W !!,"No retention period specified in Imaging Site Parameters."
 . W !,"Using default value of 30 days."
 . S CUTOFF=30
 . Q
 E  W !,"Retention period is ",CUTOFF," day" W:CUTOFF'=1 "s" W "."
 S CUTOFF=$$FMADD^XLFDT($$NOW^XLFDT()\1,-CUTOFF)
 K ENDDATE F  D  Q:$G(ENDDATE)
 . W !,"Purge all HL7 transactions up to... ",$$D(CUTOFF),"// "
 . R X:$G(DTIME,300) E  S X="^"
 . I X["^" S ENDDATE=-1 Q
 . I X="" S ENDDATE=CUTOFF Q
 . W ! I X'["?" D ^%DT I Y>0 S ENDDATE=Y Q
 . W !,"Enter a date."
 . W !,"HL7 transactions that occurred before that date will be purged.",!
 . Q
 Q:ENDDATE<1
 S RPCERR=$$CALLRPC^MAGM2VCU("MAG DICOM PURGE HL7","M",.INFO,ENDDATE)
 I RPCERR<0 W !,"Deletion may not be performed at this time." Q
 W !,INFO," transaction" W:INFO'=1 "s" W " purged.",!
 Q
 ;
D(D) Q (D#100)_"-"_$P("Jan Feb Mar Apr May Jun Jul Aug Sep Oct Nov Dec"," ",D\100#100)_"-"_(D\10000+1700)
 ;
