MAGDMFB8 ;WOIFO/PMK - Program to build DICOM Master File ; 13 Nov 2003  8:47 AM
 ;;3.0;IMAGING;**9,10,11**;MAR 19, 2002;;APR 14, 2004
 ;; Per VHA Directive 2004-038, this routine should not be modified.
 ;; +---------------------------------------------------------------+
 ;; | Property of the US Government.                                |
 ;; | No permission to copy or redistribute this software is given. |
 ;; | Use of unreleased versions of this software requires the user |
 ;; | to execute a written test agreement with the VistA Imaging    |
 ;; | Development Office of the Department of Veterans Affairs,     |
 ;; | telephone (301) 734-0100.                                     |
 ;; | The Food and Drug Administration classifies this software as  |
 ;; | a medical device.  As such, it may not be changed in any way. |
 ;; | Modifications to this software may result in an adulterated   |
 ;; | medical device under 21CFR820, the use of which is considered |
 ;; | to be a violation of US Federal Statutes.                     |
 ;; +---------------------------------------------------------------+
 ;;
 Q
 ;
SITEIEN(SITE,LOCAL) ; return the file #4 internal entry number for the site
 N BEGIN,I,IEN,LOWER,OK,UPPER,VALLOC,RPCERR
 ;
 S RPCERR=$$CALLRPC^MAGM2VCU("MAG DICOM VALID LOCATIONS","M",.VALLOC)
 I RPCERR Q "-1, RPC Error"
 S I="" F  S I=$O(VALLOC(I)) Q:I=""  D
 . S X=VALLOC(I) Q:X'["^"  Q:'X
 . S OK(+X)=""
 . Q
 ;
 I SITE="" D  Q IEN ; Local site is the default
 . S IEN=$G(^MAGDICOM(2006.563,1,"LOCATION"),"-1,Location not specified.")
 . Q
 ;
 I SITE=+SITE D  Q IEN ; Site ID was entered numerically
 . I $G(LOCAL),'$D(OK(SITE)) S IEN="-3, Site "_SITE_" is not part of this healthcare network." Q
 . S RPCERR=$$CALLRPC^MAGM2VCU("MAG DICOM FILEMAN GET","M",.INFO,4,SITE,.01)
 . I RPCERR<0 S IEN="-3, RPC Error"
 . I INFO="" S IEN="-4, Site "_SITE_" not defined." Q
 . S IEN=SITE
 . Q
 ;
 S IEN=$$TRYNAME(SITE) I IEN D  Q IEN ; name lookup matched
 . I $G(LOCAL),'$D(OK(IEN)) S IEN="-3, Site "_SITE_" is not part of this healthcare network."
 . Q
 ;
 ; if SITE was entered in lower case, convert to upper and check again
 ; if SITE was entered in upper case, convert to lower and check again
 S UPPER="ABCDEFGHIJKLMNOPQRSTUVWXYZ"
 S LOWER="abcdefghijklmnopqrstuvwxyz"
 I SITE?.E1L.E S SITE=$TR(SITE,LOWER,UPPER)
 E  D  ; Lowercase the SITE (e.g., "Mountain Home, TN")
 . S BEGIN=1 ; flag indicating the beginning of a part of the city name
 . F I=1:1:$L(SITE) D
 . . I BEGIN S BEGIN=0 Q  ; leave the first letter in upper case
 . . I $E(SITE,I)="," S I=99999 Q  ; all done (don't touch the state)
 . . I $E(SITE,I)=" " S BEGIN=1 Q  ; starting new part of the city name
 . . S $E(SITE,I)=$TR($E(SITE,I),UPPER,LOWER) ; convert to lower case
 . . Q
 . Q
 ; lookup the name again after it has had its case changed
 S IEN=$$TRYNAME(SITE) I IEN D  Q IEN ; long name lookup
 . I $G(LOCAL),'$D(OK(IEN)) S IEN="-3, Site "_SITE_" is not part of this healthcare network."
 . Q
 Q "-2, site """_SITE_""" is not in the institution file (#4)"
 ;
TRYNAME(SITE) N FATAL,INFO
 S RPCERR=$$CALLRPC^MAGM2VCU("MAG DICOM FIND LOCATION","M",.INFO,SITE)
 I RPCERR<0 Q "-13,Error in RPC"
 Q +INFO
 ;
