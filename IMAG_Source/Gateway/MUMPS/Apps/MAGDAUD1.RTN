MAGDAUD1 ;WOIFO/PMK - Program to output AUDIT information about VistA-PACS Transactions ; 12/22/2003  07:46
 ;;3.0;IMAGING;**10,11**;MAR 19, 2002;;APR 14, 2004
 ;; Per VHA Directive 2004-038, this routine should not be modified.
 ;; +---------------------------------------------------------------+
 ;; | Property of the US Government.                                |
 ;; | No permission to copy or redistribute this software is given. |
 ;; | Use of unreleased versions of this software requires the user |
 ;; | to execute a written test agreement with the VistA Imaging    |
 ;; | Development Office of the Department of Veterans Affairs,     |
 ;; | telephone (301) 734-0100.                                     |
 ;; | The Food and Drug Administration classifies this software as  |
 ;; | a medical device.  As such, it may not be changed in any way. |
 ;; | Modifications to this software may result in an adulterated   |
 ;; | medical device under 21CFR820, the use of which is considered |
 ;; | to be a violation of US Federal Statutes.                     |
 ;; +---------------------------------------------------------------+
 ;;
 N DATE,HALT,HDR,I,J,LINE,LINES,LOCATION,MSG,OK,PAGE,RPC
 ;
 S LOCATION=$G(^MAGDICOM(2006.563,1,"LOCATION"))
 I 'LOCATION D  Q
 . W !,"Location for current DICOM Gateway not defined.",!
 . Q
 ;
 S DTIME=$G(DTIME,300)
 W !!,"Generate Audit Report?  y// " R X:DTIME E  Q
 I X="" S X="yes" W X
 Q:"Yy"'[$E(X,1)
 D OUTDEV^MAGDFCNS Q:ODEVTYPE=""
 ;
 ; Obtain the statistics
 ;
 Q:$$CALLRPC^MAGM2VCU("MAG DICOM TEXT AUDIT GET","M",.AUD,LOCATION)<0
 ;
 S I=$O(AUD("")) I 'AUD(I) W !!,"No statistics available",!! Q
 ;
 S PAGE=$S(ODEVTYPE="FILE":55,1:23),LINES=PAGE+1
 S STATUS=$$USE^MAGDFCNS(ODEVTYPE,ODEVNAME)
 S HDR=0
 F  S I=$O(AUD(I)) Q:I=""  D  Q:HDR
 . I $P(AUD(I),"^",1)'="" S HDR=1,I=I-1 Q
 . S MSG=$P(AUD(I),"^",3) S:MSG'="" MSG(1,MSG)=""
 . Q
 S P=0,MSG="" F  S MSG=$O(MSG(1,MSG)) Q:MSG=""  D
 . S P=P+1,MSG(1,MSG)=P,MSG(2,P)=MSG
 . Q
 ;
 S HALT=0 D NEWLINE(0)
 S (DATE,LINE)="" F  S I=$O(AUD(I)) Q:I=""  D  Q:HALT
 . D:DATE'=$P(AUD(I),"^",1)  Q:HALT
 . . I LINE'="" D NEWLINE(1) Q:HALT  W LINE
 . . S DATE=$P(AUD(I),"^",1)
 . . S LINE=$E(DATE,4,5)_"/"_$E(DATE,6,7)_"/"_$E(DATE,2,3)
 . . Q
 . S COUNT=$P(AUD(I),"^",2),MSG=$P(AUD(I),"^",3)
 . ; Output one day's statistics
 . S J=MSG(1,MSG)
 . S $E(LINE,J-1*6+9,J-1*6+14)=$J($P(AUD(I),"^",2),6)
 . Q
 I 'HALT,LINE'="" D NEWLINE(1) W:'HALT LINE
 I ODEVTYPE="FILE" W # S STATUS=$$CLOSE^MAGDFCNS(ODEVTYPE,ODEVNAME)
 Q
 ;
NEWLINE(WAIT) N I,X
 S LINES=LINES+1,X="" I LINES<PAGE W ! Q
 D:WAIT  Q:HALT
 . I ODEVTYPE="FILE" W # Q
 . W !,"Press <Enter> to continue: " R X:$G(DTIME,300) E  S X="^"
 . I X="^" S HALT=1
 . Q
 S LINES=2
 F I=1:1:P W !?13,$C(I+64),": ",MSG(2,I) S LINES=LINES+1
 W !!?8 F I=1:1:P W "     ",$C(64+I)
 W !
 Q
 ;
PURGE N D0,DF,DL,FM,I,MAGDDATE,NM,OK,RPC,X
 ;
 S FM(2006.5761)="VistA-PACS Messages"
 S FM(2006.5762)="Image Acquisition"
 ;
 Q:$$CALLRPC^MAGM2VCU("MAG DICOM AUDIT RANGE","M",.AUD)<0
 ;
 S I="" F  S I=$O(AUD(I)) Q:I=""  D
 . S X=AUD(I) Q:'X
 . S FM(+X)=$G(FM(+X))_"^"_$P(X,"^",2,3)
 . Q
 ;
 S FM="" F  S FM=$O(FM(FM)) Q:FM=""  D
 . S NM=$P(FM(FM),"^",1)
 . S DF=$P(FM(FM),"^",2)
 . S DL=$P(FM(FM),"^",3)
 . I 'DF D  Q
 . . W !,"The database currently does not contain any auditing data"
 . . W !,"related to ",NM,"."
 . . Q
 . W !!,"The database currently contains audit data related to ",NM
 . W !,"covering the period from ",$$D(DF)," until ",$$D(DL),"."
 . K MAGDDATE F  D  Q:$G(MAGDDATE)
 . . W !,"Purge all audit data up to... ",$$D(DF),"// "
 . . R X:$G(DTIME,300) E  S X="^"
 . . I X="" S MAGDDATE=DF W $$D(DF),! Q
 . . W !
 . . I X["^" S MAGDDATE=-1 Q
 . . I X'["?" D ^%DT I Y>0 S MAGDDATE=Y Q
 . . W !,"Enter a date. Audit information that relates to activities"
 . . W !,"that occurred before that date will be purged.",!
 . . Q
 . Q:MAGDDATE<1
 . W "Purging..."
 . Q:$$CALLRPC^MAGM2VCU("MAG DICOM AUDIT PURGE","M",.AUD,FM,MAGDDATE)<0
 . W !,AUD
 . ;
 . Q:FM'=2006.5762
 . S D0=0 F  S D0=$O(^MAGDICOM(2006.5764,D0)) Q:'D0  D
 . . S X=0 F  S X=$O(^MAGDICOM(2006.5764,D0,1,X)) Q:'X  Q:X'<MAGDDATE  K ^MAGDICOM(2006.5764,D0,1,X)
 . . Q
 . Q
 ;
 Q
 ;
D(D) Q (D#100)_"-"_$P("Jan Feb Mar Apr May Jun Jul Aug Sep Oct Nov Dec"," ",D\100#100)_"-"_(D\10000+1700)
 ;
