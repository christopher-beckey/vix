Cache for Windows NT^INT^Saved from DICOM (ISW-KUZMAKP3-LT)^~Format=Cache.S~
%RO on 09/17/2012  11:24
MAGDHR9^INT^1^62692,59318^0
MAGDHR9 ;WOIFO/PMK - Read HL7 and generate DICOM ; 17 Feb 2012 10:57 AM
 ;;3.0;IMAGING;**10,49,110**;Mar 19, 2002;Build 48;Aug 23, 2012
 ;; Per VHA Directive 2004-038, this routine should not be modified.
 ;; +---------------------------------------------------------------+
 ;; | Property of the US Government.                                |
 ;; | No permission to copy or redistribute this software is given. |
 ;; | Use of unreleased versions of this software requires the user |
 ;; | to execute a written test agreement with the VistA Imaging    |
 ;; | Development Office of the Department of Veterans Affairs,     |
 ;; | telephone (301) 734-0100.                                     |
 ;; | The Food and Drug Administration classifies this software as  |
 ;; | a medical device.  As such, it may not be changed in any way. |
 ;; | Modifications to this software may result in an adulterated   |
 ;; | medical device under 21CFR820, the use of which is considered |
 ;; | to be a violation of US Federal Statutes.                     |
 ;; +---------------------------------------------------------------+
 ;;
 ; PACS interface -- Radiology HL7 data input routine #9
 ;
 ; DICOM has length restrictions of 16, 64, 1024, and 10240 characters
 ; for various different kinds of data.  Often the VistA data exceeds
 ; these length limits, and has to be sent in a different field.
 ;
ENTRY ; Various fixes to handle text longer than the DICOM field permits
 N ALLERGY,COUNT,I,J,N,TAG,Y
 ;
 ; DICOM limits Interpretation Text < 1k bytes -- see OBX^MAGDHRC6
 ; All Interpretation Text is sent as Interpretation Dx Description
 S Y="Please see Interpretation Diagnosis Description."
 S TAG="4008,010B" D SAVE^MAGDFCNS(TAG,Y)
 ;
 ; Reformat allergy data sent by HL7 to one or more < 64 char strings
 S TAG="0010,2110" I $D(^TMP("MAG",$J,"DICOM","OUT",TAG)) D
 . S COUNT=0
 . S N=^TMP("MAG",$J,"DICOM","OUT",TAG,1,0)
 . F I=1:1:N D
 . . S Y=^TMP("MAG",$J,"DICOM","OUT",TAG,1,I)
 . . I Y[", " D  ; this is for HL7 generated by the radiology package
 . . . F J=1:1:$L(Y,", ")-1 S COUNT=COUNT+1,ALLERGY(COUNT)=$P(Y,", ",J)
 . . . Q
 . . E  S COUNT=COUNT+1,ALLERGY(COUNT)=Y ; consult interface
 . . Q
 . D REPLACE^MAGDFCNS(TAG,$S(COUNT>1:"ALLERGIES:",1:"ALLERGY:"))
 . S Y="" F I=1:1:COUNT D  ; output each allergy
 . . I ($L(Y)+$L(ALLERGY(I)))>62 D  ; flush "Y" buffer
 . . . D SAVE^MAGDFCNS(TAG,Y) ; output the previous list of allergies
 . . . S Y="" ; re-initialize "Y"
 . . . Q
 . . I $L(Y) S Y=Y_", " ; add the delimiter after the last allergy
 . . S Y=Y_ALLERGY(I)
 . . Q
 . D SAVE^MAGDFCNS(TAG,Y)
 . Q
 Q



