Cache for Windows NT^INT^Saved from DICOM,DICOM (ISW-KUZMAKP3-LT)^~Format=Cache.S~
%RO on 06/07/2013  15:25
MAGDHRCA^INT^1^62980,50766.249296^0
MAGDHRCA ;WOIFO/PMK/JSL/SAF - Read HL7 and generate DICOM ; 07 Jun 2013 2:06 PM
 ;;3.0;IMAGING;**138**;Mar 19, 2002;
 ;; Per VHA Directive 2004-038, this routine should not be modified.
 ;; +---------------------------------------------------------------+
 ;; | Property of the US Government.                                |
 ;; | No permission to copy or redistribute this software is given. |
 ;; | Use of unreleased versions of this software requires the user |
 ;; | to execute a written test agreement with the VistA Imaging    |
 ;; | Development Office of the Department of Veterans Affairs,     |
 ;; | telephone (301) 734-0100.                                     |
 ;; | The Food and Drug Administration classifies this software as  |
 ;; | a medical device.  As such, it may not be changed in any way. |
 ;; | Modifications to this software may result in an adulterated   |
 ;; | medical device under 21CFR820, the use of which is considered |
 ;; | to be a violation of US Federal Statutes.                     |
 ;; +---------------------------------------------------------------+
 ;;
 Q
 ; Anatomic Pathology HL7 data input routine
 ; This module reads and parses the HL7 PID and PV1 Segments for HL7 2.5.1
 ;
PID ; PID Segment
 N AAUTH,I,ICN,IDTYPE,PID,SITEDFN,Y
 ;
 I VERSION'="2.5.1" W !,"Not HL7 Version 2.5.1: """,VERSION,"""" Q
 ;
 ; NEW CODE - Allow for multiple PIDs.  ICN is either the number with
 ; no assigning authority and ID type, or else the one with assigning
 ; authority "USVHA" and ID type "NI".
 ;
 S Y=$G(^MAGDICOM(2006.563,1,"ISSUER OF PATIENT ID"),$S($$ISIHS^MAGSPID():"USIHS",1:"USVHA"))  ;P123
 ;
 ; get SITEDFN (and DFN), ICN, and PID from PID-3.1, PID-3.2, and PID-3.3
 ;
 ; PID-3.1 is the <SITE>-<DFN>
 S AAUTH=$$GETDATA^MAGDHRC0(3,1,4),IDTYPE=$$GETDATA^MAGDHRC0(3,1,5)
 I AAUTH=Y,IDTYPE="PI" S SITEDFN=$$GETDATA^MAGDHRC0(3,1,1),DFN=$P(SITEDFN,"-",2)
 ;
 ; PID-3.2 is the national ICN
 S AAUTH=$$GETDATA^MAGDHRC0(3,2,4),IDTYPE=$$GETDATA^MAGDHRC0(3,2,5)
 I AAUTH=Y,IDTYPE="NI" S ICN=$$GETDATA^MAGDHRC0(3,2,1)
 E  S ICN=""  ; not in HL7 message
 ;
 ; PID-3.3 is the SSN (VA) or MRN (IHS)
 S AAUTH=$$GETDATA^MAGDHRC0(3,3,4),IDTYPE=$$GETDATA^MAGDHRC0(3,3,5)
 I AAUTH=Y,IDTYPE=$S($$ISIHS^MAGSPID():"MR",1:"SS") S PID=$$GETDATA^MAGDHRC0(3,3,1) ;P110
 ;
 ; get vitals information from RPC
 N RESULT,RPCERR,SIZE,VIP,WEIGHT,WORKLIST
 S RPCERR=$$CALLRPC^MAGM2VCU("MAG DICOM GET PATIENT VITALS","M",.RESULT,DFN)
 F I=1:1 Q:'$D(RESULT(I))  S X=RESULT(I),WORKLIST($P(X,"^",1))=$P(X,"^",2,999)
 D SAVE^MAGDFCNS("SITE-DFN",SITEDFN) ; "0010,1000" is for PACS short id
 D SAVE^MAGDFCNS("ICN",ICN) ; "0010,1000" is for PACS short id
 ;
 I $D(WORKLIST("HEIGHT")) D
 . I $P(WORKLIST("HEIGHT"),"^",2)="m" D
 . . S SIZE=$P(WORKLIST("HEIGHT"),"^",1)
 . . Q
 . E  D
 . . N MSG
 . . S MSG(1)="Unknown HEIGHT units <<"_$P(WORKLIST("HEIGHT"),"^",2)_">>"
 . . D WARNING^MAGDFCNS(.MSG)
 . . S SIZE=""
 . . Q
 . D SAVE^MAGDFCNS("0010,1020",SIZE)
 . Q
 ;
 I $D(WORKLIST("WEIGHT")) D
 . I $P(WORKLIST("WEIGHT"),"^",2)="kg" D
 . . S WEIGHT=$P(WORKLIST("WEIGHT"),"^",1)
 . . Q
 . E  D
 . . N MSG
 . . S MSG(1)="Unknown WEIGHT units <<"_$P(WORKLIST("WEIGHT"),"^",2)_">>"
 . . D WARNING^MAGDFCNS(.MSG)
 . . S WEIGHT=""
 . . Q
 . D SAVE^MAGDFCNS("0010,1030",WEIGHT)
 . Q
 ;
 I $D(WORKLIST("VIP")) D
 . S VIP=WORKLIST("VIP")
 . I VIP["E" D
 . . I VIP["S" S VIP="Patient is an EMPLOYEE with SENSITIVE data."
 . . E  S VIP="Patient is an EMPLOYEE."
 . . Q
 . E  I VIP["S" S VIP="Patient data is SENSITIVE."
 . D SAVE^MAGDFCNS("0040,3001",VIP)
 . Q
 ;
 S UID("PATIENT")=$$PATIENT^MAGDUID1(STATNUMB,DFN)
 S UID("VISIT")=$$VISITO^MAGDUID1(STATNUMB,DFN)
 D DEMOGRAF
 ;
 ; generate a Referenced Patient Sequence
 D SEQPUSH^MAGDFCNS("0008,1120") ; Referenced Patient Sequence
 ; save Referenced SOP Class & Instance UIDs
 S Y=$$GETUID^MAGDUID1("VA Detached Patient Management SOP Class")
 D SAVE^MAGDFCNS("0008,1150",Y)
 D SAVE^MAGDFCNS("0008,1155",UID("PATIENT"))
 D SEQPOP^MAGDFCNS ; end Referenced Patient Sequence
 ;
 ; generate a Referenced Visit Sequence
 D SEQPUSH^MAGDFCNS("0008,1125") ; Referenced Visit Sequence
 ; save Referenced SOP Class & Instance UIDs
 S Y=$$GETUID^MAGDUID1("VA Detached Visit Management SOP Class")
 D SAVE^MAGDFCNS("0008,1150",Y)
 D SAVE^MAGDFCNS("0008,1155",UID("VISIT"))
 D SEQPOP^MAGDFCNS ; end Referenced Visit Sequence
 Q
 ;
DEMOGRAF ; Output the rest of the patient demographic variables
 N L,PHONES,RACE,Y,Z
 N REPTNO ;------- repetition seq # for repeating fields
 N SSN ;---------- social security number
 N DCMDOB ;------- date of birth in DICOM format
 N PNAMEDCM ;----- patient name in DICOM format
 N HRNO ; IHS Patient ID
 ;
 I $$ISIHS^MAGSPID() D  ; P123 if this an IHS facility, use HRN as the patient ID
 . S HRNO=$$GETDATA^MAGDHRC0($S(VERSION=2.1:2,1:3),1,1) ; IHS Health Record Number
 . I $G(PID)'=HRNO S PID=HRNO
 . Q
 I $D(PID) S SSN=PID ; this is the value in PID-3
 E  D  ; Pick up SSN from PID-19 (a deprecated field)
 . S SSN=$TR($$GETDATA^MAGDHRC0(19,1,1),"-")
 . Q
 I SSN'="",$G(^MAGDICOM(2006.563,1,"SSN DASHES FOR PACS"))="YES" D
 . S SSN=$E(SSN,1,3)_"-"_$E(SSN,4,5)_"-"_$E(SSN,6,999)
 . Q
 I $$ISIHS^MAGSPID() D  ; P87 if this an IHS facility, use HRN as the patient ID
 . I $G(PID)="" S PID=SSN
 . D SAVE^MAGDFCNS("0010,0020",PID) ; IHS Patient ID is HRN
 . Q
 E  D SAVE^MAGDFCNS("0010,0020",SSN) ; VA Patient ID is SSN
 D SAVE^MAGDFCNS("0010,0021",STATNUMB) ; station number
 S PNAMEDCM=""
 S PNAMEDCM=$$GETDATA^MAGDHRC0(5,1,1,1) ; family name
 S PNAMEDCM=PNAMEDCM_"^"_$$GETDATA^MAGDHRC0(5,1,2,1) ; given name
 S PNAMEDCM=PNAMEDCM_"^"_$$GETDATA^MAGDHRC0(5,1,3,1) ; middle name
 S PNAMEDCM=PNAMEDCM_"^"_$$GETDATA^MAGDHRC0(5,1,5,1) ; name prefix (PID-5.5)
 S PNAMEDCM=PNAMEDCM_"^"_$$GETDATA^MAGDHRC0(5,1,4,1) ; name suffix (PID-5.4)
 D SAVE^MAGDFCNS("0010,0010",PNAMEDCM) ; Patient Name
 ; compute and save Quick PID (K1234)
 S L=$L(SSN) ; use for last four digits of SSN
 D SAVE^MAGDFCNS("0010,1000",$E(PNAMEDCM)_$E(SSN,L-3,L))
 ; pick up date of birth, zero-fill to satisfy DICOM DT VR
 D CVTDT^MAGDHRCU($$GETDATA^MAGDHRC0(7),.DCMDOB)
 D SAVE^MAGDFCNS("0010,0030",DCMDOB) ; Date of Birth
 S SEX=$TR($$GETDATA^MAGDHRC0(8),"U","") D SAVE^MAGDFCNS("0010,0040",SEX)
 S RACE=$$GETDATA^MAGDHRC0(10,1,4) ; pick up race code
 I RACE="" S RACE=$$GETDATA^MAGDHRC0(22,1,4) ; pick up ethnic group code
 D ETHNIC^MAGDFND4 ; set race to a 16-character value
 I RACE'="" D SAVE^MAGDFCNS("0010,2160",RACE) ; Race
 ;
 ; Home Address
 S Y=$$GETDATA^MAGDHRC0(11,1,1)_", "
 S Z=$$GETDATA^MAGDHRC0(11,1,2) S:$L(Z) Y=Y_Z_", " ; Street
 S Z=$$GETDATA^MAGDHRC0(11,1,3) S:Z'="" Y=Y_Z_", " ; City
 S Z=$$GETDATA^MAGDHRC0(11,1,4) S:Z?1.N1"^".E Z=$P(Z,"^",2) S:Z'="" Y=Y_Z ; State
 S Y=Y_" "_$$GETDATA^MAGDHRC0(11,1,5)_" "_$$GETDATA^MAGDHRC0(11,1,6) ; zip and country
 S:$TR(Y,", ")="" Y=""
 D SAVE^MAGDFCNS("0010,1040",Y) ; address
 ;
 S PHONES=$$GETDATA^MAGDHRC0(13,1,1)
 I PHONES'="" D SAVE^MAGDFCNS("0010,2154",PHONES) ; Phone
 Q
 ;
PV1 ; PV1 segment
 ; code for all patients
 N ADMITID ;-- admission id or visit id
 N HL7DTTM ;-- date/time value (HL7 format)
 N DCMDT ;---- date value (DICOM format)
 N DCMTM ;---- time value (DICOM format)
 N PREGSTAT ;- pregnancy status
 N PTIOSTAT ;--- patient inpatient/outpatient status
 ;
 S ADMITID=$$GETDATA^MAGDHRC0(19)
 D SAVE^MAGDFCNS("0038,0010",ADMITID) ; admission or visit id 
 I $$GETDATA^MAGDHRC0(2)="I" D  ; inpatient - use different visit UID
 . S UID("VISIT")=$$VISITI^MAGDUID1(STATNUMB,DFN,ADMITID)
 . D SAVE^MAGDFCNS("0038,4000","INPATIENT")
 . Q
 E  D SAVE^MAGDFCNS("0038,4000","OUTPATIENT")  ; outpatient
 ;
 I $$GETEXIST^MAGDHRC0(3) D  ; get patient location
 . ; current location of inpatient (ward, room, and bed)
 . N BED,ROOM,WARD,X
 . S WARD=$$GETDATA^MAGDHRC0(3,1,1),ROOM=$$GETDATA^MAGDHRC0(3,1,2)
 . S BED=$$GETDATA^MAGDHRC0(3,1,3)
 . S X=WARD I $L(ROOM) S X=X_" "_ROOM I $L(BED) S X=X_"-"_BED
 . ; Current patient location (per Part 3)
 . D SAVE^MAGDFCNS("0038,0300",X) ; Current Pt Location (ward)
 . ;
 . D SAVE^MAGDFCNS("0038,0400",X) ; Pt's Institutional Residence (room-bed)
 . ; Patient's Institutional Residence (room-bed) - legacy private sequence
 . Q
 Q



