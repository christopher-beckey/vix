MAGBRTB4 ;WOIFO/PMK - Program #4 to build image routing master file ; 08/11/2005  15:18
 ;;3.0;IMAGING;**9,11,30,51**;MAR 19, 2002;;JUN 16, 2006
 ;; Per VHA Directive 2004-038, this routine should not be modified.
 ;; +---------------------------------------------------------------+
 ;; | Property of the US Government.                                |
 ;; | No permission to copy or redistribute this software is given. |
 ;; | Use of unreleased versions of this software requires the user |
 ;; | to execute a written test agreement with the VistA Imaging    |
 ;; | Development Office of the Department of Veterans Affairs,     |
 ;; | telephone (301) 734-0100.                                     |
 ;; | The Food and Drug Administration classifies this software as  |
 ;; | a medical device.  As such, it may not be changed in any way. |
 ;; | Modifications to this software may result in an adulterated   |
 ;; | medical device under 21CFR820, the use of which is considered |
 ;; | to be a violation of US Federal Statutes.                     |
 ;; +---------------------------------------------------------------+
 ;;
 Q
 ;
PASS3 ; entry point from ^MAGBRTB1
 N D0,FATAL
 ; Build the DESTINATION dictionaries
 ;
 S D0=0 F  S D0=$O(^MAGDICOM(2006.59,D0)) Q:'D0  D THREE(D0) D:FATAL REMOVE(D0)
 Q
 ;
PASS4() ; entry point from ^MAGBRTB1
 N A,C,D0,D1,FATAL,N,T
 ; Final consistency check
 ;
 S (D0,N)=0 F  S D0=$O(^MAGDICOM(2006.59,D0)) Q:'D0  D
 . S T="",D1=0 F  S D1=$O(^MAGDICOM(2006.59,D0,1,D1)) Q:'D1  D
 . . S:T'="" T=T_$C(13,10) S T=T_$G(^MAGDICOM(2006.59,D0,1,D1,0))
 . S A=$D(^MAGDICOM(2006.59,D0,"ACTION"))
 . S C=$D(^MAGDICOM(2006.59,D0,"CONDITION"))
 . I A,C S N=N+1 Q
 . W !,"Cannot enter rule #",D0,": ",T
 . W !,"This rule",$S(A:" has",1:" does not have")," an ACTION part."
 . W !,"This rule",$S(C:" has",1:" does not have")," a CONDITION part."
 . W !
 . K ^MAGDICOM(2006.59,D0)
 . Q
 S $P(^MAGDICOM(2006.59,0),"^",3)=N
 Q N
 ;
THREE(D0) N FIELD,IEN,X
 S FATAL=0
 S D1=0 F  S D1=$O(^MAGDICOM(2006.59,D0,"ACTION",D1)) Q:'D1  D
 . ; check each action
 . S FIELD=^MAGDICOM(2006.59,D0,"ACTION",D1,0)
 . ;
 . ; After the pseudo-rule is processed,
 . ; remove it from the rule database.
 . ;
 . I FIELD="DESTINATION" D  S FATAL=1 Q
 . . D ERR(D0,"DESTINATION command no longer supported.")
 . . Q
 . ;
 . I FIELD="SEND" D  Q:FIELD="DICOM"
 . . ; check that the SEND action argument is defined as a destination
 . . S D2=$O(^MAGDICOM(2006.59,D0,"ACTION",D1,1," "),-1) D:D2'=1
 . . . D ERR(D0,"Wrong number of parameters: "_D2_" (should be 1)")
 . . . S FATAL=1
 . . . Q
 . . S NAME=$$UP(^MAGDICOM(2006.59,D0,"ACTION",D1,1,1,0))
 . . I $$VALDEST(NAME)<0 D  Q:FIELD'="SEND"
 . . . I $O(^MAGDICOM(2006.585,"B",NAME,"")) S FIELD="DICOM" Q
 . . . D ERR(D0,"Undefined destination: """_NAME_"""")
 . . . S FATAL=1
 . . . Q
 . . S:'FATAL ^MAGDICOM(2006.59,D0,"ACTION",D1,1,1,0)=NAME
 . . Q
 . I FIELD="DICOM" D  Q
 . . ; check that the SEND action argument is defined as a destination
 . . S D2=$O(^MAGDICOM(2006.59,D0,"ACTION",D1,1," "),-1) D:D2'=1
 . . . D ERR(D0,"Wrong number of parameters: "_D2_" (should be 1)")
 . . . S FATAL=1
 . . . Q
 . . S NAME=$$UP(^MAGDICOM(2006.59,D0,"ACTION",D1,1,1,0))
 . . D:'$O(^MAGDICOM(2006.585,"B",NAME,""))
 . . . D ERR(D0,"Undefined destination: """_NAME_"""")
 . . . S FATAL=1
 . . . Q
 . . Q:FATAL
 . . S ^MAGDICOM(2006.59,D0,"ACTION",D1,1,1,0)=NAME
 . . S ^MAGDICOM(2006.59,D0,"ACTION",D1,0)=FIELD
 . . Q
 . I FIELD="BALANCE" D  Q
 . . N C,C1,D2,P,TOT
 . . S TOT=0,C1=0
 . . S D2=0 F  S D2=$O(^MAGDICOM(2006.59,D0,"ACTION",D1,1,D2)) Q:'D2  D
 . . . S X=^MAGDICOM(2006.59,D0,"ACTION",D1,1,D2,0)
 . . . S NAME=$P(X,"=",1),P=$P(X,"=",2),NAME=$$UP($TR(NAME,""""))
 . . . S C=+$P(P,"/",2) S:'C C1=1
 . . . S P=+$TR(P,"""%"),TOT=TOT+P
 . . . I NAME'="<LOCAL>" D:$$VALDEST(NAME)<0
 . . . . D ERR(D0,"Undefined destination: """_NAME_"""")
 . . . . S FATAL=1
 . . . . Q
 . . . S P(D2)=P S:C P=P_"^"_C
 . . . S ^MAGDICOM(2006.59,D0,"ACTION",D1,1,D2,0)=NAME_"^"_P
 . . . Q
 . . I TOT'=100 D ERR(D0,"Load-Balance adds up to "_TOT_"%, not 100%.") S FATAL=1
 . . I 'C1 D ERR(D0,"At least one destination must not have a cap.") S FATAL=1
 . . Q:FATAL
 . . S C1=1 F P=2:1:100 D
 . . . S D2="" F  S D2=$O(P(D2)) Q:D2=""  I P(D2)/P["." S D2=-1 Q
 . . . S:D2="" C1=P
 . . . Q
 . . Q:C1=1
 . . S D2=0 F  S D2=$O(^MAGDICOM(2006.59,D0,"ACTION",D1,1,D2)) Q:'D2  D
 . . . S X=^MAGDICOM(2006.59,D0,"ACTION",D1,1,D2,0)
 . . . S $P(X,"^",2)=$P(X,"^",2)/C1
 . . . S ^MAGDICOM(2006.59,D0,"ACTION",D1,1,D2,0)=X
 . . . Q
 . . Q
 . Q
 Q
 ;
VALDEST(NAME) N OUT
 Q:NAME="" 0
 Q:$$CALLRPC^MAGM2VCU("MAG DICOM ROUTE VALID DEST","M",.OUT,NAME)<0 0
 Q OUT
 ;
REMOVE(D0) ;
 L +^MAGDICOM(2006.59,0)
 K ^MAGDICOM(2006.59,D0)
 S $P(^MAGDICOM(2006.59,0),"^",4)=$P(^MAGDICOM(2006.59,0),"^",4)-1
 L -^MAGDICOM(2006.59,0)
 Q
 ;
UP(X) Q $TR(X,"abcdefghijklmnopqrstuvwxyz","ABCDEFGHIJKLMNOPQRSTUVWXYZ")
 ;
ERR(D0,TXT) N D1
 I WEB W "<p><font color=#ff0000>"
 E  W !!
 W "*** Error in rule # ",D0," ***"
 W:WEB "</font>"
 S D1=0 F  S D1=$O(^MAGDICOM(2006.59,D0,1,D1)) Q:'D1  D
 . W:WEB "<br>" W !,^MAGDICOM(2006.59,D0,1,D1,0)
 . Q
 I WEB W "<br>",!,"<font color=#ff0000>"
 E  W !
 W "*** ",TXT," ***"
 I WEB W "</font></p>" Q
 W !!,"Enter <Return> to continue: " R D1:DTIME
 Q
