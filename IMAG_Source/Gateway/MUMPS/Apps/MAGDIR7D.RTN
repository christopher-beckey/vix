MAGDIR7D ;WOIFO/PMK/RRB - Read a DICOM image file ; 10 Mar 2010 1:02 PM
 ;;3.0;IMAGING;**11,51,54,99**;Mar 19, 2002;Build 2179;Jun 28, 2011
 ;; Per VHA Directive 2004-038, this routine should not be modified.
 ;; +---------------------------------------------------------------+
 ;; | Property of the US Government.                                |
 ;; | No permission to copy or redistribute this software is given. |
 ;; | Use of unreleased versions of this software requires the user |
 ;; | to execute a written test agreement with the VistA Imaging    |
 ;; | Development Office of the Department of Veterans Affairs,     |
 ;; | telephone (301) 734-0100.                                     |
 ;; | The Food and Drug Administration classifies this software as  |
 ;; | a medical device.  As such, it may not be changed in any way. |
 ;; | Modifications to this software may result in an adulterated   |
 ;; | medical device under 21CFR820, the use of which is considered |
 ;; | to be a violation of US Federal Statutes.                     |
 ;; +---------------------------------------------------------------+
 ;;
 ; This routine copies a DICOM image file to the server and creates an
 ; image abstract.
 ;
ENTRY ; entry point from ^MAGDIR71
 N ABSSIZE ;-- size of the image abstract
 N ABSTRACT ;- path for image abstract file
 N ABSTRMSG ;- path for the message from MAG_MAKEABS.EXE
 N CODE ;----- action to take for Create Abstract
 N CMD ;------ command for shell to execute
 N ERROR ;---- command shell error message
 N HDR ;------ header of e-mail message
 N IMAGEABS ;- path to temporary abstract
 N MSG ;------ error message to be mailed
 N STATUS,VERBOSE,X
 ;
 ; copy the DICOM image from the input queue to IMGFILE on the server
 ;
 D FILECOPY^MAGDIR75(FROMPATH,IMGFILE,"DICOM")
 ;
 ; Check that the full size image was written properly
 D FILECHK^MAGDIR75(IMGFILE,FILESIZE)
 ;
 ; create the About Image Text File from the DICOM header & other data
 D ^MAGDIR7T ; copy data from DICOM header and elsewhere to text file
 ;
 ; create the abstract
 ; this is a two-step process (1) create the temp abs file (2) copy it
 ;
 S IMAGEABS=$P(FROMPATH,"\",1,$L(FROMPATH,"\")-1)_"\TMP_IMAGE.ABS"
 ;
 ; delete the previous temporary image abstract first
 D DELETE^MAGDIR71(IMAGEABS,"TEMPORARY IMAGE ABSTRACT",1)
 ;
 D
 . N MSG
 . N PATH ; path for ABSTRACT folder
 . S CODE=$$CODE^MAGDUID4(SOPCLASS,"Create Abstract")
 . S PATH=$G(^MAGDICOM(2006.563,1,"ABSTRACT PATH"))
 . I PATH="" D
 . . S X=^MAGDICOM(2006.563,1,"DATA PATH",1,0)
 . . S PATH=$P(X,"\",1,$L(X,"\")-1)_"\ABSTRACT"
 . . S ^MAGDICOM(2006.563,1,"ABSTRACT PATH")=PATH
 . . Q
 . I CODE=1 S CMD="MAG_DCMabstract "_FROMPATH_" "_IMAGEABS D  Q
 . . S VERBOSE=1 D DOS^MAGDOS(CMD,.ERROR)
 . . S SIZEABS=$$ABSSIZE^MAGDIR75(IMAGEABS,"ABSTRACT")
 . . ;
 . . I 'SIZEABS D  ; error
 . . . S IMAGEABS=PATH_"\canned_image.jpg"
 . . . N I,MSG,CRITICAL,IGNORE,ROU,HDR
 . . . S MSG(1)="IMAGE ABSTRACT CREATION ERROR"
 . . . S MSG(2)="Unexpected error in """_CMD_""""
 . . . S MSG(3)="Image Pointer: "_IMAGEPTR
 . . . S MSG(4)="Canned abstract used instead"
 . . . D WARNING^MAGDFCNS(.MSG)
 . . . ; send e-mail
 . . . S IGNORE=1,ROU=$T(+0),CRITICAL=0
 . . . S HDR="FILE CREATION ERROR"
 . . . ;
 . . . ; Parameters for the message log below are: 
 . . . ; NEWLINE = 1,TAB = 0,TEXT = MSG sent as an array, and
 . . . ; OPTION = t (Date stamp at beginning of the text)
 . . . ; 
 . . . D MESSAGE^MAGDMLOG(1,0,.MSG,"t")
 . . . Q
 . . Q
 . I CODE=2 S IMAGEABS=PATH_"\MAG_Canned_PDF.bmp"  Q
 . I CODE=3 S IMAGEABS=PATH_"\MAG_Canned_ECG.bmp"  Q
 . S IMAGEABS=PATH_"\MAG_WhatEver.bmp"
 . Q
 ;
 ; Copy the temporary IMAGEABS targa file to ABSTRACT on the server
 ;
 S ABSTRACT=TOPATH_$P(FILE,".")_".ABS"
 N MSG,HDR
 D ABSCOPY^MAGDIR75(IMAGEABS,ABSTRACT,"ABSTRACT",.MSG)
 I $D(MSG) D  ; error
 . D WARNING^MAGDFCNS(.MSG)
 . ; send e-mail
 . S IGNORE=1,ROU=$T(+0),CRITICAL=0
 . S HDR="CANNOT COPY FILE TO SERVER"
 . D SEND^MAGDMMSG
 . Q
 ;
 ; clear the image being processed
 S ^MAGDICOM(2006.563,1,"CURRENT IMAGE POINTER")=""
 ;
 Q
