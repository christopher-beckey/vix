MAGDCST2 ;WOIFO/PMK - Backend for C-STORE (part 2) ; 12/22/2006 09:03
 ;;3.0;IMAGING;**11,51,54**;MAR 19, 2002;;OCT 28, 2009
 ;; Per VHA Directive 2004-038, this routine should not be modified.
 ;; +---------------------------------------------------------------+
 ;; | Property of the US Government.                                |
 ;; | No permission to copy or redistribute this software is given. |
 ;; | Use of unreleased versions of this software requires the user |
 ;; | to execute a written test agreement with the VistA Imaging    |
 ;; | Development Office of the Department of Veterans Affairs,     |
 ;; | telephone (301) 734-0100.                                     |
 ;; | The Food and Drug Administration classifies this software as  |
 ;; | a medical device.  As such, it may not be changed in any way. |
 ;; | Modifications to this software may result in an adulterated   |
 ;; | medical device under 21CFR820, the use of which is considered |
 ;; | to be a violation of US Federal Statutes.                     |
 ;; +---------------------------------------------------------------+
 ;;
 Q
 ;
ECHO ; An ECHO request was received; respond to it.
 D RESPONSE^MAGDECHO ; formulate the echo response
 D SOPCLASS^MAGDDW1
 S QUEUEOUT("FILE")=CSPDUOUT
 S EXPLICIT=0 D OUTPUT^MAGDDW2
 Q
 ;
NEXTFILE() ; Get the full name of the next image file
 ;
 ; ^MAGDINPT is a local global because it must be available when VistA
 ; is down.  Unlike ^TMP, it must be persistent and be accessible
 ; by different image storage jobs.
 ;
 N X
 L +^MAGDINPT(2006.571,0):1E9 ; serialize name generation code
 S X=$G(^MAGDINPT(2006.571,0))
 S $P(X,"^",1,2)="DICOM RAW IMAGE^2006.571"
 S $P(X,"^",3)=$P(X,"^",3)+1,IMAGEIEN=$P(X,"^",3) ; get next IEN
 S $P(X,"^",4)=$P(X,"^",4)+1 ; increment total count
 S ^MAGDINPT(2006.571,0)=X
 S PATH=IMGPATH_"\"_$$HOSTNAME^MAGOSMSC()_"_"_$TR($J(IMAGEIEN,7)," ",0)_".DCM"
 S ^MAGDINPT(2006.571,IMAGEIEN,0)=PATH_"^"_INSTNAME_"^"_0 ; first entry
 L -^MAGDINPT(2006.571,0) ; clear the serial name generation code
 D IMAGE^MAGDCST3 ; increment the image counter
 D PART10 ; create the Part 10 image header
 Q PATH
 ;
PART10 ; output DICOM meta file information
 N MULTIMSG,FILEMODE
 D MSGINIT^MAGDFCNS
 ;
 ; output the file meta information version
 D SAVE^MAGDFCNS("0002,0001",0) ; version 1, first byte is 0
 D SAVE^MAGDFCNS("0002,0001",1) ; version 1, first byte is 1
 ;
 ; copy Affected SOP Class UID to Media Storage SOP Class UID
 D SAVE^MAGDFCNS("0002,0002",^TMP("MAG",$J,"DICOM",INODE,"0000,0002",1,1))
 ;
 ; copy Affected SOP Instance UID to Media Storage SOP Instance UID
 D SAVE^MAGDFCNS("0002,0003",^TMP("MAG",$J,"DICOM",INODE,"0000,1000",1,1))
 ;
 ; copy Transfer Syntax UID
 D SAVE^MAGDFCNS("0002,0010",^TMP("MAG",$J,"DICOM","NEGOTIATE","ACCEPT",PCONTEXT,"TRANSFER UID",1))
 ;
 ; copy Implementation Class Name and UID
 D SAVE^MAGDFCNS("0002,0013",^MAGDICOM(2006.563,1,"VERSION"))
 D SAVE^MAGDFCNS("0002,0012",$$GETUID^MAGDUID1(^MAGDICOM(2006.563,1,"VERSION")))
 ;
 ; copy storage class user's AE Title
 D SAVE^MAGDFCNS("0002,0016",CALLING)
 ;
 S MESSAGE="PART 10 FILE META INFORMATION"
 S TEMPLATE=$O(^MAGDICOM(2006.52,"B",MESSAGE,""))
 I TEMPLATE="" D  Q
 . D MESSAGE^MAGDMLOG(1,0,"Undefined Message Template: "_MESSAGE,"VB")
 . Q
 S QUEUEOUT("FILE")=PATH,MULTIMSG="CLOSE",FILEMODE="IMAGE PART 10"
 ;
 ; the Part 10 DICOM header is Explicit VR, Little Endian
 S BYTEORDR="L"
 S STATUS=$$OPEN^MAGOSFIL(PATH,"W")
 I STATUS<0 D
 . N M
 . S M="Could not open file """_PATH_""" for Write."
 . W:$$USEPRIN^MAGOSFIL(1) !!!,M,!,STATUS
 . S $EC=",U"_M_","
 . Q  ; Error trap does not return
 S STATUS=$$USE^MAGOSFIL(PATH)
 F I=1:1:128 W $C(0) ; file preamble
 W "DICM" ; DICOM prefix
 I $$USEPRIN^MAGOSFIL(1)
 S EXPLICIT=1 D OUTPUT^MAGDDW2
 Q
 ;
RESPONSE ; A DATASET has been transferred, send C-STORE RESPONSE SUCCESS message
 S QUEUEIN("FILE")=CSPDUIN,(ODEVNAME,ODEVTYPE)="",INODE="IN"
 D ^MAGDDR2 ; read the control group 0 dicom file
 D MSGINIT^MAGDFCNS ; Initialize the ^TMP global for this message
 ;
 ; copy Affected SOP Class UID
 D SAVE^MAGDFCNS("0000,0002",^TMP("MAG",$J,"DICOM",INODE,"0000,0002",1,1))
 ;
 ; copy Message ID to Message ID being responded to field
 D SAVE^MAGDFCNS("0000,0120",^TMP("MAG",$J,"DICOM",INODE,"0000,0110",1,1))
 ;
 ; copy Affected SOP Instance UID
 D SAVE^MAGDFCNS("0000,1000",^TMP("MAG",$J,"DICOM",INODE,"0000,1000",1,1))
 ;
 I 1 D  ; generate a success response
 . ; set Status to 0 indicating no error
 . D SAVE^MAGDFCNS("0000,0900",0)
 . S MESSAGE="C-STORE RESPONSE SUCCESS"
 . Q
 ;
 I 0 D  ; generate an error response
 . S MESSAGE="C-STORE RESPONSE FAILURE"
 . D SAVE^MAGDFCNS("0000,0900",1) ; status
 . D SAVE^MAGDFCNS("0000,0901","0010,0010") ; offending element
 . D SAVE^MAGDFCNS("0000,0901","0010,0020") ; offending element
 . D SAVE^MAGDFCNS("0000,0902","Ya botched the image transfer, buddy") ; comment
 . D SAVE^MAGDFCNS("0000,0903","12345") ; error code
 . Q
 ;
 S MSGHANDL="NO MESSAGE HANDLE YET"
 S TEMPLATE=$O(^MAGDICOM(2006.52,"B",MESSAGE,""))
 I TEMPLATE="" D  Q
 . D MESSAGE^MAGDMLOG(1,0,"Undefined Message Template: "_MESSAGE,"VB")
 . Q
 ;
 S QUEUEOUT("FILE")=CSPDUOUT
 S EXPLICIT=0 D OUTPUT^MAGDDW2
 Q
 ;
RECV(IP,PORT,X) ; receive a message
 N LEVEL,STS,$ET
 S LEVEL=$ST
 S $ET="Q:$ST>LEVEL  S $EC="""",X=""Lost Connection"" Q -1"
 D MESSAGE^MAGDMLOG(1,0,"Recving <<","V")
 F  S STS=$$TCPUSE^MAGOSTCP($G(IP),PORT) R X#4:100 Q:$T
 I X="" D  Q "-1 TCP/IP connection to front-end dropped"
 . I $$USEPRIN^MAGOSFIL(1)
 . Q
 R X#X ; read text from C-STORE front end
 I $$USEPRIN^MAGOSFIL(1)
 D MESSAGE^MAGDMLOG(0,0,X_">>","V")
 Q 0
 ;
SEND(IP,PORT,MSG) ; send a message
 N STS
 D MESSAGE^MAGDMLOG(1,0,"Sending <<"_MSG_">>","V")
 S STS=$$TCPUSE^MAGOSTCP($G(IP),PORT) W $E(10000+$L(MSG),2,5),MSG
 S STS=$$TCPFLUSH^MAGOSTCP($G(IP),PORT) ; flush the TCP port
 I $$USEPRIN^MAGOSFIL(1)
 Q
 ;
