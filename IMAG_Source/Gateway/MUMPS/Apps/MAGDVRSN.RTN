Cache for Windows^INT^P162 MAGDVRSN Update^~Format=Cache.S~
%RO on 07 Dec 2015   7:10 AM
MAGDVRSN^INT^1^63890,42346^0
MAGDVRSN ;WOIFO/EdM/PMK/DAC - Version Number of VistA DICOM Gateway ; 28 Oct 82015 3:14 PM
 ;;3.0;IMAGING;**1,7,9,26,21,10,36,3,,30,5,51,50,52,69,75,102,103,54,53,66,49,99,123,118,87,110,138,156,160,162**;Mar 19, 2002;Build 6017;Oct 28, 2015
 ;; Per VHA Directive 2004-038, this routine should not be modified.
 ;; +---------------------------------------------------------------+
 ;; | Property of the US Government.                                |
 ;; | No permission to copy or redistribute this software is given. |
 ;; | Use of unreleased versions of this software requires the user |
 ;; | to execute a written test agreement with the VistA Imaging    |
 ;; | Development Office of the Department of Veterans Affairs,     |
 ;; | telephone (301) 734-0100.                                     |
 ;; | The Food and Drug Administration classifies this software as  |
 ;; | a medical device.  As such, it may not be changed in any way. |
 ;; | Modifications to this software may result in an adulterated   |
 ;; | medical device under 21CFR820, the use of which is considered |
 ;; | to be a violation of US Federal Statutes.                     |
 ;; +---------------------------------------------------------------+
 ;;
 N A,D,DATETIME,HALT,LINE1,LINES,P,PACKAGE,PAGE,PATCHES,R,VERSION,X
 ;
 ; Follow VA SACC guidelines for notation of the version:
 S X=$T(+2),VERSION=$P(X,";",3),PACKAGE=$P(X,";",4),PATCHES=$P(X,";",5)
 S DATETIME=$P(X,";",8)
 W !!,"This is """,PACKAGE," ",VERSION,""" created on ",DATETIME,"."
 W:PATCHES'="" !,"Active patch(es): ",PATCHES
 S R="MAG" F  S R=$O(^$R(R)) Q:$E(R,1,3)'="MAG"  D
 . S X=$T(+2^@R) Q:$P(X,";",3)'=VERSION  Q:$P(X,";",4)'=PACKAGE
 . S P=$TR($P(X,";",5),"*") Q:P=""
 . S P=$P(P,",",$L(P,",")) Q:P=""
 . S D=$P(X,";",8) S:D="" D="---??---"
 . S P(P,D,R)=""
 . Q
 S PAGE=23,LINES=PAGE+1,HALT=0,A=0 D NEWLINE(0)
 S P="" F  S P=$O(P(P)) Q:P=""  D
 . D NEWLINE(1) Q:HALT
 . W "Patch ",P,": " S X=$X
 . S A=0,D="" F  S D=$O(P(P,D)) Q:D=""  D  Q:HALT
 . . I A D NEWLINE(1) Q:HALT  W ?X
 . . W D S A=1
 . . Q
 . Q
 W !!
 Q
 ;
NEWLINE(WAIT) N I,X
 S LINES=LINES+1,X="" I LINES<PAGE W ! Q
 D:WAIT  Q:HALT
 . W !,"Press <Enter> to continue: " R X:$G(DTIME,300) E  S X="^"
 . I X="^" S HALT=1
 . Q
 S LINES=2
 W !!,"Installed patches:"
 I A S LINES=3 W !,"Patch ",P,": "
 Q
 ;
 ; VA Routine Check Sum
 ;  Write $$CHK^MAGDVRSN(name_of_routine)
 ; will return the check-sum that is used by the VA Kernel
 ; for the routine in question, as it exists in the current
 ; environment
 ;
CHK(R) N K,X,Y
 S Y=0
 F K=1:1 S X=$T(+K^@R) Q:X=""  S:K'=2 Y=Y+$$C(X)
 Q Y
 ;
C(X) N F,I,Y
 S Y=0
 S F=$F(X," "),F=$S($E(X,F)'=";":$L(X),$E(X,F+1)=";":$L(X),1:F-2)
 F I=1:1:F S Y=$A(X,I)*I+Y
 Q Y
 ;
CHK2(R) N K,X,Y
 S Y=0
 F K=1:1 S X=$T(+K^@R) Q:X=""  S:K'=2 Y=Y+$$C2(X,K)
 Q Y
 ;
C2(X,K) N F,I,Y
 S Y=0
 S F=$F(X," "),F=$S($E(X,F)'=";":$L(X),$E(X,F+1)=";":$L(X),1:F-2)
 F I=1:1:F S Y=$A(X,I)*(I+K)+Y
 Q Y
 ;
DCMVER N A,D,I,M,MD,MV,OK,P,VISTA,X,Y
 I $$CALLRPC^MAGM2VCU("MAG DICOM GET VERSION","M",.VISTA)<0 D  Q
 . W !!,"** WARNING **"
 . W !,"Cannot obtain version information from VistA at this time."
 . W !!,"Press <Enter> to continue: " R X:$G(DTIME,300)
 . Q
 S X=$TR($P($T(+2),";",5),"*")
 F I=1:1:$L(X,",") S P=$P(X,",",I) I P,(","_VISTA)'[(","_P_",") S MV(P)=1
 F I=1:1:$L(VISTA,",")-1 S P=$P(VISTA,",",I) I P,(","_X_",")'[(","_P_",") S MD(P)=1
 S OK=1
 ; Ignore VistA only patches:
 F P=6,16,17,19,22,24,25,27,29 K MD(P)
 ; Ignore DICOM only patches:
 F P=26 K MV(P)
 D:$D(MV)!$D(MD)
 . W !!,"The DICOM Gateway and the VistA system do not have the same"
 . W !,"list of patches installed:"
 . S A=$C(13,10)_"The DICOM Gateway is missing: "
 . S MD="" F  S MD=$O(MD(MD)) Q:MD=""  D
 . . I A=", ",$O(MD(MD))="" S A=" and "
 . . W A,MD S OK=0,A=", "
 . . Q
 . S A=$C(13,10)_"The VistA System is missing: "
 . S MV="" F  S MV=$O(MV(MV)) Q:MV=""  D
 . . I A=", ",$O(MV(MV))="" S A=" and "
 . . W A,MV S OK=0,A=", "
 . . Q
 . Q
 S X=$P(VISTA,"-",2),D=X#100,M=X\100#100,Y=X\10000+1700
 S:D<10 D=0_D
 S MV=D_"-"_$P("January February March April May June July August September October November December"," ",M)_"-"_Y
 S MD=$P($T(+2),";",6)
 D:MV'=MD
 . S OK=0
 . W !!,"The latest KIDS build on VistA was dated ",MV,"."
 . W !,"The latest DICOM build was dated ",MD,"."
 . Q
 Q:OK
 W !!,"*** WARNING ***"
 W !,"The software on this DICOM Gateway may not be compatible"
 W !,"with the software on the VistA system."
 W !!,"Press <Enter> to continue: " R X:$G(DTIME,300)
 Q
 ;



