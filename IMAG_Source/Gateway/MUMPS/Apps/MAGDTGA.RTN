MAGDTGA ;WOIFO/PMK - Program to read a Truevision Targa (TGA) Image File Header ; 06/06/2005  08:42
 ;;3.0;IMAGING;**11,30,51**;MAR 19, 2002;;JUN 16, 2006
 ;; Per VHA Directive 2004-038, this routine should not be modified.
 ;; +---------------------------------------------------------------+
 ;; | Property of the US Government.                                |
 ;; | No permission to copy or redistribute this software is given. |
 ;; | Use of unreleased versions of this software requires the user |
 ;; | to execute a written test agreement with the VistA Imaging    |
 ;; | Development Office of the Department of Veterans Affairs,     |
 ;; | telephone (301) 734-0100.                                     |
 ;; | The Food and Drug Administration classifies this software as  |
 ;; | a medical device.  As such, it may not be changed in any way. |
 ;; | Modifications to this software may result in an adulterated   |
 ;; | medical device under 21CFR820, the use of which is considered |
 ;; | to be a violation of US Federal Statutes.                     |
 ;; +---------------------------------------------------------------+
 ;;
ENTRY ; programmer entry only -- display a TGA file header
 W !!,"Enter the TGA file: " I $D(FILE) W """",FILE,"""// "
 R X:$G(DTIME,300) E  Q
 I X="" S X=$G(FILE) Q:X=""  W X
 S FILE=X,X=$$HEADER(FILE) W !
 I X<0 W "Error: ",X
 E  D  ; output header data
 . S X="" F  S X=$O(TGA(X)) Q:X=""  D
 . . S Y=$O(TGA(X,"")),Z=TGA(X,Y)
 . . W !,$J(Y,20)," = ",Z
 . . I X=3 W " (",$S(Z=2:"Color",Z=3:"B&W",1:"Other"),")"
 . . I X=5.6 W " (",$$DEC2BIN(Z)," binary)"
 . . Q
 . Q
 Q
 ;
DEC2BIN(X) ; decimal to binary converter
 N BINARY,EXPONENT,POWER
 I X=0 Q 0  ; special case
 F EXPONENT=0:1 S POWER=2**EXPONENT Q:X<POWER  S POWER(EXPONENT)=POWER
 S BINARY=""
 F EXPONENT=EXPONENT-1:-1:0 D
 . I X<POWER(EXPONENT) S BINARY=BINARY_"0"
 . E  S BINARY=BINARY_1,X=X-POWER(EXPONENT)
 . Q
 Q BINARY
 ;
HEADER(FILE) ; read the TGA file header
 ; Called at this label from ^MAGDIW2
 N BYTEORDR,STATUS
 ;
 S BYTEORDR="L" ; Targa format is Little Endian
 S STATUS=$$OPEN^MAGOSFIL(FILE,"R")
 I STATUS<0 Q "-1,Open Error: "_STATUS
 S STATUS=$$USE^MAGOSFIL(FILE)
 ;
 K TGA ; Output array is TGA()
 ;
 S TGA(1,"ID Length")=$$GETBYTE^MAGDDR0
 S TGA(2,"Color Map Type")=$$GETBYTE^MAGDDR0
 S TGA(3,"Image Type")=$$GETBYTE^MAGDDR0
 S TGA(4.1,"Color Map Index")=$$GETSHORT^MAGDDR0
 S TGA(4.2,"Color Map Length")=$$GETSHORT^MAGDDR0
 S TGA(4.3,"Color Map Entry Size")=$$GETBYTE^MAGDDR0
 S TGA(5.1,"Image X-Origin")=$$GETSHORT^MAGDDR0
 S TGA(5.2,"Image Y-Origin")=$$GETSHORT^MAGDDR0
 S TGA(5.3,"Image Width")=$$GETSHORT^MAGDDR0
 S TGA(5.4,"Image Height")=$$GETSHORT^MAGDDR0
 S TGA(5.5,"Image Pixel Depth")=$$GETBYTE^MAGDDR0
 S TGA(5.6,"Image Descriptor")=$$GETBYTE^MAGDDR0
 ;
 S STATUS=$$CLOSE^MAGOSFIL(FILE)
 I $$USEPRIN^MAGOSFIL(1)
 Q ""
