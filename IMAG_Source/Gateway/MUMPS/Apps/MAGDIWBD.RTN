Cache for Windows NT^INT^Saved from DICOM,ROU (VACOLTKUZMAP1)^~Format=Cache.S~
%RO on 11/13/2014  13:16
MAGDIWBD^INT^1^63481,47708.460632^0
MAGDIWBD ;WOIFO/EdM/PMK/JSL - Export of radiology images from VistA ; 3 Jan 2015 15:54 PM
 ;;3.0;IMAGING;**50,54,110,157**;Mar 19, 2002;Build 5371;Jan 3, 2015
 ;; Per VHA Directive 2004-038, this routine should not be modified.
 ;; +---------------------------------------------------------------+
 ;; | Property of the US Government.                                |
 ;; | No permission to copy or redistribute this software is given. |
 ;; | Use of unreleased versions of this software requires the user |
 ;; | to execute a written test agreement with the VistA Imaging    |
 ;; | Development Office of the Department of Veterans Affairs,     |
 ;; | telephone (301) 734-0100.                                     |
 ;; | The Food and Drug Administration classifies this software as  |
 ;; | a medical device.  As such, it may not be changed in any way. |
 ;; | Modifications to this software may result in an adulterated   |
 ;; | medical device under 21CFR820, the use of which is considered |
 ;; | to be a violation of US Federal Statutes.                     |
 ;; +---------------------------------------------------------------+
 ;;
 Q
 ;
VERIFY ; get the parameters for this run
 N DFN,RPCERR,VADM,VAICN,VAIN,VAINFO,VAPA,X
 S STORESCP=^MAGDICOM(2006.563,1,"EXPORT DICOM STORAGE SCP")
 S SORTORDR=^MAGDICOM(2006.563,1,"EXPORT DIRECTION")
 S BEGDATE=$G(^MAGDICOM(2006.563,1,"EXPORT BEGIN DATE"))
 S STOPDATE=$G(^MAGDICOM(2006.563,1,"EXPORT STOP DATE"))
 S BATCHSIZ=$G(^MAGDICOM(2006.563,1,"EXPORT BATCH SIZE"))
 S HOURS=^MAGDICOM(2006.563,1,"EXPORT HOURS OF OPERATION")
 ;
 W !!!?10,"F i n a l  P a r a m e t e r   C h e c k l i s t"
 W !?10,"------------------------------------------------"
 W !,"     DICOM storage provider: ",STORESCP
 W !,"      Report scanning order: ",SORTORDR
 I SCANMODE="NUMBER" D  ; numeric report order
 . S RARPT1=$G(^MAGDICOM(2006.563,1,"EXPORT RADIOLOGY REPORT #"))
 . W !,"       Starting with report: ",RARPT1
 . W !,"Number of studies to export: ",BATCHSIZ
 . Q
 E  I SCANMODE="PATIENT" D  ; patient scan
 . S DFN=$G(^MAGDICOM(2006.563,1,"EXPORT PATIENT"))
 . S RPCERR=$$CALLRPC^MAGM2VCU("MAG DICOM GET PATIENT","M",.VAINFO,DFN)
 . I RPCERR<0 S QUIT=-1 Q
 . D VADPT^MAGDRPC0(.VAINFO)
 . S NAME=VADM(1),DOB=$P(VADM(3),"^",2),SSN=$P(VADM(2),"^",2)
 . W !,"               Patient Name: ",NAME
 . W !,"     Social Security Number: ",SSN
 . W !,"              Date of Birth: ",DOB
 . W !,"   Earliest date for report: ",$$FMTE^XLFDT(BEGDATE,1)
 . W !,"     Latest date for report: ",$$FMTE^XLFDT(STOPDATE,1)
 . Q
 E  D  ; data scan order
 . W !,"   Earliest date for report: ",$$FMTE^XLFDT(BEGDATE,1)
 . W !,"     Latest date for report: ",$$FMTE^XLFDT(STOPDATE,1)
 . Q
 W !,"  Active hours of operation: ","M12345678901N12345678901 (M=midnight, N=noon)"
 W !,"                             ",HOURS
 ;
 I $$YESNO^MAGDIWBB("Ready to begin exporting DICOM images?","",.X)<0 S QUIT=1 Q
 I X="YES" S QUIT=0
 E  W " -- export not started" S QUIT=-1
 Q
 ;
FROMLOC(FROMLOC) ; Select "from" locations for transmitter
 N D0,DONE,FROM,I,IDEL,J,K,LIST,SITEIEN,SITENAME,STATNUMB,TMPLOC,X
 I '$G(LOCATION) D  Q
 . W !!,"This DICOM Gateway has not yet been configured completely."
 . W !,"Please run menu option 4-2-2 (Update Gateway Configuration Parameters).",!
 . Q
 S TMPLOC(LOCATION)=""
 S D0=0 F  S D0=$O(^MAGDICOM(2006.563,1,"TRANSMIT LOCATION",D0)) Q:'D0  D
 . S SITEIEN=$G(^MAGDICOM(2006.563,1,"TRANSMIT LOCATION",D0,0))
 . S:SITEIEN TMPLOC(SITEIEN)=""
 . Q
 D
 . Q:$$CALLRPC^MAGM2VCU("MAG DICOM VALID LOCATIONS","M",.FROM)<0
 . S I="",J=0 F  S I=$O(FROM(I)) Q:I=""  D
 . . S X=FROM(I),SITEIEN=+X Q:X'["^"
 . . S:SITEIEN'=LOCATION J=J+1,LIST(J)=X
 . . Q
 . Q:'J
 . W !!,"There are other locations from which to transmit DICOM objects"
 . W !,"besides the one that is currently served by this Gateway."
 . S DONE=0 F  D  Q:DONE
 . . W !!,"Enter any number from the list below to include the location in the"
 . . W !,"current selection.  Prefix the number with a minus sign (-) to remove"
 . . W !,"it from the current selection. (Numbers may be separated by commas.)"
 . . F I=1:1:J S X=LIST(I) D
 . . . S SITEIEN=$P(X,"^",1),SITENAME=$P(X,"^",2),STATNUMB=$P(X,"^",3)
 . . . W !,$J(I,3),": ",$J(STATNUMB,5)," - ",SITENAME
 . . . W:$D(TMPLOC(SITEIEN)) " (Selected)"
 . . . Q
 . . W !,"Add to or remove from selection: " R X:$G(DTIME,300) E  S DONE=1 Q
 . . I $TR(X,".")="" S DONE=1 Q
 . . F  D  Q:X=""  ; process comma-delimited string
 . . . S I=$P(X,",",1),X=$P(X,",",2,$L(X)+2),K=0
 . . . I I="." S DONE=1 Q
 . . . I I?1.N,$D(LIST(I)) S SITEIEN=$P(LIST(I),"^",1),TMPLOC(SITEIEN)="" Q
 . . . S IDEL=$TR(I,"-") ; check for -I remove site from list
 . . . I I?1"-"1.N,$L(IDEL),$D(LIST(IDEL)) D  Q
 . . . . S SITEIEN=$P(LIST(IDEL),"^",1)
 . . . . K TMPLOC(SITEIEN)
 . . . . Q
 . . . W !,"Could not process """,I,"""." R X:$G(DTIME,300) S K=1
 . . . Q
 . . ; erase the location selection text so that it can be redisplayed
 . . F I=1:1:(5+J+K) W $C(13,27),"[K",$C(27),"[1A" ; erase line, go up a line
 . . Q
 . Q
 ;
 K ^MAGDICOM(2006.563,1,"TRANSMIT LOCATION")
 S SITEIEN="",D0=0 F  S SITEIEN=$O(TMPLOC(SITEIEN)) Q:SITEIEN=""  D
 . S D0=D0+1,FROMLOC(D0)=SITEIEN
 . S ^MAGDICOM(2006.563,1,"TRANSMIT LOCATION",D0,0)=SITEIEN
 . Q
 S ^MAGDICOM(2006.563,1,"TRANSMIT LOCATION",0)="^2006.5636^"_D0_"^"_D0
 W !,"Begin transmission... "
 Q



