Cache for Windows NT^INT^Saved from DICOM,DICOM (ISW-KUZMAKP3-LT)^~Format=Cache.S~
%RO on 02/06/2013  20:45
MAGDHRC7^INT^1^62859,74715^0
MAGDHRC7 ;WOIFO/PMK - Read HL7 and generate DICOM ; 06 Feb 2013 8:45 PM
 ;;3.0;IMAGING;**11,51,49,110**;Mar 19, 2002;Build 61;Oct 10, 2012
 ;; Per VHA Directive 2004-038, this routine should not be modified.
 ;; +---------------------------------------------------------------+
 ;; | Property of the US Government.                                |
 ;; | No permission to copy or redistribute this software is given. |
 ;; | Use of unreleased versions of this software requires the user |
 ;; | to execute a written test agreement with the VistA Imaging    |
 ;; | Development Office of the Department of Veterans Affairs,     |
 ;; | telephone (301) 734-0100.                                     |
 ;; | The Food and Drug Administration classifies this software as  |
 ;; | a medical device.  As such, it may not be changed in any way. |
 ;; | Modifications to this software may result in an adulterated   |
 ;; | medical device under 21CFR820, the use of which is considered |
 ;; | to be a violation of US Federal Statutes.                     |
 ;; +---------------------------------------------------------------+
 ;;
 Q
 ; PACS interface -- Radiology HL7 data input routine
 ; This module reads and parses the HL7 OBX Segment
 ;
 ;     XXXX                                         XXX       X
 ;    XX  XX                                         XX      XX
 ;   XX         XXXX    XX XXX  XXXXXXX  XX  XXX     XX     XXXXX
 ;   XX        XX  XX   XXX XX  XX       XX  XX      XX      XX
 ;   XX    X   XX  XX   XX  XX  XXXXXXX  XX  XX      XX      XX
 ;    XX  XX   XX  XX   XX  XX       XX  XX  XX      XX      XX XX
 ;     XXXX     XXXX    XX  XX  XXXXXXX   XXX XX    XXXX      XXX
 ;
OBX ; OBX Segment
 N ID,IDNAME,MSG,RESULT,TAG,TYPE
 S TYPE=$$GETDATA^MAGDHRC0(2)
 S ID=$$GETDATA^MAGDHRC0(3,1,1)
 S IDNAME=$$GETDATA^MAGDHRC0(3,1,2)
 S RESULT=$$GETDATA^MAGDHRC0(5) ; needs unparsed data
 I TYPE="TX" D  Q  ; figure out which text observation identifier
 . ; VistA codes
 . I ID="A",IDNAME="ALLERGIES" D  Q
 . . D MULTLINE^MAGDHRC6("0010,2110",RESULT) ; Alleries
 . . Q
 . I (ID="2000.02")!(ID="RFR"),IDNAME="REASON FOR REQUEST" D  Q
 . . D TEXTOUT("0033,xx50,VA DHCP") ; History (Reason for Study)
 . . Q
 . I (ID="")!(ID="PDX"),IDNAME="PROVISIONAL DIAGNOSIS" D  Q
 . . D TEXTOUT("0033,xx50,VA DHCP") ; History (Reason for Study)
 . . Q
 . I ID="TCM",IDNAME="TECH COMMENT" D  Q
 . . D TEXTOUT("0032,4000") ; Tech comments stored as study cmts
 . . Q
 . I (ID="P")!(ID="PO"),IDNAME="POSTINGS" D  Q
 . . D TEXTOUT("0033,xx50,VA DHCP") ; History (Reason for Study)
 . . Q
 . I ID="SF",IDNAME="SIGNIFICANT FINDINGS" D  Q
 . . D TEXTOUT("0033,xx50,VA DHCP") ; History (Reason for Study)
 . . Q
 . I ID="R",IDNAME="REPORT" Q  ; ignore reports
 . ;
 . S MSG(1)="DICOM TEXT GATEWAY -- HL7 DATA WARNING"
 . S MSG(2)="Unexpected ID="""_ID_""" ID NAME="""_IDNAME_"""."
 . S MSG(3)="Routine: "_$T(+0)
 . D WARNING^MAGDFCNS(.MSG)
 . Q
 I TYPE="CE" D  Q  ; figure out which coded entry observation identifier
 . ; VistA codes
 . I (ID="")!(ID="PDX"),IDNAME="PROVISIONAL DIAGNOSIS" D  Q
 . . D TEXTOUT("0033,xx50,VA DHCP") ; History (Reason for Study)
 . . Q
 . ; EdM: Question: Sometimes ID="",IDNAME="SIG FINDINGS"
 . ; Do we need to handle that case?
 . S MSG(1)="DICOM TEXT GATEWAY -- HL7 DATA WARNING"
 . S MSG(2)="Expected ID="""", NAME=""PROVISIONAL DIAGNOSIS""."
 . S MSG(3)="Found ID="""_ID_""" ID NAME="""_IDNAME_"""."
 . S MSG(4)="Routine: "_$T(+0)
 . D WARNING^MAGDFCNS(.MSG)
 . Q
 S MSG(1)="DICOM TEXT GATEWAY -- HL7 DATA WARNING"
 S MSG(2)="Expected TYPE=""TX"" or ""CE"""
 S MSG(3)="Found TYPE="""_TYPE_"""."
 S MSG(4)="Routine: "_$T(+0)
 D WARNING^MAGDFCNS(.MSG)
 Q
 ;
TEXTOUT(TAG) ; output text
 I '$D(TEXTOUT(IDNAME)) D  ; output the title of the text
 . I $D(TEXTOUT) D MULTLINE^MAGDHRC6(TAG,$C(13,10)) ; blank line
 . D MULTLINE^MAGDHRC6(TAG,IDNAME_$C(13,10))
 . D MULTLINE^MAGDHRC6(TAG,$TR($J("",$L(IDNAME))," ","-")_$C(13,10))
 . S TEXTOUT(IDNAME)=""
 . Q
 D MULTLINE^MAGDHRC6(TAG,RESULT_$C(13,10)) ; concatenate <cr> <lf>
 Q
 ;



