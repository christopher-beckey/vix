Cache for Windows NT^INT^Saved from DICOM,DICOM (ISW-KUZMAKP3-LT)^~Format=Cache.S~
%RO on 04/01/2013  13:40
MAGDCST3^INT^1^62913,49205.219945^0
MAGDCST3 ;WOIFO/PMK - Backend for C-STORE (part 3) ; 01 Apr 2013 1:40 PM
 ;;3.0;IMAGING;**51,54**;MAR 19, 2002;;OCT 28, 2009
 ;; Per VHA Directive 2004-038, this routine should not be modified.
 ;; +---------------------------------------------------------------+
 ;; | Property of the US Government.                                |
 ;; | No permission to copy or redistribute this software is given. |
 ;; | Use of unreleased versions of this software requires the user |
 ;; | to execute a written test agreement with the VistA Imaging    |
 ;; | Development Office of the Department of Veterans Affairs,     |
 ;; | telephone (301) 734-0100.                                     |
 ;; | The Food and Drug Administration classifies this software as  |
 ;; | a medical device.  As such, it may not be changed in any way. |
 ;; | Modifications to this software may result in an adulterated   |
 ;; | medical device under 21CFR820, the use of which is considered |
 ;; | to be a violation of US Federal Statutes.                     |
 ;; +---------------------------------------------------------------+
 ;;
 Q
 ;
ZSTU ; Re-initialize activity flags at system start-up
 N D0
 S D0=0 F  S D0=$O(^MAGDICOM(2006.5764,D0)) Q:'D0  D
 . S $P(^MAGDICOM(2006.5764,D0,0),"^",2,3)="0^"_$$NOW^XLFDT()
 . Q
 Q
 ;
CONBEG ; record the start of a new connection to the MUMPS surrogate
 N D0,NOW,TODAY
 D DATETIME
 S D0=$$GETD0(INSTNAME)
 S ^MAGDICOM(2006.5764,D0,0)=INSTNAME_"^1^"_NOW
 Q
 ;
CONEND ; record the end of a connection to the MUMPS surrogate
 N D0,NOW,TODAY
 D DATETIME
 S D0=$$GETD0(INSTNAME)
 S ^MAGDICOM(2006.5764,D0,0)=INSTNAME_"^0^"_NOW
 Q
 ;
ASCBEG ; record the start of a new association
 N D0,D1,NOW,TODAY,X
 D DATETIME
 S D0=$$GETD0(INSTNAME)
 S X=$G(^MAGDICOM(2006.5764,D0,0))
 S $P(X,"^",4,5)="1^"_NOW
 S ^MAGDICOM(2006.5764,D0,0)=X
 ;
 D ADDAY(D0,TODAY)
 S X=$G(^MAGDICOM(2006.5764,D0,1,TODAY,0))
 S $P(X,"^",2)=$P(X,"^",2)+1
 S ^MAGDICOM(2006.5764,D0,1,TODAY,0)=X
 Q
 ;
ASCEND ; record the end of an association
 N D0,NOW,TODAY,X
 D DATETIME
 S D0=$$GETD0(INSTNAME)
 S X=$G(^MAGDICOM(2006.5764,D0,0))
 S $P(X,"^",4,5)="0^"_NOW
 S ^MAGDICOM(2006.5764,D0,0)=X
 Q
 ;
IMAGE ; record the transfer of each image
 N D0,D1,NOW,TODAY,X
 D DATETIME
 S D0=$$GETD0(INSTNAME)
 D ADDAY(D0,TODAY)
 S X=$G(^MAGDICOM(2006.5764,D0,1,TODAY,0))
 S $P(X,"^",3)=$P(X,"^",3)+1,$P(X,"^",4)=NOW
 S ^MAGDICOM(2006.5764,D0,1,TODAY,0)=X
 Q
 ;
DATETIME ; obtain the current date and time
 S NOW=$$NOW^XLFDT(),TODAY=NOW\1
 Q
 ;
GETD0(INSTNAME) N X
 ; Get internal entry number
 S D0=$O(^MAGDICOM(2006.5764,"B",INSTNAME,"")) Q:D0 D0
 L +^MAGDICOM(2006.5764,0):1E9 ; Background process MUST wait
 S D0=$O(^MAGDICOM(2006.5764," "),-1)+1
 S X=$G(^MAGDICOM(2006.5764,0))
 S $P(X,"^",1,2)="DICOM LOCAL INSTRUMENT STATISTICS^2006.5764"
 S $P(X,"^",3)=D0
 S $P(X,"^",4)=$P(X,"^",4)+1
 S ^MAGDICOM(2006.5764,0)=X
 S ^MAGDICOM(2006.5764,D0,0)=INSTNAME
 S ^MAGDICOM(2006.5764,"B",INSTNAME,D0)=""
 L -^MAGDICOM(2006.5764,0)
 Q D0
 ;
ADDAY(D0,TODAY) N X
 ; Add entry for 'today' if not yet present
 Q:$D(^MAGDICOM(2006.5764,D0,1,TODAY))
 L +^MAGDICOM(2006.5764,D0,1,0):1E9 ; Background process MUST wait
 S X=$G(^MAGDICOM(2006.5764,D0,1,0))
 S $P(X,"^",2)="2006.57641"
 S $P(X,"^",3)=TODAY
 S $P(X,"^",4)=$P(X,"^",4)+1
 S ^MAGDICOM(2006.5764,D0,1,0)=X
 S ^MAGDICOM(2006.5764,D0,1,TODAY,0)=TODAY
 L -^MAGDICOM(2006.5764,D0,1,0)
 Q
 ;
GETPORT(INSTNAME) ; get port number, imaging service, and instrument location
 N IMGSVC ;--- imaging service (RAD or CON)
 N INSTLOC ;-- ien (file #4) of location of the instrument acquiring the image
 N LOCATION ;- name of location of the instrument acquiring the image
 N PORT	;----- DICOM Storage SCP listener port
 N I,PORT
 F I=1:1:$L(INSTNAME) Q:$E(INSTNAME,I)'=" "  S $E(INSTNAME,I)="",I=I-1 ; remove leading spaces
 F I=$L(INSTNAME):-1:1 Q:$E(INSTNAME,I)'=" "  S $E(INSTNAME,I)="" ; remove trailing spaces
 F I=1:1:$L(INSTNAME) I $E(INSTNAME,I,I+1)="  " S $E(INSTNAME,I)="",I=I-1 ; remove extra spaces
 S PORT=$P(INSTNAME," ",1),IMGSVC=$P(INSTNAME," ",2),LOCATION=$P(INSTNAME," ",3,999)
 I (PORT'?1N.N)!(PORT>65535) D  Q 0
 . S X="-3 The Port Number "_PORT_" has to have a numeric value less than 65536"
 . D SEND^MAGDCST2("",TCPPORT,X)
 . Q
 S INSTNAME="PORT_"_PORT
 I IMGSVC'="",IMGSVC'="CON",IMGSVC'="RAD" D  Q 0
 . S X="-4 The Imaging Service """_IMGSVC_""" has to be either CON or RAD"
 . D SEND^MAGDCST2("",TCPPORT,X)
 . Q
 I LOCATION="" S INSTLOC=""
 E  D  ; use an RPC to get the INSTLOC for the LOCATION
 . ; check that the service account access/verify code are good
 . ; Set up credentials to use MUMPS-to-MUMPS Broker
 . N A,RPCERR,V,X
 . S A=$G(^MAGDICOM(2006.563,1,"M-to-M BROKER BGND ACCESS"))
 . S V=$G(^MAGDICOM(2006.563,1,"M-to-M BROKER BGND VERIFY"))
 . S X=$$DECRYP^MAGM2VCU(A)_";"_$$DECRYP^MAGM2VCU(V)
 . S ^TMP("MAG",$J,"DICOM RPC USER")="666^1^"_$$ENCRYPT^MAGM2VCU(X)
 . S X=$$RECONCT^MAGDMENA()
 . I 'X D  Q
 . . S X="-5 Could not reconnect to M2M broker to get information for location """_LOCATION_""""
 . . D SEND^MAGDCST2("",TCPPORT,X)
 . . S PORT=0
 . . Q
 . S RPCERR=$$CALLRPC^MAGM2VCU("MAG DICOM FIND LOCATION","M",.INSTLOC,LOCATION)
 . I RPCERR<0 D
 . S X="-6 RPC error """_RPCERR_""" looking up location """_LOCATION_""""
 . . D SEND^MAGDCST2("",TCPPORT,X)
 . . S PORT=0
 . . Q
 . Q:INSTLOC>0
 . S LOCATION=$TR(LOCATION,", ")
 . S RPCERR=$$CALLRPC^MAGM2VCU("MAG DICOM FIND LOCATION","M",.INSTLOC,LOCATION)
 . I RPCERR<0 D
 . . S X="-7 RPC error """_RPCERR_""" looking up location """_LOCATION_""""
 . . D SEND^MAGDCST2("",TCPPORT,X)
 . . S PORT=0
 . . Q
 . Q:INSTLOC>0
 . S X="-8 Could not find location """_LOCATION_""""
 . D SEND^MAGDCST2("",TCPPORT,X)
 . S PORT=0
 . Q
 D CLOSEM2M^MAGDMENU ; close the RPC port
 K ^TMP("MAG",$J),^TMP("MAGM2V",$J)
 S ^TMP("MAG","MAG_CSTORE",INSTNAME)=IMGSVC_"^"_INSTLOC
 Q PORT



