MAGDMFB6 ;WOIFO/PMK - Miscellaneous Master Files ; 06 Nov 2008 7:28 AM
 ;;3.0;IMAGING;**30,69,99**;Mar 19, 2002;Build 2179;Jun 28, 2011
 ;; Per VHA Directive 2004-038, this routine should not be modified.
 ;; +---------------------------------------------------------------+
 ;; | Property of the US Government.                                |
 ;; | No permission to copy or redistribute this software is given. |
 ;; | Use of unreleased versions of this software requires the user |
 ;; | to execute a written test agreement with the VistA Imaging    |
 ;; | Development Office of the Department of Veterans Affairs,     |
 ;; | telephone (301) 734-0100.                                     |
 ;; | The Food and Drug Administration classifies this software as  |
 ;; | a medical device.  As such, it may not be changed in any way. |
 ;; | Modifications to this software may result in an adulterated   |
 ;; | medical device under 21CFR820, the use of which is considered |
 ;; | to be a violation of US Federal Statutes.                     |
 ;; +---------------------------------------------------------------+
 ;;
 Q
 ;
SOP ; Extended SOP Negotiation
 N D0,I,LIST,READCODE,UIDNAME,WRITCODE,X
 ;
 D MESSAGE^MAGDMLOG(2,0,"Updating the extended SOP negotiation table.","t")
 K ^MAGDICOM(2006.531)
 S ^MAGDICOM(2006.531,0)="EXTENDED SOP NEGOTIATION^2006.531^^"
 ;
 S D0=0 F I=1:1 D  Q:X=""
 . S X=$P($T(UIDLIST+I),";;",2) Q:X=""
 . S D0=D0+1
 . S UIDNAME=$P(X,";",1),READCODE=$P(X,";",2),WRITCODE=$P(X,";",3)
 . S READCODE=$TR(READCODE,"^",".") ; {entry point}.{routine}
 . S WRITCODE=$TR(WRITCODE,"^",".") ; {entry point}.{routine}
 . S X=UIDNAME_"^"_READCODE_"^"_WRITCODE
 . S ^MAGDICOM(2006.531,D0,0)=X
 . S ^MAGDICOM(2006.531,"B",UIDNAME,D0)=""
 . S $P(^MAGDICOM(2006.531,0),"^",3,4)=D0_"^"_D0
 . Q
 S X=D0_" entr"_$S(D0=1:"y",1:"ies")_" stored."
 D MESSAGE^MAGDMLOG(1,0,X,"t")
 Q
 ;
PDU ; Build a new PDU TYPE map
 N D0,I,LIST,NAME,PDUTYPE,STANDARD,X
 D MESSAGE^MAGDMLOG(2,0,"Updating the PDU TYPE table.","t")
 K ^MAGDICOM(2006.54)
 S ^MAGDICOM(2006.54,0)="PDU TYPE^2006.54^^"
 ;
 S D0=0 F I=1:1 D  Q:X=""
 . S X=$P($T(PDUTYPE+I),";;",2) Q:X=""
 . S D0=D0+1
 . S PDUTYPE=$P(X,";",1),NAME=$P(X,";",2),STANDARD=$P(X,";",3)
 . S X=PDUTYPE_"^"_NAME_"^"_STANDARD
 . S ^MAGDICOM(2006.54,D0,0)=X
 . S ^MAGDICOM(2006.54,"B",PDUTYPE,D0)=""
 . S $P(^MAGDICOM(2006.54,0),"^",3,4)=D0_"^"_D0
 . Q
 S X=D0_" entr"_$S(D0=1:"y",1:"ies")_" stored."
 D MESSAGE^MAGDMLOG(1,0,X,"t")
 Q
 ;
UIDLIST ; list of UID names with extended negotiation sub-items and code
 ;;Patient Root Query/Retrieve Information Model - FIND;BYTE^MAGDACR2;BYTE0^MAGDACW2
 ;;Study Root Query/Retrieve Information Model - FIND;BYTE^MAGDACR2;BYTE0^MAGDACW2
 ;;Patient/Study Only Query/Retrieve Information Model - FIND (Retired);BYTE^MAGDACR2;BYTE0^MAGDACW2
 ;
PDUTYPE ; Table of PDU Types and Item Type
 ;;01H;A-ASSOCIATE-RQ;Part 8, Section 9.3.2
 ;;02H;A-ASSOCIATE-AC;Part 8, Section 9.3.3
 ;;03H;A-ASSOCIATE-RJ;Part 8, Section 9.3.4
 ;;04H;P-DATA-TF;Part 8, Section 9.3.5
 ;;05H;A-RELEASE-RQ;Part 8, Section 9.3.6
 ;;06H;A-RELEASE-RP;Part 8, Section 9.3.7
 ;;07H;A-ABORT;Part 8, Section 9.3.8
 ;;10H;Application Context Item;Part 8, Sections 9.3.2.1 and 9.3.3.1
 ;;20H;Presentation Context Item;Part 8, Section 9.3.2.2
 ;;21H;Presentation Context Item;Part 8, Section 9.3.3.2
 ;;30H;Abstract Syntax Sub-Item;Part 8, Section 9.3.2.2.1
 ;;40H;Transfer Syntax Sub-Item;Part 8, Sections 9.3.2.2.2 and 9.3.3.2.1
 ;;50H;User Information Item;Part 8, Sections 9.3.2.3 and 9.3.3.3
 ;;51H;Maximum Length Sub-Item;Part 8, Section D.1
 ;;52H;Implementation Class UID Sub-Item;Part 7, Sections D.3.3.2.1/2
 ;;53H;Asynchronous Operations Window Sub-Item;Part 7, Sections D.3.3.3.1/2
 ;;54H;SCP/SCU Role Selections Sub-Item;Part 7, Sections D.3.3.4.1/2
 ;;55H;Implementation Version Name;Part 7, Sections D.3.3.2.3/4
 ;;56H;SOP Class Extended Negotiation Sub-Item;Part 7, Sections D.3.3.5.1/2
 ;
