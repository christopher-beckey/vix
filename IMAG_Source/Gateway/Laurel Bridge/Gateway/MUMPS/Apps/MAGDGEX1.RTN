MAGDGEX1 ;WOIFO/EdM - Conversion of images on GE PACS ; 04/05/2006 13:50
 ;;3.0;IMAGING;**11,30,51,50,69**;MAR 19, 2002;;NOV 7, 2007
 ;; Per VHA Directive 2004-038, this routine should not be modified.
 ;; +---------------------------------------------------------------+
 ;; | Property of the US Government.                                |
 ;; | No permission to copy or redistribute this software is given. |
 ;; | Use of unreleased versions of this software requires the user |
 ;; | to execute a written test agreement with the VistA Imaging    |
 ;; | Development Office of the Department of Veterans Affairs,     |
 ;; | telephone (301) 734-0100.                                     |
 ;; | The Food and Drug Administration classifies this software as  |
 ;; | a medical device.  As such, it may not be changed in any way. |
 ;; | Modifications to this software may result in an adulterated   |
 ;; | medical device under 21CFR820, the use of which is considered |
 ;; | to be a violation of US Federal Statutes.                     |
 ;; +---------------------------------------------------------------+
 ;;
 Q
 ;
START N BUSY,CHANNEL,CMOVEAET,D0,D1,DEBUG,DIR,DIRLOG,FBSWITCH,HRS,LOGDIR,IMGSTU,INCOMING,MAGGEMIG,OUTGOING,PACS,PORT,SDATE,SSNDASH,STIME,T,X
 S MAGGEMIG=1 ; Makes some programs save some extra statistics
 L +^MAGDICOM("GE CONVERSION"):0 E  D  Q
 . W !!,"Another instance of this program is already running.",!
 . Q
 D:$G(^MAGDICOM(2006.563,1,"GE CONV ACTIVE"))="YES"  ; Clean up after abort
 . Q
 D PRMS^MAGDGEX2(1)
 S FBSWITCH="FOREGROUND"
 W !
 D NEWLOG^MAGDMLOG("Scan Radiology Reports for Missing Images")
 S X=$G(^MAGDICOM(2006.563,1,"DATA PATH",1,0),"C:\DICOM\")
 S X=$P($TR(X,"dicom","DICOM"),"\DICOM\",1)_"\DICOM\GE"
 S V=$$MKDIR^MAGOSDIR(X)
 I X<0 D  Q
 . W !,"Could not create directory """_X_"""."
 . W !,"Error code is ",V,!
 . Q
 S DIRLOG=X
 S ^MAGDICOM(2006.563,1,"GE CONV ACTIVE")="YES"
 S DIR=$G(^MAGDICOM(2006.563,1,"GE CONV DIR"))
 S DIR=$S(DIR="DESCENDING":-1,1:1)
 S X=$G(^MAGDICOM(2006.563,1,"GE CONV LO"))-1 S:X<1 X=0
 S ^MAGDICOM(2006.563,1,"GE CONV LO")=X S:DIR>0 D0=X
 S X=$G(^MAGDICOM(2006.563,1,"GE CONV HI"))+1
 I X<2 Q:$$CALLRPC^MAGM2VCU("MAG DICOM GET RAD RPT INFO","MT",.T,"O1"," ",-1)<0  S X=T+1
 S ^MAGDICOM(2006.563,1,"GE CONV HI")=X S:DIR<0 D0=X
 S X=$O(^MAGDICOM(2006.563,1,"GE CONV #HRS"," "),-1)+1
 S ^MAGDICOM(2006.563,1,"GE CONV #HRS",X,0)=$H
 S X=$G(^MAGDICOM(2006.563,1,"GE CONV IMGSTU"))
 S X=X\1 S:(X<1)!(X>3) X=1 S IMGSTU=X
 S X=$G(^MAGDICOM(2006.563,1,"GE CONV SSNDASH"))
 S SSNDASH=$S("Yy"[$E(X_"Y",1):"Yes",1:"No")
 S BUSY=$H
 ;
 F  D  Q:'D0
 . I $G(^MAGDICOM(2006.563,1,"GE CONV ACTIVE"))="STOP" S D0=0 Q
 . S X=$G(^MAGDICOM(2006.563,1,"GE CONV HRS"))
 . S:23[($H#7) X="" ; Saturday and Sunday are 24 hrs OK
 . S X=$E(X_"YYYYYYYYYYYYYYYYYYYYYYYY",1,24)
 . S X=$E(X,$P($H,",",2)\3600+1)
 . I X'="Y" D  Q
 . . D LOGTIME(.BUSY)
 . . D MESSAGE^MAGDMLOG(1,0,"Converter waits 5 minutes.","t")
 . . H 300
 . . Q
 . Q:$$CALLRPC^MAGM2VCU("MAG DICOM GET RAD RPT INFO","MT",.T,"O1",D0,DIR)<0
 . S D0=T Q:'D0
 . I DIR<0,D0<$G(^MAGDICOM(2006.563,1,"GE CONV LO")) S D0=0 Q
 . I DIR>0,D0>$G(^MAGDICOM(2006.563,1,"GE CONV HI")) S D0=0 Q
 . ; OK, here goes...
 . D ADD("GE CONV #STU",1)
 . S D1="" D  F  D  Q:'D1
 . . I D1'="" D  Q:'D1
 . . . Q:$$CALLRPC^MAGM2VCU("MAG DICOM GET RAD RPT INFO","MT",.D1,"O2",D0,1,D1)<0
 . . . Q
 . . S D1=+D1
 . . ;
 . . N CASE,DCASE,DFN,ERA,Q0,R,RECORD,V,VADM,VAERR,VAIN,VAPA,VASD
 . . N MESSAGE ;-- name of type of message being generated
 . . N MULTIMSG ;- multiple message flag (null, "OPEN", or "CLOSE")
 . . N STUDATE ;-- Study Date
 . . N USERAPP ;-- SCU_LIST.DIC name of service class user application
 . . ;
 . . ; D1=0 for primary case number, D1>0 for alternate case number
 . . ;
 . . I IMGSTU>1 Q:$$CALLRPC^MAGM2VCU("MAG DICOM GET RAD RPT INFO","MT",.T,"O2",D0,2005,0)<0
 . . I IMGSTU=2,T Q   ; 2= only query when there are no images
 . . I IMGSTU=3,'T Q  ; 3= only query when there are images
 . . Q:$$CALLRPC^MAGM2VCU("MAG DICOM GET RAD RPT INFO","MT",.RECORD,"G1",D0)<0
 . . S DFN=$P(RECORD,"^",2)
 . . I 'DFN D  Q
 . . . S X="Report #"_D0_", invalid patient """_DFN_"""."
 . . . D MESSAGE^MAGDMLOG(1,0,X,"t")
 . . . Q
 . . Q:$$CALLRPC^MAGM2VCU("MAG DICOM GET PATIENT","MT",.R,DFN)<0
 . . I R(1)<0 D  Q
 . . . S X="Report #"_D0_", patient """_DFN_""", VADPT error """_R(1)_"""."
 . . . D MESSAGE^MAGDMLOG(1,0,X,"t")
 . . . Q
 . . D VADPT^MAGDRPC0(.R)
 . . S VADM(2)=$$SSN(VADM(2),SSNDASH)
 . . S (R,X)=RECORD,DCASE=$P(R,"^",1),CASE=$P(R,"^",4),STUDATE=$P(R,"^",3)
 . . D:D1  Q:'X
 . . . Q:$$CALLRPC^MAGM2VCU("MAG DICOM GET RAD RPT INFO","MT",.X,"G2",D0,1,D1)<0
 . . . S:X DCASE=$P(X,"^",1),CASE=$P(X,"-",2)
 . . . Q
 . . F ERA=1:1:4 D
 . . . ; Construct a C-FIND Request msg to send to the PACS to get Study UID
 . . . S X=$G(^MAGDICOM(2006.563,1,"GE CONV ERA"_ERA))
 . . . I $P(X,"^",1),$P(X,"^",1)>STUDATE Q
 . . . I $P(X,"^",2),$P(X,"^",2)<STUDATE Q
 . . . S CHANNEL=1
 . . . D INIT^MAGDTLOG ; initialize the message log
 . . . D INIT^MAGDQUE3 ; initialization for DIRECT FILEMODE
 . . . D INIT^MAGDQRU0 ; initialize variables for C-FIND/MOVE
 . . . S QUEUEIN("FILE")=INCOMING,QUEUEOUT("FILE")=OUTGOING
 . . . D QSTUDY^MAGDQRU0 ; initialization for C-Find
 . . . ;
 . . . S SDATE=STUDATE\1+17000000 ; Study date
 . . . S STIME=$TR($J("."_$P(STUDATE,".",2)*1E6,6)," ",0) ; Study time
 . . . S CMOVEAET=$G(^MAGDICOM(2006.563,1,"MOVE DESTINATION AE TITLE"))
 . . . S:CMOVEAET="" CMOVEAET="VISTA_STORAGE"
 . . . D @("INFO"_ERA)
 . . . ;
 . . . S FILEMODE="DIRECT",MULTIMSG="" D REQUEST^MAGDDW1
 . . . S MESSAGE="QUERY/RETRIEVE (C-FIND)"
 . . . D ADD("GE CONV #QRQ",1)
 . . . D COUNT^MAGDAUD2 ; update audit record
 . . . S X="Request images for study "_DCASE_", era "_ERA
 . . . D MESSAGE^MAGDMLOG(1,0,X,"t")
 . . . ; Send the C-FIND Request message to the PACS
 . . . ; Initiate the connection with the SOP Class Provider
 . . . S USERAPP="PACS QUERY/RETRIEVE"
 . . . S X=$G(^MAGDICOM(2006.585,+$O(^MAGDICOM(2006.585,"B",USERAPP,"")),0))
 . . . S PACS=$P(X,"^",6) S:PACS="" PACS="GE"
 . . . D  ; Log this request
 . . . . N A,F,I,S,S0,S1,S2
 . . . . S X=D0,$P(X,"^",ERA+1)=$H,^MAGDICOM("GE",D0,D1)=X
 . . . . S F=DIRLOG_"\REQUEST.LOG"
 . . . . S X=$$OPEN^MAGOSFIL(F,"A")
 . . . . S X=$$USE^MAGOSFIL(F) W !,"=====",!
 . . . . S S0="" F  S S0=$O(^TMP("MAG",$J,"DICOM","OUT",S0)) Q:S0=""  D
 . . . . . S S1=0 F  S S1=$O(^TMP("MAG",$J,"DICOM","OUT",S0,S1)) Q:S1=""  D
 . . . . . . S S2=0 F  S S2=$O(^TMP("MAG",$J,"DICOM","OUT",S0,S1,S2)) Q:S2=""  D
 . . . . . . . S X=$G(^TMP("MAG",$J,"DICOM","OUT",S0,S1,S2))
 . . . . . . . W S0,", ",S1,": """
 . . . . . . . S S=1 F I=1:1:$L(X) D
 . . . . . . . . S A=$A(X,I) I A>31,A<127 W:'S ")_""" S S=1 W $C(A) Q
 . . . . . . . . W $S(S:"""_$C(",1:","),A S S=0
 . . . . . . . . Q
 . . . . . . . W $S(S:"""",1:")"),!
 . . . . . . . Q
 . . . . . . Q
 . . . . . Q
 . . . . W "RARPT: ",D0 W:D1 "/",D1 W ", era ",ERA,", ",USERAPP,!
 . . . . S X=$$CLOSE^MAGOSFIL(F)
 . . . . I $$USEPRIN^MAGOSFIL(1)
 . . . . Q
 . . . D ENTRY^MAGDACU1
 . . . Q
 . . D ADD("GE CONV #STC",1)
 . . Q
 . S:DIR>0 ^MAGDICOM(2006.563,1,"GE CONV LO")=D0
 . S:DIR<0 ^MAGDICOM(2006.563,1,"GE CONV HI")=D0
 . Q
 D LOGTIME(.BUSY)
 S ^MAGDICOM(2006.563,1,"GE CONV ACTIVE")="NO"
 S X=$O(^MAGDICOM(2006.563,1,"GE CONV #HRS"," "),-1)
 S ^MAGDICOM(2006.563,1,"GE CONV #HRS",X,1)=$H
 Q
 ;
INFO1 ;
 D SAVE^MAGDFCNS("0000,0600",CMOVEAET) ; C-Move destination
 D SAVE^MAGDFCNS("0008,0020",SDATE)
 D SAVE^MAGDFCNS("0008,0050",CASE)
 D SAVE^MAGDFCNS("0010,0010","")
 D SAVE^MAGDFCNS("0010,0020","")
 D SAVE^MAGDFCNS("0020,000D","") ; Study Instance UID is always left blank
 D SAVE^MAGDFCNS("0020,0010","")
 Q
 ;
INFO2 ;
 D SAVE^MAGDFCNS("0000,0600",CMOVEAET) ; C-Move destination
 D SAVE^MAGDFCNS("0008,0020",SDATE)
 D SAVE^MAGDFCNS("0008,0050",DCASE)
 D SAVE^MAGDFCNS("0010,0010",VADM(1))
 D SAVE^MAGDFCNS("0010,0020",VADM(2)) ; SSN
 D SAVE^MAGDFCNS("0020,000D","") ; Study Instance UID is always left blank
 D SAVE^MAGDFCNS("0020,0010","")
 Q
 ;
INFO3 ;
 D SAVE^MAGDFCNS("0000,0600",CMOVEAET) ; C-Move destination
 D SAVE^MAGDFCNS("0008,0020","")
 D SAVE^MAGDFCNS("0008,0050","")
 D SAVE^MAGDFCNS("0010,0010","")
 D SAVE^MAGDFCNS("0010,0020","")
 D SAVE^MAGDFCNS("0020,000D","") ; Study Instance UID is always left blank
 D SAVE^MAGDFCNS("0020,0010",DCASE)
 Q
 ;
INFO4 ;
 D SAVE^MAGDFCNS("0000,0600",CMOVEAET) ; C-Move destination
 D SAVE^MAGDFCNS("0008,0020",SDATE)
 D SAVE^MAGDFCNS("0008,0030",STIME)
 D SAVE^MAGDFCNS("0008,0050",DCASE)
 D SAVE^MAGDFCNS("0010,0010",VADM(1))
 D SAVE^MAGDFCNS("0010,0020",VADM(2)) ; SSN
 D SAVE^MAGDFCNS("0020,000D","") ; Study Instance UID is always left blank
 D SAVE^MAGDFCNS("0020,0010",DCASE)
 Q
 ;
STOP N X
 S X=$G(^MAGDICOM(2006.563,1,"GE CONV ACTIVE"))
 I X="NO" W !!,"Converter currently not active",! Q
 I X="STOP" W !!,"Stop already in progress",! Q
 S ^MAGDICOM(2006.563,1,"GE CONV ACTIVE")="STOP"
 Q
 ;
ADD(V,N) ;
 S ^MAGDICOM(2006.563,1,V)=$G(^MAGDICOM(2006.563,1,V))+1
 Q
 ;
LOGTIME(BUSY) N X,V
 Q:'BUSY
 S X=$H,X=(X-BUSY)*86400+$P(X,",",2)-$P(BUSY,",",2)
 S V="GE CONV #HRS"
 S ^MAGDICOM(2006.563,1,V)=$G(^MAGDICOM(2006.563,1,V))+X
 S BUSY=0
 Q
 ;
SSN(S,D) N S1,S2
 S D=(D="YES"),S1=$TR($P(S,"^",1),"-"),S2=$P(S,"^",2)
 Q:'D S1
 Q:S2'="" S2
 Q $E(S1,1,3)_"-"_$E(S1,4,5)_"-"_$E(S1,6,$L(S1))
 ;
