MAGDUID4 ;WOIFO/EdM - UID Table Management - Client Part ; 26 Feb 2009 7:44 AM
 ;;3.0;IMAGING;**54**;MAR 19, 2002;;OCT 28, 2009
 ;; Per VHA Directive 2004-038, this routine should not be modified.
 ;; +---------------------------------------------------------------+
 ;; | Property of the US Government.                                |
 ;; | No permission to copy or redistribute this software is given. |
 ;; | Use of unreleased versions of this software requires the user |
 ;; | to execute a written test agreement with the VistA Imaging    |
 ;; | Development Office of the Department of Veterans Affairs,     |
 ;; | telephone (301) 734-0100.                                     |
 ;; | The Food and Drug Administration classifies this software as  |
 ;; | a medical device.  As such, it may not be changed in any way. |
 ;; | Modifications to this software may result in an adulterated   |
 ;; | medical device under 21CFR820, the use of which is considered |
 ;; | to be a violation of US Federal Statutes.                     |
 ;; +---------------------------------------------------------------+
 ;;
 Q
 ;
CLIENT ; Call Server to obtain UID Table
 N ERR,INFO,LAST,LIST,N,REF,VAL
 ;
 K ^TMP("MAG",$J,"MASTER")
 W !!,"Fetching UID Table from VistA"
 S (ERR,N)=0,LAST="^" F  D  Q:LAST=""  Q:LAST="^"
 . S X=$$CALLRPC^MAGM2VCU("MAG DICOM GET UID TABLE","M",.INFO,LAST,100)
 . I X<0 S LAST="",ERR=ERR+1 Q
 . S I=$O(INFO("")) I I="" S ERR=ERR+1 Q
 . F  D  Q:I=""
 . . S I=$O(INFO(I)) Q:I=""  S REF=INFO(I)
 . . I REF="^" S LAST="" Q
 . . S I=$O(INFO(I)) Q:I=""  S VAL=INFO(I)
 . . S LAST=REF
 . . S REF="^TMP(""MAG"",$J,""MASTER"","_$P(REF,"(",2,$L(REF)+2)
 . . S @REF=VAL,N=N+1
 . . Q
 . Q
 I ERR D  Q
 . W !,ERR," error" W:ERR>1 "s" W " occurred while transmitting"
 . W !,"UID Table not replaced."
 . Q
 W " ... ",N," node" W:N>1 "s"
 K ^MAGDICOM(2006.539) M ^MAGDICOM=^TMP("MAG",$J,"MASTER")
 K ^TMP("MAG",$J,"MASTER")
 Q
 ;
CODE(SOPUID,ACTION) N D0,D1
 Q:$G(SOPUID)="" ""
 Q:$G(ACTION)="" ""
 S D0=$O(^MAGDICOM(2006.539,"B",SOPUID,"")) Q:'D0 ""
 S D1=$O(^MAGDICOM(2006.539,D0,1,"B",ACTION,"")) Q:'D1 ""
 Q $P($G(^MAGDICOM(2006.539,D0,1,D1,0)),"^",2)
 ;
