MAGDAIR1 ;WOIFO/PMK - Automatic Import Reconciliation Workflow ; 20 Jul 2010 1:01 PM
 ;;3.0;IMAGING;**53,49**;Mar 19, 2002;Build 2201;Jun 29, 2011
 ;; Per VHA Directive 2004-038, this routine should not be modified.
 ;; +---------------------------------------------------------------+
 ;; | Property of the US Government.                                |
 ;; | No permission to copy or redistribute this software is given. |
 ;; | Use of unreleased versions of this software requires the user |
 ;; | to execute a written test agreement with the VistA Imaging    |
 ;; | Development Office of the Department of Veterans Affairs,     |
 ;; | telephone (301) 734-0100.                                     |
 ;; | The Food and Drug Administration classifies this software as  |
 ;; | a medical device.  As such, it may not be changed in any way. |
 ;; | Modifications to this software may result in an adulterated   |
 ;; | medical device under 21CFR820, the use of which is considered |
 ;; | to be a violation of US Federal Statutes.                     |
 ;; +---------------------------------------------------------------+
 ;;
 ; Top level routine for automatic import reconciliation - 6 entry points
 ;
FROMDOD ; entry point from ^MAGDMENU for DoD Import
 N HEADING ;-- application title for top of screen
 N ORDERED ;-- flag to indicate if the study was already ordered by the VA
 N ORIGINDX ;- origin index 
 S ORDERED=0,ORIGINDX="D" ; from DoD
 S HEADING="Import DoD DICOM Studies that were Transmitted to the VA"
 G TRANSMIT
 ;
DICOMFIX ; entry point from ^MAGDMENU for DICOM correct
 N HEADING ;-- application title for top of screen
 N ORDERED ;-- flag to indicate if the study was already ordered by the VA
 N ORIGINDX ;- origin index 
 S ORDERED=1,ORIGINDX="V" ; from VA
 S HEADING="Correct Local VA Study Patient and Study Identification Information"
 G TRANSMIT
 ;
TRANSMIT ; common pathway for transmitted dicom objects
 N DATAFILE ;- global reference for import data
 N MEDIA ;---- flag to indicate the kind of transport mechanism
 N REQLOC ;--- order requesting location
 N REQPHYS ;-- order requesting physician
 ;
 S DATAFILE="^MAGDICOM(2006.5751)"
 S MEDIA="T" ; tcp/ip transmission
 I 'ORDERED D ORDERING^MAGDAIR2(.REQPHYS,.REQLOC) ; get ordering provider and ordering location
 D START
 K ^TMP("MAG",$J,"IMPORT")
 L -^MAGDICOM(2006.563,1,"IMPORTER")
 Q
 ;
CONTRACT ; entry point from ^MAGDMENU for ordered (contract) studies from CD & DV
 N HEADING ;-- application title for top of screen
 N ORDERED ;-- flag to indicate if the study was already ordered by the VA
 N ORIGINDX ;- origin index  
 N OPTION
 N X
 S OPTION(1)="Disk (CD/DVD)"
 S OPTION(2)="Transmission"
 S ORDERED=1,ORIGINDX="F" ; for fee
 S HEADING="Import Studies that were Ordered by the VA and Contracted to be Performed on the Outside"
 I $$CHOOSE^MAGDAIR0("How did the outside studies arrive? ","Disk",.X,.OPTION)<0 Q
 I X=OPTION(1) G MEDIA
 G TRANSMIT
 ;
UNORDERD ; entry point from ^MAGDMENU for importing unordered DoD studies from CD & DVD
 N HEADING ;-- application title for top of screen
 N ORDERED ;-- flag to indicate if the study was already ordered by the VA
 N ORIGINDX ;- origin index 
 S ORDERED=0,ORIGINDX="D" ; from DoD
 S HEADING="Import Unordered Studies from CDs and DVDs from DoD facilities"
 G MEDIA
 ;
UNORDERV ; entry point from ^MAGDMENU for importing unordered studies from CD & DVD
 N HEADING ;-- application title for top of screen
 N ORDERED ;-- flag to indicate if the study was already ordered by the VA
 N ORIGINDX ;- origin index 
 S ORDERED=0,ORIGINDX="V" ; from other VA facilities
 S HEADING="Import Unordered Studies from CDs and DVDs from other VA facilities"
 G MEDIA
 ;
UNORDERN ; entry point from ^MAGDMENU for importing unordered studies from CD & DVD
 N HEADING ;-- application title for top of screen
 N ORDERED ;-- flag to indicate if the study was already ordered by the VA
 N ORIGINDX ;- origin index 
 S ORDERED=0,ORIGINDX="N" ; from non-VA facilities
 S HEADING="Import Unordered Studies from CDs and DVDs from non-VA facilities"
 G MEDIA
 ;
MEDIA ; common pathway for handling dicom objects on portable media
 N DATAFILE ;- global reference for import data
 N DRIVE ;---- drive or path for CD/DVD input device
 N MEDIA ;---- flag to indicate the kind of transport mechanism
 N REQLOC ;--- order requesting location
 N REQPHYS ;-- order requesting physician
 ;
 I 'ORDERED D ORDERING^MAGDAIR2(.REQPHYS,.REQLOC) ; get ordering provider and ordering location
 D DRIVE^MAGDAIR2(.DRIVE) ; get importer drive
 W @IOF,?20,HEADING
 W !?20,$TR($J("",$L(HEADING))," ","-")
 S DATAFILE=$NA(^TMP("MAG",$J,"IMPORT MEDIA"))
 S MEDIA="D" ; disc
 D ENTRY^MAGDAIRM(DRIVE)
 D START
 K @DATAFILE
 L -^MAGDICOM(2006.563,1,"IMPORTER")
 Q
 ;
START ; mainline process
 N AUTODEL ;-- flag to automatically delete DICOM files that have already been imported
 N BADPARAM ;- list of missing parameters
 N COMPLETE ;- number of DICOM objects that have already been imported
 N DONE ;----- FOR-loop completion flag
 N DUPCOUNT ;- count of duplicate Study Instance UIDs
 N DUPFILE ;-- duplicate DICOM file on VistA
 N ONFILE ;--- number of studies that have already been stored on VistA 
 N NOIMPORT ;- number of DICOM objects with presentation contexts that can't be imported
 N PARTIAL ;-- number of DICOM studies that have been partially imported
 N PIDCACHE ;- cache of selected patient identification data
 N RADCACHE ;- cache of selected radiology procedures
 N RPCERR ; -- rpc error indicator
 N STUDYCNT ;- count of studies to be imported
 N USERNAME ;- name of the person logged into to DICOM Gateway
 N ERROR,X
 ;
 L +^MAGDICOM(2006.563,1,"IMPORTER"):0 E  D  Q
 . N MSG
 . S MSG(1)="Sorry, only one instance of the Importer may be run at a time."
 . S MSG(2)="This is a patient safety issue, to insure that you don't"
 . S MSG(3)="confuse patients between different Importer sessions."
 . D WARNING^MAGDFCNS(.MSG)
 . Q
 ;
 I $G(^MAGDICOM(2006.563,1,"IMPORTER"))'="YES" D
 . N MSG
 . S MSG(1)="The Importer is disabled in the DICOM Gateway configuration file."
 . S MSG(2)="Use menu option 4-2-2 to enable the Importer."
 . D WARNING^MAGDFCNS(.MSG)
 . D CONTINUE^MAGDAIR0
 . Q
 ;
 S X=$$LICENSE^MAGDAIR0()
 I X D
 . N MSG
 . S MSG(1)="Possible Laurel Bridge license problem"
 . S MSG(2)=X
 . D WARNING^MAGDFCNS(.MSG)
 . D CONTINUE^MAGDAIR0
 . Q
 ;
 K ^TMP("MAG",$J,"IMPORT")
 I '$O(@DATAFILE@(0)) W !!,"No images to correct" Q
 ;
 S RPCERR=$$CALLRPC^MAGM2VCU("MAG DICOM GET USERNAME","MT",.USERNAME)
 I $G(USERNAME)="" W "Error: NO USER NAME DEFINED" Q
 ;
 S ERROR="" I 'ORDERED D  Q:ERROR
 . N NOW
 . ; download radiology procedures and modifiers once a day
 . S NOW=$$NOW^XLFDT()
 . I (NOW\1)>($G(^MAGDICOM(2006.563,1,"IMPORTER RAD DICT DOWNLOAD"))\1) D
 . . S ERROR=$$BUILD^MAGDAIRR() Q:ERROR  ; download radiology procedures
 . . D BUILD1^MAGDAIRR ; download radiology modifiers
 . . S ^MAGDICOM(2006.563,1,"IMPORTER RAD DICT DOWNLOAD")=NOW
 . . Q
 . Q
 ;
 I $$VERIFY^MAGDAIRC(.BADPARAM) D  Q
 . N I,J,MSG
 . S I=0
 . S I=I+1,MSG(I)="One or more of the required parameters for changing the exam/study state"
 . S I=I+1,MSG(I)="to the ""Examined""/""Electronically Filed"" state has not been defined:"
 . F J=1:1:BADPARAM(0) S I=I+1,MSG(I)="     "_BADPARAM(J) ; list of bad parameters
 . S I=I+1,MSG(I)="Use menu option 2-12-9 Modify Examined/Electronically Filed Parameters"
 . S I=I+1,MSG(I)="to initialize them."
 . D WARNING^MAGDFCNS(.MSG)
 . Q
 ;
 ; get list of studies to import
 S STUDYCNT=$$GETSTUDY^MAGDAIR3(.NOIMPORT)
 I STUDYCNT<0 W !!,"*** Error: ",STUDYCNT," ***" Q
 I NOIMPORT D
 . N COUNT,ERROR,I,MSG,REASON,SOPCLASS,TSYNTAX
 . S I=0
 . S I=I+1,MSG(I)=""
 . I NOIMPORT=1 D
 . . S I=I+1,MSG(I)="     There is one DICOM object that cannot be stored on VistA."
 . . S I=I+1,MSG(I)="     It is flagged with a yellow question mark "
 . . Q
 . E  D
 . . S I=I+1,MSG(I)="     There are "_NOIMPORT_" DICOM objects that cannot be stored on VistA."
 . . S I=I+1,MSG(I)="     Each is flagged with a yellow question mark "
 . . Q
 . S MSG(I)=MSG(I)_$$HIGHLITE^MAGDAIR0("YELLOW","?")_" below."
 . S SOPCLASS=""
 . F  S SOPCLASS=$O(^TMP("MAG",$J,"IMPORT MEDIA","PCONTEXT ERROR",SOPCLASS)) Q:SOPCLASS=""  D
 . . S I=I+1,MSG(I)=""
 . . D ERRMSG1^MAGDAIR0(.MSG,.I,"SOP Class: "_$$GETNAME^MAGDUID2(SOPCLASS))
 . . S TSYNTAX=""
 . . F  S TSYNTAX=$O(^TMP("MAG",$J,"IMPORT MEDIA","PCONTEXT ERROR",SOPCLASS,TSYNTAX)) Q:TSYNTAX=""  D
 . . . D ERRMSG1^MAGDAIR0(.MSG,.I,"Transfer Syntax: "_$$GETNAME^MAGDUID2(TSYNTAX))
 . . . S ERROR=""
 . . . F  S ERROR=$O(^TMP("MAG",$J,"IMPORT MEDIA","PCONTEXT ERROR",SOPCLASS,TSYNTAX,ERROR)) Q:ERROR=""  D
 . . . . S COUNT=^TMP("MAG",$J,"IMPORT MEDIA","PCONTEXT ERROR",SOPCLASS,TSYNTAX,ERROR)
 . . . . I ERROR=2 S REASON="No VISTA_STORAGE AE TITLE - check SCP_LIST.DIC"
 . . . . E  I ERROR=3 S REASON="The SOP Class is not supported"
 . . . . E  I ERROR=4 S REASON="The Transfer Syntax is not supported"
 . . . . E  S REASON="Unknown error: "_ERROR
 . . . . S I=I+1,MSG(I)="Problem: "_REASON_" -- "_COUNT_" DICOM objects"
 . . . . Q
 . . . Q
 . . Q
 . S I=I+1,MSG(I)=""
 . W ! D WARNING^MAGDFCNS(.MSG)
 . D CONTINUE^MAGDAIR0
 . Q
 S ONFILE=$$STUDYUID^MAGDAIRU()
 I ONFILE D
 . N I,MSG
 . S (I,DUPCOUNT)=0 F STUDYN=1:1:STUDYCNT D
 . . I $G(^TMP("MAG",$J,"IMPORT","LIST",STUDYN,"DUP UID")) S DUPCOUNT=DUPCOUNT+1
 . . Q
 . I DUPCOUNT D
 . . S I=I+1,MSG(I)=""
 . . I DUPCOUNT=1 D
 . . . S I=I+1,MSG(I)=$$HIGHLITE^MAGDAIR0("YELLOW","     There is one study whose Study Instance UID has already been")
 . . . S I=I+1,MSG(I)=$$HIGHLITE^MAGDAIR0("YELLOW","     used to identify other studies on VistA.  It is highlighted")
 . . . S I=I+1,MSG(I)=$$HIGHLITE^MAGDAIR0("YELLOW","     in yellow below and flagged with an asterisk.")
 . . . Q
 . . E  D
 . . . S I=I+1,MSG(I)=$$HIGHLITE^MAGDAIR0("YELLOW","     There is are "_DUPCOUNT_" studies whose Study Instance UID have already been")
 . . . S I=I+1,MSG(I)=$$HIGHLITE^MAGDAIR0("YELLOW","     used to identify other studies on VistA.  They are highlighted")
 . . . S I=I+1,MSG(I)=$$HIGHLITE^MAGDAIR0("YELLOW","     in yellow below and flagged with an asterisk.")
 . . . Q
 . . Q
 . S COMPLETE=$$IMAGEUID^MAGDAIRU()
 . S PARTIAL=ONFILE-COMPLETE
 . I COMPLETE D
 . . S I=I+1,MSG(I)=""
 . . I COMPLETE=1 D
 . . . S I=I+1,MSG(I)="     There is one study that is completely stored on VistA."
 . . . S I=I+1,MSG(I)="     It is flagged with a red lowercase "
 . . . Q
 . . E  D
 . . . S I=I+1,MSG(I)="     There are "_COMPLETE_" studies that are completely stored on VistA."
 . . . S I=I+1,MSG(I)="     Each is flagged with a red lowercase "
 . . . Q
 . . S MSG(I)=MSG(I)_$$HIGHLITE^MAGDAIR0("RED","c")_" below."
 . . I MEDIA="T" D
 . . . S I=I+1,MSG(I)="     These DICOM files will be deleted automatically."
 . . . Q
 . . Q
 . I PARTIAL D
 . . S I=I+1,MSG(I)=""
 . . I PARTIAL=1 D
 . . . S I=I+1,MSG(I)="     There is one study that is partially stored on VistA."
 . . . S I=I+1,MSG(I)="     It is flagged with a yellow lowercase "
 . . . Q
 . . E  D
 . . . S I=I+1,MSG(I)="     There are "_PARTIAL_" studies that are partially stored on VistA."
 . . . S I=I+1,MSG(I)="     Each is flagged with a yellow lowercase "
 . . . Q
 . . S MSG(I)=MSG(I)_$$HIGHLITE^MAGDAIR0("YELLOW","p")_" below."
 . . Q
 . S I=I+1,MSG(I)=""
 . W ! D WARNING^MAGDFCNS(.MSG)
 . D CONTINUE^MAGDAIR0
 . Q
 E  S COMPLETE=0
 ;
 ; Automatic Import Reconciliation Workflow
 ;
 S DONE=0 F  D  Q:DONE
 . N COUNT,I,PROMPT
 . ; select studies to import
 . I $$SELECT^MAGDAIR2(STUDYCNT)<0 S DONE=-1 Q
 . ;
 . ; import the selected studies
 . F I=1:1:STUDYCNT D
 . . I $D(^TMP("MAG",$J,"IMPORT","LIST",I,"PATIENT")) S COUNT("IMPORT")=$G(COUNT("IMPORT"))+1
 . . I $D(^TMP("MAG",$J,"IMPORT","LIST",I,"DELETE")) S COUNT("DELETE")=$G(COUNT("DELETE"))+1
 . . Q
 . I $D(COUNT("IMPORT")),'$G(COUNT("DELETE")) S PROMPT="import"
 . E  I $D(COUNT("DELETE")),'$G(COUNT("IMPORT")) S PROMPT="delete"
 . E  I $D(COUNT("IMPORT")),$G(COUNT("DELETE")) S PROMPT="import and/or delete"
 . I $D(PROMPT) D  Q:DONE
 . . I $$YESNO^MAGDAIR0("Ready to "_PROMPT_" the DICOM objects?","n",.X)<0 S DONE=-1 Q
 . . I X="YES" S DONE=1 Q
 . . Q
 . E  S PROMPT="process" ; no studies were selected
 . I $$YESNO^MAGDAIR0("Exit the application and not "_PROMPT_" the DICOM objects?","n",.X)<0 S DONE=-1 Q
 . I X="YES" W "   (Leaving the Importer application.)" S DONE=2 Q
 . Q
 I DONE=1 D
 . S AUTODEL=0
 . S DUPFILE=0 F STUDYN=1:1:STUDYCNT D  ; see if there are any duplicate DICOM objects
 . . S X=$G(^TMP("MAG",$J,"IMPORT","LIST",STUDYN,"ALREADY ON FILE")) Q:X=""
 . . S DUPFILE=DUPFILE+$P(X,"^",1)
 . . Q
 . I MEDIA="T",DUPFILE S DONE=0 F  D  Q:DONE
 . . ; delete the DICOM files that have previously been imported?
 . . I COMPLETE=1 D
 . . . W !!,"There is one study on the import list that is already stored on VistA."
 . . . Q
 . . E  I COMPLETE>1 D
 . . . W !!,"There are "_COMPLETE_" studies that are already completely stored on VistA."
 . . . Q
 . . I PARTIAL=1 D
 . . . W !!,"There is one study on the import list that is partially stored on VistA."
 . . . Q
 . . E  I PARTIAL>1 D
 . . . W !!,"There are "_PARTIAL_" studies that are partially stored on VistA."
 . . . Q
 . . S X="Do you wish to delete the duplicate DICOM objects from the import list that are already stored on VistA?"
 . . I $$YESNO^MAGDAIR0(X,"y",.X)<0 S DONE=-1 Q
 . . I X="YES" S AUTODEL=1
 . . S DONE=1
 . . Q
 . D IMPORT^MAGDAIR5
 . Q
 ;
 K ^TMP("MAG",$J,"DICOM"),^("IMPORT"),^("IMPORT MEDIA")
 ;
 Q
