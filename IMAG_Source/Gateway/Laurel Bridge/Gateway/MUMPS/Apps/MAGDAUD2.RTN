MAGDAUD2 ;WOIFO/PMK - Program to write AUDIT information about VistA-PACS Transactions ; 05/04/2007 08:01
 ;;3.0;IMAGING;**10,11,30,51,54**;MAR 19, 2002;;OCT 28, 2009
 ;; Per VHA Directive 2004-038, this routine should not be modified.
 ;; +---------------------------------------------------------------+
 ;; | Property of the US Government.                                |
 ;; | No permission to copy or redistribute this software is given. |
 ;; | Use of unreleased versions of this software requires the user |
 ;; | to execute a written test agreement with the VistA Imaging    |
 ;; | Development Office of the Department of Veterans Affairs,     |
 ;; | telephone (301) 734-0100.                                     |
 ;; | The Food and Drug Administration classifies this software as  |
 ;; | a medical device.  As such, it may not be changed in any way. |
 ;; | Modifications to this software may result in an adulterated   |
 ;; | medical device under 21CFR820, the use of which is considered |
 ;; | to be a violation of US Federal Statutes.                     |
 ;; +---------------------------------------------------------------+
 ;;
 Q  ; do not call this routine directly
 ;
INCOMING ; Save information about a PACS-to-VistA input transaction
 D SAVE("IN","INCOMING",QUEUEIN("FILE"))
 Q
 ;
OUTGOING ; Save information about a VistA-to-PACS output transaction
 D SAVE("OUT","OUTGOING",QUEUEOUT("FILE"))
 Q
 ;
SAVE(INODE,FLOW,FILE) ; check for a DICOM SEND_REQUEST message file
 N COMMAND,D1,FILENUMB,HANDLE,QLETTER,REQFILE,TODAY,NOW,X,Y
 S NOW=$$NOW^XLFDT(),TODAY=NOW\1
 S HANDLE=$G(^TMP("MAG",$J,"DICOM",INODE,"FFFD,xx10,VA DHCP",1,2))
 S HANDLE=$P(HANDLE,$C(13)) ; retain only the file path
 S COMMAND=+$G(^TMP("MAG",$J,"DICOM",INODE,"0000,0100",1,1))
 I COMMAND<$$HEX2DEC^MAGOSMSC("8000") D  Q
 . ; Process a DICOM SEND_REQUEST message file
 . D COUNT ; update statistics
 . S FILENUMB=$P(FILE,"\",$L(FILE,"\"))
 . S X=$E(FILENUMB) X ^%ZOSF("UPPERCASE") S QLETTER=Y
 . S D1=$O(^MAGDICOM(2006.577,CHANNEL,1,"B",QLETTER,""))
 . S ^MAGDICOM(2006.577,CHANNEL,1,D1,0)=QLETTER_"^"_$E(FILENUMB,2,8)_"^"_NOW
 . Q
 D
 . ; Process a DICOM response message file
 . I FLOW="INCOMING" D  ; get VistA request message file
 . . S REQFILE=HANDLE ; get DOS path of the request from handle
 . . Q
 . E  D  ; get PACS SEND_REQUEST message file
 . . S REQFILE=QUEUEIN("FILE") ; input file now being processed
 . . Q
 . ;
 . ; update statistics for a DICOM response message
 . S FILENUMB=$P(REQFILE,"\",$L(REQFILE,"\"))
 . S X=$E($P(FILE,"\",$L(FILE,"\"))) X ^%ZOSF("UPPERCASE") S QLETTER=Y
 . S D1=$O(^MAGDICOM(2006.577,CHANNEL,1,"B",QLETTER,""))
 . S ^MAGDICOM(2006.577,CHANNEL,1,D1,0)=QLETTER_"^"_$E(FILENUMB,2,8)_"^"_NOW
 . Q
 Q
 ;
COUNT ; update today's count -- entry point from DIRECT FILEMODE routines
 N AUD,D2,FLAG,LOCATION,TODAY,NOW,RPCERR,X
 I $G(MESSAGE)="" Q  ; Only count valid messages
 S FLAG="M"
 S:MESSAGE="EXAM COMPLETE" FLAG="MC"
 S:MESSAGE="QUERY/RETRIEVE (C-FIND)" FLAG="MC"
 S TODAY=$$NOW^XLFDT()\1
 S LOCATION=^MAGDICOM(2006.563,1,"LOCATION")
 S RPCERR=$$CALLRPC^MAGM2VCU("MAG DICOM AUDIT COUNT",FLAG,.AUD,LOCATION,MESSAGE)
 Q
 ;
