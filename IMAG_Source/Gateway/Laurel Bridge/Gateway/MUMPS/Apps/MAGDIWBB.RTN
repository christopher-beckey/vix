MAGDIWBB ;WOIFO/EdM/PMK - Export of radiology images from VistA ; 05/04/2007 10:03
 ;;3.0;IMAGING;**30,50,54**;MAR 19, 2002;;OCT 28, 2009
 ;; Per VHA Directive 2004-038, this routine should not be modified.
 ;; +---------------------------------------------------------------+
 ;; | Property of the US Government.                                |
 ;; | No permission to copy or redistribute this software is given. |
 ;; | Use of unreleased versions of this software requires the user |
 ;; | to execute a written test agreement with the VistA Imaging    |
 ;; | Development Office of the Department of Veterans Affairs,     |
 ;; | telephone (301) 734-0100.                                     |
 ;; | The Food and Drug Administration classifies this software as  |
 ;; | a medical device.  As such, it may not be changed in any way. |
 ;; | Modifications to this software may result in an adulterated   |
 ;; | medical device under 21CFR820, the use of which is considered |
 ;; | to be a violation of US Federal Statutes.                     |
 ;; +---------------------------------------------------------------+
 ;;
 Q
 ;
PATIENT ; entry point from ^MAGDIWBA
 K ^MAGDICOM(2006.563,1,"EXPORT RADIOLOGY REPORT #")
 D PATIENT^MAGDIWBC
 Q
 ;
STORESCP ; get the DICOM storage SCP for the export
 N STORESCP,X
 S STORESCP=$G(^MAGDICOM(2006.563,1,"EXPORT DICOM STORAGE SCP"))
 I $L(STORESCP) D
 . W !!,"The DICOM storage provider is """,STORESCP,"""."
 . I $$YESNO("Do you wish to change it?","n",.X)<0 S QUIT=1 Q
 . I X="YES" D STORESC1
 . Q
 E  D STORESC1
 Q
 ;
STORESC1 ; get new value
 N OK
 S OK=0 F  D  Q:OK
 . W !!,"Please select the DICOM storage destination application entity"
 . S STORESCP=$$PICKSCP^MAGDACU(STORESCP)
 . I STORESCP="" W !!,"No Storage SCP was selected" S OK=-1 Q
 . S ^MAGDICOM(2006.563,1,"EXPORT DICOM STORAGE SCP")=STORESCP
 . S OK=1
 I OK<0 S QUIT=1
 Q
 ;
SORTORDR ; get the direction for the ^RARPT search
 N X
 S SORTORDR=$G(^MAGDICOM(2006.563,1,"EXPORT DIRECTION"))
 I $L(SORTORDR) D
 . W !!,"The radiology reports will be scanned in the """,SORTORDR,""" order."
 . I $$YESNO("Do you wish to change it?","n",.X)<0 S QUIT=1 Q
 . I X="YES" D SORTORD1
 . Q
 E  D SORTORD1
 Q
 ;
SORTORD1 ; get new value
 N OK
 S OK=0 F  D  Q:OK
 . I SORTORDR="" S SORTORDR="ASCENDING"
 . W !!,"Enter the scanning order for radiology reports ",SORTORDR," // "
 . R X:DTIME E  S OK=-1 Q
 . I X["^" S OK=-1 Q
 . I X="" S X=SORTORDR,OK=2 W X Q
 . I "Aa"[$E(X) S SORTORDR="ASCENDING",OK=1 Q
 . I "Dd"[$E(X) S SORTORDR="DESCENDING",OK=1 Q
 . W !!,"Enter either Ascending or Descending.",!
 . Q
 I OK<0 S QUIT=1 Q
 I SORTORDR'=$G(^MAGDICOM(2006.563,1,"EXPORT DIRECTION")) D
 . S ^MAGDICOM(2006.563,1,"EXPORT DIRECTION")=SORTORDR
 . W:OK=1 " -- changed"
 . Q
 Q
 ;
BEGDATE ; get the begining date for the scan
 N %DT,X,Y
 W !!,"Enter the earliest date for the report.",!
 S Y=$G(^MAGDICOM(2006.563,1,"EXPORT BEGIN DATE"))
 I Y S %DT("B")=$$FMTE^XLFDT(Y,1) ; default
 S %DT="AEPT",%DT(0)="-NOW",%DT("A")="Earliest Report Date: ",X="" D ^%DT
 I Y=-1 S QUIT=1
 E  S ^MAGDICOM(2006.563,1,"EXPORT BEGIN DATE")=Y
 Q
 ;
STOPDATE ; get the ending date for the scan
 N %DT,X,Y
 W !!,"Enter the latest date for the report.",!
 S Y=$G(^MAGDICOM(2006.563,1,"EXPORT STOP DATE"))
 I Y S %DT("B")=$$FMTE^XLFDT(Y,1) ; default
 S %DT="AEPT",%DT(0)=^MAGDICOM(2006.563,1,"EXPORT BEGIN DATE")
 S %DT("A")="Latest Report Date: ",X="" D ^%DT
 I Y=-1 S QUIT=1
 E  D
 . I Y'[".",$E(Y,4,5),$E(Y,6,7) S Y=Y+.2359 ; end of day
 . S ^MAGDICOM(2006.563,1,"EXPORT STOP DATE")=Y
 . Q
 Q
 ;
RARPT1 ; get the radiology report ien
 N RARPT1,X
 S RARPT1=$G(^MAGDICOM(2006.563,1,"EXPORT RADIOLOGY REPORT #"))
 I RARPT1 D
 . W !!,"Scanning will start with radiology report # """,RARPT1,"""."
 . I $$YESNO("Do you wish to change it?","n",.X)<0 S QUIT=1 Q
 . I X="YES" S RARPT1=+RARPT1 D RARPT1A
 . Q
 E  D RARPT1A
 Q
 ;
RARPT1A ; get new value
 N BEGPTR ; first possible ^RARPT pointer value
 N NEWPTR ; selected next possible ^RARPT pointer value
 N ENDPTR ; last possible ^RARPT pointer value
 N ACCNUMB ; accession number (long case number)
 N DATE ; radiology report date
 N DEFAULT,OK,X,Z
 ;
 S Z=$$CALLRPC^MAGM2VCU("MAG DICOM GET RAD RPT INFO","MT",.BEGPTR,"O1",0,1)
 D RARPT1B(BEGPTR,.ACCNUMB,.DATE)
 W !!,"The first radiology report is #",BEGPTR," (",ACCNUMB,") entered on ",DATE,"."
 ;
 S Z=$$CALLRPC^MAGM2VCU("MAG DICOM GET RAD RPT INFO","MT",.ENDPTR,"O1",99999999999,-1)
 D RARPT1B(ENDPTR,.ACCNUMB,.DATE)
 W !,"The last radiology report is #",ENDPTR," (",ACCNUMB,") entered on ",DATE,"."
 ;
 S OK=0 F  D  Q:OK
 . S DEFAULT=$S(RARPT1:RARPT1,SORTORDR="ASCENDING":BEGPTR,SORTORDR="DESCENDING":ENDPTR)
 . W !!,"Enter the new value of the radiology report #: ",DEFAULT," // "
 . R X:DTIME E  S OK=-1 Q
 . I X["^" S OK=-1 Q
 . I X="" S X=DEFAULT W X
 . I (X>ENDPTR)!(X<BEGPTR) D  Q
 . . W !!,"Please enter a number between ",BEGPTR," and ",ENDPTR,"."
 . . Q
 . S NEWPTR=X
 . D RARPT1B(NEWPTR,.ACCNUMB,.DATE)
 . W !!,"Radiology report #",NEWPTR," (",ACCNUMB,") entered on ",DATE,"."
 . I $$YESNO("Is this where to begin scanning?","n",.X)<0 S OK=-1 Q
 . S:X="YES" OK=$S(NEWPTR=DEFAULT:2,1:1)
 . Q
 I OK<0 S QUIT=1 Q
 I NEWPTR'=RARPT1 D
 . S ^MAGDICOM(2006.563,1,"EXPORT RADIOLOGY REPORT #")=NEWPTR_" ("_DATE_")"
 . W:OK=1 " -- changed"
 . Q
 Q
 ;
RARPT1B(PTR,ACCNUMB,DATE) ; get accession number and date from ^RARPT(POINTER,0)
 N RARPT0,Y,Z
 S Z=$$CALLRPC^MAGM2VCU("MAG DICOM GET RAD RPT INFO","MT",.RARPT0,"G1",PTR)
 S ACCNUMB=$P(RARPT0,"^",1)
 S DATE=$P($$FMTE^XLFDT($P(RARPT0,"^",3),1),"@",1)
 Q
 ;
BATCHSIZ ; get the size of the batch of studies to be exported in one run
 N BATCHSIZ,X
 S BATCHSIZ=$G(^MAGDICOM(2006.563,1,"EXPORT BATCH SIZE"))
 I BATCHSIZ D
 . W !!,"This run will try to export images for ",BATCHSIZ," studies."
 . W !,"(Studies without images will not be included in this count.)"
 . I $$YESNO("Do you wish to change this count?","n",.X)<0 S QUIT=1 Q
 . I X="YES" D BATCHSZ1
 . Q
 E  D BATCHSZ1
 Q
 ;
BATCHSZ1 ; get the batch size for the export run
 N DEFAULT,OK,X
 S DEFAULT=$S(BATCHSIZ:BATCHSIZ,1:100)
 S OK=0 F  D  Q:OK
 . W !!,"Enter the new value of the batch size: ",DEFAULT," // "
 . R X:DTIME E  S OK=-1 Q
 . I X["^" S OK=-1 Q
 . I X="" S X=DEFAULT W X
 . I X?1.N S OK=$S(X=DEFAULT:2,1:1) Q
 . W "  ??? (enter a a positive integer number)"
 . Q
 I OK<0 S QUIT=1 Q
 D:BATCHSIZ'=X
 . S ^MAGDICOM(2006.563,1,"EXPORT BATCH SIZE")=X
 . W:X=1 " -- changed"
 . Q
 Q
 ;
HOURS ; get hours of operation
 N HOURS,X
 S HOURS=$G(^MAGDICOM(2006.563,1,"EXPORT HOURS OF OPERATION"))
 I HOURS'="" D
 . W !!,"The active hours of operation are indicated below with a ""Y"""
 . W !?18,"M12345678901N12345678901 (M=midnight, N=noon)"
 . W !,"Active hours are: ",HOURS
 . I $$YESNO("Do you wish to change these hours?","n",.X)<0 S QUIT=1 Q
 . I X="YES" D HOURS1
 . Q
 E  D HOURS1
 Q
 ;
HOURS1 ; initialize/modify the hours of operations
 N DEFAULT,FULLDAY,I,OK,X
 S FULLDAY=$TR($J("",24)," ","Y") ; 24 hours
 S DEFAULT=$S($L(HOURS):HOURS,1:FULLDAY)
 S DEFAULT=$E(DEFAULT_"NNNNNNNNNNNNNNNNNNNNNNNN",1,24)
 S OK=0 F  D  Q:OK
 . W !?18,"M12345678901N12345678901 (M=midnight, N=noon)"
 . W !,"Active hours are: ",DEFAULT
 . W !?15,"// " R X:DTIME E  S OK=-1 Q
 . I X["^" S OK=-1 Q
 . I X="" S X=DEFAULT W X
 . S X=$TR(X,"yn","YN")
 . I $TR(X,"YN")="" D
 . . S X=$E(X_FULLDAY,1,24)
 . . S OK=1
 . . S ^MAGDICOM(2006.563,1,"EXPORT HOURS OF OPERATION")=X
 . . W " -- changed"
 . . Q
 . E  D
 . . W !,?18 F I=1:1:24 S Z=$E(X,I) W $S("YN"'[Z:"^",1:" ")
 . . W !!,"Enter a sequence of (up to) 24 ""Y's"" and ""N's""."
 . . W !,"Every ""Y"" represents an hour when DICOM Export will be active."
 . . W !,"Every ""N"" represents an hour when DICOM Export will not be active.",!
 . . Q
 . Q
 I OK=-1 S QUIT=1
 Q
 ;
YESNO(PROMPT,DEFAULT,CHOICE) ; generic YES/NO question driver
 N OK,X
 S OK=0 F  D  Q:OK
 . W !!,PROMPT," " I $L($G(DEFAULT)) W DEFAULT," // "
 . R X:DTIME E  S OK=-1 Q
 . I X="",$L($G(DEFAULT)) S X=DEFAULT W X
 . I X="",'$L($G(DEFAULT)) S X="*" ; fails Y/N tests
 . I X["^" S CHOICE="^",OK=-1 Q
 . I "Yy"[$E(X) S CHOICE="YES",OK=1 Q
 . I "Nn"[$E(X) S CHOICE="NO",OK=1 Q
 . W "   ??? - Please enter ""Yes"" or ""No""."
 . Q
 Q OK
 ;
