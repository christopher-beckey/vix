Cache for Windows NT^INT^Saved from DICOM,ROU (ISW-KUZMAKP2)^~Format=Cache.S~
%RO on 03/22/2012  09:21
MAGDIW4^INT^1^62536,53318.598616^0
MAGDIW4 ;WOIFO/PMK - Reconstruct a DICOM file from a .txt and a .tga file ; 20 Mar 2012 2:48 PM
 ;;3.0;IMAGING;**10,51,50,54**;MAR 19, 2002;;OCT 28, 2009
 ;; Per VHA Directive 2004-038, this routine should not be modified.
 ;; +---------------------------------------------------------------+
 ;; | Property of the US Government.                                |
 ;; | No permission to copy or redistribute this software is given. |
 ;; | Use of unreleased versions of this software requires the user |
 ;; | to execute a written test agreement with the VistA Imaging    |
 ;; | Development Office of the Department of Veterans Affairs,     |
 ;; | telephone (301) 734-0100.                                     |
 ;; | The Food and Drug Administration classifies this software as  |
 ;; | a medical device.  As such, it may not be changed in any way. |
 ;; | Modifications to this software may result in an adulterated   |
 ;; | medical device under 21CFR820, the use of which is considered |
 ;; | to be a violation of US Federal Statutes.                     |
 ;; +---------------------------------------------------------------+
 ;;
 Q
 ;
ENTRY ; create the image header and concatenate the TGA image to it
 N BYTEORDR ;- byte order for message, always little endian
 D HEADER
 D CONCAT
 Q
 ;
HEADER ; use the data in ^TMP to create a *.dcm file
 ; create the image dataset (text portion)
 ;
 N CHANNEL ;-- number "n" of the "\DICOM\DATAn" message directory
 N DEBUG ;---- debug output control
 N D0
 N DIMSE ;---- DICOM Message Service Element
 N EXPLICIT ;- explicit/implicit VR flag
 N FBSWITCH ;- foreground / midground / background flag for ^MAGDMLOG
 N FILEMODE ;- either "FIFO QUEUE" (for PACS) or "DIRECT" (for MWL)
 N INODE ;---- node in ^TMP global for DICOM data storage
 N MESSAGE ;-- name of type of message being generated
 N MSGHANDL ;- pointer in DICOM message to HL7 message number
 N MSGID ;---- DICOM message id
 N MULTIMSG ;- multiple message flag (null, "OPEN", or "CLOSE")
 N QUEUEOUT ;- output file array
 N SOPCLASS ;- SOP Class of message being created and sent
 N TEMPLATE ;- defined differently in ^MAGDDW2
 N TYPEID ;--- id of type of event within SOP class
 ;
 S FBSWITCH="FOREGROUND",CHANNEL=1,MULTIMSG=""
 S FILEMODE="IMAGE DATASET" ; used in ^MAGDDW2
 S EXPLICIT=0 ; implicit VR - used in ^MAGDDW2
 S MESSAGE="C-STORE REQUEST"
 S MSGHANDL="NO MESSAGE HANDLE YET"
 S QUEUEOUT("FILE")=FILEDCM ; create the image dataset
 D OUTPUT^MAGDDW2
 ;
 Q
 ;
CONCAT ; concatenate the tga image to the end of the header
 N CMD,ERROR,VERBOSE
 S CMD="MAG_TGATODCM "_FILETGA_" "_FILEDCM_" "_IMAGESIZ
 S VERBOSE=0 D DOS^MAGDOS(CMD,.ERROR)
 I ERROR(0) D  ; fatal error
 . N I,MSG
 . S MSG(1)="DICOM IMAGE TARGA RECONSITUTION CREATION ERROR"
 . S MSG(2)="Unexpected error in """_CMD_""""
 . F I=1:1:ERROR(0) S MSG(I+2)=$J(I,3)_") "_ERROR(I)
 . D TRAPERR^MAGDSTRT(1,$T(+0),"DICOM IMAGE PROCESSING ERROR",.MSG)
 . Q  ; Error trap does not return
 ;
 I IMAGESIZ#2 D  ; DICOM requires an even number of bytes
 . N PIXDATAL,X
 . S X=$$OPEN^MAGOSFIL(FILEDCM,"M") ; open for read/write
 . S X=$$USE^MAGOSFIL(FILEDCM)
 . S X=$$SEEK^MAGOSFIL(FILEDCM,-(IMAGESIZ+4),2) ; position to pixel data length
 . S PIXDATAL=$$GETULONG^MAGDDR0 ; read pixel data size
 . I PIXDATAL'=IMAGESIZ D  ; fatal error
 . . N MSG
 . . S MSG(1)="DICOM IMAGE TARGA RECONSITUTION CREATION ERROR"
 . . S MSG(2)="Unexpected error in processing a file with an odd-number of pixel bytes"
 . . S MSG(3)="Reconstituted DICOM file has "_PIXDATAL_" bytes"
 . . S MSG(4)="TGA image file has "_IMAGESIZ_" bytes - these should be the same"
 . . D TRAPERR^MAGDSTRT(1,$T(+0),"DICOM IMAGE PROCESSING ERROR",.MSG)
 . . Q  ; Error trap does not return
 . S X=$$SEEK^MAGOSFIL(FILEDCM,-4,1)  ; position to pixel data length
 . D PUTULONG^MAGDDW0(IMAGESIZ+1) ; make pixel data length an even value
 . S X=$$SEEK^MAGOSFIL(FILEDCM,0,2) ; position to end of file
 . D PUTBYTE^MAGDDW0(0) ; output a trailer byte
 . S X=$$CLOSE^MAGOSFIL(FILEDCM)
 . S X=$$USEPRIN^MAGOSFIL(1) ; use the principal device
 . Q
 Q
 ;



