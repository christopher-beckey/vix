MAGDMMSG ;WOIFO/EdM/RRB - Send an e-mail for an error ; 19 Dec 2008 3:12 PM
 ;;3.0;IMAGING;**21,11,30,51,50,54**;MAR 19, 2002;;OCT 28, 2009
 ;; Per VHA Directive 2004-038, this routine should not be modified.
 ;; +---------------------------------------------------------------+
 ;; | Property of the US Government.                                |
 ;; | No permission to copy or redistribute this software is given. |
 ;; | Use of unreleased versions of this software requires the user |
 ;; | to execute a written test agreement with the VistA Imaging    |
 ;; | Development Office of the Department of Veterans Affairs,     |
 ;; | telephone (301) 734-0100.                                     |
 ;; | The Food and Drug Administration classifies this software as  |
 ;; | a medical device.  As such, it may not be changed in any way. |
 ;; | Modifications to this software may result in an adulterated   |
 ;; | medical device under 21CFR820, the use of which is considered |
 ;; | to be a violation of US Federal Statutes.                     |
 ;; +---------------------------------------------------------------+
 ;;
 Q
 ; The following entry is intended to be called to transmit
 ; error messages by e-mail.
 ; The main caller is TRAPERR^MAGDSTRT.
 ; RECONCT^MAGDMENA may also call this subroutine.
 ; Expected variables:
 ;   FATAL    - switch that tells MAGDSTRT how to "return"
 ;   ROU      - name of routine that invoked the error trap
 ;   HDR      - header of mail message
 ;   MSG()    - text for mail message
 ;   IGNORE   - switch: false = call ^%ET
 ;   CRITICAL - switch: true = send to Silver Spring
 ;
SEND N D0,D1,I,SENDOK,X
 S:$G(HDR)="" HDR="?"
 ; The purpose of the lock on "RETRY" is to make sure that
 ; MAGDBB will not attempt a "retry" while we're making
 ; the first transmission attempt.
 L +(^MAGDICOM(2006.598,0),^MAGDICOM(2006.598,"RETRY")):1E9 ; Non-interactive process must wait
 S X=$G(^MAGDICOM(2006.598,0))
 S $P(X,"^",1,2)="DICOM ERROR MESSAGE QUEUE^2006.598"
 S D0=$O(^MAGDICOM(2006.598," "),-1)+1,$P(X,"^",3)=D0
 S $P(X,"^",4)=$P(X,"^",4)+1
 S ^MAGDICOM(2006.598,0)=X
 S ^MAGDICOM(2006.598,D0,0)=HDR
 S ^MAGDICOM(2006.598,"B",HDR,D0)=""
 L -^MAGDICOM(2006.598,0)
 S ^MAGDICOM(2006.598,D0,1)=$G(ROU)_"^"_$G(IGNORE)_"^"_$G(CRITICAL)_"^"_$H
 S D1=0
 S:$G(MSG)'="" D1=D1+1,^MAGDICOM(2006.598,D0,2,D1,0)=MSG
 S I="" F  S I=$O(MSG(I)) Q:I=""  S D1=D1+1,^MAGDICOM(2006.598,D0,2,D1,0)=MSG(I)
 S D1=D1+1,^MAGDICOM(2006.598,D0,2,D1,0)=" "
 S D1=D1+1,^MAGDICOM(2006.598,D0,2,D1,0)="Message generated at "_$$NOW()
 S D1=D1+1,^MAGDICOM(2006.598,D0,2,D1,0)=" "
 S SENDOK=0 D XMIT(D0)
 D:SENDOK
 . L +^MAGDICOM(2006.598,0):1E9 ; Non-interactive process must wait
 . S $P(^MAGDICOM(2006.598,0),"^",4)=$P(^MAGDICOM(2006.598,0),"^",4)-1
 . K ^MAGDICOM(2006.598,D0),^MAGDICOM(2006.598,"B",HDR,D0)
 . L -^MAGDICOM(2006.598,0)
 . Q
 L -^MAGDICOM(2006.598,"RETRY")
 Q
 ;
LOGSTS(ACTION) N D1
 S D1=$O(^MAGDICOM(2006.598,D0,3," "),-1)+1
 S ^MAGDICOM(2006.598,D0,3,D1,0)="Could not "_ACTION_" post-office at "_OFF_", status is "_STS
 Q
 ;
TRAP ;
 D:$G(OFF)'=""
 . S STS=$$TCPCLOSE^MAGOSTCP(OFF,$G(POSTPORT,25)),OFF=""
 . I $$USEPRIN^MAGOSFIL(1)
 . Q
 D:$G(D0)
 . S D1=$O(^MAGDICOM(2006.598,D0,3," "),-1)+1
 . S ^MAGDICOM(2006.598,D0,3,D1,0)=$EC,D0=0
 . Q
 S:$ST<$G(TRAPLVL) $EC=""
 Q
 ;
READ() N DTIME,R,TO
 S DTIME=1000,R="",TO=0 F  D  Q:R'=""  Q:TO
 . R R:DTIME S DTIME=1,TO='$T
 . Q
 Q R
 ;
XMIT(D0) N EMAIL,H,NL,I,OFF,POSTPORT,R,SID,SSEMAIL,STS,T,Z
 N $ET,TRAPLVL
 S $ET="D TRAP^"_$T(+0),TRAPLVL=$ST+1
 S SSEMAIL="VHAISWIMCS@MED.VA.GOV" ; email for customer support
 S HDR=$G(^MAGDICOM(2006.598,D0,0))
 S X=$G(^MAGDICOM(2006.598,D0,1))
 S ROU=$P(X,"^",1),IGNORE=$P(X,"^",2),CRITICAL=$P(X,"^",3)
 S NL=$C(13,10),V="",H=$H
 S SID=$G(^MAGDICOM(2006.563,1,"DOMAIN")) S:SID="" SID="unknown"
 S SID=$$UPPER^MAGOSMSC(SID) S:SID'[".VA.GOV" SID=SID_".VA.GOV"
 S OFF=$G(^MAGDICOM(2006.563,1,"POST OFFICE")) S:OFF="" OFF="10.2.29.131"
 S POSTPORT=$G(^MAGDICOM(2006.563,1,"POST PORT")) S:'POSTPORT POSTPORT=25
 S Z=$$TIMEZONE^MAGDMSGT() ; Get TIMEZONE offset
 I '$G(Z) S Z="-0500" ; Default TIMEZONE offset
 D
 . ; This block is indented, so that error trapping
 . ; will return control to the right DO level if
 . ; an additional problem occurs.
 . ;
 . S EMAIL=$G(^MAGDICOM(2006.563,1,"MAILGROUP"))
 . S NOMAILGP=0 I EMAIL="" D  ; use Silver Spring as the default
 . . S NOMAILGP=1 ; this is for a reminder at the end of the message
 . . S EMAIL=SSEMAIL
 . . Q
 . ;
 . S STS=$$TCPCLOSE^MAGOSTCP(OFF,POSTPORT)
 . S STS=$$TCPOPEN^MAGOSTCP(OFF,POSTPORT,1) I STS<0 D LOGSTS("Open") Q
 . S STS=$$TCPUSE^MAGOSTCP(OFF,POSTPORT) I STS<0 D LOGSTS("Use") Q
 . ; Expected result: 220 <mailserver> <version> ready
 . S R=$$READ() S:+R'=220 V=V_";1:"_R
 . W "HELO med.va.gov",NL S STS=$$TCPFLUSH^MAGOSTCP(OFF,POSTPORT)
 . ; Expected result: 250 OK
 . S R=$$READ() S:+R'=250 V=V_";2:"_R
 . W "MAIL FROM:<Postmaster@"_SID_">"_NL S STS=$$TCPFLUSH^MAGOSTCP(OFF,POSTPORT)
 . ; Expected result: 250 OK
 . S R=$$READ() S:+R'=250 V=V_";3:"_R
 . I EMAIL'="" D  ; ignore null TO: email addresses
 . . W "RCPT TO:<"_EMAIL_">"_NL
 . . S STS=$$TCPFLUSH^MAGOSTCP(OFF,POSTPORT)
 . . ; Expected result: 250 OK
 . . S R=$$READ() S:+R'=250 V=V_";4:"_R_"="_EMAIL
 . . Q
 . I $G(CRITICAL) D  ; critical message -- send to Silver Spring
 . . W "RCPT TO:<"_SSEMAIL_">"_NL
 . . S STS=$$TCPFLUSH^MAGOSTCP(OFF,POSTPORT)
 . . ; Expected result: 250 OK
 . . S R=$$READ() S:+R'=250 V=V_";5:"_R_"="_SSEMAIL
 . . Q
 . W "DATA",NL S STS=$$TCPFLUSH^MAGOSTCP(OFF,POSTPORT)
 . ; Expected result: 354 Send data.  End with CRLF.CRLF
 . S R=$$READ() S:+R'=354 V=V_";6:"_R
 . W "Subject: Fatal Error: ",$G(HDR),NL
 . D
 . . N D,M,W,Y
 . . W "Date: " D H2YMD^MAGDBB(+H,.Y,.M,.D,.W)
 . . W W,", ",D," ",$P("Jan Feb Mar Apr May Jun Jul Aug Sep Oct Nov Dec"," ",M)
 . . W " ",Y S Y=$P(H,",",2)
 . . W " ",$TR($J(Y\3600,2)," ",0),":",$TR($J(Y\60#60,2)," ",0)," ",Z
 . . Q
 . W $C(13,10,13,10)
 . ;
 . I $G(HDR)'="" W HDR,NL
 . S I=0 F  S I=$O(^MAGDICOM(2006.598,D0,2,I)) Q:'I  W $G(^MAGDICOM(2006.598,D0,2,I,0)),NL
 . W " "_NL
 . W "Routine: "_$S($G(ROU)'="":ROU,1:$TR($ST($ST-1,"PLACE"),":")),NL
 . ;
 . S X=$G(^MAGDICOM(2006.563,1,"SYSTEM TITLE"))_" ("_$$HOSTNAME^MAGOSMSC()_")"
 . W "DICOM Gateway """_X_"""."_NL
 . W " "_NL
 . W "Message transmitted at "_$$NOW()
 . W " "_NL
 . ;
 . I NOMAILGP W ">>> SITE NEEDS TO SETUP THE LOCAL MAIL GROUP <<<",NL
 . ;
 . W NL,".",NL S STS=$$TCPFLUSH^MAGOSTCP(OFF,POSTPORT)
 . ; Expected result: 250 OK
 . S R=$$READ() S:+R'=250 V=V_";7:"_R
 . W "QUIT",NL S STS=$$TCPFLUSH^MAGOSTCP(OFF,POSTPORT)
 . ; Expected result: 221 OK
 . S R=$$READ() S:+R'=221 V=V_";8:"_R
 . S STS=$$TCPCLOSE^MAGOSTCP(OFF,POSTPORT)
 . I $$USEPRIN^MAGOSFIL(1)
 . S SENDOK=1
 . Q
 Q
 ;
RETRY N D0,HDR,SENDOK,X
 S D0=0 F  S D0=$O(^MAGDICOM(2006.598,D0)) Q:'D0  D
 . S HDR=$G(^MAGDICOM(2006.598,D0,0)) S:HDR="" HDR=" "
 . S X=$$H($P($G(^MAGDICOM(2006.598,D0,1)),"^",4)) Q:'X
 . Q:$$H($H)-X<3600
 . S SENDOK=0 D XMIT(D0)
 . I 'SENDOK S $P(^MAGDICOM(2006.598,D0,1),"^",4)=$H Q
 . L +^MAGDICOM(2006.598,0):1E9 ; Non-interactive process must wait
 . S $P(^MAGDICOM(2006.598,0),"^",4)=$P(^MAGDICOM(2006.598,0),"^",4)-1
 . K ^MAGDICOM(2006.598,D0),^MAGDICOM(2006.598,"B",HDR,D0)
 . L -^MAGDICOM(2006.598,0)
 . Q
 Q
 ;
H(H) Q H*86400+$P(H,",",2)
 ;
NOW() N D,H,M,S,X,Y
 S X=$$NOW^XLFDT(),H=X*1E6#1E6,X=X\1
 S Y=X\10000+1700,M=X\100#100,D=X#100
 S X=D_"-"_$P("Jan Feb Mar Apr May Jun Jul Aug Sep Oct Nov Dec"," ",M)_"-"_Y
 Q:'H X
 S S=H#100,M=H\100#100,H=H\10000 S:H<10 H=0_H S:M<10 M=0_M S:S<10 S=0_S
 S X=X_", "_H_":"_M_":"_S
 Q X
 ;
