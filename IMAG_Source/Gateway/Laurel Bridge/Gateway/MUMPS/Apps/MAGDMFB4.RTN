MAGDMFB4 ;WOIFO/PMK - DICOM Master File Macro Expander ; 12/27/2005 08:26
 ;;3.0;IMAGING;**30,69**;MAR 19, 2002;;NOV 7, 2007
 ;; Per VHA Directive 2004-038, this routine should not be modified.
 ;; +---------------------------------------------------------------+
 ;; | Property of the US Government.                                |
 ;; | No permission to copy or redistribute this software is given. |
 ;; | Use of unreleased versions of this software requires the user |
 ;; | to execute a written test agreement with the VistA Imaging    |
 ;; | Development Office of the Department of Veterans Affairs,     |
 ;; | telephone (301) 734-0100.                                     |
 ;; | The Food and Drug Administration classifies this software as  |
 ;; | a medical device.  As such, it may not be changed in any way. |
 ;; | Modifications to this software may result in an adulterated   |
 ;; | medical device under 21CFR820, the use of which is considered |
 ;; | to be a violation of US Federal Statutes.                     |
 ;; +---------------------------------------------------------------+
 ;;
 Q
 ; Macros begin with a {$define <macro name>} statement and are
 ; terminated with an {$end <macro name>} statement.
 ;
 ; A macro is invoked as follows:
 ;         xxx{<macro name>}yyy
 ; Everything to the left and right of the {<macro name>} are
 ; copied to every line of the expansion.
 ;
 ; Macro expansions may be nested.
 ;
ENTRY(FILE) ; entry point from ^MAGDMFB3
 N INFILE,MACNAME,MACRO,PATH,OUTFILE
 S FATAL=+$G(FATAL)
 S INFILE=$G(^MAGDICOM(2006.563,1,"DICT PATH"))_"\"_FILE_".DIC"
 S OUTFILE=$G(^MAGDICOM(2006.563,1,"DICT PATH"))_"\"_FILE_".TMP"
 S STATUS=$$OPEN^MAGOSFIL(INFILE,"R")
 I STATUS<0 D  Q
 . D MESSAGE^MAGDMLOG(1,0,"Open Error on "_INFILE_": "_STATUS,"t")
 . S FATAL=FATAL+1
 . Q
 S STATUS=$$OPEN^MAGOSFIL(OUTFILE,"W")
 I STATUS<0 D  Q
 . D MESSAGE^MAGDMLOG(1,0,"Open Error on "_OUTFILE_": "_STATUS,"t")
 . S FATAL=FATAL+1
 . Q
 ;
 S MACRO=""
 K ^TMP("MAG",$J,"DICOM","MACRO")
 F  D  Q:STATUS'=""
 . S STATUS=$$READ^MAGOSFIL(INFILE,.X)
 . I $$USEPRIN^MAGOSFIL(1)
 . D:'$G(SILENT) MESSAGE^MAGDMLOG(1,0,X,"t")
 . I STATUS<0 Q:+STATUS=-2  D  Q
 . . D MESSAGE^MAGDMLOG(1,0,"Read Error: "_STATUS,"t")
 . . S FATAL=FATAL+1
 . . Q
 . I MACRO="" D  ; not defining or expanding a macro
 . . I X?1"{$define".E D  Q  ; starting a macro definition
 . . . S MACNAME=$P(X,"{$define ",2),$E(MACNAME,$L(MACNAME))=""
 . . . I $D(^TMP("MAG",$J,"DICOM","MACRO",MACNAME)) D  Q
 . . . . D MESSAGE^MAGDMLOG(1,0,"Error: "_MACNAME_" already defined","t")
 . . . . S FATAL=FATAL+1
 . . . . I FBSWITCH="FOREGROUND" R Z:DTIME
 . . . . Q
 . . . S ^TMP("MAG",$J,"DICOM","MACRO",MACNAME,0)=0
 . . . S MACRO="D"
 . . . Q
 . . D PROCESS(X) ; process the line X -- may come from file or global
 . . Q
 . E  I MACRO="D" D  ; currently defining a macro
 . . I X?1"{$end".E S MACRO="" ; end of macro definition
 . . E  S ^(0)=^TMP("MAG",$J,"DICOM","MACRO",MACNAME,0)+1,^(^(0))=X
 . . Q
 . Q
 S STATUS=$$CLOSE^MAGOSFIL(INFILE)
 S STATUS=$$CLOSE^MAGOSFIL(OUTFILE)
 Q
 ;
PROCESS(X) ;
 N LEFT,RIGHT,MACNAME,I,Y
 I X["{" D  Q  ; starting a new macro expansion
 . S LEFT=$P(X,"{"),RIGHT=$P(X,"}",2),MACNAME=$P($P(X,"{",2),"}")
 . I '$D(^TMP("MAG",$J,"DICOM","MACRO",MACNAME)) D  Q
 . . D MESSAGE^MAGDMLOG(1,0,"Error: "_MACNAME_" not defined","t")
 . . S FATAL=FATAL+1
 . . I FBSWITCH="FOREGROUND" R Z:DTIME
 . . Q
 . F I=1:1:^TMP("MAG",$J,"DICOM","MACRO",MACNAME,0) D  ; output each line
 . . S Y=^TMP("MAG",$J,"DICOM","MACRO",MACNAME,I)
 . . D PROCESS(LEFT_Y_RIGHT) ; recursion
 . . Q
 . Q
 S Y=$$USE^MAGOSFIL(OUTFILE) W X,!
 I $$USEPRIN^MAGOSFIL(1)
 Q
