MAGDSTAT ;WOIFO/PMK - Program to output color of VistA-PACS Interface ; 02 Apr 2009 12:51 PM
 ;;3.0;IMAGING;**10,11,30,69,54**;MAR 19, 2002;;OCT 28, 2009
 ;; Per VHA Directive 2004-038, this routine should not be modified.
 ;; +---------------------------------------------------------------+
 ;; | Property of the US Government.                                |
 ;; | No permission to copy or redistribute this software is given. |
 ;; | Use of unreleased versions of this software requires the user |
 ;; | to execute a written test agreement with the VistA Imaging    |
 ;; | Development Office of the Department of Veterans Affairs,     |
 ;; | telephone (301) 734-0100.                                     |
 ;; | The Food and Drug Administration classifies this software as  |
 ;; | a medical device.  As such, it may not be changed in any way. |
 ;; | Modifications to this software may result in an adulterated   |
 ;; | medical device under 21CFR820, the use of which is considered |
 ;; | to be a violation of US Federal Statutes.                     |
 ;; +---------------------------------------------------------------+
 ;;
 N DIFF,FILENUMB,GATEWAY,HOST,PORTIEN,TAB,TITLE1,TITLE2
 S PROMPT="Real-Time DICOM Communications Statistics"
 I $G(^MAGDICOM(2006.563,1,"SEND PACS TEXT"))="YES" D  ; PACS messages
 . S PORTIEN=$$SELECT^MAGDTCP(PROMPT)
 . I 'PORTIEN D  Q  ; nothing selected
 . . S CHANNEL=0,(TITLE1,TITLE2)="<none>" ; worklist only
 . . Q
 . E  D  Q:ERR  ; get parameters for the selected communication channel
 . . S X=^MAGDICOM(2006.584,PORTIEN,0)
 . . S CHANNEL=$P(X,"^",5),ERR=0
 . . S X=$P(X,"^"),TITLE1=$P(X," "),TITLE2=$P(X," ",2)
 . . ;
 . . I '$D(^MAGDICOM(2006.563,1,"DATA PATH",CHANNEL,0)) D  Q
 . . . W !,"*** Undefined Communications Channel: ",CHANNEL," ***"
 . . . S ERR=1
 . . . Q
 . . Q
 . Q
 E  I $G(^MAGDICOM(2006.563,1,"MODALITY WORKLIST"))="YES" D  ; worklist
 . S (CHANNEL,PORTIEN)=0,(TITLE1,TITLE2)="<none>" ; worklist only
 . Q
 E  D  Q
 . N M
 . S M(1)="This system is incorrectly configured as a DICOM Text Gateway."
 . S M(2)="It can neither send DICOM text messages to a commercial PACS or Broker,"
 . S M(3)="nor can it function as a Modality Worklist Provider."
 . S M(4)=""
 . S M(5)="Please check the system's configuration."
 . D WARNING^MAGDFCNS(.M)
 . Q
 ;
 S ^TMP("MAG",$J,"RESTARTSTAT")=PORTIEN_"^"_CHANNEL_"^"_TITLE1_"^"_TITLE2
 ;
 D  ; This DO level is needed for the error trapping in MAGDSTRT to work
 . N $ES,$ET
 . S ENTRY="REENTER^"_$T(+0),$ET="D ERROR^MAGDSTRT"
 . D REENTER
 . Q
 Q
 ;
REENTER ; Return here after DDP failure (and re-establishment)
 N COLOR,DISKFREE,LOCATION
 S X=$G(^TMP("MAG",$J,"RESTARTSTAT")) ; get saved parameters
 S PORTIEN=$P(X,"^",1),CHANNEL=$P(X,"^",2)
 S TITLE1=$P(X,"^",3),TITLE2=$P(X,"^",4)
 S IOF="#,$C(27,91,72,27,91,74,8,8,8,8)"
 S COLOR=1
 S COLOR(1)=$C(27)_"[1;44m" ; blue
 S COLOR(2)=$C(27)_"[1;45m" ; magenta
 S COLOR(3)=$C(27)_"[1;43m" ; yellow
 S COLOR(4)=$C(27)_"[1;41m" ; red
 S HALT=0 F  D  Q:HALT
 . ; output the screen
 . W !,COLOR(COLOR),@IOF S COLOR=1
 . I CHANNEL D  ; output statistics, if there is a channel
 . . ; output the statistics for one channel
 . . W $TR($J("",80)," ","*")
 . . W !,"*",?7-($L(TITLE1)\2),TITLE1
 . . W ?16,"Priority",?30,"PACS",?43,"PACS",?54,"Hospital",?67,"Hospital",?79,"*"
 . . W !,"*",?7-($L(TITLE2)\2),TITLE2
 . . W ?18,"Level",?28,"Request",?41,"Response",?54,"Request",?67,"Response",?79,"*"
 . . W !,"*",?3,"HL7 Delay",?18,"Name",?29,"Queue",?42,"Queue",?55,"Queue",?68,"Queue",?79,"*"
 . . W !,"*",?79,"*"
 . . ;
 . . F PRIORITY="HIGH","MEDIUM","LOW" D
 . . . W !,"*"
 . . . I PRIORITY="LOW" D  ; display free disk space
 . . . . N DRIVE,FREE,PCT,TOTAL
 . . . . S DRIVE=$$DRIVE($G(^MAGDICOM(2006.563,1,"DATA PATH",1,0)))
 . . . . D DISK1(DRIVE)
 . . . . S DISKFREE=PCT\1
 . . . . W ?3,"Space: ",DISKFREE,"%"
 . . . . Q
 . . . I PRIORITY="HIGH" D  ; compute HL7 queue delay
 . . . . D HL7QUEUE S DIFF=HOST-GATEWAY
 . . . . I 'DIFF W ?5,"<none>"
 . . . . E  W ?8-($L(DIFF)/2),DIFF
 . . . . Q
 . . . W ?17,PRIORITY
 . . . S TAB=26 W ?TAB
 . . . D QUEUE("INCOMING","REQUEST")
 . . . D QUEUE("OUTGOING","RESPONSE")
 . . . D QUEUE("OUTGOING","REQUEST")
 . . . D QUEUE("INCOMING","RESPONSE")
 . . . W ?79,"*"
 . . . I PRIORITY'="LOW" ; W !,"*",?79,"*"
 . . . S TAB=15
 . . . Q
 . . W !,$TR($J("",80)," ","*")
 . . ;
 . . ; check differences in queues
 . . F PRIORITY="HIGH","MEDIUM","LOW","HL7" D  ;
 . . . I PRIORITY="HL7" S DIFF=HOST-GATEWAY
 . . . E  D  ;
 . . . . S DIFF=FILENUMB("OUTGOING","REQUEST",PRIORITY)
 . . . . S DIFF=DIFF-FILENUMB("INCOMING","RESPONSE",PRIORITY)
 . . . . Q
 . . . I DIFF>5 D  Q
 . . . . W !,"Warning: Processing of records in the "
 . . . . W PRIORITY," queue is ",DIFF," records behind."
 . . . . D COLORBAR
 . . . . Q
 . . . Q
 . . ;
 . . I DISKFREE<15 D  ; there is less free disk space than desirable
 . . . W !,"Warning: Only ",DISKFREE,"% of the total disk space is now available."
 . . . I DISKFREE<6 S COLOR=4
 . . . I DISKFREE<9 S COLOR=$S(COLOR>3:COLOR,1:3)
 . . . I DISKFREE<12 S COLOR=$S(COLOR>2:COLOR,1:2)
 . . . Q
 . . Q
 . E  D  ;  for sites without communication statistics, only worklist
 . . W $TR($J("",80)," ","*")
 . . W !,"*",?79,"*"
 . . W !,"*   Modality Worklist Message Processing Statistics"
 . . W ?60,"HL7 Delay: "
 . . D HL7QUEUE S DIFF=HOST-GATEWAY
 . . I 'DIFF W ?5,"<none>"
 . . E  W DIFF
 . . W ?79,"*",!,"*",?79,"*"
 . . W !,$TR($J("",80)," ","*")
 . . I DIFF>5 D
 . . . W !,"Warning: Processing of HL7 records is ",DIFF," behind."
 . . . D COLORBAR
 . . . Q
 . . W !
 . . W !?24,"Events:  Count   Time"
 . . W !?24,"------   -----   ----"
 . . Q
 . ;
 . ; output count of transactions
 . S LOCATION=^MAGDICOM(2006.563,1,"LOCATION")
 . ; arg-2 = 1 to only show today's statistics
 . S RPCERR=$$CALLRPC^MAGM2VCU("MAG DICOM TEXT AUDIT GET","MT",.AUD,LOCATION,1)
 . ;
 . S I=$O(AUD("")) I I="" W !!,"No statistics available",!!
 . E  D  ; output statistics
 . . N DATE,LINE
 . . S HDR=0
 . . F  S I=$O(AUD(I)) Q:I=""  D  Q:HDR
 . . . I $P(AUD(I),"^",1)'="" S HDR=1,I=I-1 Q
 . . . S MSG=$P(AUD(I),"^",3) S:MSG'="" MSG(1,MSG)=""
 . . . Q
 . . S P=0,MSG="" F  S MSG=$O(MSG(1,MSG)) Q:MSG=""  D
 . . . S P=P+1,MSG(1,MSG)=P,MSG(2,P)=MSG
 . . . Q
 . . ;
 . . F J=1:1:P W !?13,$C(J+64),": ",MSG(2,J)
 . . W !!?8 F J=1:1:P W "     ",$C(64+J)
 . . S (DATE,LINE)="" F  S I=$O(AUD(I)) Q:I=""  D
 . . . D:DATE'=$P(AUD(I),"^",1)
 . . . . W:LINE'="" !,LINE
 . . . . S DATE=$P(AUD(I),"^",1)
 . . . . S LINE=$E(DATE,4,5)_"/"_$E(DATE,6,7)_"/"_$E(DATE,2,3)
 . . . . Q
 . . . S COUNT=$P(AUD(I),"^",2),MSG=$P(AUD(I),"^",3) Q:MSG=""
 . . . ; Output one day's statistics
 . . . S J=MSG(1,MSG)
 . . . S $E(LINE,J-1*6+9,J-1*6+14)=$J($P(AUD(I),"^",2),6)
 . . . Q
 . . W:LINE'="" !,LINE
 . . Q
 . W !,"Exit?  no // " R X:30 S:X="" X="no"
 . I "^Yy"[$E(X) S HALT=1
 . Q
 Q
 ;
COLORBAR ; select the screen color
 I DIFF>150 S COLOR=4
 I DIFF>100 S COLOR=$S(COLOR>3:COLOR,1:3)
 I DIFF>050 S COLOR=$S(COLOR>2:COLOR,1:2)
 Q
 ;
HL7QUEUE ; get the HL7 queue pointers
 N INFO,RPCERR
 S RPCERR=$$CALLRPC^MAGM2VCU("MAG DICOM GET HIGHEST HL7","MT",.INFO)
 S HOST=+INFO
 S GATEWAY=+$G(^MAGDICOM(2006.563,1,"HL7_PTR"))
 Q
 ;
QUEUE(FLOW,SENDTYPE) ; output queue value
 N D1
 S X=^MAGDICOM(2006.564,"X",FLOW,SENDTYPE,PRIORITY),X=^MAGDICOM(2006.564,X,0)
 S TYPE=$S(CHANNEL=1:1,1:$P(X,"^",2)) I TYPE="*" S TYPE=CHANNEL
 S D1=$O(^MAGDICOM(2006.577,TYPE,1,"B",$E(X,1),""))
 S FILENUMB=$P(^MAGDICOM(2006.577,TYPE,1,D1,0),"^",2)
 W ?TAB+2,$E(X),":",FILENUMB
 S TAB=TAB+13 W ?TAB
 I FILENUMB=9999999 S FILENUMB=-1
 S FILENUMB(FLOW,SENDTYPE,PRIORITY)=+FILENUMB
 Q
 ;
DISK ; determine the amount of free space on the disk drive
 N DRIVE
 S DRIVE=$$DRIVE($G(^MAGDICOM(2006.563,1,"DATA PATH",1,0)))
 D DISK1(DRIVE)
 W !!,"Free  space on drive ",DRIVE," is ",$J(FREE,20)
 W !,"Total space on drive ",DRIVE," is ",$J(TOTAL,20)
 Q
 ;
DISK1(DRIVE) ; determine the amount of free space on the disk drive
 S TOTAL=$$DISKSIZE^MAGOSMSC(DRIVE)
 I 'TOTAL S (TOTAL,FREE)="0 Bytes",PCT=0 Q
 S FREE=$$DISKFREE^MAGOSMSC(DRIVE)
 S PCT=FREE/TOTAL*100
 S FREE=$$UNIT(FREE,"Byte",1024)
 S TOTAL=$$UNIT(TOTAL,"Byte",1024)
 Q
 ;
UNIT(V,N,K) ; return numbers with units
 N OUTPUT,POWERS,PREFIX,X
 S K=+$G(K) S:'K K=1000
 S POWERS=" Kilo Mega Giga Tera Peta Exa Zetta Yotta"
 S X=V F  D  Q:X=""
 . S PREFIX=$P(POWERS," ",1)
 . S POWERS=$S(PREFIX:PREFIX+3,1:$P(POWERS," ",2,$L(POWERS)+2))
 . S:PREFIX PREFIX="10**"_U_" "
 . I X<1000 S:X'=1 N=N_"s" S OUTPUT=X_" "_PREFIX_N,X="" Q
 . S X=+$J(X/K,0,3)
 . Q
 Q OUTPUT
 ;
DRIVE(D) N X
 I $G(D)="" Q "C:"
 I $E(D,1,2)="\\" Q $P(D,"\",1,4)
 Q $E(D)_":"
 ;
