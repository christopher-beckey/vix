Cache for Windows NT^INT^Saved from DICOM,DICOM (ISW-KUZMAKP3-LT)^~Format=Cache.S~
%RO on 12/19/2012  12:35
MAGBRTA6^INT^1^62810,45343^0
MAGBRTA6 ;WOIFO/EdM/PMK - Action Queue Processor ; 19 Dec 2012 12:35 PM
 ;;3.0;IMAGING;**11,51,54,110**;Mar 19, 2002;Build 61;Oct 10, 2012
 ;; Per VHA Directive 2004-038, this routine should not be modified.
 ;; +---------------------------------------------------------------+
 ;; | Property of the US Government.                                |
 ;; | No permission to copy or redistribute this software is given. |
 ;; | Use of unreleased versions of this software requires the user |
 ;; | to execute a written test agreement with the VistA Imaging    |
 ;; | Development Office of the Department of Veterans Affairs,     |
 ;; | telephone (301) 734-0100.                                     |
 ;; | The Food and Drug Administration classifies this software as  |
 ;; | a medical device.  As such, it may not be changed in any way. |
 ;; | Modifications to this software may result in an adulterated   |
 ;; | medical device under 21CFR820, the use of which is considered |
 ;; | to be a violation of US Federal Statutes.                     |
 ;; +---------------------------------------------------------------+
 ;;
 Q
 ;
DICOM ; DICOM type file copy for routing
 N XMIT ;----- Timestamp (start of transmission)
 ; DEST ;----- Pointer to destination in 2006.587
 N LDEST ;---- Pointer to destination in 2006.585
 N NAME ;----- Name of destination
 N OK,OUT,RETRY,STOP,X ; scratch variables
 ;
 S NAME=$P(DEST,"^",2),LDEST=0
 S:NAME'="" LDEST=$O(^MAGDICOM(2006.585,"B",NAME,""))
 I 'LDEST D STATUS^MAGBRTA5(D0,"SENT",LOCATION) Q
 ;
 S X=$G(^MAGDICOM(2006.585,LDEST,0))
 S:FILSRC_FILDST["~NO NETWORK LOCATION DEFINED" (FILSRC,FILDST)="-1~No routable files found for image "_IMAGE
 I FILSRC<0 D STATUS^MAGBRTA5(D0,"SENT",LOCATION) Q
 I FILDST<0 D STATUS^MAGBRTA5(D0,"SENT",LOCATION) Q
 I FILSRC="" D STATUS^MAGBRTA5(D0,"SENT",LOCATION) Q
 S (OK,RETRY)=0 F  D  Q:OK  Q:RETRY>$P(NODE3,"^",3)  Q:STOP
 . N RETURN ; return code from SEND^MAGDDCF1
 . ;
 . D CHECK^MAGBRTA4 Q:STOP
 . ;
 . S XMIT=$$NOW^XLFDT()
 . ;
 . ; call SEND^MAGDDCF1 to send the image using DCF_STORE_SCU.EXE
 . K ^TMP("MAG",$J,"DCF_STORE_SCU")
 . S ^TMP("MAG",$J,"DCF_STORE_SCU",0)=1
 . S ^TMP("MAG",$J,"DCF_STORE_SCU",1,"FILE")=FILDST
 . S RETURN=$$SEND^MAGDDCF1(NAME)
 . K ^TMP("MAG",$J,"DCF_STORE_SCU")
 . ;
 . I RETURN=0 S OK=1
 . E  I RETURN?1"-11,".E S OK=666
 . E  I RETURN?1"-12,".E S OK=666
 . E  S OK=-13
 . ; 
 . I OK'=1 D  S RETRY=RETRY+1
 . . D MESSAGE^MAGDMLOG(1,0,"DCF_STORE_SCU returned """_RETURN_"""","t")
 . . Q
 . Q
 D STATUS^MAGBRTA5(D0,$S(OK=666:"NOT FOUND",OK:"SENT",1:"FAILED"),LOCATION)
 Q:'OK
 S:$$CALLRPC^MAGM2VCU("MAG DICOM ROUTE LOG XMIT","MT",.OUT,D0,DEST,LOCATION,$S(OK=666:"NOT FOUND",OK:"SENT",1:"FAILED"),FILDST,XMIT,2)<0 FATAL=1
 Q
 ;



